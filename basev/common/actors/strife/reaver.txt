//------------------------------------------------------------------------------
actor Reaver 3001
{
	Game Strife
	ConversationID 52, -1, -1
	Health 150
	Radius 20
	Height 60
	Mass 500
	Speed 12
	PainChance 128
	MaxDropoffHeight 32
	MinMissileChance 150
	Monster
	+InCombat
	+NoBlood
	SeeSound "reaver/sight"
	ActiveSound "reaver/active"
	PainSound "reaver/pain"
	DeathSound "reaver/death"
	Obituary "$ob_reaver"
	HitObituary "$ob_reaverhit"

	action native A_ReaverRanged();

	states
	{
	Spawn:
		ROB1 AA 10 A_Look
		Loop
	See:
		ROB1 BBCCDDEE 3 A_Chase
		Loop
	Melee:
		ROB1 H 6 A_FaceTarget
		ROB1 I 8 A_CustomMeleeAttack(random(1, 8) * 3, "reaver/blade")
		ROB1 H 6
		Goto See
	Missile:
		ROB1 F 8 A_FaceTarget
		ROB1 G 11 Bright A_ReaverRanged
		Goto See
	Pain:
		ROB1 A 2
		ROB1 A 2 A_Pain
		Goto See
	Death:
		ROB1 J 6 Bright
		ROB1 K 6 Bright A_Scream
		ROB1 L 5 Bright
		ROB1 M 5 Bright A_NoBlocking
		ROB1 NOP 5 Bright
		ROB1 Q 6 Bright A_Explode(32, 32, 1, 1)
		ROB1 R -1
		Stop
	XDeath:
		ROB1 L 5 Bright A_TossGib
		ROB1 M 5 Bright A_Scream
		ROB1 N 5 Bright A_TossGib
		ROB1 O 5 Bright A_NoBlocking
		ROB1 P 5 Bright A_TossGib
		ROB1 Q 5 Bright A_Explode(32, 32, 1, 1)
		ROB1 R -1
		Stop
	}
}
