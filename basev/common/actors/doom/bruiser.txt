//------------------------------------------------------------------------------
actor BaronOfHell 3003
{
  game Doom
  SpawnID 3
  Health 1000
  Radius 24
  Height 64
  Mass 1000
  Speed 8
  PainChance 50
  SeeSound "baron/sight"
  ActiveSound "baron/active"
  PainSound "baron/pain"
  DeathSound "baron/death"
  Obituary "$ob_baron"
  HitObituary "$ob_baronhit"
  BloodType BloodGreen, BloodSplatterGreen
  BloodColor "00 66 00"
  Monster
  +FloorClip
  states
  {
  Spawn:
    BOSS AB 10 A_Look
    Loop
  See:
    BOSS AABBCCDD 3 A_Chase
    Loop
  Melee:
  Missile:
    BOSS EF 8 A_FaceTarget
    BOSS G 8 A_BruisAttack
    Goto See
  Pain:
    BOSS H 2
    BOSS H 2 A_Pain
    Goto See
  Death:
    BOSS I 8
    BOSS J 8 A_Scream
    BOSS K 8
    BOSS L 8 A_NoBlocking
    BOSS MN 8
    BOSS O -1 A_BossDeath
    Stop
  Raise:
    BOSS ONMLKJI 8
    Goto See
  }
}

//------------------------------------------------------------------------------
actor BaronBall
{
  game Doom
  SpawnID 154
  Radius 6
  Height 8
  Speed 15
  FastSpeed 20
  Damage 8
  RenderStyle Add
  Alpha 1
  SeeSound "baron/attack"
  DeathSound "baron/shotx"
  Projectile
  +Randomize
  states
  {
  Spawn:
    BAL7 AB 4 Bright
    Loop
  Death:
    BAL7 CDE 6 Bright
    Stop
  }
}

//------------------------------------------------------------------------------
actor HellKnight : BaronOfHell 69
{
  game Doom
  SpawnID 113
  Health 500
  SeeSound "knight/sight"
  ActiveSound "knight/active"
  PainSound "knight/pain"
  DeathSound "knight/death"
  Obituary "$ob_knight"
  HitObituary "$ob_knighthit"
  BloodType BloodGreen, BloodSplatterGreen
  BloodColor "00 66 00"
  states
  {
  Spawn:
    BOS2 AB 10 A_Look
    Loop
  See:
    BOS2 AABBCCDD 3 A_Chase
    Loop
  Melee:
  Missile:
    BOS2 EF 8 A_FaceTarget
    BOS2 G 8 A_BruisAttack
    Goto See
  Pain:
    BOS2 H 2
    BOS2 H 2 A_Pain
    Goto See
  Death:
    BOS2 I 8
    BOS2 J 8 A_Scream
    BOS2 K 8
    BOS2 L 8 A_NoBlocking
    BOS2 MN 8
    BOS2 O -1
    Stop
  Raise:
    BOS2 ONMLKJI 8
    Goto See
  }
}
