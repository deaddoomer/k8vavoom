//------------------------------------------------------------------------------
actor Cacodemon 3005
{
  game Doom
  SpawnID 19
  Health 400
  Radius 31
  Height 56
  Mass 400
  Speed 8
  PainChance 128
  SeeSound "caco/sight"
  ActiveSound "caco/active"
  PainSound "caco/pain"
  DeathSound "caco/death"
  Obituary "$ob_caco"
  HitObituary "$ob_cacohit"
  Monster
  /*
  BloodType BloodBlue, BloodSplatterBlue
  BloodColor "00 00 66"
  */
  BloodTranslation "16:48=240:247", "64:79=240:247", "164:167=201:207", "168:191=192:207", "232:235=100:207", "236:239=199:207"
  +NoGravity
  +Float
  states
  {
  Spawn:
    HEAD A 10 A_Look
    Loop
  See:
    HEAD A 3 A_Chase
    Loop
  Missile:
    HEAD BC 5 A_FaceTarget
    HEAD D 5 Bright A_HeadAttack
    Goto See
  Pain:
    HEAD E 3
    HEAD E 3 A_Pain
    HEAD F 6
    Goto See
  Death:
    HEAD G 8
    HEAD H 8 A_Scream
    HEAD IJ 8
    HEAD K 8 A_NoBlocking
    HEAD L -1 A_SetFloorClip
    Stop
  Raise:
    HEAD L 8 A_UnSetFloorClip
    HEAD KJIHG 8
    Goto See
  }
}

//------------------------------------------------------------------------------
actor CacodemonBall
{
  game Doom
  SpawnID 126
  Radius 6
  Height 8
  Speed 10
  FastSpeed 20
  Damage 5
  RenderStyle Add
  Alpha 1
  SeeSound "caco/attack"
  DeathSound "caco/shotx"
  Projectile
  +Randomize
  states
  {
  Spawn:
    BAL2 AB 4 Bright
    Loop
  Death:
    BAL2 CDE 6 Bright
    Stop
  }
}
