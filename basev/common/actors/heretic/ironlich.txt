//------------------------------------------------------------------------------
actor Ironlich 6
{
	Game Heretic
	SpawnID 20
	Health 700
	Radius 40
	Height 72
	Mass 325
	Speed 6
	PainChance 32
	Monster
	+NoBlood
	+DontMorph
	+BossDeath
	+DontSquash
	SeeSound "ironlich/sight"
	ActiveSound "ironlich/active"
	AttackSound "ironlich/attack1"
	PainSound "ironlich/pain"
	DeathSound "ironlich/death"
	Obituary "$ob_ironlich"
	HitObituary "$ob_ironlichhit"
	DropItem BlasterAmmo, 84, 10
	DropItem ArtiEgg 51

	action native A_LichAttack();

	states
	{
	Spawn:
		LICH A 10 A_Look
		Loop
	See:
		LICH A 4 A_Chase
		Loop
	Missile:
		LICH A 5 A_FaceTarget
		LICH B 20 A_LichAttack
		Goto See
	Pain:
		LICH A 4
		LICH A 4 A_Pain
		Goto See
	Death:
		LICH C 7
		LICH D 7 A_Scream
		LICH EF 7
		LICH G 7 A_NoBlocking
		LICH H 7
		LICH I -1 A_BossDeath
		Stop
	}
}

//------------------------------------------------------------------------------
actor HeadFX1
{
	Game Heretic
	SpawnID 164
	Radius 12
	Height 6
	Speed 13
	FastSpeed 20
	Damage 1
	RenderStyle Add
	Projectile
	-ActivatePCross
	-ActivateImpact
	+ThruGhost

	action native A_LichIceImpact();

	states
	{
	Spawn:
		FX05 ABC 6
		Loop
	Death:
		FX05 D 5 A_LichIceImpact
		FX05 EFG 5
		Stop
	}
}

//------------------------------------------------------------------------------
actor HeadFX2
{
	Radius 12
	Height 6
	Speed 8
	Damage 3
	RenderStyle Add
	Projectile
	-ActivatePCross
	-ActivateImpact
	states
	{
	Spawn:
		FX05 HIJ 6
		Loop
	Death:
		FX05 DEFG 5
		Stop
	}
}

//------------------------------------------------------------------------------
actor HeadFX3
{
	Radius 14
	Height 12
	Speed 10
	FastSpeed 18
	Damage 5
	RenderStyle Add
	Projectile
	-ActivatePCross
	-ActivateImpact
	+WindThrust

	action native A_LichFireGrow();

	states
	{
	Spawn:
		FX06 ABC 4 A_LichFireGrow
		Loop
	NoGrow:
		FX06 ABC 5
		Loop
	Death:
		FX06 DEFG 5
		Stop
	}
}
