//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************
//#define K8GORE_DEBUG

class Level_K8Gore : replaces(Level);

const int DefaultMaxAllowedBloodEntities = 1500;

int bloodEntCounter;
int bloodSkipDeathCounter;

#ifdef K8GORE_DEBUG
bool cvDebugDump;
#endif
bool cvLimiterDebugDump;
bool cvColoredBlood;
int cvMaxEntities = -1;


final void updateCvarCache () {
#ifdef K8GORE_DEBUG
  cvDebugDump = GetCvarB('k8GoreOpt_DbgReportNumber');
#endif
  cvLimiterDebugDump = GetCvarB('K8BDWOpt_LimiterDebug');
  cvColoredBlood = GetCvarB('k8GoreOpt_ForceColoredBlood');
  cvMaxEntities = GetCvar('k8GoreOpt_MaxBloodEntities');
  if (cvMaxEntities < 0) cvMaxEntities = DefaultMaxAllowedBloodEntities;
}


override void BeforeWorldTick (float DeltaTime) {
  updateCvarCache();
  ::BeforeWorldTick(DeltaTime);
  //print("BEFORE: %s", DeltaTime);
}

//override void AfterWorldTick (float DeltaTime) { ::AfterWorldTick(DeltaTime); print("AFTER: %s", DeltaTime); }


override void AfterUnarchiveThinkers () {
  updateCvarCache();
  ::AfterUnarchiveThinkers();
  k8LimiterUnarchiveThinkers(K8Gore_BloodBase, cvMaxEntities, ref bloodEntCounter, ref bloodSkipDeathCounter, cvLimiterDebugDump);
}


override void OnEntitySpawned (Entity ent) {
  if (cvMaxEntities < 0) updateCvarCache();
  ::OnEntitySpawned(ent);
  //print("SPAWNED: %C", ent);
  if (ent isa K8Gore_BloodBase) {
#ifdef K8GORE_DEBUG
    if (cvDebugDump) print("SPAWNED: %C (%d current)", ent, bloodEntCounter);
#endif
    ++bloodEntCounter;
    int allowed = cvMaxEntities;
    if (bloodEntCounter > allowed) {
      k8LimiterCleanup(K8Gore_BloodBase, allowed, ref bloodEntCounter, ref bloodSkipDeathCounter, cvLimiterDebugDump);
    }
  }
  if (cvColoredBlood) {
    EntityEx ee = EntityEx(ent);
    if (ee isa Cacodemon) {
      ee.BloodType = K8Gore_BloodBlue;
      ee.BloodSplatterType = K8Gore_Blood_SplatterReplacerBlue;
      ee.BloodColor = 0xff_00_00_66;
      ee.BloodTranslation = R_GetBloodTranslation(ee.BloodColor);
    } else if (ee isa BaronOfHell || ee isa HellKnight) {
      ee.BloodType = K8Gore_BloodGreen;
      ee.BloodSplatterType = K8Gore_Blood_SplatterReplacerGreen;
      ee.BloodColor = 0xff_00_66_00;
      ee.BloodTranslation = R_GetBloodTranslation(ee.BloodColor);
    }
  }
}


override void OnEntityDying (Entity ent) {
  ::OnEntityDying(ent);
  //print("DYING: %C", ent);
  if (ent isa K8Gore_BloodBase) {
    if (cvMaxEntities < 0) updateCvarCache();
    if (bloodSkipDeathCounter) {
      --bloodSkipDeathCounter;
#ifdef K8GORE_DEBUG
      if (cvDebugDump) print("DYING SKIP: %C (%d left; %d left to skip)", ent, bloodEntCounter, bloodSkipDeathCounter);
#endif
    } else {
      --bloodEntCounter;
#ifdef K8GORE_DEBUG
      if (cvDebugDump) print("DYING: %C (%d left)", ent, bloodEntCounter);
#endif
    }
  }
}
