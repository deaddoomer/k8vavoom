ACTOR BDW_ShotgunAmmo : Ammo {
  Inventory.Amount 0
  Inventory.MaxAmount 8
  Ammo.BackpackAmount 0
  Ammo.BackpackMaxAmount 8
  Inventory.Icon "SHTCA0"
}


ACTOR BDW_ShotgunShell : Ammo Replaces Shell {
  Inventory.PickupMessage "$GOTSHELLS"
  Inventory.Amount 4
  Inventory.MaxAmount 50
  Ammo.BackpackAmount 8
  Ammo.BackpackMaxAmount 100
  Inventory.Icon "SHELA0"
  States {
    Spawn:
      SHEL A -1
      Stop
  }
}


ACTOR BDW_ShotgunShellBox : BDW_ShotgunShell Replaces ShellBox {
  Inventory.PickupMessage "$GOTSHELLBOX"
  Inventory.Amount 20
  States {
    Spawn:
      SBOX A -1
      Stop
  }
}


ACTOR BDW_Shotgun : Shotgun Replaces Shotgun {
  Game Doom
  SpawnID 27
  Weapon.SlotNumber 3
  Weapon.SlotPriority 666 // goes before ssg; was 200
  Weapon.SelectionOrder 200
  Weapon.AmmoUse1 0
  Weapon.AmmoGive1 4
  Weapon.AmmoUse2 0
  Weapon.AmmoGive2 0
  Weapon.AmmoType1 "BDW_ShotgunShell"
  Weapon.AmmoType2 "BDW_ShotgunAmmo"
  Inventory.PickupMessage "$GOTSHOTGUN"
  Inventory.PickupSound "weapons/bdw_shotgun/sgpump"
  Obituary "$OB_MPSHOTGUN"
  AttackSound "None"
  +WEAPON.NOALERT
  +WEAPON.NOAUTOAIM
  +WEAPON.NOAUTOFIRE
  +FORCEXYBILLBOARD
  +WEAPON.NO_AUTO_SWITCH
  Scale 0.9

  var int user_ShotgunWasEmpty;
  var int user_Unloading;
  //var float user_arr[2];

  States {
    Ready:
      TNT1 A 0 NoDelay A_SetUserVar("user_ShotgunWasEmpty", 0);
      TNT1 A 0 A_SetUserVar("user_Unloading", 0);
      //TNT1 A 0 NoDelay A_SetUserArrayFloat("user_arr", 1, 666.69);
      //TNT1 A 0 NoDelay A_k8ConLog(va("%s", user_arr[1]));
      TNT1 A 0 A_JumpIfInventory("BDW_ShotgunAmmo", 1, 2)
      TNT1 A 0 A_SetUserVar("user_ShotgunWasEmpty", 1);
      //TNT1 A 0 NoDelay A_k8ConLog(va("user_ShotgunWasEmpty: %s", user_ShotgunWasEmpty));
      SHSS A 1 A_PlaySound("weapons/bdw_shotgun/sgpump")
      SHSS BCD 1
      Goto ShakeRight

    Spawn:
      SHTC A -1
      Stop

    OkToFire:
      SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWREFIRE)
      Loop

    ShakeLeft:
      SHTI GH 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWREFIRE)
      SHTI IJK 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWREFIRE)

    ShakeRight:
      SHTI ABC 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWREFIRE)
      SHTI DEF 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWREFIRE)
      SHTI GHK 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWREFIRE)
      SHTI ABEF 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWREFIRE)
      Goto OkToFire

    Steady:
      TNT1 A 1
      Goto Ready

    Deselect:
      SHSS DCBA 1
      TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
      TNT1 A 1
      Wait

    Select:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
      Goto Ready

    Fire:
      TNT1 A 0 A_JumpIfInventory("BDW_ShotgunAmmo", 1, "CanPerformFire")
      Goto Reload

    CanPerformFire:
      TNT1 A 0 A_JumpIf(user_ShotgunWasEmpty, "Pump2")
      TNT1 A 0 A_AlertMonsters
      TNT1 A 0 A_PlaySoundEx("weapons/bdw_shotgun/sgfire", "Weapon")
      TNT1 A 0 A_SpawnItemEx("BDW_PlayerMuzzle", 30, 5, 30)
      //TNT1 AAAAA 0 A_FireCustomMissile("BDW_Tracer", random(-5, 5), 0, -1, -12, 0, random(-5, 5))
      TNT1 A 0 A_TakeInventory("BDW_ShotgunAmmo", 1)
      SHTG A 0 A_FireBullets(3.6, 3.6, 9, 7, "BDW_ShotgunPuff")
      TNT1 A 0 A_SetPitch(-8.0+pitch)
      SHTF B 1 BRIGHT
      SHTF C 1 A_SetPitch(+1.0+pitch)
      SHTF DE 1 A_SetPitch(+1.0+pitch)
      SHTG A 1 A_SetPitch(+3.0+pitch)
      SHTG AA 1 A_SetPitch(+1.0+pitch)
      Goto Pump1

    Pump1:
      SHTG BCDEFG 1
      TNT1 A 0 A_FireCustomMissile("BDW_ShotCaseSpawn", 0, 0, -4, -4)
      SHTG H 1 A_PlaySoundEx("weapons/bdw_shotgun/sgpump", "Item")
      SHTG IJ 1
      SHTG KL 1
      SHTG KJIH 1
      TNT1 A 0 A_SetUserVar("user_ShotgunWasEmpty", 0)
      Goto ReadyToFireAgain

    Pump2:
      SHTG FG 1
      SHTG H 1 A_PlaySoundEx("weapons/bdw_shotgun/sgpump", "Item")
      SHTG IJ 1
      SHTG KL 1
      SHTG KJIH 1
      TNT1 A 0 A_SetUserVar("user_ShotgunWasEmpty", 0)

    ReadyToFireAgain:
      SHTG A 1 Offset(10, 36) // <-- New frames
      SHTG A 1 Offset(9, 40) //
      SHTG A 1 Offset(4, 40) //
      SHTG A 1 Offset(0, 32) //
      SHTG A 0 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWREFIRE) //A_Refire
      Goto ShakeLeft


    Reload:
      TNT1 A 0 A_SetUserVar("user_ShotgunWasEmpty", 0)
      TNT1 A 0 A_JumpIfInventory("BDW_ShotgunAmmo", 8, "OkToFire")
      SHTG A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH) // reset "fire" button
      TNT1 A 0 A_JumpIfInventory("BDW_ShotgunAmmo", 1, "ReloadNormally")
      TNT1 A 0 A_SetUserVar("user_ShotgunWasEmpty", 1)

    ReloadNormally:
      TNT1 A 0 A_JumpIfInventory("BDW_ShotgunShell", 1, "ReloadNormallyHasShells")
      Goto OkToFire

    ReloadNormallyHasShells:
      SHTG B 1
      SHTG CDE 1

    ReloadNormally18:
      TNT1 A 0 A_JumpIfInventory("BDW_ShotgunShell", 1, "ReloadNormallyHasShells18")

    ReloadNormally53:
      TNT1 A 0 A_JumpIf(user_ShotgunWasEmpty, "Pump2")
      SHTG FEDCB 1 //A_WeaponReady(WRF_ALLOWRELOAD)
      SHTG A 1 //A_WeaponReady(WRF_ALLOWRELOAD)
      Goto ShakeLeft

    ReloadNormallyHasShells18:
      TNT1 A 0 A_JumpIfInventory("BDW_ShotgunAmmo", 8, "ReloadNormally53")
      TNT1 A 0 A_GiveInventory("BDW_ShotgunAmmo", 1)
      TNT1 A 0 A_TakeInventory("BDW_ShotgunShell", 1)
      SSHR ABC 1
      SSHR D 1 A_PlaySound("weapons/bdw_shotgun/shellin")
      SSHR EFG 1
      SSHR A 2 A_Refire
      Goto ReloadNormally18


    StartUnload:
      SHTG A 1
      TNT1 A 0 A_SetUserVar("user_Unloading", 0)
      TNT1 A 0 A_SetUserVar("user_ShotgunWasEmpty", 1)
      TNT1 A 0 A_JumpIfInventory("BDW_ShotgunAmmo", 1, "CanPerformStartUnload")
      Goto OkToFire
    CanPerformStartUnload:
      TNT1 A 0 A_SetUserVar("user_Unloading", 0)
      SHTG ABCDEF 1

    RemoveBullets:
      TNT1 A 0 A_JumpIfInventory("BDW_ShotgunAmmo", 1, "CanPerformRemoveBullets")
      Goto FinishUnload

    CanPerformRemoveBullets:
      TNT1 A 0 A_TakeInventory("BDW_ShotgunAmmo", 1)
      TNT1 A 0 A_GiveInventory("BDW_ShotgunShell", 1)
      SHTG G 1
      SHTG H 1 A_PlaySoundEx("weapons/bdw_shotgun/sgpump", "Item")
      SHTG IJ 1
      SHTG KL 1
      SHTG KJIH 1
      TNT1 A 0 A_SetUserVar("user_ShotgunWasEmpty", 1)
      SHTG GF 1
      Goto RemoveBullets

  FinishUnload:
      SHTG EDCBA 1
      TNT1 A 0 A_SetUserVar("user_Unloading", 0)
      Goto OkToFire
  }
}
