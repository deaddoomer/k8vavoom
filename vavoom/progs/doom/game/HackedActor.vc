//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class HackedActor : Actor
	abstract;

int numbraintargets;
int braintargeton;
int brain_easy;
Actor braintargets[32];

string GOTARMOR;
string GOTMEGA;
string GOTHTHBONUS;
string GOTARMBONUS;
string GOTSTIM;
string GOTMEDINEED;
string GOTMEDIKIT;
string GOTSUPER;
string GOTBLUECARD;
string GOTYELWCARD;
string GOTREDCARD;
string GOTBLUESKUL;
string GOTYELWSKUL;
string GOTREDSKULL;
string GOTINVUL;
string GOTBERSERK;
string GOTINVIS;
string GOTSUIT;
string GOTMAP;
string GOTVISOR;
string GOTMSPHERE;
string GOTCLIP;
string GOTCLIPBOX;
string GOTROCKET;
string GOTROCKBOX;
string GOTCELL;
string GOTCELLBOX;
string GOTSHELLS;
string GOTSHELLBOX;
string GOTBACKPACK;
string GOTBFG9000;
string GOTCHAINGUN;
string GOTCHAINSAW;
string GOTLAUNCHER;
string GOTPLASMA;
string GOTSHOTGUN;
string GOTSHOTGUN2;

//==========================================================================
//
//	DropStuff
//
//==========================================================================

void DropStuff()
{
	classid item;
	Actor A;

	// Drop stuff.
	// This determines the kind of object spawned
	// during the death frame of a thing.
	if (Class == WolfSS ||
		Class == Zombieman)
	{
		item = ItemAmmoBulletClip;
	}
	else if (Class == ShotgunGuy)
	{
		item = ItemWeaponShotgun;
	}
	else if (Class == ChaingunGuy)
	{
		item = ItemWeaponChaingun;
	}
	else
	{
		return;
	}

	A = Spawn(item, vector(Origin.x, Origin.y, FloorZ));
	A.bDropped = true;	// special versions of items
}

//==========================================================================
//
//	HandlePickup
//
//==========================================================================

bool HandlePickup(Actor Toucher)
{
	int i;
	name sound;

	sound = 'misc/i_pkup';

	// Identify by sprite.
	// armor
	if (SpriteName == 'ARM1')
	{
		if (!Toucher.Player.GiveArmor(1))
			return false;
		cprint(Toucher.Player, GOTARMOR);
	}
	else if (SpriteName == 'ARM2')
	{
		if (!Toucher.Player.GiveArmor(2))
			return false;
		cprint(Toucher.Player, GOTMEGA);
	}
		// bonus items
	else if (SpriteName == 'BON1')
	{
		Toucher.Player.Health++;	// can go over 100%
		if (Toucher.Player.Health > 200)
			Toucher.Player.Health = 200;
		Toucher.Health = Toucher.Player.Health;
		cprint(Toucher.Player, GOTHTHBONUS);
	}
	else if (SpriteName == 'BON2')
	{
		Toucher.Player.ArmorPoints++;	// can go over 100%
		if (Toucher.Player.ArmorPoints > 200)
			Toucher.Player.ArmorPoints = 200;
		if (!Toucher.Player.ArmorType)
			Toucher.Player.ArmorType = 1;
		cprint(Toucher.Player, GOTARMBONUS);
	}
	else if (SpriteName == 'SOUL')
	{
		Toucher.Player.Health += SOULSPHERE_HEALTH;
		if (Toucher.Player.Health > SOULSPHERE_MAX)
			Toucher.Player.Health = SOULSPHERE_MAX;
		Toucher.Player.MO.Health = Toucher.Player.Health;
		cprint(Toucher.Player, GOTSUPER);
		sound = 'misc/p_pkup';
	}
	else if (SpriteName == 'MEGA')
	{
		Toucher.Player.Health = MEGASPHERE_HEALTH;
		Toucher.Health = Toucher.Player.Health;
		Toucher.Player.GiveArmor(2);
		cprint(Toucher.Player, GOTMSPHERE);
		sound = 'misc/p_pkup';
	}
		// cards
		// leave cards for everyone
	else if (SpriteName == 'BKEY')
	{
		if (Toucher.Player.GiveCard(it_bluecard))
			cprint(Toucher.Player, GOTBLUECARD);
		if (Level.Game.netgame)
			return false;
	}
	else if (SpriteName == 'YKEY')
	{
		if (Toucher.Player.GiveCard(it_yellowcard))
			cprint(Toucher.Player, GOTYELWCARD);
		if (Level.Game.netgame)
			return false;
	}
	else if (SpriteName == 'RKEY')
	{
		if (Toucher.Player.GiveCard(it_redcard))
			cprint(Toucher.Player, GOTREDCARD);
		if (Level.Game.netgame)
			return false;
	}
	else if (SpriteName == 'BSKU')
	{
		if (Toucher.Player.GiveCard(it_blueskull))
			cprint(Toucher.Player, GOTBLUESKUL);
		if (Level.Game.netgame)
			return false;
	}
	else if (SpriteName == 'YSKU')
	{
		if (Toucher.Player.GiveCard(it_yellowskull))
			cprint(Toucher.Player, GOTYELWSKUL);
		if (Level.Game.netgame)
			return false;
	}
	else if (SpriteName == 'RSKU')
	{
		if (Toucher.Player.GiveCard(it_redskull))
			cprint(Toucher.Player, GOTREDSKULL);
		if (Level.Game.netgame)
			return false;
	}
		// medikits, heals
	else if (SpriteName == 'STIM')
	{
		if (!Toucher.Player.GiveBody(10, MAXHEALTH))
			return false;
		cprint(Toucher.Player, GOTSTIM);
	}
	else if (SpriteName == 'MEDI')
	{
		if (!Toucher.Player.GiveBody(25, MAXHEALTH))
			return false;

		if (Toucher.Player.Health < 25)
			cprint(Toucher.Player, GOTMEDINEED);
		else
			cprint(Toucher.Player, GOTMEDIKIT);
	}
		// power ups
	else if (SpriteName == 'PINV')
	{
		if (!Toucher.Player.GivePower(pw_invulnerability))
			return false;
		cprint(Toucher.Player, GOTINVUL);
		sound = 'misc/p_pkup';
	}
	else if (SpriteName == 'PSTR')
	{
		if (!Toucher.Player.GivePower(pw_strength))
			return false;
		cprint(Toucher.Player, GOTBERSERK);
		sound = 'misc/p_pkup';
	}
	else if (SpriteName == 'PINS')
	{
		if (!Toucher.Player.GivePower(pw_invisibility))
			return false;
		cprint(Toucher.Player, GOTINVIS);
		sound = 'misc/p_pkup';
	}
	else if (SpriteName == 'SUIT')
	{
		if (!Toucher.Player.GivePower(pw_ironfeet))
			return false;
		cprint(Toucher.Player, GOTSUIT);
		sound = 'misc/p_pkup';
	}
	else if (SpriteName == 'PVIS')
	{
		if (!Toucher.Player.GivePower(pw_infrared))
			return false;
		cprint(Toucher.Player, GOTVISOR);
		sound = 'misc/p_pkup';
	}
		//  Items
	else if (SpriteName == 'PMAP')
	{
		if (!Toucher.Player.GiveItem(IT_ALL_MAP))
			return false;
		cprint(Toucher.Player, GOTMAP);
		sound = 'misc/p_pkup';
	}
		// ammo
	else if (SpriteName == 'CLIP')
	{
		if (bDropped)
		{
			if (!Toucher.Player.GiveAmmo(DoomDefs::am_clip, MainGameInfo(Level.Game).clipammo[DoomDefs::am_clip] / 2))
				return false;
		}
		else
		{
			if (!Toucher.Player.GiveAmmo(DoomDefs::am_clip, MainGameInfo(Level.Game).clipammo[DoomDefs::am_clip]))
				return false;
		}
		cprint(Toucher.Player, GOTCLIP);
	}
	else if (SpriteName == 'AMMO')
	{
		if (!Toucher.Player.GiveAmmo(DoomDefs::am_clip, 5 * MainGameInfo(Level.Game).clipammo[DoomDefs::am_clip]))
			return false;
		cprint(Toucher.Player, GOTCLIPBOX);
	}
	else if (SpriteName == 'ROCK')
	{
		if (!Toucher.Player.GiveAmmo(DoomDefs::am_misl, MainGameInfo(Level.Game).clipammo[DoomDefs::am_misl]))
			return false;
		cprint(Toucher.Player, GOTROCKET);
	}
	else if (SpriteName == 'BROK')
	{
		if (!Toucher.Player.GiveAmmo(DoomDefs::am_misl, 5 * MainGameInfo(Level.Game).clipammo[DoomDefs::am_misl]))
			return false;
		cprint(Toucher.Player, GOTROCKBOX);
	}
	else if (SpriteName == 'CELL')
	{
		if (!Toucher.Player.GiveAmmo(DoomDefs::am_cell, MainGameInfo(Level.Game).clipammo[DoomDefs::am_cell]))
			return false;
		cprint(Toucher.Player, GOTCELL);
	}
	else if (SpriteName == 'CELP')
	{
		if (!Toucher.Player.GiveAmmo(DoomDefs::am_cell, 5 * MainGameInfo(Level.Game).clipammo[DoomDefs::am_cell]))
			return false;
		cprint(Toucher.Player, GOTCELLBOX);
	}
	else if (SpriteName == 'SHEL')
	{
		if (!Toucher.Player.GiveAmmo(DoomDefs::am_shell, MainGameInfo(Level.Game).clipammo[DoomDefs::am_shell]))
			return false;
		cprint(Toucher.Player, GOTSHELLS);
	}
	else if (SpriteName == 'SBOX')
	{
		if (!Toucher.Player.GiveAmmo(DoomDefs::am_shell, 5 * MainGameInfo(Level.Game).clipammo[DoomDefs::am_shell]))
			return false;
		cprint(Toucher.Player, GOTSHELLBOX);
	}
	else if (SpriteName == 'BPAK')
	{
		if (!Toucher.Player.Backpack)
		{
			Toucher.Player.Backpack = true;
			for (i = 0; i < DoomDefs::NUMAMMO; i++)
				Toucher.Player.MaxAmmo[i] *= 2;
		}
		for (i = 0; i < DoomDefs::NUMAMMO; i++)
		{
			Toucher.Player.GiveAmmo(i, MainGameInfo(Level.Game).clipammo[i]);
		}
		cprint(Toucher.Player, GOTBACKPACK);
	}
		// weapons
	else if (SpriteName == 'BFUG')
	{
		if (!Toucher.Player.GiveWeapon(DoomDefs::wp_bfg, false))
			return false;
		cprint(Toucher.Player, GOTBFG9000);
		sound = 'misc/w_pkup';
	}
	else if (SpriteName == 'MGUN')
	{
		if (!Toucher.Player.GiveWeapon(DoomDefs::wp_chaingun, bDropped))
			return false;
		cprint(Toucher.Player, GOTCHAINGUN);
		sound = 'misc/w_pkup';
	}
	else if (SpriteName == 'CSAW')
	{
		if (!Toucher.Player.GiveWeapon(DoomDefs::wp_chainsaw, false))
			return false;
		cprint(Toucher.Player, GOTCHAINSAW);
		sound = 'misc/w_pkup';
	}
	else if (SpriteName == 'LAUN')
	{
		if (!Toucher.Player.GiveWeapon(DoomDefs::wp_missile, false))
			return false;
		cprint(Toucher.Player, GOTLAUNCHER);
		sound = 'misc/w_pkup';
	}
	else if (SpriteName == 'PLAS')
	{
		if (!Toucher.Player.GiveWeapon(DoomDefs::wp_plasma, false))
			return false;
		cprint(Toucher.Player, GOTPLASMA);
		sound = 'misc/w_pkup';
	}
	else if (SpriteName == 'SHOT')
	{
		if (!Toucher.Player.GiveWeapon(DoomDefs::wp_shotgun, bDropped))
			return false;
		cprint(Toucher.Player, GOTSHOTGUN);
		sound = 'misc/w_pkup';
	}
	else if (SpriteName == 'SGN2')
	{
		if (!Toucher.Player.GiveWeapon(DoomDefs::wp_supershotgun, bDropped))
			return false;
		cprint(Toucher.Player, GOTSHOTGUN2);
		sound = 'misc/w_pkup';
	}
	Toucher.PlaySound(sound, CHAN_ITEM);
	return true;
}

//===========================================================================
//
//  A_BFGSpray
//
//  Spawn a BFG explosion on every monster in view
//
//===========================================================================

void A_BFGSpray()
{
	int i;
	int j;
	int damage;
	TAVec aimAng;
	Actor emo;

	// offset angles from its attack angle
	for (i = 0; i < 40; i++)
	{
		aimAng = Angles;
		aimAng.yaw = AngleMod360(aimAng.yaw - 45.0 + 90.0 / 40.0 * itof(i));

		// target is the originator (player)
		//  of the missile
		Instigator.AimLineAttack(aimAng, 16.0 * 64.0);
		if (!Instigator.linetarget)
		{
			continue;
		}

		emo = Spawn(ExtraBFG, Instigator.linetarget.Origin +
			vector(0.0, 0.0, Instigator.linetarget.Height / 4.0));

		MSG_Select(MSG_SV_DATAGRAM);
		MSG_WriteByte(DoomDefs::svc_explosion2);
		MSG_WriteByte(DoomDefs::EF_DL_GBALL);
		MSG_WriteShort(ftoi(emo.Origin.x));
		MSG_WriteShort(ftoi(emo.Origin.y));
		MSG_WriteShort(ftoi(emo.Origin.z));

		damage = 0;
		for (j = 0; j < 15; j++)
			damage += (P_Random() & 7) + 1;

		linetarget.Damage(Instigator, Instigator, damage);
	}
}

//==========================================================================
//
//  A_PosAttack
//
//  Zombieman attack.
//
//==========================================================================

void A_PosAttack()
{
	int damage;
	TVec dir;

	if (!Enemy)
		return;

	A_FaceTarget();

	dir = AimLineAttack(Angles, MISSILERANGE);
	VectorRotateAroundZ(&dir, (Random() - Random()) * 45.0 / 2.0);

	PlaySound('grunt/attack', CHAN_WEAPON);

	damage = ((P_Random() % 5) + 1) * 3;

	LineAttack(dir, MISSILERANGE, damage);

	Effects |= DoomDefs::EF_DL_MUZZLEFLASH;
}

//==========================================================================
//
//  A_SPosAttack
//
//  Shotgun guy, Spider mastermind attack.
//
//==========================================================================

void A_SPosAttack()
{
	int i;
	int damage;
	TVec aimDir;
	TVec dir;

	if (!Enemy)
		return;

	PlaySound('shotguy/attack', CHAN_WEAPON);
	A_FaceTarget();
	aimDir = AimLineAttack(Angles, MISSILERANGE);
	for (i = 0; i < 3; i++)
	{
		dir = aimDir;
		VectorRotateAroundZ(&dir, (Random() - Random()) * 45.0 / 2.0);
		damage = ((P_Random() % 5) + 1) * 3;
		LineAttack(dir, MISSILERANGE, damage);
	}

	Effects |= DoomDefs::EF_DL_MUZZLEFLASH;
}

//==========================================================================
//
//  A_TroopAttack
//
//  Imp attack.
//
//==========================================================================

void A_TroopAttack()
{
	int damage;

	if (!Enemy)
		return;

	A_FaceTarget();
	if (CheckMeleeRange())
	{
		PlaySound('imp/melee', CHAN_WEAPON);
		damage = (P_Random() % 8 + 1) * 3;
		Enemy.Damage(self, self, damage);
		return;
	}

	// launch a missile
	SpawnMissile(Enemy, ImpMissile);
}

//==========================================================================
//
//  A_SargAttack
//
//  Demon, Spectre attack.
//
//==========================================================================

void A_SargAttack()
{
	int damage;

	if (!Enemy)
		return;

	A_FaceTarget();
	if (CheckMeleeRange())
	{
		damage = ((P_Random() % 10) + 1) * 4;
		Enemy.Damage(self, self, damage);
	}
}

//==========================================================================
//
//  A_HeadAttack
//
//  Cacodemon attack.
//
//==========================================================================

void A_HeadAttack()
{
	int damage;

	if (!Enemy)
		return;

	A_FaceTarget();
	if (CheckMeleeRange())
	{
		damage = (P_Random() % 6 + 1) * 10;
		Enemy.Damage(self, self, damage);
		return;
	}

	// launch a missile
	SpawnMissile(Enemy, CacodemonMissile);
}

//==========================================================================
//
//  A_BruisAttack
//
//  Hell knight, Baron of hell attack.
//
//==========================================================================

void A_BruisAttack()
{
	int damage;

	if (!Enemy)
		return;

	if (CheckMeleeRange())
	{
		PlaySound('baron/melee', CHAN_WEAPON);
		damage = (P_Random() % 8 + 1) * 10;
		Enemy.Damage(self, self, damage);
		return;
	}

	// launch a missile
	SpawnMissile(Enemy, BruiserShot);
}

//==========================================================================
//
//  A_SkullAttack
//
//  Lost soul attack. Fly at the player like a missile.
//
//==========================================================================

void A_SkullAttack()
{
	if (!Enemy)
		return;

	bSkullFly = true;

	PlaySound(AttackSound, CHAN_VOICE);
	A_FaceTarget();
	Velocity = Normalise(MobjCenter(Enemy) - Origin) * 700.0;//SKULLSPEED;
}

//==========================================================================
//
//  A_Hoof
//
//  Cyberdemon move with hoof sound.
//
//==========================================================================

void A_Hoof()
{
	PlaySound('cyber/hoof', CHAN_BODY);
	A_Chase();
}

//==========================================================================
//
//  A_Metal
//
//  Cyberdemon move with metal sound.
//
//==========================================================================

void A_Metal()
{
	PlaySound('cyber/walk', CHAN_BODY);
	A_Chase();
}

//==========================================================================
//
//  A_CyberAttack
//
//  Cyberdemon attack.
//
//==========================================================================

void A_CyberAttack()
{
	if (!Enemy)
		return;

	A_FaceTarget();
	SpawnMissile(Enemy, Rocket);
}

//==========================================================================
//
//  A_SpidRefire
//
//  Spider mastermind refire.
//
//==========================================================================

void A_SpidRefire()
{
	// keep firing unless target got out of sight
	A_FaceTarget();

	if (P_Random() < 10)
		return;

	if (!Enemy || Enemy.Health <= 0 || !CanSee(Enemy))
	{
		SetState(SeeState);
	}
}

//==========================================================================
//
//  A_CPosAttack
//
//  Heavy weapon dude attack.
//
//==========================================================================

void A_CPosAttack()
{
	int damage;
	TVec dir;

	if (!Enemy)
		return;

	PlaySound('chainguy/attack', CHAN_WEAPON);
	A_FaceTarget();
	dir = AimLineAttack(Angles, MISSILERANGE);
	VectorRotateAroundZ(&dir, (Random() - Random()) * 45.0 / 2.0);
	damage = ((P_Random() % 5) + 1) * 3;
	LineAttack(dir, MISSILERANGE, damage);

	Effects |= DoomDefs::EF_DL_MUZZLEFLASH;
}

//==========================================================================
//
//  A_CPosRefire
//
//  Heavy weapon dude refire.
//
//==========================================================================

void A_CPosRefire()
{
	// keep firing unless target got out of sight
	A_FaceTarget();

	if (P_Random() < 40)
		return;

	if (!Enemy || Enemy.Health <= 0 || !CanSee(Enemy))
	{
		SetState(SeeState);
	}
}

//==========================================================================
//
//  A_BabyMetal
//
//  Arachnotron move with metal sound.
//
//==========================================================================

void A_BabyMetal()
{
	PlaySound('baby/walk', CHAN_BODY);
	A_Chase();
}

//==========================================================================
//
//  A_BspiAttack
//
//  Arachnotron attack.
//
//==========================================================================

void A_BspiAttack()
{
	if (!Enemy)
		return;

	A_FaceTarget();

	// launch a missile
	SpawnMissile(Enemy, ArachnotronPlasma);
}

//==========================================================================
//
//  A_PainShootSkull
//
//  Spawn a lost soul and launch it at the target
//
//==========================================================================

void A_PainShootSkull(float angle)
{
	Actor mo;
	HackedActor newmobj;
	float prestep;
	int count;

	// count total number of skull currently on the level
	count = 0;
	FOREACH(Actor, mo)
	{
		if (mo.Class == LostSoul)
			count++;
	}

	// if there are allready 20 skulls on the level,
	// don't spit another one
	if (count > 20)
		return;

	// okay, there's playe for another one
	prestep = 4.0 + 3.0 * (Radius + 16.0) / 2.0;//SKULL_RADIUS

	newmobj = HackedActor(Spawn(LostSoul, Origin + vector(
		prestep * cos(angle), prestep * sin(angle), 8.0)));

	// Check for movements.
	if (!newmobj.TryMove(newmobj.Origin))
	{
		// kill it immediately
		newmobj.Damage(self, self, 10000);
		return;
	}

	newmobj.Enemy = Enemy;
	newmobj.A_SkullAttack();
}

//==========================================================================
//
//  A_PainAttack
//
//  Pain elemental attack. Spawn a lost soul and launch it at the target.
// 
//==========================================================================

void A_PainAttack()
{
	if (!Enemy)
		return;

	A_FaceTarget();
	A_PainShootSkull(Angles.yaw);
}

//==========================================================================
//
//  A_PainDie
//
//  Pain elemental death.
//
//==========================================================================

void A_PainDie()
{
	A_Fall();
	A_PainShootSkull(Angles.yaw + 90.0);
	A_PainShootSkull(Angles.yaw + 180.0);
	A_PainShootSkull(Angles.yaw + 270.0);
}

//==========================================================================
//
//  A_SkelWhosh
//
//  Ravenant attack sound.
//
//==========================================================================

void A_SkelWhoosh()
{
	if (!Enemy)
		return;
	A_FaceTarget();
	PlaySound('skeleton/swing', CHAN_WEAPON);
}

//==========================================================================
//
//  A_SkelFist
//
//  Ravenant close attack.
//
//==========================================================================

void A_SkelFist()
{
	int damage;

	if (!Enemy)
		return;

	A_FaceTarget();

	if (CheckMeleeRange())
	{
		damage = ((P_Random() % 10) + 1) * 6;
		PlaySound('skeleton/melee', CHAN_WEAPON);
		Enemy.Damage(self, self, damage);
	}
}

//==========================================================================
//
//  A_SkelMissile
//
//  Ravenant far attack.
//
//==========================================================================

void A_SkelMissile()
{
	Actor A;

	if (!Enemy)
		return;

	A_FaceTarget();
	Origin.z += 16.0;	// so missile spawns higher
	A = SpawnMissile(Enemy, RevenantTracer);
	Origin.z -= 16.0;	// back to normal

	A.Origin = A.Origin + A.Velocity * 0.03;
	A.Enemy = Enemy;
}

//==========================================================================
//
//  A_Tracer
//
//  Ravenant missile tracer.
//
//==========================================================================

#define TRACEADJUST	0.2865921825

void A_Tracer()
{
	Actor A;
	TVec wishdir;
	TVec currdir;
	TVec delta;
	float adjustlen;

	if (Level.Game.level->tictime & 3)
		return;

	// spawn a puff of smoke behind the rocket      
	SpawnPuff(Origin, MISSILERANGE);

	A = Spawn(RevenantTracerSmoke, Origin - Velocity * Level.Game.frametime);

	A.Velocity.z = 1.0 * 35.0;
	A.StateTime -= Random() * 0.1;
	if (A.StateTime < 0.1)
		A.StateTime = 0.1;

	// adjust direction
	if (!Enemy || Enemy.Health <= 0)
		return;

	wishdir = Normalise(MobjCenter(Enemy) - Origin);
	currdir = Normalise(Velocity);
	delta = wishdir - currdir;
	adjustlen = Length(delta);
	if (adjustlen > TRACEADJUST)
		adjustlen = TRACEADJUST;
	wishdir = currdir + delta * adjustlen;
	Velocity = wishdir * Speed;
	VectorAngles(&wishdir, &Angles);
}

//==========================================================================
//
//  A_FatRaise
//
//  Mancubus attack sound.
//
//==========================================================================

void A_FatRaise()
{
	A_FaceTarget();
	PlaySound('fatso/raiseguns', CHAN_WEAPON);
}

//==========================================================================
//
//  A_FatAttack1
//
//  Mancubus attack, firing three missiles (bruisers) in three different
// directions? Doesn't look like it.
//
//==========================================================================

void A_FatAttack1()
{
	Actor A;

	A_FaceTarget();
	// Change direction  to ...
	Angles.yaw = AngleMod360(Angles.yaw + (90.0 / 8.0));
	SpawnMissile(Enemy, MancubusMissile);

	A = SpawnMissile(Enemy, MancubusMissile);
	A.Angles.yaw = AngleMod360(A.Angles.yaw + (90.0 / 8.0));
	VectorRotateAroundZ(&A.Velocity, (90.0 / 8.0));
}

//==========================================================================
//
//  A_FatAttack2
//
//  Mancubus attack, second direction.
//
//==========================================================================

void A_FatAttack2()
{
	Actor A;

	A_FaceTarget();
	// Now here choose opposite deviation.
	Angles.yaw = AngleMod360(Angles.yaw - (90.0 / 8.0));
	SpawnMissile(Enemy, MancubusMissile);

	A = SpawnMissile(Enemy, MancubusMissile);
	A.Angles.yaw = AngleMod360(A.Angles.yaw - (90.0 / 8.0) * 2.0);
	VectorRotateAroundZ(&A.Velocity, -(90.0 / 8.0) * 2.0);
}

//==========================================================================
//
//  A_FatAttack3
//
//  Mancubus attack, third direction.
//
//==========================================================================

void A_FatAttack3()
{
	Actor A;

	A_FaceTarget();

	A = SpawnMissile(Enemy, MancubusMissile);
	A.Angles.yaw = AngleMod360(A.Angles.yaw - (90.0 / 8.0) / 2.0);
	VectorRotateAroundZ(&A.Velocity, -(90.0 / 8.0) / 2.0);

	A = SpawnMissile(Enemy, MancubusMissile);
	A.Angles.yaw = AngleMod360(A.Angles.yaw + (90.0 / 8.0) / 2.0);
	VectorRotateAroundZ(&A.Velocity, (90.0 / 8.0) / 2.0);
}

//==========================================================================
//
//  A_VileChase
//
//  Arch-vile check for ressurecting a body.
//
//==========================================================================

void A_VileChase()
{
	int xl;
	int xh;
	int yl;
	int yh;

	int bx;
	int by;

	Actor temp;

	if (MoveDir != DI_NODIR)
	{
		// check for corpses to raise
		viletryx = Origin.x + StepSpeed * MainGameInfo(Level.Game).xspeed[MoveDir];
		viletryy = Origin.y + StepSpeed * MainGameInfo(Level.Game).yspeed[MoveDir];

		xl = MapBlock(viletryx - XLevel.BlockMapOrgX - MAXRADIUS * 2.0);
		xh = MapBlock(viletryx - XLevel.BlockMapOrgX + MAXRADIUS * 2.0);
		yl = MapBlock(viletryy - XLevel.BlockMapOrgY - MAXRADIUS * 2.0);
		yh = MapBlock(viletryy - XLevel.BlockMapOrgY + MAXRADIUS * 2.0);

		for (bx = xl; bx <= xh; bx++)
		{
			for (by = yl; by <= yh; by++)
			{
				// Call PIT_VileCheck to check whether object is a corpse
				// that canbe raised.
				if (!P_BlockThingsIterator(bx, by, self, 'PIT_VileCheck'))
				{
					// got one!
					temp = Enemy;
					Enemy = CorpseHit;
					A_FaceTarget();
					Enemy = temp;

					SetState(FindState('S_VILE_HEAL1'));
					CorpseHit.PlaySound('vile/raise', CHAN_BODY);

					CorpseHit.SetState(CorpseHit.RaiseState);
					CorpseHit.Height *= 4.0;
					CorpseHit.bShootable = true;
					CorpseHit.bFloat = CorpseHit.bDefaultFloat;
					CorpseHit.bNoGravity = CorpseHit.bDefaultNoGravity;
					CorpseHit.bDropOff = false;
					CorpseHit.bSolid = true;
					CorpseHit.bCorpse = false;
					CorpseHit.Health = CorpseHit.SpawnHealth;
					CorpseHit.Enemy = none;
				}
			}
		}
	}


	// Return to normal attack.
	A_Chase();
}

//==========================================================================
//
//  A_VileStart
//
//  Arch-vile attack start sound.
//
//==========================================================================

void A_VileStart()
{
	PlaySound('vile/start', CHAN_WEAPON);
}

//==========================================================================
//
//  A_VileTarget
//
//  Spawn the hellfire.
//
//==========================================================================

void A_VileTarget()
{
	Actor fog;

	if (!Enemy)
		return;

	A_FaceTarget();

	fog = Spawn(ArchvileFire, Enemy.Origin);

	Instigator = fog;//FIXME!!!!!!!
	fog.Instigator = self;
	fog.Enemy = Enemy;
	fog.SetState(fog.IdleState);
}

//==========================================================================
//
//  A_VileAttack
//
//  Arch-vile attack.
//
//==========================================================================

void A_VileAttack()
{
	Actor fire;

	if (!Enemy)
		return;

	A_FaceTarget();

	if (!CanSee(Enemy))
		return;

	PlaySound('vile/stop', CHAN_WEAPON);
	Enemy.Damage(self, self, 20);
	Enemy.Velocity.z = 1000.0 / Enemy.Mass * 35.0;

	fire = Instigator;//FIXME!!!!!!

	if (!fire)
		return;

	// move the fire between the vile and the player
	fire.Origin.x = Enemy.Origin.x - 24.0 * cos(Angles.yaw);
	fire.Origin.y = Enemy.Origin.y - 24.0 * sin(Angles.yaw);
	fire.RadiusAttack(70, 0, 0);
}

//==========================================================================
//
//  A_Fire
//
//  Keep fire in front of player unless out of sight.
//
//==========================================================================

void A_Fire()
{
	if (!Enemy)
		return;

	// don't move it if the vile lost sight
	if (!Instigator.CanSee(Enemy))
		return;

	UnlinkFromWorld();
	Origin.x = Enemy.Origin.x + 24.0 * cos(Enemy.Angles.yaw);
	Origin.y = Enemy.Origin.y + 24.0 * sin(Enemy.Angles.yaw);
	Origin.z = Enemy.Origin.z;
	LinkToWorld();
}

//==========================================================================
//
//  A_StartFire
//
//  Make fire start sound.
//
//==========================================================================

void A_StartFire()
{
	PlaySound('vile/firestrt', CHAN_VOICE);
	A_Fire();
}

//==========================================================================
//
//  A_FireCrackle
//
//  Fire sound.
//
//==========================================================================

void A_FireCrackle()
{
	PlaySound('vile/firecrkl', CHAN_VOICE);
	A_Fire();
}

//==========================================================================
//
//  A_BrainPain
//
//  Brain pain sound.
//
//==========================================================================

void A_BrainPain()
{
	PlayFullVolumeSound('brain/pain', CHAN_VOICE);
}

//==========================================================================
//
//  A_BrainScream
//
//  Brain death sound, make explosions.
//
//==========================================================================

void A_BrainScream()
{
	TVec org;
	Actor A;

	for (org.x = Origin.x - 196.0; org.x < Origin.x + 320.0; org.x += 8.0)
	{
		org.y = Origin.y - 320.0;
		org.z = 1.0 / 512.0 + Random() * 512.0;
		A = Spawn(Rocket, org);
		A.Velocity.z = Random() * 2.0 * 35.0;

		A.SetState(FindState('S_BRAINEXPLODE1'));

		A.StateTime -= Random() * 0.2;
		if (A.StateTime < 0.1)
			A.StateTime = 0.1;
	}

	PlayFullVolumeSound('brain/death', CHAN_VOICE);
}

//==========================================================================
//
//  A_BrainExplode
//
//  Brain explosions.
//
//==========================================================================

void A_BrainExplode()
{
	TVec org;
	Actor A;

	org.x = Origin.x + (Random() - Random()) * 8.0;
	org.y = Origin.y;
	org.z = 1.0 / 512.0 + Random() * 512.0;
	A = Spawn(Rocket, org);
	A.Velocity.z = Random() * 2.0 * 35.0;

	A.SetState(FindState('S_BRAINEXPLODE1'));

	A.StateTime -= Random() * 0.2;
	if (A.StateTime < 0.1)
		A.StateTime = 0.1;
}

//==========================================================================
//
//  A_BrainDie
//
//  Killed brain, exit level.
//
//==========================================================================

void A_BrainDie()
{
	G_ExitLevel(0);
}

//==========================================================================
//
//  A_BrainAwake
//
//  Init boss spawner.
//
//==========================================================================

void A_BrainAwake()
{
	Actor m;

	// find all the target spots
	numbraintargets = 0;
	FOREACH(Actor, m)
	{
		if (m.Class == BossTarget)
		{
			braintargets[numbraintargets] = m;
			numbraintargets++;
		}
	}
	braintargeton = 0;

	PlayFullVolumeSound('brain/sight', CHAN_VOICE);
}

//==========================================================================
//
//  A_BrainSpit
//
//  Spawn a cube.
//
//==========================================================================

void A_BrainSpit()
{
	Actor targ;
	Actor A;

	brain_easy ^= 1;
	if (Level.Game.gameskill <= sk_easy && (!brain_easy))
		return;

	// shoot a cube at current target
	targ = braintargets[braintargeton];
	braintargeton = (braintargeton + 1) % numbraintargets;

	// spawn brain missile
	A = SpawnMissile(targ, SpawnShot);
	A.Enemy = targ;
	A.ReactionCount = ftoi((targ.Origin.y - Origin.y) /
		A.Velocity.y / A.StateTime);

	PlayFullVolumeSound('brain/spit', CHAN_WEAPON);
}

//==========================================================================
//
//  A_SpawnFly
//
//  Cube flying, spawn monster, when finished.
//
//==========================================================================

void A_SpawnFly()
{
	Actor newmobj;
	Actor fog;
	int r;
	classid type;

	if (--ReactionCount)
		return;	// still flying

	// First spawn teleport fog.
	fog = Spawn(SpawnFire, Enemy.Origin);
	fog.PlaySound('brain/spawn', CHAN_VOICE);

	// Randomly select monster to spawn.
	r = P_Random();

	// Probability distribution (kind of :),
	// decreasing likelihood.
	if (r < 50)
		type = Imp;
	else if (r < 90)
		type = Demon;
	else if (r < 120)
		type = Shadows;
	else if (r < 130)
		type = PainElemental;
	else if (r < 160)
		type = Cacodemon;
	else if (r < 162)
		type = ArchVile;
	else if (r < 172)
		type = Revenant;
	else if (r < 192)
		type = Arachnotron;
	else if (r < 222)
		type = Mancubus;
	else if (r < 246)
		type = HellKnight;
	else
		type = BaronOfHell;

	newmobj = Spawn(type, Enemy.Origin);
	if (newmobj.LookForPlayers(true))
		newmobj.SetState(newmobj.SeeState);

	// telefrag anything in this spot
	newmobj.bTelestomp = true;
	newmobj.TeleportMove(newmobj.Origin);

	// remove self (i.e., cube).
	Remove();
}

//==========================================================================
//
//  A_SpawnSound
//
//  Travelling cube sound.
//
//==========================================================================

void A_SpawnSound()
{
	PlaySound('brain/cube', CHAN_VOICE);
	A_SpawnFly();
}

//==========================================================================
//
//  A_KeenDie
//
//  DOOM II special, map 32. Uses special tag 666.
//
//==========================================================================

void A_KeenDie()
{
	A_Fall();

	// scan the remaining thinkers to see if all Keens are dead
	if (FindOther())
	{
		return;
	}
	DoomLevelInfo(Level).EV_DoDoor(666, 16, 150, 0, 0, VerticalDoor::DOOREV_Open, NULL, none);
}

//==========================================================================
//
//  A_BossDeath
//
//  Possibly trigger special effects if on first boss level
//
//==========================================================================

void A_BossDeath()
{
	string mapname;
	int i;
	Actor Other;
	int args[5];

	mapname = ARR2STR(Level.Game.level->mapname);

	if (!strcmp(mapname, "E1M8"))
	{
		if (Class != BaronOfHell)
			return;
	}
	else if (!strcmp(mapname, "E2M8"))
	{
		if (Class != Cyberdemon)
			return;
	}
	else if (!strcmp(mapname, "E3M8"))
	{
		if (Class != SpiderMastermind)
			return;
	}
	else if (!strcmp(mapname, "E4M6"))
	{
		if (Class != Cyberdemon)
			return;
	}
	else if (!strcmp(mapname, "E4M8"))
	{
		if (Class != SpiderMastermind)
			return;
	}
	else if (!strcmp(mapname, "MAP07"))
	{
		if ((Class != Mancubus) && (Class != Arachnotron))
			return;
	}
	else
	{
		//  Not a boss level
		return;
	}

	// make sure there is a player alive for victory
	for (i = 0; i < MAXPLAYERS; i++)
		if (Level.Game.Players[i] && Level.Game.Players[i].bSpawned && Level.Game.Players[i].Health > 0)
			break;

	if (i == MAXPLAYERS)
		return;	// no one left alive, so do not end game

	// scan the remaining thinkers to see if all bosses are dead
	FOREACH(Actor, Other)
	{
		if (Other != self && Other.Class == Class && Other.Health > 0)
		{
			// other boss not dead
			return;
		}
	}

	// victory!
	if (!strcmp(mapname, "E1M8"))
	{
		DoomLevelInfo(Level).EV_DoFloor(666, 8, 0, 0, 0, FloorMover::FLOOREV_LowerToLowest, NULL);
		return;
	}
	if (!strcmp(mapname, "E4M6"))
	{
		DoomLevelInfo(Level).EV_DoDoor(666, 64, 150, 0, 0, VerticalDoor::DOOREV_Open, NULL, none);
		return;
	}
	if (!strcmp(mapname, "E4M8"))
	{
		DoomLevelInfo(Level).EV_DoFloor(666, 8, 0, 0, 0, FloorMover::FLOOREV_LowerToLowest, NULL);
		return;
	}
	if (!strcmp(mapname, "MAP07"))
	{
		if (Class == Mancubus)
		{
			DoomLevelInfo(Level).EV_DoFloor(666, 8, 0, 0, 0, FloorMover::FLOOREV_LowerToLowest, NULL);
		}
		else	// mo->Class == MT_BABY
		{
			DoomLevelInfo(Level).EV_DoFloor(667, 8, 0, 0, 0, FloorMover::FLOOREV_RaiseByTexture, NULL);
		}
		return;
	}

	G_ExitLevel(0);
}

//==========================================================================
//
//	OnMapSpawn
//
//==========================================================================

void OnMapSpawn(mthing_t *mthing)
{
	::OnMapSpawn(mthing);
	if (Class == LostSoul)
	{
		bNoGravKill = true;
		bTriggerHappy = true;
	}
	if (Class == BaronOfHell || Class == HellKnight)
	{
		ImuneMissileType = BruiserShot;
	}
	if (Class == Cyberdemon)
	{
		MissileChance = 160.0;
		bTriggerHappy = true;
		bBoss = true;
	}
	if (Class == SpiderMastermind)
 	{
		bTriggerHappy = true;
		bBoss = true;
	}
	if (Class == Revenant)
	{
		bTriggerHappy = true;
		MissileMinRange = 196.0;
	}
	if (Class == ArchVile)
	{
		bNoGrudge = true;
		bNeverTarget = true;
		MissileMaxRange = 14.0 * 64.0;
	}

	if (Class == ItemInvulnerability || Class == ItemInvisibility)
	{
		bNoRespawn = true;
	}
}

defaultproperties
{
	Health = 0;
	GibsHealth = 0;
	Mass = 0.0;
	GOTARMOR = "Picked up the armor.";
	GOTMEGA = "Picked up the MegaArmor!";
	GOTHTHBONUS = "Picked up a health bonus.";
	GOTARMBONUS = "Picked up an armor bonus.";
	GOTSTIM = "Picked up a stimpack.";
	GOTMEDINEED = "Picked up a medikit that you REALLY need!";
	GOTMEDIKIT = "Picked up a medikit.";
	GOTSUPER = "Supercharge!";
	GOTBLUECARD = "Picked up a blue keycard.";
	GOTYELWCARD = "Picked up a yellow keycard.";
	GOTREDCARD = "Picked up a red keycard.";
	GOTBLUESKUL = "Picked up a blue skull key.";
	GOTYELWSKUL = "Picked up a yellow skull key.";
	GOTREDSKULL = "Picked up a red skull key.";
	GOTINVUL = "Invulnerability!";
	GOTBERSERK = "Berserk!";
	GOTINVIS = "Partial Invisibility";
	GOTSUIT = "Radiation Shielding Suit";
	GOTMAP = "Computer Area Map";
	GOTVISOR = "Light Amplification Visor";
	GOTMSPHERE = "MegaSphere!";
	GOTCLIP = "Picked up a clip.";
	GOTCLIPBOX = "Picked up a box of bullets.";
	GOTROCKET = "Picked up a rocket.";
	GOTROCKBOX = "Picked up a box of rockets.";
	GOTCELL = "Picked up an energy cell.";
	GOTCELLBOX = "Picked up an energy cell pack.";
	GOTSHELLS = "Picked up 4 shotgun shells.";
	GOTSHELLBOX = "Picked up a box of shotgun shells.";
	GOTBACKPACK = "Picked up a backpack full of ammo!";
	GOTBFG9000 = "You got the BFG9000!  Oh, yes.";
	GOTCHAINGUN = "You got the chaingun!";
	GOTCHAINSAW = "A chainsaw!  Find some meat!";
	GOTLAUNCHER = "You got the rocket launcher!";
	GOTPLASMA = "You got the plasma gun!";
	GOTSHOTGUN = "You got the shotgun!";
	GOTSHOTGUN2 = "You got the super shotgun!";
}

//**************************************************************************
//
//  $Log$
//  Revision 1.4  2006/02/19 14:19:33  dj_jl
//  Some changes because of compiler changes.
//
//  Revision 1.3  2006/02/17 19:06:51  dj_jl
//  Moved common defs to classes.
//
//  Revision 1.2  2006/02/15 23:29:43  dj_jl
//  Moved all server progs global variables to classes.
//
//  Revision 1.1  2006/02/13 18:30:16  dj_jl
//  Moved all server progs global functions to classes.
//
//  Revision 1.12  2005/12/11 21:31:40  dj_jl
//  Made path traversal callbacks class members.
//
//  Revision 1.11  2005/11/17 23:05:22  dj_jl
//  Renamed sounds.
//
//  Revision 1.10  2005/04/04 07:40:01  dj_jl
//  Fixed dehacked support.
//
//  Revision 1.9  2004/12/27 12:23:11  dj_jl
//  Multiple small changes for version 1.16
//
//  Revision 1.8  2003/07/11 16:47:39  dj_jl
//  Made array of players with pointers
//
//  Revision 1.7  2002/09/07 16:28:24  dj_jl
//  Added Level class.
//
//  Revision 1.6  2002/08/08 18:04:40  dj_jl
//  Fixes for renamed functions.
//
//  Revision 1.5  2002/07/13 08:01:48  dj_jl
//  Moved some functions to the Entity class.
//
//  Revision 1.4  2002/06/22 07:22:16  dj_jl
//  Added default values to Actor.
//
//  Revision 1.3  2002/06/14 15:53:12  dj_jl
//  Fixed DeHackEd support.
//
//  Revision 1.2  2002/05/18 16:59:58  dj_jl
//  Renamed SpawnState to IdleState, other changes.
//
//  Revision 1.1  2002/05/03 17:11:53  dj_jl
//  Object classes split up into their files.
//
//**************************************************************************
