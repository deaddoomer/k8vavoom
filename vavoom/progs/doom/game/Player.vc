//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Player : PlayerEx;

const float VIEWHEIGHT		= 41.0;

const float INVULNTIME		= 30.0;
const float INVISTIME		= 60.0;
const float INFRATIME		= 120.0;
const float IRONTIME		= 60.0;

// 16 pixels of bob
const float MAXBOB			= 16.0;

const int INVERSECOLOURMAP	= 32;

const float USERANGE		= 64.0;

const float ST_STRAIGHTFACETIME		= 0.5;
const float ST_TURNTIME				= 1.0;
const float ST_EVILGRINTIME			= 2.0;
const float ST_RAMPAGEDELAY			= 2.0;

const int ST_MUCHPAIN				= 20;

const int MAXHEALTH					= 100;

int ArmorPoints;

// Power ups. invinc and invis are tic counters.
float Powers[NUMPOWERS];

int Keys;

// Refired shots are less accurate.
int Refire;

WeaponEx ReadyWeapon;

// Is wp_nochange if not changing.
WeaponEx PendingWeapon;

// Armor type is 0-2.
int ArmorType;

//
//  FACE WIDGET DATA
//

int SBFlags;

// count until face changes
float sb_facetime;

// used for evil grin
bool sb_GotWeapon;

// used to use appopriately pained face
int sb_oldhealth;

// int sb_face_lastattackdown = -1;
int sb_face_priority;

int sb_cpo_lastcalc;
int sb_cpo_oldhealth;

float sb_face_lastattackdown;

// current face index, used by w_faces
int sb_faceindex;

BotPlayer Bot;
Inventory SavedInventory;

replication
{
	reliable if (!bIsClient)
		ArmorPoints, SBFlags, sb_faceindex, ReadyWeapon;

	unreliable if (!bIsClient)
		ClientGunShot, ClientBlood, ClientExplosion, ClientExplosion2;
}

//==========================================================================
//
//  PutClientIntoServer
//
//==========================================================================

void PutClientIntoServer()
{
	if (bIsBot)
	{
		Bot = BotPlayer(SpawnObject(BotPlayer));
		Bot.player = self;
		Bot.OnBeginPlay();
	}
}

//==========================================================================
//
//  SpawnClient
//
//==========================================================================

void SpawnClient()
{
	if (Level.Game.deathmatch)
	{
		DeathMatchSpawnPlayer();
	}
	else
	{
		SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
			GetPlayerNum()]);
	}
}

//==========================================================================
//
//  NetGameReborn
//
//	Respawn at the start
//
//==========================================================================

void NetGameReborn()
{
	int i;

	// remove pitch and roll angles from corpse
	MO.Angles.pitch = 0.0;
	MO.Angles.roll = 0.0;

	// first dissasociate the corpse
	MO.Player = none;
	MO.bIsPlayer = false;

	// spawn at random spot if in death match
	if (Level.Game.deathmatch)
	{
		DeathMatchSpawnPlayer();
		return;
	}

	if (CheckSpot(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
		GetPlayerNum()]))
	{
		SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
			GetPlayerNum()]);
		return;
	}

	// try to spawn at one of the other players spots
	for (i = 0; i < MAXPLAYERS; i++)
	{
		if (CheckSpot(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS + i]))
		{
			SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS + i]);
			return;
		}
		// he's going to be inside something.  Too bad.
	}
	SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
		GetPlayerNum()]);
}

//==========================================================================
//
//  DisconnectClient
//
//==========================================================================

void DisconnectClient()
{
	int i;

	if (Bot != none)
	{
		Bot.Destroy();
		Bot = none;
	}
	if (MO)
	{
		MO.Player = none;
		MO.bIsPlayer = false;
		Actor(MO).Damage(none, none, 10000);
	}
	if (ViewEnt)
	{
		ViewEnt.Destroy();
		ViewEnt = none;
	}
	Level.bprint("%s left the game\n", PlayerName);
	MO.PlaySound('misc/chat', CHAN_AUTO, 1.0, ATTN_NONE);
}

//==========================================================================
//
//  UserinfoChanged
//
//==========================================================================

void UserinfoChanged()
{
	if (!bSpawned)
	{
		return;
	}
	MO.Translation = Colour;
	SetClientModel();
}

//==========================================================================
//
//	SetClientModel
//
//==========================================================================

void SetClientModel()
{
	string model;

	model = Info_ValueForKey(UserInfo, "model");
	if (!model)
	{
		MO.bFixedModel = false;
		MO.ModelSkin = "";
		return;
	}

	MO.bFixedModel = true;
	MO.FixedModelName = va("players/%s/player.xml", model);
	MO.ModelSkin = Info_ValueForKey(UserInfo, "skin");
}

//==========================================================================
//
//  DeathMatchSpawnPlayer
//
//  Spawns a player at one of the random death match spots called at level
// load and each death
//
//==========================================================================

void DeathMatchSpawnPlayer()
{
	int i;
	int j;

	for (j = 0; j < 20; j++)
	{
		i = P_Random() % Level.NumDeathmatchStarts;
		if (CheckSpot(&Level.DeathmatchStarts[i]))
		{
			SpawnPlayer(&Level.DeathmatchStarts[i]);
			return;
		}
	}

	// no good spot, so the player will probably get stuck 
	SpawnPlayer(&Level.PlayerStarts[GetPlayerNum()]);
}

//==========================================================================
//
//  CheckSpot
//
//  Returns false if the player cannot be respawned at the given mthing_t
// spot because something is occupying it
//
//==========================================================================

bool CheckSpot(mthing_t * mthing)
{
	float x;
	float y;
	sector_t *sec;
	float an;
	Actor A;
	int mtnum;
	int sfxnum;
	int i;

	if (!MO)
	{
		// first spawn of level, before corpses
		for (i = 0; i < MAXPLAYERS; i++)
			if (Level.Game.Players[i] && Level.Game.Players[i].MO &&
				Level.Game.Players[i].MO.Origin.x == mthing->x &&
				Level.Game.Players[i].MO.Origin.y == mthing->y)
				return false;
		return true;
	}

	x = mthing->x;
	y = mthing->y;

	if (!MO.CheckPosition(vector(x, y, Actor::ONFLOORZ)))
	{
		return false;
	}

	// flush an old corpse if needed 
	if (DoomLevelInfo(Level).bodyqueslot >= DoomLevelInfo::BODYQUESIZE)
		DoomLevelInfo(Level).bodyque[DoomLevelInfo(Level).bodyqueslot % DoomLevelInfo::BODYQUESIZE].Destroy();
	DoomLevelInfo(Level).bodyque[DoomLevelInfo(Level).bodyqueslot % DoomLevelInfo::BODYQUESIZE] = Actor(MO);
	DoomLevelInfo(Level).bodyqueslot++;

	// spawn a teleport fog 
	sec = Level.XLevel.PointInSector(vector(x, y, 0.0));
	an = itof(45 * (mthing->angle / 45));

	A = DoomLevelInfo(Level).Spawn(TeleportFog, vector(x + 20.0 * cos(an), y + 20.0 * sin(an),
		sec->floorheight));
	A.PlaySound('misc/teleport', CHAN_VOICE);

	return true;
}

//==========================================================================
//
//  SpawnPlayer
//
//  Called when a player is spawned on the level. Most of the player
// structure stays unchanged between levels.
//
//==========================================================================

void SpawnPlayer(mthing_t * mthing)
{
	int i;
	float x;
	float y;
	float z;
	DoomPlayer PP;
	bool ResetInventory = false;
	Inventory Item;

	if (PlayerState == PST_REBORN)
	{
		PlayerReborn();
		ResetInventory = true;
	}

	x = mthing->x;
	y = mthing->y;
	z = Actor::ONFLOORZ;
	PP = DoomLevelInfo(Level).Spawn(DoomPlayer, vector(x, y, z));
	PP.Origin.z += mthing->height;
	PP.LinkToWorld();

	// set colour translations for player sprites
	PP.Translation = Colour;

	PP.Angles.yaw = itof(mthing->angle);
	PP.Player = self;
	PP.bIsPlayer = true;
	PP.Health = Health;

	MO = PP;
	PlayerState = PST_LIVE;
	Refire = 0;
	DamageFlash = 0.0;
	BonusFlash = 0.0;
	ExtraLight = 0;
	FixedColourmap = 0;
	LastSectorDamageTime = 0.0;
	LastHazardTime = 0.0;
	ViewHeight = VIEWHEIGHT;
	ViewAngles = PP.Angles;
	bFixAngle = true;

	sb_faceindex = 0;
	sb_oldhealth = -1;
	sb_GotWeapon = false;

	PP.Inventory = SavedInventory;
	for (Item = PP.Inventory; Item; Item = Item.Inventory)
	{
		Item.Owner = MO;
	}

	if (ResetInventory)
	{
		while (PP.Inventory)
		{
			PP.Inventory.Destroy();
		}
		for (i = 0; i < PP.InitialInventory.Num; i++)
		{
			Item = Level.Spawn(PP.InitialInventory[i].Type);
			if (PP.InitialInventory[i].Amount)
			{
				//	Temporary hack for Dehacked support.
				if (PP.InitialInventory[i].Type == Clip)
				{
					Item.Amount = MainGameInfo(Level.Game).INITIAL_AMMO;
				}
				else
				{
					Item.Amount = PP.InitialInventory[i].Amount;
				}
			}
			if (Weapon(Item))
			{
				//	For better control empty weapon's ammo.
				Weapon(Item).AmmoGive1 = 0;
				Weapon(Item).AmmoGive2 = 0;
			}
			if (!Item.TryPickup(PP))
			{
				Item.Destroy();
			}
			else if (Weapon(Item) && Weapon(Item).CheckAmmo(false))
			{
				SetWeapon(WeaponEx(Item));
			}
		}
	}

	// setup gun psprite
	SetupPsprites();

	// give all cards in death match mode
	if (Level.Game.deathmatch)
	{
		Keys = 0x3f;
	}

	// wake up the status bar
	SB_Start();

	if (bIsBot)
	{
		Bot.OnSpawn();
	}

	SetClientModel();
}

//==========================================================================
//
//  PlayerReborn
//
//  Called after a player dies almost everything is cleared and initialised
//
//==========================================================================

void PlayerReborn()
{
	BotPlayer b;

	//  Clear player struct
	b = Bot;
	ClearPlayer();
	Bot = b;

	//  Set initial data
	bUseDown = true;	// don't do anything immediately
	bAttackDown = true;
	PlayerState = PST_LIVE;
	Health = MainGameInfo(Level.Game).INITIAL_HEALTH;
	LocalQuakeHappening = 0;
}

//==========================================================================
//
//  PlayerExitMap
//
//  Call when a player completes a level.
//
//==========================================================================

void PlayerExitMap(bool clusterChange)
{
	int i;

	if (clusterChange)
	{
		Keys = 0;
	}

	for (i = 0; i < NUMPOWERS; i++)
	{
		Powers[i] = 0.0;
	}
	Items = 0;
	MO.Angles.pitch = 0.0;
	MO.Alpha = 1.0;	// cancel invisibility
	ExtraLight = 0;			// cancel gun flashes
	FixedColourmap = 0;		// cancel ir gogles
	DamageFlash = 0.0;		// no palette changes
	BonusFlash = 0.0;
	SavedInventory = EntityEx(MO).Inventory;
}

//==========================================================================
//
//	GiveAmmo
//
//	Returns false if the ammo can't be picked up at all
//
//==========================================================================

bool GiveAmmo(class<Ammo> ammo, int num)
{
	int				oldammo;
	int				max;
	Ammo			AmmoItem;

	if (!ammo)
		return false;

	AmmoItem = Ammo(EntityEx(MO).FindInventory(ammo));
	if (!AmmoItem)
	{
		AmmoItem = Level.Spawn(ammo);
		AmmoItem.AttachToOwner(EntityEx(MO));
		AmmoItem.Amount = 0;
	}
	if (!num)
	{
		return false;
	}
	max = AmmoItem.MaxAmount;
	if (AmmoItem.Amount == max)
		return false;

	if (Level.Game.gameskill == sk_baby || Level.Game.gameskill == sk_nightmare)
	{
		// give double ammo in trainer mode,
		// you'll need in nightmare
		num <<= 1;
	}

	oldammo = AmmoItem.Amount;
	AmmoItem.Amount += num;

	if (AmmoItem.Amount > max)
		AmmoItem.Amount = max;

	// If non zero ammo, 
	// don't change up weapons,
	// player was lower on purpose.
	if (oldammo)
		return true;

	GotAmmo(AmmoItem);
	return true;
}

//==========================================================================
//
//	GotAmmo
//
//==========================================================================

void GotAmmo(Ammo NewAmmo)
{
	// We were down to zero,
	// so select a new weapon.
	// Preferences are not user selectable.
	if (ReadyWeapon.bWimpyWeapon)
	{
		WeaponEx Best = WeaponEx(BestWeapon(class<Ammo>(NewAmmo.Class)));
		if (Best && Best.SelectionOrder < ReadyWeapon.SelectionOrder)
		{
			PendingWeapon = Best;
		}
	}
}

//==========================================================================
//
//	GiveBody
//
//	Returns false if the body isn't needed at all
//
//==========================================================================

bool GiveBody(int num, int max)
{
	if (MO.Health >= max)
	{
		return false;
	}

	MO.Health += num;
	if (MO.Health > max)
	{
		MO.Health = max;
	}
	Health = MO.Health;

	return true;
}

//==========================================================================
//
//	GiveArmor
//
//	Returns false if the armor is worse than the current armor.
//
//==========================================================================

bool GiveArmor(int armortype)
{
	int hits;

	hits = armortype * 100;
	if (ArmorPoints >= hits)
		return false;	// don't pick up

	ArmorType = armortype;
	ArmorPoints = hits;

	return true;
}

//==========================================================================
//
//	GiveArmor2
//
//	Returns false if the armor is worse than the current armor.
//
//==========================================================================

bool GiveArmor2(int armortype, int num, int max)
{
	if (ArmorPoints >= max)
	{
		return false;	// don't pick up
	}
	ArmorPoints += num;
	if (ArmorPoints > max)
	{
		ArmorPoints = max;
	}
	if (ArmorType < armortype)
	{
		ArmorType = armortype;
	}
	return true;
}

//==========================================================================
//
//  GiveItem
//
//==========================================================================

bool GiveItem(int item)
{
	if (Items & item)
	{
		return false;	// already got it
	}

	Items |= item;
	return true;
}

//==========================================================================
//
//	GivePower
//
//==========================================================================

bool GivePower(int power)
{
	if (power == pw_invulnerability)
	{
		Powers[power] = INVULNTIME;
		return true;
	}

	if (power == pw_invisibility)
	{
		Powers[power] = INVISTIME;
		MO.Alpha = 0.1;
		return true;
	}

	if (power == pw_infrared)
	{
		Powers[power] = INFRATIME;
		return true;
	}

	if (power == pw_ironfeet)
	{
		Powers[power] = IRONTIME;
		return true;
	}

	if (power == pw_strength)
	{
		GiveBody(100, MAXHEALTH);
		Powers[power] = 1.0;
		if (!Fist(ReadyWeapon))
		{
			PendingWeapon = WeaponEx(EntityEx(MO).FindInventory(Fist));
		}
		return true;
	}

	if (Powers[power])
		return false;	// already got it

	Powers[power] = 1.0;
	return true;
}

//==========================================================================
//
//	GivePower2
//
//==========================================================================

bool GivePower2(int power, float duration, float max)
{
	if (Powers[power] >= max)
		return false;	// already got it

	Powers[power] += duration;
	if (Powers[power] >= max)
	{
		Powers[power] = max;
	}

	if (power == pw_invisibility)
	{
		MO.Alpha = 0.1;
	}
	if (power == pw_strength)
	{
		if (!Fist(ReadyWeapon))
		{
			PendingWeapon = WeaponEx(EntityEx(MO).FindInventory(Fist));
		}
	}

	return true;
}

//==========================================================================
//
//  ThrustPlayer
//
//  Moves the given origin along a given angle.
//
//==========================================================================

void ThrustPlayer(float angle, float move, float deltaTime)
{
	if ((MO.Sector->special & SECSPEC_BASE_MASK) == SECSPEC_FrictionLow)
	{
		move *= 0.5;
	}
	MO.Velocity.x += move * cos(angle) * deltaTime;
	MO.Velocity.y += move * sin(angle) * deltaTime;
}

//==========================================================================
//
//  CalcHeight
//
//  Calculate the walking / running height adjustment
//
//==========================================================================

void CalcHeight(float deltaTime)
{
	float angle;
	float bob;

	// Regular movement bobbing
	// (needs to be calculated for gun swing even if not on ground)
	Bob = MO.Velocity.x * MO.Velocity.x + MO.Velocity.y * MO.Velocity.y;
	Bob /= 4.0 * 35.0 * 35.0;
	if (Bob > MAXBOB)
		Bob = MAXBOB;

	angle = (180.0 * 35.0 / 10.0) * Level.XLevel.Time;
	bob = Bob / 2.0 * sin(angle);

	// move viewheight
	if (PlayerState == PST_LIVE)
	{
		ViewHeight += DeltaViewHeight * deltaTime;

		if (ViewHeight > VIEWHEIGHT)
		{
			ViewHeight = VIEWHEIGHT;
			DeltaViewHeight = 0.0;
		}

		if (ViewHeight < VIEWHEIGHT / 2.0)
		{
			ViewHeight = VIEWHEIGHT / 2.0;
			if (DeltaViewHeight <= 0.0)
				DeltaViewHeight = 0.00001;
		}

		if (DeltaViewHeight)
		{
			DeltaViewHeight += 256.0 * deltaTime;
			if (!DeltaViewHeight)
				DeltaViewHeight = 0.00001;
		}
	}
	ViewOrg.z = MO.Origin.z + ViewHeight + bob;

	if (PlayerState != PST_DEAD && MO.Origin.z <= MO.FloorZ)
	{
		ViewOrg.z -= MO.FloorClip;
	}
	if (ViewOrg.z < MO.FloorZ + 4.0)
		ViewOrg.z = MO.FloorZ + 4.0;

	if (ViewOrg.z > MO.CeilingZ - 4.0)
		ViewOrg.z = MO.CeilingZ - 4.0;
}

//==========================================================================
//
//  MovePlayer
//
//==========================================================================

void MovePlayer(float deltaTime)
{
	float forward;
	float side;
	bool onground;

	// Do not let the player control movement
	//  if not onground.
	onground = MO.Origin.z <= MO.FloorZ || Actor(MO).bOnMobj;

	forward = ForwardMove * 5.0;
	side = SideMove * 5.0;

	if (!onground)
	{
		forward *= MainGameInfo(Level.Game).air_control;
		side *= MainGameInfo(Level.Game).air_control;
	}

	if (forward)
	{
		ThrustPlayer(MO.Angles.yaw, forward, deltaTime);
	}
	if (side)
	{
		ThrustPlayer(MO.Angles.yaw - 90.0, side, deltaTime);
	}

	if (forward || side)
	{
		SetPlayerRunState();
	}

	if ((Buttons & BT_JUMP) && onground && !JumpTime)
	{
		//  Jump
		MO.Velocity.z = 9.0 * 35.0;
		Actor(MO).bOnMobj = false;
		JumpTime = 0.5;
	}
}

//==========================================================================
//
//  CheckWaterJump
//
//==========================================================================

void CheckWaterJump()
{
	TVec start;
	TVec end;
	TVec vforward;
	TVec HitPoint;
	TVec HitNormal;

	// check for a jump-out-of-water
	AngleVector(&MO.Angles, &vforward);
	start = MO.Origin;
	start.z += MO.Height * 0.5 + 8.0;
	vforward.z = 0.0;
	vforward = Normalise(vforward);
	end = start + vforward * 24.0;
	if (!Level.XLevel.TraceLine(start, end, HitPoint, HitNormal))
	{
		// solid at waist
		start.z = MO.Origin.z + MO.Height;
		end = start + vforward * 24.0;
		MoveDir = HitNormal * -50.0;
		if (Level.XLevel.TraceLine(start, end, HitPoint, HitNormal))
		{
			// open at eye level
			Actor(MO).bWaterJump = true;
			MO.Velocity.z = 320.0;
			Actor(MO).ReactionTime = 2.0;	// safety net
		}
	}
}

//==========================================================================
//
//  WaterMove
//
//==========================================================================

void WaterMove(float deltaTime)
{
	float forward;
	float side;
	TVec vforward;
	TVec vright;
	TVec vup;
	TVec wishvel;

	AngleVectors(&MO.Angles, &vforward, &vright, &vup);

	forward = ForwardMove;
	side = SideMove;

	wishvel = forward * vforward + side * vright;
	if (!forward && !side /* && !cmd.upmove */ )
		wishvel.z -= 60.0;	// drift towards bottom
//  else
//      wishvel.z += cmd.upmove;

	MO.Velocity += 3.5 * deltaTime * wishvel;

	if (forward || side)
	{
		SetPlayerRunState();
	}

	if (Buttons & BT_JUMP)
	{
		if (MO.WaterType == CONTENTS_WATER)
			MO.Velocity.z = 100.0;
		else if (MO.WaterType == CONTENTS_NUKAGE || MO.WaterType == CONTENTS_SLIME)
			MO.Velocity.z = 80.0;
		else
			MO.Velocity.z = 50.0;
	}
	CheckWaterJump();
}

//==========================================================================
//
//  WaterJump
//
//==========================================================================

void WaterJump()
{
	if (!Actor(MO).ReactionTime || !MO.WaterLevel)
	{
		Actor(MO).bWaterJump = false;
		Actor(MO).ReactionTime = 0.0;
	}
	MO.Velocity.x = MoveDir.x;
	MO.Velocity.y = MoveDir.y;
}

//==========================================================================
//
//  PlayerInSpecialSector
//
//  Called every tic frame that the player origin is in a special sector
//
//==========================================================================

void PlayerInSpecialSector(float deltaTime)
{
	float speed;
	float finean;

	//	Falling, not all the way down yet?
	if (MO.Origin.z != GetPlanePointZ(&MO.Sector->floor, MO.Origin))
		return;

	if (MO.Sector->special & SECSPEC_SECRET_MASK)
	{
		//	Secret area.
		SecretCount++;
		Level.CurrentSecret++;
		MO.Sector->special &= ~SECSPEC_SECRET_MASK;
		centreprint("You found a secret area");
	}

	// Has hitten ground.
	if (MO.Sector->special >= SECSPEC_LightFlicker &&
		MO.Sector->special <= 255)
	{
		switch (MO.Sector->special)
		{
		case SECSPEC_DamageHellslime:
			if (!Powers[pw_ironfeet] &&
				Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 10);
			}
			break;
		case SECSPEC_DamageSludge:
			if (!Powers[pw_ironfeet] &&
				Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 4);
			}
			break;
		case SECSPEC_DamageNukage:
			if (!Powers[pw_ironfeet] &&
				Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 5);
			}
			break;
		case SECSPEC_LightStrobeFastDamage:
		case SECSPEC_DamageSuperHellslime:
			if ((!Powers[pw_ironfeet] || (P_Random() < 5)) &&
				Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 20);
			}
			break;
		case SECSPEC_DamageSuperHellslimeExit:
			// EXIT SUPER DAMAGE! (for E1M8 finale)
			Cheats &= ~CF_GODMODE;
	
			if (Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 20);
			}
	
			if (Health <= 10)
			{
				Level.ExitLevel(0);
			}
			break;
		case SECSPEC_DamageLavaWimpy:
			if (Level.XLevel.Time - LastSectorDamageTime >= 16.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 5, 'Fire');
				Actor(MO).HitFloorType();
			}
			break;
		case SECSPEC_DamageLavaHefty:
			if (Level.XLevel.Time - LastSectorDamageTime >= 16.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 8, 'Fire');
				Actor(MO).HitFloorType();
			}
			break;
		case SECSPEC_ScrollEastLavaDamage:
			ThrustPlayer(0.0, 1024.0, deltaTime);
			if (Level.XLevel.Time - LastSectorDamageTime >= 16.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 5, 'Fire');
				Actor(MO).HitFloorType();
			}
			break;
		case SECSPEC_DamageHazard:
			if (!Powers[pw_ironfeet])
				HazardTime += 2.0 * deltaTime;
			break;
		case SECSPEC_DamageInstantDeath:
			Actor(MO).Damage(none, none, 999);
			break;
		case SECSPEC_DamageSuperHazard:
			if (!Powers[pw_ironfeet])
				HazardTime += 4.0 * deltaTime;
			break;
		}
	}
	else
	{
		//	Extended sector damage type.
		switch (MO.Sector->special & SECSPEC_DAMAGE_MASK)
		{
		case 0x0100:
			if (!Powers[pw_ironfeet] &&
				Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 5);
			}
			break;
		case 0x0200:
			if (!Powers[pw_ironfeet] &&
				Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 10);
			}
			break;
		case 0x0300:
			if ((!Powers[pw_ironfeet] || (P_Random() < 5)) &&
				Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, 20);
			}
			break;
		}
	}

	//	Apply any customizable damage
	if (MO.Sector->Damage)
	{
		if (MO.Sector->Damage < 20)
		{
			if (!Powers[pw_ironfeet] &&
				Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, MO.Sector->Damage);
			}
		}
		else if (MO.Sector->Damage < 50)
		{
			if ((!Powers[pw_ironfeet] || (P_Random() < 5)) &&
				Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.XLevel.Time;
				Actor(MO).Damage(none, none, MO.Sector->Damage);
			}
		}
		else if (Level.XLevel.Time - LastSectorDamageTime >= 1.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, MO.Sector->Damage);
		}
	}

	switch (MO.Sector->special & 255)
	{
	case SECSPEC_ScrollCurrent:
		speed = itof((MO.Sector->tag - 100) % 10) / 16.0 * 35.0;
		finean = itof((MO.Sector->tag - 100) / 10) * 45.0;
		MO.Velocity.x += speed * cos(finean);
		MO.Velocity.y += speed * sin(finean);
		break;
	case SECSPEC_ScrollNorthSlow:
	case SECSPEC_ScrollNorthMedium:
	case SECSPEC_ScrollNorthFast:
		ThrustPlayer(90.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorthSlow], deltaTime);
		break;
	case SECSPEC_ScrollEastSlow:
	case SECSPEC_ScrollEastMedium:
	case SECSPEC_ScrollEastFast:
		ThrustPlayer(0.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollEastSlow], deltaTime);
		break;
	case SECSPEC_ScrollSouthSlow:
	case SECSPEC_ScrollSouthMedium:
	case SECSPEC_ScrollSouthFast:
		ThrustPlayer(270.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouthSlow], deltaTime);
		break;
	case SECSPEC_ScrollWestSlow:
	case SECSPEC_ScrollWestMedium:
	case SECSPEC_ScrollWestFast:
		ThrustPlayer(180.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollWestSlow], deltaTime);
		break;
	case SECSPEC_ScrollNorthWestSlow:
	case SECSPEC_ScrollNorthWestMedium:
	case SECSPEC_ScrollNorthWestFast:
		ThrustPlayer(135.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorthWestSlow], deltaTime);
		break;
	case SECSPEC_ScrollNorthEastSlow:
	case SECSPEC_ScrollNorthEastMedium:
	case SECSPEC_ScrollNorthEastFast:
		ThrustPlayer(45.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorthEastSlow], deltaTime);
		break;
	case SECSPEC_ScrollSouthEastSlow:
	case SECSPEC_ScrollSouthEastMedium:
	case SECSPEC_ScrollSouthEastFast:
		ThrustPlayer(315.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouthEastSlow], deltaTime);
		break;
	case SECSPEC_ScrollSouthWestSlow:
	case SECSPEC_ScrollSouthWestMedium:
	case SECSPEC_ScrollSouthWestFast:
		ThrustPlayer(225.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouthWestSlow], deltaTime);
		break;
	case SECSPEC_ScrollEast5:
	case SECSPEC_ScrollEast10:
	case SECSPEC_ScrollEast25:
	case SECSPEC_ScrollEast30:
	case SECSPEC_ScrollEast35:
		ThrustPlayer(0.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollEast5], deltaTime);
		break;
	case SECSPEC_ScrollNorth5:
	case SECSPEC_ScrollNorth10:
	case SECSPEC_ScrollNorth25:
	case SECSPEC_ScrollNorth30:
	case SECSPEC_ScrollNorth35:
		ThrustPlayer(90.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorth5], deltaTime);
		break;
	case SECSPEC_ScrollSouth5:
	case SECSPEC_ScrollSouth10:
	case SECSPEC_ScrollSouth25:
	case SECSPEC_ScrollSouth30:
	case SECSPEC_ScrollSouth35:
		ThrustPlayer(270.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouth5], deltaTime);
		break;
	case SECSPEC_ScrollWest5:
	case SECSPEC_ScrollWest10:
	case SECSPEC_ScrollWest25:
	case SECSPEC_ScrollWest30:
	case SECSPEC_ScrollWest35:
		ThrustPlayer(180.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollWest5], deltaTime);
		break;
	};
}

//==========================================================================
//
//  PlayerInContents
//
//==========================================================================

void PlayerInContents(float deltaTime)
{
	if (!MO.WaterLevel)
	{
		return;
	}
	switch (MO.WaterType)
	{
	case CONTENTS_LAVA:
		if (!Powers[pw_ironfeet] &&
			Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 10, 'Fire');
		}
		break;

	case CONTENTS_NUKAGE:
		if (!Powers[pw_ironfeet] &&
			Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 5);
		}
		break;

	case CONTENTS_SLIME:
		if (!Powers[pw_ironfeet] &&
			Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 10);
		}
		break;

	case CONTENTS_HELLSLIME:
		if ((!Powers[pw_ironfeet] || (P_Random() < 5)) &&
			Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 20);
		}
		break;

	case CONTENTS_SLUDGE:
		if (!Powers[pw_ironfeet] &&
			Level.XLevel.Time - LastSectorDamageTime >= 32.0 / 35.0)
		{
			LastSectorDamageTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 4);
		}
		break;

	case CONTENTS_HAZARD:
		HazardTime += 2.0 * deltaTime;
		break;
	}
}

//==========================================================================
//
//  DeathPlayerTick
//
//  Fall on your face when dying. Decrease POV height to floor height.
//
//==========================================================================

void DeathPlayerTick(float deltaTime)
{
	float angle;
	float delta;

	MovePsprites(deltaTime);

	if (MO.WaterLevel > 1)
	{
		MO.Velocity.z = -60.0;	// drift towards bottom
	}

	// fall to the ground
	if (ViewHeight > 6.0)
		ViewHeight -= 1.0;

	if (ViewHeight < 6.0)
		ViewHeight = 6.0;

	DeltaViewHeight = 0.0;
	CalcHeight(deltaTime);

	if (Attacker && Attacker != MO)
	{
		angle = atan2(Attacker.Origin.y - MO.Origin.y,
			Attacker.Origin.x - MO.Origin.x);

		delta = AngleMod180(angle - ViewAngles.yaw);

		if (delta < 5.0 || delta > -5.0)
		{
			// Looking at killer,
			//  so fade damage flash down.
			ViewAngles.yaw = angle;

			if (DamageFlash)
			{
				DamageFlash -= deltaTime;
				if (DamageFlash <= 0.0)
					DamageFlash = 0.0;
			}
		}
		else if (delta > 0.0)
			ViewAngles.yaw += 5.0;
		else
			ViewAngles.yaw -= 5.0;
	}
	else if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (Buttons & BT_USE)
		PlayerState = PST_REBORN;
}

//==========================================================================
//
//  PrevWeapon
//
//==========================================================================

void PrevWeapon()
{
	WeaponEx Best = WeaponEx(GetPrevWeapon(ReadyWeapon, true));
	if (Best && Best != ReadyWeapon)
	{
		PendingWeapon = Best;
	}
}

//==========================================================================
//
//  NextWeapon
//
//==========================================================================

void NextWeapon()
{
	WeaponEx Best = WeaponEx(GetNextWeapon(ReadyWeapon, true));
	if (Best && Best != ReadyWeapon)
	{
		PendingWeapon = Best;
	}
}

//==========================================================================
//
//  PlayerImpulse
//
//==========================================================================

void PlayerImpulse()
{
	int impulse;

	impulse = Impulse;
	Impulse = 0;

	if (impulse <= 7)
	{
		ChangeWeapon(impulse - 1);
	}
	else if (impulse == 16)
	{
		//  Suicide
		Actor(MO).Damage(none, none, 10000);
	}
	else if (impulse == 17)
	{
		PrevWeapon();
	}
	else if (impulse == 18)
	{
		NextWeapon();
	}
}

//==========================================================================
//
//  PlayerTick
//
//==========================================================================

void PlayerTick(float deltaTime)
{
	if (bIsBot)
	{
		Bot.BotTick(deltaTime);
	}

	// fixme: do this in the cheat code
	if (Cheats & CF_NOCLIP)
	{
		MO.bColideWithThings = false;
		MO.bColideWithWorld = false;
	}
	else
	{
		MO.bColideWithThings = true;
		MO.bColideWithWorld = true;
	}

	if (Actor(MO).bJustAttacked)
	{
		ForwardMove = 100.0;
		SideMove = 0.0;
		Actor(MO).bJustAttacked = false;
	}

	if (PlayerState == PST_DEAD)
	{
		DeathPlayerTick(deltaTime);
		return;
	}

	// Copy view angles
	MO.Angles = ViewAngles;

	if (JumpTime)
	{
		JumpTime -= deltaTime;
		if (JumpTime < 0.0)
		{
			JumpTime = 0.0;
		}
	}

	// Move around.
	// Reactiontime is used to prevent movement
	//  for a bit after a teleport.
	if (Actor(MO).ReactionTime)
	{
		Actor(MO).ReactionTime -= deltaTime;
		if (Actor(MO).ReactionTime <= 0.0)
			Actor(MO).ReactionTime = 0.0;
		if (Actor(MO).bWaterJump)
			WaterJump();
	}
	else
	{
		if (MO.WaterLevel > 1)
			WaterMove(deltaTime);
		else
			MovePlayer(deltaTime);
	}

	CalcHeight(deltaTime);

	if (MO.Sector->special)
		PlayerInSpecialSector(deltaTime);
	PlayerInContents(deltaTime);

	// Check for weapon change.
	if (Impulse)
	{
		PlayerImpulse();
	}

	// check for use
	if (Buttons & BT_USE)
	{
		if (!bUseDown)
		{
			P_UseLines();
			bUseDown = true;
		}
	}
	else
	{
		bUseDown = false;
	}

	//  cycle psprites
	MovePsprites(deltaTime);

	//  Counters, time dependend power ups.

	//  Strength counts up to diminish fade.
	if (Powers[pw_strength])
		Powers[pw_strength] += deltaTime;

	if (Powers[pw_invulnerability])
	{
		Powers[pw_invulnerability] -= deltaTime;
		if (Powers[pw_invulnerability] <= 0.0)
			Powers[pw_invulnerability] = 0.0;
	}

	if (Powers[pw_invisibility])
	{
		Powers[pw_invisibility] -= deltaTime;
		if (Powers[pw_invisibility] <= 0.0)
		{
			Powers[pw_invisibility] = 0.0;
			MO.Alpha = 1.0;
		}
	}

	if (Powers[pw_infrared])
	{
		Powers[pw_infrared] -= deltaTime;
		if (Powers[pw_infrared] <= 0.0)
			Powers[pw_infrared] = 0.0;
	}

	if (Powers[pw_ironfeet])
	{
		Powers[pw_ironfeet] -= deltaTime;
		if (Powers[pw_ironfeet] <= 0.0)
			Powers[pw_ironfeet] = 0.0;
	}

	if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (BonusFlash)
	{
		BonusFlash -= deltaTime;
		if (BonusFlash <= 0.0)
			BonusFlash = 0.0;
	}

	if (HazardTime)
	{
		HazardTime -= deltaTime;
		if (HazardTime <= 0.0)
			HazardTime = 0.0;
		if (Level.XLevel.Time - LastHazardTime >= 32.0 / 35.0 && HazardTime > 16.0)
		{
			LastHazardTime = Level.XLevel.Time;
			Actor(MO).Damage(none, none, 5);
		}
	}

	// Handling colourmaps.
	if (Powers[pw_invulnerability])
	{
		if (Powers[pw_invulnerability] > 4.0 ||
			(ftoi(4.0 * Powers[pw_invulnerability]) & 1))
			FixedColourmap = INVERSECOLOURMAP;
		else
			FixedColourmap = 0;
	}
	else if (Powers[pw_infrared])
	{
		if (Powers[pw_infrared] > 4.0 ||
			(ftoi(4.0 * Powers[pw_infrared]) & 1))
		{
			// almost full bright
			FixedColourmap = 1;
		}
		else
			FixedColourmap = 0;
	}
	else
		FixedColourmap = 0;
}

//==========================================================================
//
//  PaletteFlash
//
//  Do red-/gold-shifts from damage/items
//
//==========================================================================

void PaletteFlash()
{
	CShifts[CSHIFT_Contents] = 0;
	if (MO.WaterLevel == 3)
	{
		switch (MO.WaterType)
		{
		case CONTENTS_WATER:
			CShifts[CSHIFT_Contents] = RGBA(130, 80, 50, 128);
			break;

		case CONTENTS_LAVA:
			CShifts[CSHIFT_Contents] = RGBA(255, 80, 0, 150);
			break;

		case CONTENTS_NUKAGE:
			CShifts[CSHIFT_Contents] = RGBA(50, 255, 50, 150);
			break;

		case CONTENTS_SLIME:
			CShifts[CSHIFT_Contents] = RGBA(0, 25, 5, 150);
			break;

		case CONTENTS_HELLSLIME:
			CShifts[CSHIFT_Contents] = RGBA(255, 80, 0, 150);
			break;

		case CONTENTS_BLOOD:
			CShifts[CSHIFT_Contents] = RGBA(160, 16, 16, 150);
			break;

		case CONTENTS_SLUDGE:
			CShifts[CSHIFT_Contents] = RGBA(128, 160, 128, 150);
			break;

		case CONTENTS_HAZARD:
			CShifts[CSHIFT_Contents] = RGBA(128, 160, 128, 128);
			break;
		}
	}

	CShifts[CSHIFT_Damage] = 0;
	if (DamageFlash)
	{
		int Amount = ftoi(114.0 * DamageFlash);
		if (Amount >= 228)
			Amount = 228;
		CShifts[CSHIFT_Damage] = RGBA(255, 0, 0, Amount);
	}

	CShifts[CSHIFT_Bonus] = 0;
	if (BonusFlash)
	{
		int Amount = ftoi(256.0 * BonusFlash);
		if (Amount >= 128)
			Amount = 128;
		CShifts[CSHIFT_Bonus] = RGBA(215, 186, 68, Amount);
	}

	CShifts[CSHIFT_Berserk] = 0;
	if (Powers[pw_strength])
	{
		// slowly fade the berzerk out
		int Amount = 12 - ftoi(Powers[pw_strength] / 2.0);
		if (Amount > 0)
		{
			Amount = (Amount + 7) >> 3;
			CShifts[CSHIFT_Berserk] = RGBA(255, 0, 0, (Amount + 1) * 28);
		}
	}

	CShifts[CSHIFT_Power] = 0;
	if (Powers[pw_ironfeet] > 4.0 || ftoi(4.0 * Powers[pw_ironfeet]) & 1)
	{
		CShifts[CSHIFT_Power] = RGBA(0, 255, 0, 32);
	}
}

//==========================================================================
//
//  CalcPainOffset
//
//==========================================================================

int CalcPainOffset()
{
	int health;

	health = Health > 100 ? 100 : Health;

	if (health != sb_cpo_oldhealth)
	{
		sb_cpo_lastcalc =
			DoomDefs::ST_FACESTRIDE * (((100 - health) * DoomDefs::ST_NUMPAINFACES) / 101);
		sb_cpo_oldhealth = health;
	}
	return sb_cpo_lastcalc;
}

//==========================================================================
//
//  UpdateFaceWidget
//
//  This is a not-very-pretty routine which handles the face states and
// their timing.
//  the precedence of expressions is:
//  dead > evil grin > turned head > straight ahead
//
//==========================================================================

void UpdateFaceWidget(float FrameTime)
{
	int i;
	float badguyangle;
	float diffang;
	bool doevilgrin;

	// a random number per tick
	int st_randomnumber;

	st_randomnumber = P_Random();

	if (sb_face_priority < 10)
	{
		// dead
		if (!Health)
		{
			sb_face_priority = 9;
			sb_faceindex = DoomDefs::ST_DEADFACE;
			sb_facetime = 0.01;
		}
	}

	if (sb_face_priority < 9)
	{
		if (BonusFlash)
		{
			// picking up bonus
			doevilgrin = false;

			if (sb_GotWeapon)
			{
				doevilgrin = true;
				sb_GotWeapon = false;
			}
			if (doevilgrin)
			{
				// evil grin if just picked up weapon
				sb_face_priority = 8;
				sb_facetime = ST_EVILGRINTIME;
				sb_faceindex = CalcPainOffset() + DoomDefs::ST_EVILGRINOFFSET;
			}
		}
	}

	if (sb_face_priority < 8)
	{
		if (DamageFlash && Attacker && Attacker != MO)
		{
			// being attacked
			sb_face_priority = 7;

			if (Health - sb_oldhealth > ST_MUCHPAIN)
			{
				sb_facetime = ST_TURNTIME;
				sb_faceindex = CalcPainOffset() + DoomDefs::ST_OUCHOFFSET;
			}
			else
			{
				badguyangle = atan2(Attacker.Origin.y - MO.Origin.y,
					Attacker.Origin.x - MO.Origin.x);

				if (badguyangle > MO.Angles.yaw)
				{
					// whether right or left
					diffang = AngleMod360(badguyangle - MO.Angles.yaw);
					i = diffang > 180.0;
				}
				else
				{
					// whether left or right
					diffang = AngleMod360(MO.Angles.yaw - badguyangle);
					i = diffang <= 180.0;
				}	// confusing, aint it?

				sb_facetime = ST_TURNTIME;
				sb_faceindex = CalcPainOffset();

				if (diffang < 45.0)
				{
					// head-on
					sb_faceindex += DoomDefs::ST_RAMPAGEOFFSET;
				}
				else if (i)
				{
					// turn face right
					sb_faceindex += DoomDefs::ST_TURNOFFSET;
				}
				else
				{
					// turn face left
					sb_faceindex += DoomDefs::ST_TURNOFFSET + 1;
				}
			}
		}
	}

	if (sb_face_priority < 7)
	{
		// getting hurt because of your own damn stupidity
		if (DamageFlash)
		{
			if (Health - sb_oldhealth > ST_MUCHPAIN)
			{
				sb_face_priority = 7;
				sb_facetime = ST_TURNTIME;
				sb_faceindex = CalcPainOffset() + DoomDefs::ST_OUCHOFFSET;
			}
			else
			{
				sb_face_priority = 6;
				sb_facetime = ST_TURNTIME;
				sb_faceindex = CalcPainOffset() + DoomDefs::ST_RAMPAGEOFFSET;
			}
		}
	}

	if (sb_face_priority < 6)
	{
		// rapid firing
		if (bAttackDown)
		{
			if (sb_face_lastattackdown == -1.0)
				sb_face_lastattackdown = ST_RAMPAGEDELAY;
			else
			{
				sb_face_lastattackdown -= FrameTime;
				if (sb_face_lastattackdown <= 0.0)
				{
					sb_face_priority = 5;
					sb_faceindex = CalcPainOffset() + DoomDefs::ST_RAMPAGEOFFSET;
					sb_facetime = 0.01;
					sb_face_lastattackdown = 0.01;
				}
			}
		}
		else
			sb_face_lastattackdown = -1.0;
	}

	if (sb_face_priority < 5)
	{
		// invulnerability
		if ((Cheats & CF_GODMODE) || Powers[pw_invulnerability])
		{
			sb_face_priority = 4;

			sb_faceindex = DoomDefs::ST_GODFACE;
			sb_facetime = 0.01;
		}
	}

	// look left or look right if the facecount has timed out
	if (sb_facetime <= 0.0)
	{
		sb_faceindex = CalcPainOffset() + (st_randomnumber % 3);
		sb_facetime = ST_STRAIGHTFACETIME;
		sb_face_priority = 0;
	}

	sb_facetime -= FrameTime;
	sb_oldhealth = Health;
}

//==========================================================================
//
//  SetViewPos
//
//==========================================================================

void SetViewPos()
{
	ViewOrg.x = MO.Origin.x;
	ViewOrg.y = MO.Origin.y;

	if (LocalQuakeHappening)
	{
		float intensity;

		intensity = itof(LocalQuakeHappening);
		ViewOrg.x += (Random() - 0.5) * (intensity * 4.0);
		ViewOrg.y += (Random() - 0.5) * (intensity * 4.0);
	}

	if (PlayerState != PST_DEAD)
	{
		ViewAngles = MO.Angles;
	}

	PaletteFlash();

	UpdateFaceWidget(Level.Game.frametime);

	SBFlags = Keys;
	if ((Cheats & CF_GODMODE) || Powers[pw_invulnerability])
		SBFlags |= DoomDefs::SBF_GOD_MODE;

	ClientSetViewOrg(ViewOrg.x, ViewOrg.y, ViewOrg.z);
}

//==========================================================================
//
//  SetPlayerRunState
//
//==========================================================================

void SetPlayerRunState()
{
	if (MO.State == Actor(MO).IdleState)
	{
		MO.SetState(Actor(MO).SeeState);
	}
}

//***************************************************************************
//
//  WEAPON UTILITES
//
//***************************************************************************

//==========================================================================
//
//	SetWeapon
//
//==========================================================================

void SetWeapon(WeaponEx NewWeapon)
{
	ReadyWeapon = NewWeapon;
	PendingWeapon = none;
	ViewEnt = ReadyWeapon;
	MO.ModelVersion = ReadyWeapon.PlayerModelVersion;
}

//===========================================================================
//
//  BringUpWeapon
//
//  Starts bringing the pending weapon up from the bottom of the screen.
// Uses player
//
//===========================================================================

void BringUpWeapon()
{
	if (ReadyWeapon.WeaponType == DoomDefs::wp_chainsaw)
		MO.PlaySound('weapons/sawup', CHAN_WEAPON);

	PendingWeapon = none;
	ViewStates[ps_weapon].SY = WeaponEx::WEAPONBOTTOM;

	SetViewState(ps_weapon, WeaponEx(ViewEnt).UpState);
	MO.ModelVersion = WeaponEx(ViewEnt).PlayerModelVersion;
}

//==========================================================================
//
//  ChangeWeapon
//
//  The actual changing of the weapon is done when the weapon psprite can
// do it (read: not in the middle of an attack).
//
//==========================================================================

void ChangeWeapon(int newweapon)
{
	WeaponEx NewWpn = WeaponEx(GetSlotChangeWeapon(newweapon + 1, ReadyWeapon, false));
	if (NewWpn && NewWpn != ReadyWeapon)
	{
		PendingWeapon = NewWpn;
	}
}

//===========================================================================
//
//  DropWeapon
//
//  Player died, so put the weapon away.
//
//===========================================================================

void DropWeapon()
{
	SetViewState(ps_weapon, WeaponEx(ViewEnt).DownState);
}

//===========================================================================
//
//  SetupPsprites
//
//  Called at start of level for each player.
//
//===========================================================================

void SetupPsprites()
{
	int i;

	// remove all psprites
	for (i = 0; i < NUMPSPRITES; i++)
	{
		SetViewState(i, none);
	}

	// spawn the gun
	BringUpWeapon();
}

//==========================================================================
//
//  MovePsprites
//
//  Called every tic by player thinking routine.
//
//==========================================================================

void MovePsprites(float deltaTime)
{
	AdvanceViewStates(deltaTime);
	ViewStates[ps_flash].SX = ViewStates[ps_weapon].SX;
	ViewStates[ps_flash].SY = ViewStates[ps_weapon].SY;
}

//===========================================================================
//
//  FireWeapon
//
//===========================================================================

void FireWeapon()
{
	if (!ReadyWeapon.CheckAmmo(true))
	{
		return;
	}

	MO.SetState(Actor(MO).MeleeState);
	SetViewState(ps_weapon, WeaponEx(ViewEnt).AttackState);
	DoomLevelInfo(Level).NoiseAlert(Actor(MO), Actor(MO));
}

//==========================================================================
//
//  P_UseLines
//
//  Looks for special lines in front of the player to activate.
//
//==========================================================================

void P_UseLines()
{
	float x1;
	float y1;
	float x2;
	float y2;
	TVec useDir;
	intercept_t* in;

	AngleVector(&MO.Angles, &useDir);
	x1 = MO.Origin.x;
	y1 = MO.Origin.y;
	x2 = x1 + USERANGE * useDir.x;
	y2 = y1 + USERANGE * useDir.y;
	foreach MO.PathTraverse(in, x1, y1, x2, y2, PT_ADDLINES)
	{
		if (!in->line->special || (Level.GET_SPAC(in->line->flags) != SPAC_USE &&
			Level.GET_SPAC(in->line->flags) != SPAC_USETHROUGH))
		{
			TVec hit_point;
			opening_t *open;

			hit_point = MO.Origin + (USERANGE * in->frac) * useDir;
			open = LineOpenings(in->line, hit_point);
			open = FindOpening(open, hit_point.z, hit_point.z + MO.Height);
			if (!open)
			{
				MO.PlaySound('*usefail', CHAN_VOICE);

				// can't use through a wall
				break;
			}
			// not a special line, but keep checking
			continue;
		}

		if (PointOnPlaneSide(MO.Origin, in->line) == 1)
		{
			//	Don't use back side unless it's a two-sided line.
			if (in->line->backsector != NULL)
				continue;
			break;
		}

		DoomLevelInfo(Level).ActivateLine(in->line, Actor(MO), 0, SPAC_USE);

		//WAS can't use more than one special line in a row
		//jff 3/21/98 NOW multiple use allowed with enabling line flag
		if (Level.GET_SPAC(in->line->flags) != SPAC_USETHROUGH)
		{
			break;
		}
	}
}

//==========================================================================
//
//  Cheat_God
//
//==========================================================================

void Cheat_God()
{
	Cheats ^= CF_GODMODE;
	if (Cheats & CF_GODMODE)
	{
		if (MO)
			MO.Health = MainGameInfo(Level.Game).GOD_HEALTH;
		Health = MainGameInfo(Level.Game).GOD_HEALTH;
		cprint("Degreelessness Mode On");
	}
	else
		cprint("Degreelessness Mode Off");
}

//==========================================================================
//
//  Cheat_NoClip
//
//==========================================================================

void Cheat_NoClip()
{
	Cheats ^= CF_NOCLIP;
	if (Cheats & CF_NOCLIP)
		cprint("No Clipping Mode ON");
	else
		cprint("No Clipping Mode OFF");
}

//==========================================================================
//
//  Cheat_Gimme
//
//==========================================================================

const int
	GIMME_BACKPACK			= 0x00000080,
	GIMME_BLUECARD			= 0x00001000,
	GIMME_BLUESKULL			= 0x00002000,
	GIMME_YELLOWCARD		= 0x00004000,
	GIMME_YELLOWSKULL		= 0x00008000,
	GIMME_REDCARD			= 0x00010000,
	GIMME_REDSKULL			= 0x00020000,
	GIMME_INVULNERABILITY	= 0x00040000,
	GIMME_STRENGTH			= 0x00080000,
	GIMME_INVISIBILITY		= 0x00100000,
	GIMME_IRONFEET			= 0x00200000,
	GIMME_ALLMAP			= 0x00400000,
	GIMME_INFRARED			= 0x00800000,
	GIMME_HEALTH			= 0x01000000,
	GIMME_HEALTH2			= 0x02000000,
	GIMME_ARMOR				= 0x04000000,
	GIMME_ARMOR2			= 0x08000000,
	GIMME_KEYS				= 0x0003f000,
	GIMME_POWERS			= 0x00fc0000,
	GIMME_ALL				= 0xffffffff;

void Cheat_Gimme()
{
	int					i;
	int					i1;
	class<Inventory>	Cls;

	i1 = 0;
	// All
	if (Cmd_CheckParm("All"))
		i1 |= GIMME_ALL;

	// Backpack
	if (Cmd_CheckParm("Backpack"))
		i1 |= GIMME_BACKPACK;

	// Keys
	if (Cmd_CheckParm("Keys"))
		i1 |= GIMME_KEYS;
	if (Cmd_CheckParm("BlueCard"))
		i1 |= GIMME_BLUECARD;
	if (Cmd_CheckParm("BlueSkull"))
		i1 |= GIMME_BLUESKULL;
	if (Cmd_CheckParm("YellowCard"))
		i1 |= GIMME_YELLOWCARD;
	if (Cmd_CheckParm("YellowSkull"))
		i1 |= GIMME_YELLOWSKULL;
	if (Cmd_CheckParm("RedCard"))
		i1 |= GIMME_REDCARD;
	if (Cmd_CheckParm("RedSkull"))
		i1 |= GIMME_REDSKULL;

	// Powers
	if (Cmd_CheckParm("Powers"))
		i1 |= GIMME_POWERS;
	if (Cmd_CheckParm("Invulnerability"))
		i1 |= GIMME_INVULNERABILITY;
	if (Cmd_CheckParm("Bersek"))
		i1 |= GIMME_STRENGTH;
	if (Cmd_CheckParm("Invisibility"))
		i1 |= GIMME_INVISIBILITY;
	if (Cmd_CheckParm("Suit"))
		i1 |= GIMME_IRONFEET;
	if (Cmd_CheckParm("AllMap"))
		i1 |= GIMME_ALLMAP;
	if (Cmd_CheckParm("LiteAmp"))
		i1 |= GIMME_INFRARED;

	// Health
	if (Cmd_CheckParm("Health"))
		i1 |= GIMME_HEALTH;
	if (Cmd_CheckParm("Health2"))
		i1 |= GIMME_HEALTH2;

	// Armor
	if (Cmd_CheckParm("Armor"))
		i1 |= GIMME_ARMOR;
	if (Cmd_CheckParm("Armor2"))
		i1 |= GIMME_ARMOR2;

	// Weapons
	if (Cmd_CheckParm("Weapons"))
	{
		for (i = 0; i < DoomDefs::NUMWEAPONS; i++)
		{
			WeaponEx Wpn = Level.Spawn(MainGameInfo(Level.Game).WeaponClasses[i]);
			Wpn.AmmoGive1 = 0;
			Wpn.AmmoGive2 = 0;
			if (!Wpn.TryPickup(EntityEx(MO)))
			{
				Wpn.Destroy();
			}
		}
	}

	// Backpack
	if (i1 & GIMME_BACKPACK)
	{
		Backpack Pack = Level.Spawn(Backpack);
		Pack.bDropped = true;
		if (!Pack.TryPickup(EntityEx(MO)))
		{
			Pack.Destroy();
		}
	}

	// Ammo
	if (Cmd_CheckParm("Ammo"))
	{
		foreach AllClasses(Ammo, Cls)
		{
			//	Only direct descendants.
			if (GetClassParent(Cls) != Ammo)
			{
				continue;
			}
			Inventory AmmoItem = EntityEx(MO).FindInventory(Cls);
			if (!AmmoItem)
			{
				AmmoItem = Level.Spawn(Cls);
				if (!AmmoItem.TryPickup(EntityEx(MO)))
				{
					AmmoItem.Destroy();
					AmmoItem = none;
				}
			}
			if (AmmoItem)
			{
				AmmoItem.Amount = AmmoItem.MaxAmount;
			}
		}
	}

	// Keys
	if (i1 & GIMME_BLUECARD)
		Keys |= 1 << it_bluecard;
	if (i1 & GIMME_YELLOWCARD)
		Keys |= 1 << it_yellowcard;
	if (i1 & GIMME_REDCARD)
		Keys |= 1 << it_redcard;
	if (i1 & GIMME_BLUESKULL)
		Keys |= 1 << it_blueskull;
	if (i1 & GIMME_YELLOWSKULL)
		Keys |= 1 << it_yellowskull;
	if (i1 & GIMME_REDSKULL)
		Keys |= 1 << it_redskull;

	// Powers
	if (i1 & GIMME_INVULNERABILITY)
		GivePower(pw_invulnerability);
	if (i1 & GIMME_STRENGTH)
		GivePower(pw_strength);
	if (i1 & GIMME_INVISIBILITY)
		GivePower(pw_invisibility);
	if (i1 & GIMME_IRONFEET)
		GivePower(pw_ironfeet);
	if (i1 & GIMME_INFRARED)
		GivePower(pw_infrared);

	//  Items
	if (i1 & GIMME_ALLMAP)
		GiveItem(IT_ALL_MAP);

	// Health
	if ((i1 & GIMME_HEALTH) && (Health < 100))
	{
		Health = 100;
		MO.Health = 100;
	}
	if (i1 & GIMME_HEALTH2)
	{
		Health = 200;
		MO.Health = 200;
	}

	// Armor
	if ((i1 & GIMME_ARMOR) && (ArmorPoints < 100))
	{
		ArmorType = 1;
		ArmorPoints = 100;
	}
	if (i1 & GIMME_ARMOR2)
	{
		ArmorType = 2;
		ArmorPoints = 200;
	}
	cprint("YOU GOT IT");
}

//==========================================================================
//
//  Cheat_KillAll
//
//==========================================================================

void Cheat_KillAll()
{
	cprint("%d MONSTERS KILLED", DoomLevelInfo(Level).P_Massacre());
}

//==========================================================================
//
//  ClientGunShot
//
//==========================================================================

void ClientGunShot(TVec org)
{
	int i;
	particle_t *p;
	int c;

	for (i = 0; i < 20; i++)
	{
		p = Level.NewParticle();
		if (!p)
			return;

		p->org = org;
		c = P_Random() >> 1;
		p->colour = RGB(c, c, c);
		p->die = Level.XLevel.Time + 0.5 * Random();
		p->vel.x = 32.0 * (Random() - 0.5);
		p->vel.y = 32.0 * (Random() - 0.5);
		p->vel.z = 32.0 * (Random() - 0.5);
		p->type = LineSpecialLevelInfo::pt_static;
	}
}

//==========================================================================
//
//  ClientBlood
//
//==========================================================================

void ClientBlood(TVec org, int damage)
{
	int i;
	particle_t *p;
	int c;

	for (i = 0; i < 4 * damage; i++)
	{
		p = Level.NewParticle();
		if (!p)
			return;

		p->org = org;
		c = P_Random() >> 1;
		p->colour = RGB(32 + c, c >> 4, c >> 4);
		p->die = Level.XLevel.Time + 0.5 * Random();
		p->vel.x = 32.0 * (Random() - 0.5);
		p->vel.y = 32.0 * (Random() - 0.5);
		p->vel.z = 32.0 * (Random() - 0.5);
		p->type = LineSpecialLevelInfo::pt_static;
	}
}

//==========================================================================
//
//  ClientExplosion
//
//==========================================================================

void ClientExplosion(TVec org)
{
	int i;
	particle_t *p;
	dlight_t *dl;

	for (i = 0; i < 1024; i++)
	{
		p = Level.NewParticle();
		if (!p)
			return;
		p->die = Level.XLevel.Time + 5.0;
		p->colour = DoomDefs.default.ramp1[0];
		p->ramp = Random() * 4.0;
		if (i & 1)
		{
			p->type = LineSpecialLevelInfo::pt_explode;
		}
		else
		{
			p->type = LineSpecialLevelInfo::pt_explode2;
		}
		p->org.x = org.x + ((Random() * 32.0) - 16.0);
		p->org.y = org.y + ((Random() * 32.0) - 16.0);
		p->org.z = org.z + ((Random() * 32.0) - 16.0);
		p->vel.x = (Random() * 512.0) - 256.0;
		p->vel.y = (Random() * 512.0) - 256.0;
		p->vel.z = (Random() * 512.0) - 256.0;
	}

	dl = Level.AllocDlight(none);
	dl->origin = org;
	dl->radius = 350.0;
	dl->colour = 0xffffffff;
	dl->die = Level.XLevel.Time + 0.5;
	dl->decay = 300.0;
}

//==========================================================================
//
//  ClientExplosion2
//
//==========================================================================

void ClientExplosion2(int etype, TVec org)
{
	dlight_t *dl;

	dl = Level.AllocDlight(none);
	dl->origin = org;
	dl->radius = 350.0;
	dl->colour = DoomDefs.default.ecolours[etype];
	dl->die = Level.XLevel.Time + 0.5;
	dl->decay = 300.0;
}

//==========================================================================
//
//  GetWeaponKickBack
//
//==========================================================================

float GetWeaponKickBack()
{
	//	No kickback from chainsaw.
	if (ReadyWeapon.WeaponType != DoomDefs::wp_chainsaw)
		return 100.0;
	return 0.0;
}

//==========================================================================
//
//  HaveInvulnerabilityPower
//
//==========================================================================

bool HaveInvulnerabilityPower()
{
	return !!Powers[pw_invulnerability];
}

//==========================================================================
//
//  ArmorAbsorbDamage
//
//==========================================================================

int ArmorAbsorbDamage(EntityEx inflictor, EntityEx source, int damage,
	name DmgType)
{
	int saved;

	if (ArmorType)
	{
		if (ArmorType == 1)
			saved = damage / 3;
		else
			saved = damage / 2;

		if (ArmorPoints <= saved)
		{
			// armor is used up
			saved = ArmorPoints;
			ArmorType = 0;
		}
		ArmorPoints -= saved;
		damage -= saved;
	}
	return damage;
}

defaultproperties
{
}
