//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Player : BasePlayer;

//
//  FIRST FIELDS ARE STATS THAT ARE SENT TO CLIENT FOR STATUS BAR
//

sb_stats_t Stats;

int Ammo[NUMAMMO];
int MaxAmmo[NUMAMMO];
int ReadyAmmo;

bool WeaponOwned[NUMWEAPONS];

int ArmorPoints;

int ReadyWeapon;

// For screen flashing (red or bright).
float DamageFlash;
float BonusFlash;

//
//  ----------
//

bool Backpack;

// Bit flags, for cheats and debug.
// See cheat_t, above.
int Cheats;

// Power ups. invinc and invis are tic counters.
float Powers[NUMPOWERS];

int Keys;

// Base height above floor for viewz.
float ViewHeight;
// Bob/squat speed.
float DeltaViewHeight;
// bounded/scaled total momentum.
float Bob;

// Refired shots are less accurate.
int Refire;

// Is wp_nochange if not changing.
int PendingWeapon;

// Who did damage (NULL for floors/ceilings).
Actor Attacker;

int JumpTics;
int LocalQuakeHappening;

// Armor type is 0-2.
int ArmorType;

TVec MoveDir;

#ifndef	NO_BOTS
BotPlayer Bot;
#endif

//==========================================================================
//
//  PutClientIntoServer
//
//==========================================================================

void PutClientIntoServer()
{
	ViewEnts[ps_weapon] = SpawnObject(Weapon, none);
	ViewEnts[ps_weapon].Player = self;
	ViewEnts[ps_flash] = SpawnObject(Weapon, none);
	ViewEnts[ps_flash].Player = self;
#ifndef	NO_BOTS
	if (bIsBot)
	{
		Bot = SpawnObject(BotPlayer, none);
		Bot.player = self;
		Bot.OnBeginPlay();
	}
#endif
}

//==========================================================================
//
//  SpawnClient
//
//==========================================================================

void SpawnClient()
{
	if (deathmatch)
	{
		DeathMatchSpawnPlayer();
	}
	else
	{
		SpawnPlayer(&level->playerstarts[RebornPosition * MAXPLAYERS +
			P_GetPlayerNum(self)]);
	}
}

//==========================================================================
//
//  NetGameReborn
//
//	Respawn at the start
//
//==========================================================================

void NetGameReborn()
{
	int i;

	// remove pitch and roll angles from corpse
	MO.Angles.pitch = 0.0;
	MO.Angles.roll = 0.0;

	// first dissasociate the corpse
	MO.Player = none;
	MO.bIsPlayer = false;

	// spawn at random spot if in death match
	if (deathmatch)
	{
		DeathMatchSpawnPlayer();
		return;
	}

	if (CheckSpot(&level->playerstarts[RebornPosition * MAXPLAYERS +
		P_GetPlayerNum(self)]))
	{
		SpawnPlayer(&level->playerstarts[RebornPosition * MAXPLAYERS +
			P_GetPlayerNum(self)]);
		return;
	}

	// try to spawn at one of the other players spots
	for (i = 0; i < MAXPLAYERS; i++)
	{
		if (CheckSpot(&level->playerstarts[RebornPosition * MAXPLAYERS + i]))
		{
			SpawnPlayer(&level->playerstarts[RebornPosition * MAXPLAYERS + i]);
			return;
		}
		// he's going to be inside something.  Too bad.
	}
	SpawnPlayer(&level->playerstarts[RebornPosition * MAXPLAYERS +
		P_GetPlayerNum(self)]);
}

//==========================================================================
//
//  DisconnectClient
//
//==========================================================================

void DisconnectClient()
{
	int i;

#ifndef	NO_BOTS
	if (Bot != none)
	{
		Bot.Destroy();
		Bot = none;
	}
#endif
	if (MO)
	{
		MO.Player = none;
		MO.bIsPlayer = false;
		MO.Damage(none, none, 10000);
	}
	for (i = 0; i < NUMPSPRITES; i++)
	{
		if (ViewEnts[i])
		{
			ViewEnts[i].Destroy();
			ViewEnts[i] = none;
		}
	}
	bprint("%s left the game\n", ARR2STR(PlayerName));
	MO.PlayFullVolumeSound('Chat', CHAN_AUTO);
}

//==========================================================================
//
//  UserinfoChanged
//
//==========================================================================

void UserinfoChanged()
{
	if (!bSpawned)
	{
		return;
	}
	MO.Translation = Color;
	SetClientModel();
}

//==========================================================================
//
//	SetClientModel
//
//==========================================================================

void SetClientModel()
{
	string model;
	string skin;

	model = Info_ValueForKey(ARR2STR(UserInfo), "model");
	if (!model || !strgetchar(model, 0))
	{
		MO.bFixedModel = false;
		MO.ModelSkinNum = 0;
		return;
	}

	MO.bFixedModel = true;
	MO.ModelIndex = FindModel(va("players/%s/tris.md2", model));
	skin = Info_ValueForKey(ARR2STR(UserInfo), "skin");
	if (skin && strgetchar(skin, 0))
	{
		MO.ModelSkinNum = FindSkin(va("players/%s/%s", model, skin));
	}
	else
	{
		MO.ModelSkinNum = 0;
	}
}

//==========================================================================
//
//  DeathMatchSpawnPlayer
//
//  Spawns a player at one of the random death match spots called at level
// load and each death
//
//==========================================================================

void DeathMatchSpawnPlayer()
{
	int i;
	int j;

	for (j = 0; j < 20; j++)
	{
		i = P_Random() % level->numdeathmatchstarts;
		if (CheckSpot(&level->deathmatchstarts[i]))
		{
			SpawnPlayer(&level->deathmatchstarts[i]);
			return;
		}
	}

	// no good spot, so the player will probably get stuck 
	SpawnPlayer(&level->playerstarts[P_GetPlayerNum(self)]);
}

//==========================================================================
//
//  CheckSpot
//
//  Returns false if the player cannot be respawned at the given mthing_t
// spot because something is occupying it
//
//==========================================================================

bool CheckSpot(mthing_t * mthing)
{
	float x;
	float y;
	sector_t *sec;
	float an;
	Actor A;
	int mtnum;
	int sfxnum;
	int i;

	if (!MO)
	{
		// first spawn of level, before corpses
		for (i = 0; i < MAXPLAYERS; i++)
			if (svvars->Players[i] && svvars->Players[i].MO &&
				svvars->Players[i].MO.Origin.x == mthing->x &&
				svvars->Players[i].MO.Origin.y == mthing->y)
				return false;
		return true;
	}

	x = mthing->x;
	y = mthing->y;

	if (!MO.CheckPosition(vector(x, y, ONFLOORZ)))
	{
		return false;
	}

	// flush an old corpse if needed 
	if (bodyqueslot >= BODYQUESIZE)
		bodyque[bodyqueslot % BODYQUESIZE].Remove();
	bodyque[bodyqueslot % BODYQUESIZE] = MO;
	bodyqueslot++;

	// spawn a teleport fog 
	sec = GLevel.PointInSector(vector(x, y, 0.0));
	an = itof(45 * (mthing->angle / 45));

	A = Spawn(TeleportFog, vector(x + 20.0 * cos(an), y + 20.0 * sin(an),
		sec->floorheight));
	A.PlaySound('Teleport', CHAN_VOICE);

	return true;
}

//==========================================================================
//
//  SpawnPlayer
//
//  Called when a player is spawned on the level. Most of the player
// structure stays unchanged between levels.
//
//==========================================================================

void SpawnPlayer(mthing_t * mthing)
{
	float x;
	float y;
	float z;
	PlayerPawn PP;

	if (PlayerState == PST_REBORN)
		PlayerReborn();

	x = mthing->x;
	y = mthing->y;
	z = ONFLOORZ;
	PP = PlayerPawn(Spawn(DoomPlayer, vector(x, y, z)));
	PP.Origin.z += mthing->height;
	PP.LinkToWorld();

	// set color translations for player sprites
	PP.Translation = Color;

	PP.Angles.yaw = itof(mthing->angle);
	PP.Player = self;
	PP.bIsPlayer = true;
	PP.Health = Health;

	MO = PP;
	PlayerState = PST_LIVE;
	Refire = 0;
	DamageFlash = 0.0;
	BonusFlash = 0.0;
	ExtraLight = 0;
	FixedColormap = 0;
	ViewHeight = VIEWHEIGHT;
	ViewAngles = PP.Angles;
	bFixAngle = true;

	// setup gun psprite
	SetupPsprites();

	// give all cards in death match mode
	if (deathmatch)
	{
		Keys = 0x3f;
	}

	// wake up the status bar
	SB_Start();

#ifndef	NO_BOTS
	if (bIsBot)
	{
		Bot.OnSpawn();
	}
#endif

	SetClientModel();
}

//==========================================================================
//
//  PlayerReborn
//
//  Called after a player dies almost everything is cleared and initialized
//
//==========================================================================

void PlayerReborn()
{
#ifndef	NO_BOTS
	BotPlayer b;
#endif

	//  Clear player struct
#ifndef	NO_BOTS
	b = Bot;
#endif
	ClearPlayer(self);
#ifndef	NO_BOTS
	Bot = b;
#endif

	//  Set initial data
	bUseDown = true;	// don't do anything immediately
	bAttackDown = true;
	PlayerState = PST_LIVE;
	Health = INITIAL_HEALTH;
#ifdef DDF
	SetInitialWeapons(self);
#else
	Ammo[am_clip] = INITIAL_AMMO;
	MaxAmmo[am_clip] = maxammo[am_clip];
	MaxAmmo[am_shell] = maxammo[am_shell];
	MaxAmmo[am_misl] = maxammo[am_misl];
	MaxAmmo[am_cell] = maxammo[am_cell];
	WeaponOwned[wp_fist] = true;
	WeaponOwned[wp_pistol] = true;
	SetWeapon(wp_pistol);
#endif
	LocalQuakeHappening = 0;
}

//==========================================================================
//
//  PlayerExitMap
//
//  Call when a player completes a level.
//
//==========================================================================

void PlayerExitMap(bool clusterChange)
{
	int i;

	if (clusterChange)
	{
		Keys = 0;
	}

	for (i = 0; i < NUMPOWERS; i++)
	{
		Powers[i] = 0.0;
	}
	Items = 0;
	MO.Angles.pitch = 0.0;
	MO.Translucency = 0;	// cancel invisibility
	ExtraLight = 0;			// cancel gun flashes
	FixedColormap = 0;		// cancel ir gogles
	DamageFlash = 0.0;		// no palette changes
	BonusFlash = 0.0;
}

//==========================================================================
//
//	GiveAmmo
//
//	Returns false if the ammo can't be picked up at all
//
//==========================================================================

bool GiveAmmo(int ammo, int num)
{
	int oldammo;
	int max;

	if (ammo == am_noammo)
		return false;

	if (ammo < 0 || ammo > NUMAMMO)
		Error("P_GiveAmmo: bad type %i", ammo);

	max = MaxAmmo[ammo];
	if (Ammo[ammo] == max)
		return false;

	if (gameskill == sk_baby || gameskill == sk_nightmare)
	{
		// give double ammo in trainer mode,
		// you'll need in nightmare
		num <<= 1;
	}

	oldammo = Ammo[ammo];
	Ammo[ammo] += num;

	if (Ammo[ammo] > max)
		Ammo[ammo] = max;

	// If non zero ammo, 
	// don't change up weapons,
	// player was lower on purpose.
	if (oldammo)
		return true;

	// We were down to zero,
	// so select a new weapon.
	// Preferences are not user selectable.
	switch (ammo)
	{
	case am_clip:
		if (ReadyWeapon == wp_fist)
		{
			if (WeaponOwned[wp_chaingun])
				PendingWeapon = wp_chaingun;
			else
				PendingWeapon = wp_pistol;
		}
		break;

	case am_shell:
		if (ReadyWeapon == wp_fist || ReadyWeapon == wp_pistol)
		{
			if (WeaponOwned[wp_shotgun])
				PendingWeapon = wp_shotgun;
		}
		break;

	case am_cell:
		if (ReadyWeapon == wp_fist || ReadyWeapon == wp_pistol)
		{
			if (WeaponOwned[wp_plasma])
				PendingWeapon = wp_plasma;
		}
		break;

	case am_misl:
		if (ReadyWeapon == wp_fist)
		{
			if (WeaponOwned[wp_missile])
				PendingWeapon = wp_missile;
		}
	default:
		break;
	}

	return true;
}

//==========================================================================
//
//  GiveWeapon
//
//  The weapon name may have a MF_DROPPED flag ored in.
//
//==========================================================================

bool GiveWeapon(int weapon, bool dropped)
{
#ifdef DDF
	if (WeaponOwned[weapon])
	{
		return false;
	}
	WeaponOwned[weapon] = true;
	PendingWeapon = weapon;
	return true;
#else
	bool gaveammo;
	bool gaveweapon;
	int ammoCount;

	ammoCount = clipammo[weapon_ammo_type[weapon]];
	if (netgame && (deathmatch < 2) && !dropped)
	{
		// leave placed weapons forever on net games
		if (WeaponOwned[weapon])
			return false;

		BonusFlash += BONUSADD;
		WeaponOwned[weapon] = true;

		if (deathmatch)
			GiveAmmo(weapon_ammo_type[weapon], 5 * ammoCount);
		else
			GiveAmmo(weapon_ammo_type[weapon], 2 * ammoCount);
		PendingWeapon = weapon;

		MO.PlaySound('PickupWeapon', CHAN_ITEM);
		return false;
	}

	gaveammo = false;
	if (weapon_ammo_type[weapon] != am_noammo)
	{
		// give one clip with a dropped weapon,
		// two clips with a found weapon
		if (dropped)
			gaveammo = GiveAmmo(weapon_ammo_type[weapon], 1 * ammoCount);
		else
			gaveammo = GiveAmmo(weapon_ammo_type[weapon], 2 * ammoCount);
	}

	gaveweapon = false;
	if (!WeaponOwned[weapon])
	{
		gaveweapon = true;
		WeaponOwned[weapon] = true;
		PendingWeapon = weapon;
	}

	return (gaveweapon || gaveammo);
#endif
}

//==========================================================================
//
//	GiveBody
//
//	Returns false if the body isn't needed at all
//
//==========================================================================

bool GiveBody(int num, int max)
{
	if (MO.Health >= max)
	{
		return false;
	}

	MO.Health += num;
	if (MO.Health > max)
	{
		MO.Health = max;
	}
	Health = MO.Health;

	return true;
}

//==========================================================================
//
//	GiveArmor
//
//	Returns false if the armor is worse than the current armor.
//
//==========================================================================

bool GiveArmor(int armortype)
{
	int hits;

	hits = armortype * 100;
	if (ArmorPoints >= hits)
		return false;	// don't pick up

	ArmorType = armortype;
	ArmorPoints = hits;

	return true;
}

//==========================================================================
//
//	GiveArmor2
//
//	Returns false if the armor is worse than the current armor.
//
//==========================================================================

bool GiveArmor2(int armortype, int num, int max)
{
	if (ArmorPoints >= max)
	{
		return false;	// don't pick up
	}
	ArmorPoints += num;
	if (ArmorPoints > max)
	{
		ArmorPoints = max;
	}
	if (ArmorType < armortype)
	{
		ArmorType = armortype;
	}
	return true;
}

//==========================================================================
//
//	GiveCard
//
//==========================================================================

bool GiveCard(int card)
{
	if (Keys & (1 << card))
		return false;

	BonusFlash = BONUSADD;
	Keys |= 1 << card;
	return true;
}

//==========================================================================
//
//  GiveItem
//
//==========================================================================

bool GiveItem(int item)
{
	if (Items & item)
	{
		return false;	// already got it
	}

	Items |= item;
	return true;
}

//==========================================================================
//
//	GivePower
//
//==========================================================================

bool GivePower(int power)
{
	if (power == pw_invulnerability)
	{
		Powers[power] = INVULNTIME;
		return true;
	}

	if (power == pw_invisibility)
	{
		Powers[power] = INVISTIME;
		MO.Translucency = 90;
		return true;
	}

	if (power == pw_infrared)
	{
		Powers[power] = INFRATIME;
		return true;
	}

	if (power == pw_ironfeet)
	{
		Powers[power] = IRONTIME;
		return true;
	}

	if (power == pw_strength)
	{
		GiveBody(100, MAXHEALTH);
		Powers[power] = 1.0;
		if (ReadyWeapon != wp_fist)
			PendingWeapon = wp_fist;
		return true;
	}

	if (Powers[power])
		return false;	// already got it

	Powers[power] = 1.0;
	return true;
}

//==========================================================================
//
//	GivePower2
//
//==========================================================================

bool GivePower2(int power, float duration, float max)
{
	if (Powers[power] >= max)
		return false;	// already got it

	Powers[power] += duration;
	if (Powers[power] >= max)
	{
		Powers[power] = max;
	}

	if (power == pw_invisibility)
	{
		MO.Translucency = 90;
	}
	if (power == pw_strength)
	{
		if (ReadyWeapon != wp_fist)
			PendingWeapon = wp_fist;
	}

	return true;
}

//==========================================================================
//
//  ThrustPlayer
//
//  Moves the given origin along a given angle.
//
//==========================================================================

void ThrustPlayer(float angle, float move, float deltaTime)
{
	MO.Velocity.x += move * cos(angle) * deltaTime;
	MO.Velocity.y += move * sin(angle) * deltaTime;
}

//==========================================================================
//
//  CalcHeight
//
//  Calculate the walking / running height adjustment
//
//==========================================================================

void CalcHeight(float deltaTime)
{
	float angle;
	float bob;

	// Regular movement bobbing
	// (needs to be calculated for gun swing even if not on ground)
	Bob = MO.Velocity.x * MO.Velocity.x + MO.Velocity.y * MO.Velocity.y;
	Bob /= 4.0 * 35.0 * 35.0;
	if (Bob > MAXBOB)
		Bob = MAXBOB;

	angle = (180.0 * 35.0 / 10.0) * level->time;
	bob = Bob / 2.0 * sin(angle);

	// move viewheight
	if (PlayerState == PST_LIVE)
	{
		ViewHeight += DeltaViewHeight * deltaTime;

		if (ViewHeight > VIEWHEIGHT)
		{
			ViewHeight = VIEWHEIGHT;
			DeltaViewHeight = 0.0;
		}

		if (ViewHeight < VIEWHEIGHT / 2.0)
		{
			ViewHeight = VIEWHEIGHT / 2.0;
			if (DeltaViewHeight <= 0.0)
				DeltaViewHeight = 0.00001;
		}

		if (DeltaViewHeight)
		{
			DeltaViewHeight += 256.0 * deltaTime;
			if (!DeltaViewHeight)
				DeltaViewHeight = 0.00001;
		}
	}
	ViewOrg.z = MO.Origin.z + ViewHeight + bob;

	if (ViewOrg.z > MO.CeilingZ - 4.0)
		ViewOrg.z = MO.CeilingZ - 4.0;
}

//==========================================================================
//
//  MovePlayer
//
//==========================================================================

void MovePlayer(float deltaTime)
{
	float forward;
	float side;

	// Do not let the player control movement
	//  if not onground.
	onground = MO.Origin.z <= MO.FloorZ || MO.bOnMobj;

	forward = ForwardMove * 5.0;
	side = SideMove * 5.0;

	if (!onground)
	{
		forward *= air_control;
		side *= air_control;
	}

	if (forward)
	{
		ThrustPlayer(MO.Angles.yaw, forward, deltaTime);
	}
	if (side)
	{
		ThrustPlayer(MO.Angles.yaw - 90.0, side, deltaTime);
	}

	if (forward || side)
	{
		SetPlayerRunState();
	}

	if ((Buttons & BT_JUMP) && onground && !JumpTics)
	{
		//  Jump
		MO.Velocity.z = 9.0 * 35.0;
		JumpTics = 18;
	}
}

//==========================================================================
//
//  CheckWaterJump
//
//==========================================================================

void CheckWaterJump()
{
	TVec start;
	TVec end;
	TVec vforward;

	// check for a jump-out-of-water
	AngleVector(&MO.Angles, &vforward);
	start = MO.Origin;
	start.z += MO.Height * 0.5 + 8.0;
	vforward.z = 0.0;
	vforward = Normalize(vforward);
	end = start + vforward * 24.0;
	if (!TraceLine(start, end))
	{
		// solid at waist
		start.z = MO.Origin.z + MO.Height;
		end = start + vforward * 24.0;
		MoveDir = trace_plane_normal * -50.0;
		if (TraceLine(start, end))
		{
			// open at eye level
			MO.bWaterJump = true;
			MO.Velocity.z = 320.0;
			MO.ReactionTime = 2.0;	// safety net
		}
	}
}

//==========================================================================
//
//  WaterMove
//
//==========================================================================

void WaterMove(float deltaTime)
{
	float forward;
	float side;
	TVec vforward;
	TVec vright;
	TVec vup;
	TVec wishvel;

	// Do not let the player control movement
	//  if not onground.
	onground = MO.Origin.z <= MO.FloorZ || MO.bOnMobj;

	AngleVectors(&MO.Angles, &vforward, &vright, &vup);

	forward = ForwardMove;
	side = SideMove;

	wishvel = forward * vforward + side * vright;
	if (!forward && !side /* && !cmd.upmove */ )
		wishvel.z -= 60.0;	// drift towards bottom
//  else
//      wishvel.z += cmd.upmove;

	MO.Velocity += 3.5 * deltaTime * wishvel;

	if (forward || side)
	{
		SetPlayerRunState();
	}

	if (Buttons & BT_JUMP)
	{
		if (MO.WaterType == CONTENTS_WATER)
			MO.Velocity.z = 100.0;
		else if (MO.WaterType == CONTENTS_NUKAGE || MO.WaterType == CONTENTS_SLIME)
			MO.Velocity.z = 80.0;
		else
			MO.Velocity.z = 50.0;
	}
	CheckWaterJump();
}

//==========================================================================
//
//  WaterJump
//
//==========================================================================

void WaterJump()
{
	if (!MO.ReactionTime || !MO.WaterLevel)
	{
		MO.bWaterJump = false;
		MO.ReactionTime = 0.0;
	}
	MO.Velocity.x = MoveDir.x;
	MO.Velocity.y = MoveDir.y;
}

//==========================================================================
//
//  PlayerInSpecialSector
//
//  Called every tic frame that the player origin is in a special sector
//
//==========================================================================

void PlayerInSpecialSector()
{
	// Falling, not all the way down yet?
	if (MO.Origin.z != MO.Sector->floorheight)
		return;

	if (MO.Sector->special & SECRET_MASK)
	{
		// SECRET SECTOR
		SecretCount++;
		centerprint(self, "You found a secret area");
		MO.Sector->special &= ~SECRET_MASK;
	}

	// Has hitten ground.
	switch (MO.Sector->special & 255)
	{
	case 64 + 5:
		// HELLSLIME DAMAGE
		if (!Powers[pw_ironfeet])
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 10);
		break;

	case 64 + 7:
		// NUKAGE DAMAGE
		if (!Powers[pw_ironfeet])
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 5);
		break;

	case 64 + 16:
		// SUPER HELLSLIME DAMAGE
	case 64 + 4:
		// STROBE HURT
		if (!Powers[pw_ironfeet] || (P_Random() < 5))
		{
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 20);
		}
		break;

	case 64 + 11:
		// EXIT SUPER DAMAGE! (for E1M8 finale)
		Cheats &= ~CF_GODMODE;

		if (!(level->tictime & 0x1f))
			MO.Damage(none, none, 20);

		if (Health <= 10)
			G_ExitLevel();
		break;

	default:
		break;
	};
}

//==========================================================================
//
//  PlayerInContents
//
//==========================================================================

void PlayerInContents()
{
	if (!MO.WaterLevel)
	{
		return;
	}
	switch (MO.WaterType)
	{
	case CONTENTS_NUKAGE:
		// NUKAGE DAMAGE
		if (!Powers[pw_ironfeet])
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 5);
		break;

	case CONTENTS_SLIME:
		// HELLSLIME DAMAGE
		if (!Powers[pw_ironfeet])
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 10);
		break;

	case CONTENTS_LAVA:
		// SUPER HELLSLIME DAMAGE
		if (!Powers[pw_ironfeet] || (P_Random() < 5))
		{
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 20);
		}
		break;
	}
}

//==========================================================================
//
//  DeathPlayerTick
//
//  Fall on your face when dying. Decrease POV height to floor height.
//
//==========================================================================

void DeathPlayerTick(float deltaTime)
{
	float angle;
	float delta;

	MovePsprites();

	onground = MO.Origin.z <= MO.FloorZ || MO.bOnMobj;

	if (MO.WaterLevel > 1)
	{
		MO.Velocity.z = -60.0;	// drift towards bottom
	}

	// fall to the ground
	if (ViewHeight > 6.0)
		ViewHeight -= 1.0;

	if (ViewHeight < 6.0)
		ViewHeight = 6.0;

	DeltaViewHeight = 0.0;
	CalcHeight(deltaTime);

	if (Attacker && Attacker != MO)
	{
		angle = atan2(Attacker.Origin.y - MO.Origin.y,
			Attacker.Origin.x - MO.Origin.x);

		delta = AngleMod180(angle - ViewAngles.yaw);

		if (delta < 5.0 || delta > -5.0)
		{
			// Looking at killer,
			//  so fade damage flash down.
			ViewAngles.yaw = angle;

			if (DamageFlash)
			{
				DamageFlash -= deltaTime;
				if (DamageFlash <= 0.0)
					DamageFlash = 0.0;
			}
		}
		else if (delta > 0.0)
			ViewAngles.yaw += 5.0;
		else
			ViewAngles.yaw -= 5.0;
	}
	else if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (Buttons & BT_USE)
		PlayerState = PST_REBORN;
}

//==========================================================================
//
//  PlayerImpulse
//
//==========================================================================

void PlayerImpulse()
{
	int impulse;

	impulse = Impulse;
	Impulse = 0;

#ifdef DDF
	if (impulse <= 10)
	{
		ChangeWeapon(impulse - 1);
	}
#else
	if (impulse <= 7)
	{
		ChangeWeapon(impulse - 1);
	}
#endif
	else if (impulse == 16)
	{
		//  Suicide
		MO.Damage(none, none, 10000);
	}
}

//==========================================================================
//
//  PlayerTick
//
//==========================================================================

void PlayerTick(float deltaTime)
{
#ifndef	NO_BOTS
	if (bIsBot)
	{
		Bot.BotTick(deltaTime);
	}
#endif

	// fixme: do this in the cheat code
	if (Cheats & CF_NOCLIP)
	{
		MO.bColideWithThings = false;
		MO.bColideWithWorld = false;
	}
	else
	{
		MO.bColideWithThings = true;
		MO.bColideWithWorld = true;
	}

	if (MO.bJustAttacked)
	{
		ForwardMove = 100.0;
		SideMove = 0.0;
		MO.bJustAttacked = false;
	}

	if (PlayerState == PST_DEAD)
	{
		DeathPlayerTick(deltaTime);
		return;
	}

	// Copy view angles
	MO.Angles = ViewAngles;

	if (JumpTics)
	{
		JumpTics--;
	}

	// Move around.
	// Reactiontime is used to prevent movement
	//  for a bit after a teleport.
	if (MO.ReactionTime)
	{
		MO.ReactionTime -= deltaTime;
		if (MO.ReactionTime <= 0.0)
			MO.ReactionTime = 0.0;
		if (MO.bWaterJump)
			WaterJump();
	}
	else
	{
		if (MO.WaterLevel > 1)
			WaterMove(deltaTime);
		else
			MovePlayer(deltaTime);
	}

	CalcHeight(deltaTime);

	if (MO.Sector->special)
		PlayerInSpecialSector();
	PlayerInContents();

	// Check for weapon change.
	if (Impulse)
	{
		PlayerImpulse();
	}

	// check for use
	if (Buttons & BT_USE)
	{
		if (!bUseDown)
		{
			P_UseLines(self);
			bUseDown = true;
		}
	}
	else
	{
		bUseDown = false;
	}

	//  cycle psprites
	MovePsprites();

	//  Counters, time dependend power ups.

	//  Strength counts up to diminish fade.
	if (Powers[pw_strength])
		Powers[pw_strength] += deltaTime;

	if (Powers[pw_invulnerability])
	{
		Powers[pw_invulnerability] -= deltaTime;
		if (Powers[pw_invulnerability] <= 0.0)
			Powers[pw_invulnerability] = 0.0;
	}

	if (Powers[pw_invisibility])
	{
		Powers[pw_invisibility] -= deltaTime;
		if (Powers[pw_invisibility] <= 0.0)
		{
			Powers[pw_invisibility] = 0.0;
			MO.Translucency = 0;
		}
	}

	if (Powers[pw_infrared])
	{
		Powers[pw_infrared] -= deltaTime;
		if (Powers[pw_infrared] <= 0.0)
			Powers[pw_infrared] = 0.0;
	}

	if (Powers[pw_ironfeet])
	{
		Powers[pw_ironfeet] -= deltaTime;
		if (Powers[pw_ironfeet] <= 0.0)
			Powers[pw_ironfeet] = 0.0;
	}

	if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (BonusFlash)
	{
		BonusFlash -= deltaTime;
		if (BonusFlash <= 0.0)
			BonusFlash = 0.0;
	}

	// Handling colormaps.
	if (Powers[pw_invulnerability])
	{
		if (Powers[pw_invulnerability] > 4.0 ||
			(ftoi(4.0 * Powers[pw_invulnerability]) & 1))
			FixedColormap = INVERSECOLORMAP;
		else
			FixedColormap = 0;
	}
	else if (Powers[pw_infrared])
	{
		if (Powers[pw_infrared] > 4.0 ||
			(ftoi(4.0 * Powers[pw_infrared]) & 1))
		{
			// almost full bright
			FixedColormap = 1;
		}
		else
			FixedColormap = 0;
	}
	else
		FixedColormap = 0;
}

//==========================================================================
//
//  PaletteFlash
//
//  Do red-/gold-shifts from damage/items
//
//==========================================================================

void PaletteFlash()
{
	int cnt;
	int bzc;

	CShifts[CSHIFT_CONTENTS] = 0;
	if (MO.WaterLevel == 3)
	{
		switch (MO.WaterType)
		{
		case CONTENTS_WATER:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(130, 80, 50, 128);
			break;

		case CONTENTS_BLOOD:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(160, 16, 16, 150);
			break;

		case CONTENTS_NUKAGE:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(50, 255, 50, 150);
			break;

		case CONTENTS_SLIME:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(0, 25, 5, 150);
			break;

		case CONTENTS_LAVA:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(255, 80, 0, 150);
			break;
		}
	}

	cnt = ftoi(32.0 * DamageFlash);

	if (Powers[pw_strength])
	{
		// slowly fade the berzerk out
		bzc = 12 - ftoi(Powers[pw_strength] / 2.0);

		if (bzc > cnt)
			cnt = bzc;
	}

	if (cnt)
	{
		Palette = (cnt + 7) >> 3;

		if (Palette >= NUMREDPALS)
			Palette = NUMREDPALS - 1;

		Palette += STARTREDPALS;
	}
	else if (BonusFlash)
	{
		Palette = (ftoi(32.0 * BonusFlash) + 7) >> 3;

		if (Palette >= NUMBONUSPALS)
			Palette = NUMBONUSPALS - 1;

		Palette += STARTBONUSPALS;
	}
	else if (Powers[pw_ironfeet] > 4.0 ||
		ftoi(4.0 * Powers[pw_ironfeet]) & 1)
		Palette = RADIATIONPAL;
	else
		Palette = 0;
}

//==========================================================================
//
//  SetViewPos
//
//==========================================================================

void SetViewPos()
{
	ViewOrg.x = MO.Origin.x;
	ViewOrg.y = MO.Origin.y;

	if (LocalQuakeHappening)
	{
		float intensity;

		intensity = itof(LocalQuakeHappening);
		ViewOrg.x += (Random() - 0.5) * (intensity * 4.0);
		ViewOrg.y += (Random() - 0.5) * (intensity * 4.0);
	}

	if (PlayerState != PST_DEAD)
	{
		ViewAngles = MO.Angles;
	}

	PaletteFlash();

	Stats.flags = Keys;
	if ((Cheats & CF_GODMODE) || Powers[pw_invulnerability])
		Stats.flags |= SBF_GOD_MODE;

	if (Weapon(ViewEnts[ps_weapon]).Ammo == am_noammo)
		ReadyAmmo = 1994;	// means "n/a"
	else
		ReadyAmmo = Ammo[Weapon(ViewEnts[ps_weapon]).Ammo];
}

//==========================================================================
//
//  SetPlayerRunState
//
//==========================================================================

void SetPlayerRunState()
{
	if (MO.StateNum == MO.IdleState)
	{
		MO.SetState(MO.SeeState);
	}
}

//==========================================================================
//
//  SpawnPlayerMissile
//
//  Tries to aim at a nearby monster
//
//==========================================================================

Actor SpawnPlayerMissile(classid type)
{
	Actor A;
	TVec dir;

	dir = Aim(MO, 16.0 * 64.0);

	A = Spawn(type, MO.Origin + vector(0.0, 0.0, 32.0));

	if (A.SightSound)
		A.PlaySound(A.SightSound, CHAN_VOICE);

	A.Instigator = MO;
	A.Velocity = dir * A.Speed;
	VectorAngles(&dir, &A.Angles);

	A.CheckMissileSpawn();

	return A;
}

#ifdef DDF
//==========================================================================
//
//	ActPlayerAttack
//
//==========================================================================

void ActPlayerAttack(AttackType *attack)
{
	int i;
	float range;
	int damage;
	TAVec aimAngles;
	TVec dir;
	TVec aimDir;
	Actor proj;

	range = (attack->Range > 0.0) ? attack->Range : MISSILERANGE;

	switch (attack->AttackStyle)
	{
	case ATK_CloseCombat:
		if (attack->Sound)
			MO.PlaySound(attack->Sound, CHAN_WEAPON);

		DAMAGE_COMPUTE(damage, attack->Damage);

		if (Powers[pw_strength])
			damage *= 10;

		aimAngles = MO.Angles;
		aimAngles.yaw = AngleMod360(aimAngles.yaw + (Random() -
			Random()) * 45.0 / 8.0);
		dir = AimLineAttack(MO, aimAngles, attack->Range);
		LineAttack(MO, dir, attack->Range, damage);

		// turn to face target
		if (linetarget && attack->bFaceTarget)
		{
			MO.Angles.yaw = atan2(
				linetarget.Origin.y - MO.Origin.y,
				linetarget.Origin.x - MO.Origin.x);
			bFixAngle = true;
		}
		break;

	case ATK_Projectile:
		DAMAGE_COMPUTE(damage, attack->Damage);
		proj = SpawnPlayerMissile(attack->AtkMobj);
		proj.MissileDamage = damage;
		break;

	case ATK_Shot:
		if (attack->Sound)
			MO.PlaySound(attack->Sound, CHAN_WEAPON);

		aimDir = Aim(MO, 16.0 * 64.0);
		// -AJA- 1999/09/10: apply the attack's angle offsets.
		//objangle -= attack->angle_offset;
		//objslope += attack->slope_offset;
		for (i = 0; i < attack->Count; i++)
		{
			//  Make a copy of angles because inacurate shoot will modify them
			dir = aimDir;
			if (!Refire)
			{
				TAVec angles;

				VectorAngles(&dir, &angles);
				if (attack->AccuracyAngle > 0.0)
					angles.yaw += attack->AccuracyAngle * (Random() - Random());
				//if (attack->accuracy_slope > 0)
				//	slope += attack->accuracy_slope * (P_RandomNegPos() / 255.0);
				AngleVector(&angles, &dir);
			}
			DAMAGE_COMPUTE(damage, attack->Damage);
			LineAttack(MO, dir, MISSILERANGE, damage);
		}
		break;

	case ATK_SmartProjectile:	// EXTRA
		print("Smart proj");
		//LaunchSmartProjectile(object, target, attack->atk_mobj);
		break;

	case ATK_RandomSpread:		// EXTRA
		print("Random spread");
		//P_ActLaunchRandomSpread(object);
		break;

	case ATK_ShootToSpot:		// EXTRA
		print("Shoot to spot");
		//ShootToSpot(object);
		break;

	case ATK_SkullFly:			// EXTRA
		Error("Player can't skullfly");
		break;

	case ATK_Spawner:			// EXTRA
		print("spawner");
		//ObjectSpawning(object, object->angle);
		break;

	case ATK_Spreader:			// EXTRA
		print("Spreader");
		//P_ActLaunchOrderedSpread(object);
		break;

	case ATK_Tracker:			// EXTRA
		print("Tracker");
		//LaunchTracker(object);
		break;

	case ATK_TripleSpawner:		// EXTRA
		print("Tripple spawner");
		//ObjectTripleSpawn(object);
		break;

	case ATK_Spray:				// EXTRA
		Error("Player spray");
		break;

	default:
		print("Bad attack type %d", attack->AttackStyle);
	}
}
#endif

//***************************************************************************
//
//  WEAPON UTILITES
//
//***************************************************************************

//==========================================================================
//
//	SetWeapon
//
//==========================================================================

void SetWeapon(int NewWeapon)
{
	ReadyWeapon = NewWeapon;
	PendingWeapon = wp_nochange;
	if (ViewEnts[ps_weapon])
	{
		ViewEnts[ps_weapon].Destroy();
	}
	ViewEnts[ps_weapon] = SpawnObject(WeaponClasses[NewWeapon], none);
	ViewEnts[ps_weapon].Player = self;
	WeaponModel =
		GetModelIndex(Weapon(ViewEnts[ps_weapon]).ThirdPersonModel);
}

//===========================================================================
//
//  SetPsprite
//
//===========================================================================

void SetPsprite(int position, int stnum)
{
	ViewEnts[position].SetState(stnum);
}

//===========================================================================
//
//  BringUpWeapon
//
//  Starts bringing the pending weapon up from the bottom of the screen.
// Uses player
//
//===========================================================================

void BringUpWeapon()
{
#ifdef DDF
	if (Weapon(ViewEnts[ps_weapon]).StartSound)
		MO.PlaySound(Weapon(ViewEnts[ps_weapon]).StartSound, CHAN_WEAPON);
#else
	if (ReadyWeapon == wp_chainsaw)
		MO.PlaySound('ChainsawStart', CHAN_WEAPON);
#endif

	PendingWeapon = wp_nochange;
	ViewEnts[ps_weapon].SY = WEAPONBOTTOM;

	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).UpState);
}

//==========================================================================
//
//  ChangeWeapon
//
//  The actual changing of the weapon is done when the weapon psprite can
// do it (read: not in the middle of an attack).
//
//==========================================================================

void ChangeWeapon(int newweapon)
{
#ifdef DDF
	int count;
	int i;
	int start;
	int choices[8];
	int key;
	Weapon tmp;

	count = 0;
	start = 0;
	for (i = 0; i < NUMWEAPONS; i++)
	{
		if (!WeaponOwned[i])
			continue;
		if (!WeaponClasses[i])
			continue;
		tmp = Weapon(SpawnObject(WeaponClasses[i], none));
		if (tmp.BindKey == ((newweapon + 1) % 10))
		{
			choices[count] = i;
			if (i == ReadyWeapon)
				start = count;
			count++;
		}
		tmp.Destroy();
	}
	if (count)
	{
		newweapon = choices[(start + 1) % count];
		if (newweapon != ReadyWeapon)
		{
			PendingWeapon = newweapon;
		}
	}
#else //---------
	if (newweapon == wp_fist && WeaponOwned[wp_chainsaw] &&
		!(ReadyWeapon == wp_chainsaw && Powers[pw_strength]))
	{
		newweapon = wp_chainsaw;
	}

#ifdef DOOM2
	if (newweapon == wp_shotgun && WeaponOwned[wp_supershotgun] &&
		ReadyWeapon != wp_supershotgun)
	{
		newweapon = wp_supershotgun;
	}
#endif

#ifdef GRENADE_LAUNCHER
	if (newweapon == wp_missile && WeaponOwned[wp_grenade] &&
		ReadyWeapon != wp_grenade)
	{
		newweapon = wp_grenade;
	}
#endif

	if (WeaponOwned[newweapon] && newweapon != ReadyWeapon)
	{
#ifdef DOOM2
		PendingWeapon = newweapon;
#else
		// Do not go to plasma or BFG in shareware, even if cheated.
		if ((newweapon != wp_plasma && newweapon != wp_bfg) || !shareware)
		{
			PendingWeapon = newweapon;
		}
#endif
	}
#endif
}

//===========================================================================
//
//  DropWeapon
//
//  Player died, so put the weapon away.
//
//===========================================================================

void DropWeapon()
{
	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).DownState);
}

//===========================================================================
//
//  SetupPsprites
//
//  Called at start of level for each player.
//
//===========================================================================

void SetupPsprites()
{
	int i;

	// remove all psprites
	for (i = 0; i < NUMPSPRITES; i++)
	{
		ViewEnts[i].StateNum = 0;
	}

	// spawn the gun
	BringUpWeapon();
}

//==========================================================================
//
//  MovePsprites
//
//  Called every tic by player thinking routine.
//
//==========================================================================

void MovePsprites()
{
	int i;
	ViewEntity e;

	for (i = 0; i < NUMPSPRITES; i++)
	{
		e = ViewEnts[i];
		// a null state means not active
		if (e.StateNum)
		{
			// drop tic count and possibly change state

			// a -1 tic count never changes
			if (e.StateTime != -1.0)
			{
				e.StateTime -= frametime;
				if (e.StateTime <= 0.0)
				{
					e.StateTime = 0.0;
					SetPsprite(i, e.NextState);
				}
			}
		}
	}

	ViewEnts[ps_flash].SX = ViewEnts[ps_weapon].SX;
	ViewEnts[ps_flash].SY = ViewEnts[ps_weapon].SY;
}

//==========================================================================
//
//  CheckAmmo
//
//  Returns true if there is enough ammo to shoot.
// If not, selects the next weapon to use.
//
//==========================================================================

int CheckAmmo()
{
	int ammo;
	int count;

	ammo = Weapon(ViewEnts[ps_weapon]).Ammo;

#ifdef DDF
	count = Weapon(ViewEnts[ps_weapon]).AmmoPerShot;
#else
	// Minimal amount for one shot varies.
	if (ReadyWeapon == wp_bfg)
		count = BFGCELLS;
#ifdef DOOM2
	else if (ReadyWeapon == wp_supershotgun)
		count = 2;	// Double barrel.
#endif
	else
		count = 1;	// Regular.
#endif

	// Some do not need ammunition anyway.
	// Return if current ammunition sufficient.
	if (ammo == am_noammo || Ammo[ammo] >= count)
		return true;

	// Out of ammo, pick a weapon to change to.
	// Preferences are set here.
	do
	{
#ifndef DOOM2
		if (WeaponOwned[wp_plasma] && Ammo[am_cell] && (!shareware))
#else
		if (WeaponOwned[wp_plasma] && Ammo[am_cell])
#endif
		{
			PendingWeapon = wp_plasma;
		}
#ifdef DOOM2
		else if (WeaponOwned[wp_supershotgun] && Ammo[am_shell] > 2)
		{
			PendingWeapon = wp_supershotgun;
		}
#endif
		else if (WeaponOwned[wp_chaingun] && Ammo[am_clip])
		{
			PendingWeapon = wp_chaingun;
		}
		else if (WeaponOwned[wp_shotgun] && Ammo[am_shell])
		{
			PendingWeapon = wp_shotgun;
		}
		else if (Ammo[am_clip])
		{
			PendingWeapon = wp_pistol;
		}
		else if (WeaponOwned[wp_chainsaw])
		{
			PendingWeapon = wp_chainsaw;
		}
		else if (WeaponOwned[wp_missile] && Ammo[am_misl])
		{
			PendingWeapon = wp_missile;
		}
#ifdef DOOM2
		else if (WeaponOwned[wp_bfg] && Ammo[am_cell] > 40)
#else
		else if (WeaponOwned[wp_bfg] && Ammo[am_cell] > BFGCELLS &&
			(!shareware))
#endif
		{
			PendingWeapon = wp_bfg;
		}
		else
		{
			// If everything fails.
			PendingWeapon = wp_fist;
		}
	}
	while (PendingWeapon == wp_nochange);

	// Now set appropriate weapon overlay.
	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).DownState);

	return false;
}

//===========================================================================
//
//  FireWeapon
//
//===========================================================================

void FireWeapon()
{
	if (!CheckAmmo())
	{
		return;
	}

	MO.SetState(MO.MeleeState);
	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).AttackState);
	P_NoiseAlert(MO, MO);
}

#ifdef DDF
//==========================================================================
//
//  CheckAmmoSA
//
//  Returns true if there is enough ammo to shoot.
// If not, selects the next weapon to use.
//
//==========================================================================

int CheckAmmoSA()
{
	int ammo;
	int count;

	ammo = Weapon(ViewEnts[ps_weapon]).SAAmmo;

	count = Weapon(ViewEnts[ps_weapon]).SAAmmoPerShot;

	// Some do not need ammunition anyway.
	// Return if current ammunition sufficient.
	if (ammo == am_noammo || Ammo[ammo] >= count)
		return true;

	// Out of ammo, pick a weapon to change to.
	// Preferences are set here.
	do
	{
#ifndef DOOM2
		if (WeaponOwned[wp_plasma] && Ammo[am_cell] && (!shareware))
#else
		if (WeaponOwned[wp_plasma] && Ammo[am_cell])
#endif
		{
			PendingWeapon = wp_plasma;
		}
#ifdef DOOM2
		else if (WeaponOwned[wp_supershotgun] && Ammo[am_shell] > 2)
		{
			PendingWeapon = wp_supershotgun;
		}
#endif
		else if (WeaponOwned[wp_chaingun] && Ammo[am_clip])
		{
			PendingWeapon = wp_chaingun;
		}
		else if (WeaponOwned[wp_shotgun] && Ammo[am_shell])
		{
			PendingWeapon = wp_shotgun;
		}
		else if (Ammo[am_clip])
		{
			PendingWeapon = wp_pistol;
		}
		else if (WeaponOwned[wp_chainsaw])
		{
			PendingWeapon = wp_chainsaw;
		}
		else if (WeaponOwned[wp_missile] && Ammo[am_misl])
		{
			PendingWeapon = wp_missile;
		}
#ifdef DOOM2
		else if (WeaponOwned[wp_bfg] && Ammo[am_cell] > 40)
#else
		else if (WeaponOwned[wp_bfg]
			&& Ammo[am_cell] > BFGCELLS && (!shareware))
#endif
		{
			PendingWeapon = wp_bfg;
		}
		else
		{
			// If everything fails.
			PendingWeapon = wp_fist;
		}
	}
	while (PendingWeapon == wp_nochange);

	// Now set appropriate weapon overlay.
	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).DownState);

	return false;
}

//===========================================================================
//
//  FireWeaponSA
//
//===========================================================================

void FireWeaponSA()
{
	if (!Weapon(ViewEnts[ps_weapon]).SAAttackState)
	{
		return;
	}
	if (!CheckAmmoSA())
	{
		return;
	}

	MO.SetState(MO.MeleeState);
	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).SAAttackState);
	P_NoiseAlert(MO, MO);
}
#endif

defaultproperties
{
}

//**************************************************************************
//
//  $Log$
//  Revision 1.5  2004/11/01 07:38:21  dj_jl
//  Almost all Hexen line specials are available in all games.
//
//  Revision 1.4  2004/08/28 12:35:20  dj_jl
//  Move general client functions to player class.
//
//  Revision 1.3  2004/08/23 19:23:42  dj_jl
//  Moved weapon utilites to player class.
//
//  Revision 1.2  2004/08/22 19:23:04  dj_jl
//  Moved many functions to player class.
//
//  Revision 1.1  2003/11/12 16:49:08  dj_jl
//  Changed player structure into a class
//
//**************************************************************************
