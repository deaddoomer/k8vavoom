//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

//***************************************************************************
//
//  WEAPON UTILITES
//
//***************************************************************************

//==========================================================================
//
//	SetWeapon
//
//==========================================================================

void SetWeapon(Player player, int NewWeapon)
{
	player.ReadyWeapon = NewWeapon;
	player.PendingWeapon = wp_nochange;
	if (player.ViewEnts[ps_weapon])
	{
		player.ViewEnts[ps_weapon].Destroy();
	}
	player.ViewEnts[ps_weapon] = SpawnObject(WeaponClasses[NewWeapon], none);
	player.ViewEnts[ps_weapon].Player = player;
	player.WeaponModel =
		GetModelIndex(Weapon(player.ViewEnts[ps_weapon]).ThirdPersonModel);
}

//===========================================================================
//
//  SetPsprite
//
//===========================================================================

void SetPsprite(Player player, int position, int stnum)
{
	player.ViewEnts[position].SetState(stnum);
}

//===========================================================================
//
//  BringUpWeapon
//
//  Starts bringing the pending weapon up from the bottom of the screen.
// Uses player
//
//===========================================================================

void BringUpWeapon(Player player)
{
#ifdef DDF
	if (Weapon(player.ViewEnts[ps_weapon]).StartSound)
		player.MO.PlaySound(Weapon(player.ViewEnts[ps_weapon]).StartSound, CHAN_WEAPON);
#else
	if (player.ReadyWeapon == wp_chainsaw)
		player.MO.PlaySound('ChainsawStart', CHAN_WEAPON);
#endif

	player.PendingWeapon = wp_nochange;
	player.ViewEnts[ps_weapon].SY = WEAPONBOTTOM;

	SetPsprite(player, ps_weapon, Weapon(player.ViewEnts[ps_weapon]).UpState);
}

//==========================================================================
//
//  ChangeWeapon
//
//  The actual changing of the weapon is done when the weapon psprite can
// do it (read: not in the middle of an attack).
//
//==========================================================================

void ChangeWeapon(Player player, int newweapon)
{
#ifdef DDF
	int count;
	int i;
	int start;
	int choices[8];
	int key;
	Weapon tmp;

	count = 0;
	start = 0;
	for (i = 0; i < NUMWEAPONS; i++)
	{
		if (!player.WeaponOwned[i])
			continue;
		if (!WeaponClasses[i])
			continue;
		tmp = Weapon(SpawnObject(WeaponClasses[i], none));
		if (tmp.BindKey == ((newweapon + 1) % 10))
		{
			choices[count] = i;
			if (i == player.ReadyWeapon)
				start = count;
			count++;
		}
		tmp.Destroy();
	}
	if (count)
	{
		newweapon = choices[(start + 1) % count];
		if (newweapon != player.ReadyWeapon)
		{
			player.PendingWeapon = newweapon;
		}
	}
#else //---------
	if (newweapon == wp_fist && player.WeaponOwned[wp_chainsaw] &&
		!(player.ReadyWeapon == wp_chainsaw && player.Powers[pw_strength]))
	{
		newweapon = wp_chainsaw;
	}

#ifdef DOOM2
	if (newweapon == wp_shotgun && player.WeaponOwned[wp_supershotgun]
		&& player.ReadyWeapon != wp_supershotgun)
	{
		newweapon = wp_supershotgun;
	}
#endif

#ifdef GRENADE_LAUNCHER
	if (newweapon == wp_missile && player.WeaponOwned[wp_grenade]
		&& player.ReadyWeapon != wp_grenade)
	{
		newweapon = wp_grenade;
	}
#endif

	if (player.WeaponOwned[newweapon] && newweapon != player.ReadyWeapon)
	{
#ifdef DOOM2
		player.PendingWeapon = newweapon;
#else
		// Do not go to plasma or BFG in shareware, even if cheated.
		if ((newweapon != wp_plasma && newweapon != wp_bfg) || !shareware)
		{
			player.PendingWeapon = newweapon;
		}
#endif
	}
#endif
}

//===========================================================================
//
//  DropWeapon
//
//  Player died, so put the weapon away.
//
//===========================================================================

void DropWeapon(Player player)
{
	SetPsprite(player, ps_weapon, Weapon(player.ViewEnts[ps_weapon]).DownState);
}

//===========================================================================
//
//  SetupPsprites
//
//  Called at start of level for each player.
//
//===========================================================================

void SetupPsprites(Player player)
{
	int i;

	// remove all psprites
	for (i = 0; i < NUMPSPRITES; i++)
	{
		player.ViewEnts[i].StateNum = 0;
	}

	// spawn the gun
	BringUpWeapon(player);
}

//==========================================================================
//
//  MovePsprites
//
//  Called every tic by player thinking routine.
//
//==========================================================================

void MovePsprites(Player player)
{
	int i;
	ViewEntity e;

	for (i = 0; i < NUMPSPRITES; i++)
	{
		e = player.ViewEnts[i];
		// a null state means not active
		if (e.StateNum)
		{
			// drop tic count and possibly change state

			// a -1 tic count never changes
			if (e.StateTime != -1.0)
			{
				e.StateTime -= frametime;
				if (e.StateTime <= 0.0)
				{
					e.StateTime = 0.0;
					SetPsprite(player, i, e.NextState);
				}
			}
		}
	}

	player.ViewEnts[ps_flash].SX = player.ViewEnts[ps_weapon].SX;
	player.ViewEnts[ps_flash].SY = player.ViewEnts[ps_weapon].SY;
}

//==========================================================================
//
//  CheckAmmo
//
//  Returns true if there is enough ammo to shoot.
// If not, selects the next weapon to use.
//
//==========================================================================

int CheckAmmo(Player player)
{
	int ammo;
	int count;

	ammo = Weapon(player.ViewEnts[ps_weapon]).Ammo;

#ifdef DDF
	count = Weapon(player.ViewEnts[ps_weapon]).AmmoPerShot;
#else
	// Minimal amount for one shot varies.
	if (player.ReadyWeapon == wp_bfg)
		count = BFGCELLS;
#ifdef DOOM2
	else if (player.ReadyWeapon == wp_supershotgun)
		count = 2;	// Double barrel.
#endif
	else
		count = 1;	// Regular.
#endif

	// Some do not need ammunition anyway.
	// Return if current ammunition sufficient.
	if (ammo == am_noammo || player.Ammo[ammo] >= count)
		return true;

	// Out of ammo, pick a weapon to change to.
	// Preferences are set here.
	do
	{
#ifndef DOOM2
		if (player.WeaponOwned[wp_plasma]
			&& player.Ammo[am_cell] && (!shareware))
#else
		if (player.WeaponOwned[wp_plasma] && player.Ammo[am_cell])
#endif
		{
			player.PendingWeapon = wp_plasma;
		}
#ifdef DOOM2
		else if (player.WeaponOwned[wp_supershotgun]
			&& player.Ammo[am_shell] > 2)
		{
			player.PendingWeapon = wp_supershotgun;
		}
#endif
		else if (player.WeaponOwned[wp_chaingun] && player.Ammo[am_clip])
		{
			player.PendingWeapon = wp_chaingun;
		}
		else if (player.WeaponOwned[wp_shotgun] && player.Ammo[am_shell])
		{
			player.PendingWeapon = wp_shotgun;
		}
		else if (player.Ammo[am_clip])
		{
			player.PendingWeapon = wp_pistol;
		}
		else if (player.WeaponOwned[wp_chainsaw])
		{
			player.PendingWeapon = wp_chainsaw;
		}
		else if (player.WeaponOwned[wp_missile] && player.Ammo[am_misl])
		{
			player.PendingWeapon = wp_missile;
		}
#ifdef DOOM2
		else if (player.WeaponOwned[wp_bfg] && player.Ammo[am_cell] > 40)
#else
		else if (player.WeaponOwned[wp_bfg]
			&& player.Ammo[am_cell] > BFGCELLS && (!shareware))
#endif
		{
			player.PendingWeapon = wp_bfg;
		}
		else
		{
			// If everything fails.
			player.PendingWeapon = wp_fist;
		}
	}
	while (player.PendingWeapon == wp_nochange);

	// Now set appropriate weapon overlay.
	SetPsprite(player, ps_weapon, Weapon(player.ViewEnts[ps_weapon]).DownState);

	return false;
}

//===========================================================================
//
//  FireWeapon
//
//===========================================================================

void FireWeapon(Player player)
{
	if (!CheckAmmo(player))
	{
		return;
	}

	player.MO.SetState(player.MO.MeleeState);
	SetPsprite(player, ps_weapon, Weapon(player.ViewEnts[ps_weapon]).AttackState);
	P_NoiseAlert(player.MO, player.MO);
}

#ifdef DDF
//==========================================================================
//
//  CheckAmmoSA
//
//  Returns true if there is enough ammo to shoot.
// If not, selects the next weapon to use.
//
//==========================================================================

int CheckAmmoSA(Player player)
{
	int ammo;
	int count;

	ammo = Weapon(player.ViewEnts[ps_weapon]).SAAmmo;

	count = Weapon(player.ViewEnts[ps_weapon]).SAAmmoPerShot;

	// Some do not need ammunition anyway.
	// Return if current ammunition sufficient.
	if (ammo == am_noammo || player.Ammo[ammo] >= count)
		return true;

	// Out of ammo, pick a weapon to change to.
	// Preferences are set here.
	do
	{
#ifndef DOOM2
		if (player.WeaponOwned[wp_plasma]
			&& player.Ammo[am_cell] && (!shareware))
#else
		if (player.WeaponOwned[wp_plasma] && player.Ammo[am_cell])
#endif
		{
			player.PendingWeapon = wp_plasma;
		}
#ifdef DOOM2
		else if (player.WeaponOwned[wp_supershotgun]
			&& player.Ammo[am_shell] > 2)
		{
			player.PendingWeapon = wp_supershotgun;
		}
#endif
		else if (player.WeaponOwned[wp_chaingun] && player.Ammo[am_clip])
		{
			player.PendingWeapon = wp_chaingun;
		}
		else if (player.WeaponOwned[wp_shotgun] && player.Ammo[am_shell])
		{
			player.PendingWeapon = wp_shotgun;
		}
		else if (player.Ammo[am_clip])
		{
			player.PendingWeapon = wp_pistol;
		}
		else if (player.WeaponOwned[wp_chainsaw])
		{
			player.PendingWeapon = wp_chainsaw;
		}
		else if (player.WeaponOwned[wp_missile] && player.Ammo[am_misl])
		{
			player.PendingWeapon = wp_missile;
		}
#ifdef DOOM2
		else if (player.WeaponOwned[wp_bfg] && player.Ammo[am_cell] > 40)
#else
		else if (player.WeaponOwned[wp_bfg]
			&& player.Ammo[am_cell] > BFGCELLS && (!shareware))
#endif
		{
			player.PendingWeapon = wp_bfg;
		}
		else
		{
			// If everything fails.
			player.PendingWeapon = wp_fist;
		}
	}
	while (player.PendingWeapon == wp_nochange);

	// Now set appropriate weapon overlay.
	SetPsprite(player, ps_weapon, Weapon(player.ViewEnts[ps_weapon]).DownState);

	return false;
}

//===========================================================================
//
//  FireWeaponSA
//
//===========================================================================

void FireWeaponSA(Player player)
{
	if (!Weapon(player.ViewEnts[ps_weapon]).SAAttackState)
	{
		return;
	}
	if (!CheckAmmoSA(player))
	{
		return;
	}

	player.MO.SetState(player.MO.MeleeState);
	SetPsprite(player, ps_weapon, Weapon(player.ViewEnts[ps_weapon]).SAAttackState);
	P_NoiseAlert(player.MO, player.MO);
}

#endif
//===========================================================================
//
//  GunShot
//
//===========================================================================

void GunShot(Actor A, int accurate, TVec dir)
{
	int damage;

	//  Make a copy of angles because inacurate shoot will modify them
	if (!accurate)
	{
		TAVec angles;

		VectorAngles(&dir, &angles);
		angles.yaw = AngleMod360(angles.yaw +
			(Random() - Random()) * 45.0 / 8.0);
		AngleVector(&angles, &dir);
	}

	damage = 5 * (P_Random() % 3 + 1);

	LineAttack(A, dir, MISSILERANGE, damage);
}

//**************************************************************************
//
//  $Log$
//  Revision 1.6  2003/11/12 16:48:33  dj_jl
//  Changed player structure into a class
//
//  Revision 1.5  2002/10/26 16:32:21  dj_jl
//  New style of defining classes.
//
//  Revision 1.4  2002/07/23 13:07:30  dj_jl
//  Final touches for switching to floating-point time, beautification.
//
//  Revision 1.3  2002/06/14 15:51:21  dj_jl
//  A lots of changes, new functions etc for DDF.
//
//  Revision 1.2  2002/05/18 16:59:59  dj_jl
//  Renamed SpawnState to IdleState, other changes.
//
//  Revision 1.1  2002/05/03 17:11:55  dj_jl
//  Object classes split up into their files.
//
//**************************************************************************
