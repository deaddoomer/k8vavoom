//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

//**************************************************************************
//
//	SpiderMastermind
//
//**************************************************************************

class SpiderMastermind:Pawn
	__mobjinfo__(7)
{

//==========================================================================
//
//  A_Metal
//
//  Cyberdemon move with metal sound.
//
//==========================================================================

void A_Metal(void)
{
	PlaySound('CyberWalk2', CHAN_BODY);
	A_Chase();
}

//==========================================================================
//
//  A_SPosAttack
//
//  Shotgun guy, Spider mastermind attack.
//
//==========================================================================

void A_SPosAttack(void)
{
	int i;
	int damage;
	TVec aimDir;
	TVec dir;

	if (!Enemy)
		return;

	PlaySound('ShotgunFire', CHAN_WEAPON);
	A_FaceTarget();
	aimDir = AimLineAttack(self, Angles, MISSILERANGE);
	for (i = 0; i < 3; i++)
	{
		dir = aimDir;
		VectorRotateAroundZ(&dir, (Random() - Random()) * 45.0 / 2.0);
		damage = ((P_Random() % 5) + 1) * 3;
		LineAttack(self, dir, MISSILERANGE, damage);
	}

	Effects |= EF_DL_MUZZLEFLASH;
}

//==========================================================================
//
//  A_SpidRefire
//
//  Spider mastermind refire.
//
//==========================================================================

void A_SpidRefire(void)
{
	// keep firing unless target got out of sight
	A_FaceTarget();

	if (P_Random() < 10)
		return;

	if (!Enemy || Enemy.Health <= 0 || !P_CheckSight(self, Enemy))
	{
		SetState(SeeState);
	}
}

//==========================================================================
//
//  A_BossDeath
//
//  Possibly trigger special effects if on first boss level
//
//==========================================================================

void A_BossDeath(void)
{
	string mapname;
	int i;
	Actor Other;
	int args[5];

	mapname = ARR2STR(level->mapname);
	if (strcmp(mapname, "E3M8") && strcmp(mapname, "E4M8"))
	{
		//  Not a boss level
		return;
	}

	// make sure there is a player alive for victory
	for (i = 0; i < MAXPLAYERS; i++)
		if (players[i].bSpawned && players[i].Health > 0)
			break;

	if (i == MAXPLAYERS)
		return;	// no one left alive, so do not end game

	// scan the remaining thinkers to see if all bosses are dead
	FOREACH(Actor, Other)
	{
		if (Other != self && Other.Class == Class && Other.Health > 0)
		{
			// other boss not dead
			return;
		}
	}

	// victory!
	if (!strcmp(mapname, "E4M8"))
	{
		args[0] = 666;
		args[1] = 8;
		args[2] = 0;
		args[3] = 0;
		args[4] = 0;
		EV_DoFloor(NULL, &args[0], FLEV_LOWERFLOORTOLOWEST);
		return;
	}

	G_ExitLevel();
}

__states__
{
	S_SPID_STND('SPID', 0, 'monsters/spider.md2', 0, 10.0 / 35.0, S_SPID_STND2) { A_Look(); }
	S_SPID_STND2('SPID', 1, 'monsters/spider.md2', 1, 10.0 / 35.0, S_SPID_STND) { A_Look(); }
	S_SPID_RUN1('SPID', 0, 'monsters/spider.md2', 2, 3.0 / 35.0, S_SPID_RUN2) { A_Metal(); }
	S_SPID_RUN2('SPID', 0, 'monsters/spider.md2', 3, 3.0 / 35.0, S_SPID_RUN3) { A_Chase(); }
	S_SPID_RUN3('SPID', 1, 'monsters/spider.md2', 4, 3.0 / 35.0, S_SPID_RUN4) { A_Chase(); }
	S_SPID_RUN4('SPID', 1, 'monsters/spider.md2', 5, 3.0 / 35.0, S_SPID_RUN5) { A_Chase(); }
	S_SPID_RUN5('SPID', 2, 'monsters/spider.md2', 6, 3.0 / 35.0, S_SPID_RUN6) { A_Metal(); }
	S_SPID_RUN6('SPID', 2, 'monsters/spider.md2', 7, 3.0 / 35.0, S_SPID_RUN7) { A_Chase(); }
	S_SPID_RUN7('SPID', 3, 'monsters/spider.md2', 8, 3.0 / 35.0, S_SPID_RUN8) { A_Chase(); }
	S_SPID_RUN8('SPID', 3, 'monsters/spider.md2', 9, 3.0 / 35.0, S_SPID_RUN9) { A_Chase(); }
	S_SPID_RUN9('SPID', 4, 'monsters/spider.md2', 10, 3.0 / 35.0, S_SPID_RUN10) { A_Metal(); }
	S_SPID_RUN10('SPID', 4, 'monsters/spider.md2', 11, 3.0 / 35.0, S_SPID_RUN11) { A_Chase(); }
	S_SPID_RUN11('SPID', 5, 'monsters/spider.md2', 12, 3.0 / 35.0, S_SPID_RUN12) { A_Chase(); }
	S_SPID_RUN12('SPID', 5, 'monsters/spider.md2', 13, 3.0 / 35.0, S_SPID_RUN1) { A_Chase(); }
	S_SPID_ATK1('SPID', 0 | FF_FULLBRIGHT, 'monsters/spider.md2', 14, 20.0 / 35.0, S_SPID_ATK2) { A_FaceTarget(); }
	S_SPID_ATK2('SPID', 6 | FF_FULLBRIGHT, 'monsters/spider.md2', 15, 4.0 / 35.0, S_SPID_ATK3) { A_SPosAttack(); }
	S_SPID_ATK3('SPID', 7 | FF_FULLBRIGHT, 'monsters/spidatk.md2', 0, 4.0 / 35.0, S_SPID_ATK4) { A_SPosAttack(); }
	S_SPID_ATK4('SPID', 7 | FF_FULLBRIGHT, 'monsters/spidatk.md2', 0, 1.0 / 35.0, S_SPID_ATK2) { A_SpidRefire(); }
	S_SPID_PAIN('SPID', 8, 'monsters/spider.md2', 16, 3.0 / 35.0, S_SPID_PAIN2) { }
	S_SPID_PAIN2('SPID', 8, 'monsters/spider.md2', 17, 3.0 / 35.0, S_SPID_RUN1) { A_Pain(); }
	S_SPID_DIE1('SPID', 9, 'monsters/spiddie.md2', 0, 20.0 / 35.0, S_SPID_DIE2) { A_Scream(); }
	S_SPID_DIE2('SPID', 10, 'monsters/spiddie.md2', 1, 10.0 / 35.0, S_SPID_DIE3) { A_Fall(); }
	S_SPID_DIE3('SPID', 11, 'monsters/spiddie.md2', 2, 10.0 / 35.0, S_SPID_DIE4) { }
	S_SPID_DIE4('SPID', 12, 'monsters/spiddie.md2', 3, 10.0 / 35.0, S_SPID_DIE5) { }
	S_SPID_DIE5('SPID', 13, 'monsters/spiddie.md2', 4, 10.0 / 35.0, S_SPID_DIE6) { }
	S_SPID_DIE6('SPID', 14, 'monsters/spiddie.md2', 5, 10.0 / 35.0, S_SPID_DIE7) { }
	S_SPID_DIE7('SPID', 15, 'monsters/spiddie.md2', 6, 10.0 / 35.0, S_SPID_DIE8) { }
	S_SPID_DIE8('SPID', 16, 'monsters/spiddie.md2', 7, 10.0 / 35.0, S_SPID_DIE9) { }
	S_SPID_DIE9('SPID', 17, 'monsters/spiddie.md2', 8, 10.0 / 35.0, S_SPID_DIE10) { }
	S_SPID_DIE10('SPID', 18, 'monsters/spiddie.md2', 9, 30.0 / 35.0, S_SPID_DIE11) { }
	S_SPID_DIE11('SPID', 18, 'monsters/spiddie.md2', 10, -1.0, S_NULL) { A_BossDeath(); }
}

defaultproperties
{
	Health = 3000;
	Radius = 128.0;
	Height = 100.0;
	Mass = 1000.0;
	StepSpeed = 12.0;
	PainChance = 40;
	bBoss = true;
	bTriggerHappy = true;
	IdleState = S_SPID_STND;
	SeeState = S_SPID_RUN1;
	MissileState = S_SPID_ATK1;
	PainState = S_SPID_PAIN;
	DeathState = S_SPID_DIE1;
	SightSound = 'SpiderSight';
	ActiveSound = 'DemonActive';
	AttackSound = 'ShotgunFire';
	PainSound = 'DemonPain';
	DeathSound = 'SpiderDeath';
}

}

//**************************************************************************
//
//  $Log$
//  Revision 1.13  2002/05/18 16:59:59  dj_jl
//  Renamed SpawnState to IdleState, other changes.
//
//  Revision 1.12  2002/05/03 17:11:55  dj_jl
//  Object classes split up into their files.
//
//  Revision 1.11  2002/03/20 19:07:44  dj_jl
//  Got rid of a lots of legacy, beautification.
//
//  Revision 1.10  2002/03/02 17:41:12  dj_jl
//  Renamed Enemy to Target. Beautification.
//
//  Revision 1.9  2002/02/26 17:49:47  dj_jl
//  no message
//
//  Revision 1.8  2002/01/28 18:50:18  dj_jl
//  Made some functions Actor members.
//  Replaced some pointers with references.
//
//  Revision 1.7  2002/01/24 18:19:25  dj_jl
//  Split common class Entity, made SetMobjState an Entity function
//
//  Revision 1.6  2002/01/15 18:26:59  dj_jl
//  Some property names with logical words starting with capital letter.
//  Moved everything from Entity class into Actor class.
//
//  Revision 1.5  2002/01/07 12:21:11  dj_jl
//  Changed copyright year
//
//  Revision 1.4  2001/12/12 18:57:40  dj_jl
//  State functions now are methods of Actor and Weapon classes
//
//  Revision 1.3  2001/09/24 17:24:09  dj_jl
//  Beautification
//
//  Revision 1.2  2001/07/27 14:27:51  dj_jl
//  Update with Id-s and Log-s, some fixes
//
//**************************************************************************
