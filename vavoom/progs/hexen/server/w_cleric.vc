//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

// MACROS ------------------------------------------------------------------

// CONSTANTS ---------------------------------------------------------------

// TYPES -------------------------------------------------------------------

// FUNCTION PROTOTYPES -----------------------------------------------------

// DATA DECLARATIONS -------------------------------------------------------

// CODE --------------------------------------------------------------------

//===========================================================================
//
//  Weapon::A_CMaceAttack
//
//===========================================================================

void Weapon::A_CMaceAttack(void)
{
	int damage;
	int i;
	TAVec angles;
	TVec vforward;

	damage = 25 + (P_Random() & 15);
	PuffType = HammerPuff;
	for (i = 0; i < 16; i++)
	{
		angles = Player->mo->Angles;
		angles.yaw = AngleMod360(angles.yaw + itof(i) * (45.0 / 16.0));
		AimLineAttack(Player->mo, &angles, 2.0 * MELEERANGE);
		if (linetarget)
		{
			AngleVector(&angles, &vforward);
			if (LineAttack(Player->mo, &vforward, 2.0 * MELEERANGE, damage))
			{
				Player->mo->PlaySound('FighterHammerMiss', CHAN_WEAPON);
			}
			AdjustPlayerAngle(Player->mo);
			return;
		}
		angles = Player->mo->Angles;
		angles.yaw = AngleMod360(angles.yaw - itof(i) * (45.0 / 16.0));
		AimLineAttack(Player->mo, &angles, 2.0 * MELEERANGE);
		if (linetarget)
		{
			AngleVector(&angles, &vforward);
			if (LineAttack(Player->mo, &vforward, 2.0 * MELEERANGE, damage))
			{
				Player->mo->PlaySound('FighterHammerMiss', CHAN_WEAPON);
			}
			AdjustPlayerAngle(Player->mo);
			return;
		}
	}
	// didn't find any creatures, so try to strike any walls
	Player->mo->Special1 = 0;

	avcpy(Player->mo->Angles, angles);
	AimLineAttack(Player->mo, &angles, MELEERANGE);
	AngleVector(&angles, &vforward);
	if (LineAttack(Player->mo, &vforward, MELEERANGE, damage))
	{
		Player->mo->PlaySound('FighterHammerMiss', CHAN_WEAPON);
	}
}

//============================================================================
//
//  Weapon::A_CStaffInitBlink
//
//============================================================================

void Weapon::A_CStaffInitBlink(void)
{
	Player->mo->Special1 = (P_Random() >> 1) + 20;
}

//============================================================================
//
//  Weapon::A_CStaffCheckBlink
//
//============================================================================

void Weapon::A_CStaffCheckBlink(void)
{
	if (!--Player->mo->Special1)
	{
		SetPsprite(Player, ps_weapon, S_CSTAFFBLINK1);
		Player->mo->Special1 = (P_Random() + 50) >> 2;
	}
}

//============================================================================
//
//  Weapon::A_CStaffCheck
//
//============================================================================

void Weapon::A_CStaffCheck(void)
{
	mobj_t *pmo;
	int damage;
	int newLife;
	int i;
	TAVec angles;
	TVec vforward;

	pmo = Player->mo;
	damage = 20 + (P_Random() & 15);
	PuffType = ClericStaffPuff;
	for (i = 0; i < 3; i++)
	{
		angles = pmo->Angles;
		angles.yaw = AngleMod360(angles.yaw + itof(i) * (45.0 / 16.0));
		AimLineAttack(pmo, &angles, 3.0 * MELEERANGE / 2.0);
		if (linetarget)
		{
			AngleVector(&angles, &vforward);
			LineAttack(pmo, &vforward, 1.5 * MELEERANGE, damage);
			pmo->Angles.yaw = atan2(linetarget->Origin.y - pmo->Origin.y,
				linetarget->Origin.x - pmo->Origin.x);
			if ((linetarget->bIsPlayer || linetarget->bCountKill) &&
				!linetarget->bDormant && !linetarget->bInvulnerable)
			{
				newLife = Player->Health + (damage >> 3);
				newLife = newLife > 100 ? 100 : newLife;
				pmo->Health = newLife;
				Player->Health = newLife;
				SetPsprite(Player, ps_weapon, S_CSTAFFATK2_1);
			}
			Player->Mana[MANA_1] -=
				WeaponManaUse(Player->PClass, Player->ReadyWeapon);
			break;
		}
		angles = pmo->Angles;
		angles.yaw = AngleMod360(angles.yaw - itof(i) * (45.0 / 16.0));
		AimLineAttack(Player->mo, &angles, 3.0 * MELEERANGE / 2.0);
		if (linetarget)
		{
			AngleVector(&angles, &vforward);
			LineAttack(pmo, &vforward, 1.5 * MELEERANGE, damage);
			pmo->Angles.yaw = atan2(linetarget->Origin.y - pmo->Origin.y,
				linetarget->Origin.x - pmo->Origin.x);
			if (linetarget->bIsPlayer || linetarget->bCountKill)
			{
				newLife = Player->Health + (damage >> 4);
				newLife = newLife > 100 ? 100 : newLife;
				pmo->Health = newLife;
				Player->Health = pmo->Health;
				SetPsprite(Player, ps_weapon, S_CSTAFFATK2_1);
			}
			Player->Mana[MANA_1] -=
				WeaponManaUse(Player->PClass, Player->ReadyWeapon);
			break;
		}
	}
}

//============================================================================
//
//  Weapon::A_CStaffAttack
//
//============================================================================

void Weapon::A_CStaffAttack(void)
{
	Actor mo;
	mobj_t *pmo;

	Player->Mana[MANA_1] -=
		WeaponManaUse(Player->PClass, Player->ReadyWeapon);
	pmo = Player->mo;
	mo = pmo->SpawnPlayerMissileAngle(ClericStaffMissile,
		pmo->Angles.yaw - (45.0 / 15.0));
	if (mo)
	{
		mo.Special2 = 32;
	}
	mo = pmo->SpawnPlayerMissileAngle(ClericStaffMissile,
		pmo->Angles.yaw + (45.0 / 15.0));
	if (mo)
	{
		mo.Special2 = 0;
	}
	Player->mo->PlaySound('ClericCStaffFire', CHAN_WEAPON);
}

//============================================================================
//
//  Actor::A_CStaffMissileSlither
//
//============================================================================

void Actor::A_CStaffMissileSlither(void)
{
	float newX, newY;
	int weaveXY;
	float angle;

	weaveXY = Special2;
	angle = AngleMod360(Angles.yaw + 90.0);
	newX = Origin.x - cos(angle) * FloatBobOffsets[weaveXY];
	newY = Origin.y - sin(angle) * FloatBobOffsets[weaveXY];
	weaveXY = (weaveXY + 3) & 63;
	newX += cos(angle) * FloatBobOffsets[weaveXY];
	newY += sin(angle) * FloatBobOffsets[weaveXY];
	P_TryMove(this, newX, newY);
	Special2 = weaveXY;
}

//============================================================================
//
//  Weapon::A_CFlameAttack
//
//============================================================================

void Weapon::A_CFlameAttack(void)
{
	Actor mo;

	mo = Player->mo->SpawnPlayerMissile(ClericFlameMissile);
	if (mo)
	{
		mo.bBlaster = true;
		mo.Special1 = 2;
	}

	Player->Mana[MANA_2] -=
		WeaponManaUse(Player->PClass, Player->ReadyWeapon);
	Player->mo->PlaySound('ClericFlameFire', CHAN_WEAPON);
}

//============================================================================
//
//  Actor::A_CFlameMissile
//
//============================================================================

#define FLAMESPEED		(9.0 / 20.0 * 35.0)

void Actor::A_CFlameMissile(void)
{
	int i;
	float an;
	float dist;
	mobj_t *mo;
	mobj_t *bmo;

	A_UnHideThing();
	PlaySound('ClericFlameExplode', CHAN_VOICE);
	bmo = &tmtrace.BlockingMobj;
	if (bmo && bmo->bShootable)
	{
		// Hit something, so spawn the flame circle around the thing
		dist = bmo->Radius + 18.0;
		for (i = 0; i < 4; i++)
		{
			an = itof(i) * 45.0;
			mo = P_SpawnMobj(bmo->Origin.x + dist * cos(an),
				bmo->Origin.y + dist * sin(an),
				bmo->Origin.z + 5.0, CircleFlame);
			if (mo)
			{
				mo->Angles.yaw = an;
				mo->Owner = Owner;
				mo->Velocity.x = FLAMESPEED * cos(an);
				mo->Velocity.y = FLAMESPEED * sin(an);
				mo->Special1f = mo->Velocity.x;
				mo->Special2f = mo->Velocity.y;
				mo->StateTime -= Random() * 0.1;
			}
			mo = P_SpawnMobj(bmo->Origin.x - dist * cos(an),
				bmo->Origin.y - dist * sin(an),
				bmo->Origin.z + 5.0, CircleFlame);
			if (mo)
			{
				mo->Angles.yaw = AngleMod360(180.0 + an);
				mo->Owner = Owner;
				mo->Velocity.x = -FLAMESPEED * cos(an);
				mo->Velocity.y = -FLAMESPEED * sin(an);
				mo->Special1f = mo->Velocity.x;
				mo->Special2f = mo->Velocity.y;
				mo->StateTime -= Random() * 0.1;
			}
		}
		SetState(S_FLAMEPUFF2_1);
	}
}

//============================================================================
//
//	Actor::A_CFlameRotate
//
//============================================================================

#define FLAMEROTSPEED	2.0 * 35.0

void Actor::A_CFlameRotate(void)
{
	float an;

	an = AngleMod360(Angles.yaw + 90.0);
	Velocity.x = Special1f + FLAMEROTSPEED * cos(an);
	Velocity.y = Special2f + FLAMEROTSPEED * sin(an);
	Angles.yaw = AngleMod360(Angles.yaw + 90.0 / 15.0);
}

//============================================================================
//
//	Actor::A_CFlamePuff
//
//============================================================================

void Actor::A_CFlamePuff(void)
{
	A_UnHideThing();
	Velocity = vector(0.0, 0.0, 0.0);
	PlaySound('ClericFlameExplode', CHAN_VOICE);
}

//============================================================================
//
//  Weapon::A_CHolyAttack
//
//============================================================================

void Weapon::A_CHolyAttack(void)
{
	Player->Mana[MANA_1] -=
		WeaponManaUse(Player->PClass, Player->ReadyWeapon);
	Player->Mana[MANA_2] -=
		WeaponManaUse(Player->PClass, Player->ReadyWeapon);
	Player->mo->SpawnPlayerMissile(HolyMissile);
	Player->DamageCount = 0;
	Player->BonusCount = 0;
	Player->WeaponPalette = STARTHOLYPAL;
	Player->mo->PlaySound('HolySymbolFire', CHAN_WEAPON);
}

//============================================================================
//
//  Weapon::A_CHolyPalette
//
//============================================================================

void Weapon::A_CHolyPalette(void)
{
	int pal;

	pal = STARTHOLYPAL + StateNum - S_CHOLYATK_6;
	if (pal == STARTHOLYPAL + 3)
	{
		// reset back to original playpal
		pal = 0;
	}
	Player->WeaponPalette = pal;
}

//============================================================================
//
//  Actor::A_CHolyAttack2
//
//  Spawns the spirits
//
//============================================================================

void Actor::A_CHolyAttack2(void)
{
	int j;
	int i;
	mobj_t *mo;
	mobj_t *tail;
	mobj_t *next;

	for (j = 0; j < 4; j++)
	{
		mo = P_SpawnMobj(Origin.x, Origin.y, Origin.z, HolyFX);
		if (!mo)
		{
			continue;
		}
		switch (j)
		{	// float bob index
		case 0:
			mo->Special2 = P_Random() & 7;	// upper-left
			break;
		case 1:
			mo->Special2 = 32 + (P_Random() & 7);	// upper-right
			break;
		case 2:
			mo->Special2 = (32 + (P_Random() & 7)) << 16;	// lower-left
			break;
		case 3:
			mo->Special2 =
				((32 + (P_Random() & 7)) << 16) + 32 + (P_Random() & 7);
			break;
		}
		mo->Origin.z = Origin.z;
		mo->Angles.yaw = AngleMod360(Angles.yaw +
			(45.0 + 45.0 / 2.0) - 45.0 * itof(j));
		mo->Thrust(mo->Angles.yaw, mo->Speed * frametime);
		mo->Owner = Owner;
		mo->Args[0] = 10;	// initial turn value
		mo->Args[1] = 0;	// initial look angle
		if (deathmatch)
		{
			// Ghosts last slightly less longer in DeathMatch
			mo->Health = 85;
		}
		if (linetarget)
		{
			mo->SpecialMobj1 = linetarget;
			mo->bNoClip = true;
			mo->bSkullFly = true;
			mo->bMissile = false;
		}
		tail = P_SpawnMobj(mo->Origin.x, mo->Origin.y, mo->Origin.z,
			HolyTail);
		tail->SpecialMobj2 = mo;	// parent
		for (i = 1; i < 3; i++)
		{
			next = P_SpawnMobj(mo->Origin.x, mo->Origin.y, mo->Origin.z,
				HolyTail);
			next->SetState(next->SpawnState + 1);
			tail->SpecialMobj1 = next;
			tail = next;
		}
		tail->SpecialMobj1 = NULL;	// last tail bit
	}
}

//============================================================================
//
//  Actor::CHolyFindTarget
//
//============================================================================

void Actor::CHolyFindTarget(void)
{
	Actor targ;

	targ = RoughMonsterSearch(6);
	if (targ)
	{
		SpecialMobj1 = &targ;
		bNoClip = true;
		bSkullFly = true;
		bMissile = false;
	}
}

//============================================================================
//
//  Actor::CHolySeekerMissile
//
//  Similar to SeekerMissile, but seeks to a random Z on the target
//
//============================================================================

void Actor::CHolySeekerMissile(float thresh, float turnMax)
{
	int dir;
	float dist;
	float delta;
	float angle;
	mobj_t *targ;
	float newZ;
	float deltaZ;

	targ = SpecialMobj1;
	if (!targ)
	{
		return;
	}
	if (!targ->bShootable || (!targ->bCountKill && !targ->bIsPlayer))
	{
		// Target died/target isn't a player or creature
		SpecialMobj1 = NULL;
		bNoClip = false;
		bSkullFly = false;
		bMissile = true;
		CHolyFindTarget();
		return;
	}
	dir = FaceActor(*targ, &delta);
	if (delta > thresh)
	{
		delta /= 2.0;
		if (delta > turnMax)
		{
			delta = turnMax;
		}
	}
	if (dir)
	{
		// Turn clockwise
		Angles.yaw += delta;
	}
	else
	{
		// Turn counter clockwise
		Angles.yaw -= delta;
	}
	Angles.yaw = AngleMod360(Angles.yaw);
	angle = Angles.yaw;
	Velocity.x = Speed * cos(angle);
	Velocity.y = Speed * sin(angle);
	if (!(level->tictime & 15) ||
		Origin.z > targ->Origin.z + targ->Height ||
		Origin.z + Height < targ->Origin.z)
	{
		newZ = targ->Origin.z + Random() * targ->Height;
		deltaZ = newZ - Origin.z;
		if (fabs(deltaZ) > 15.0)
		{
			if (deltaZ > 0.0)
			{
				deltaZ = 15.0;
			}
			else
			{
				deltaZ = -15.0;
			}
		}
		dist = MobjDist2(*targ, self);
		dist = dist / Speed;
		if (dist < 1.0)
		{
			dist = 1.0;
		}
		Velocity.z = deltaZ / dist;
	}
	return;
}

//============================================================================
//
//	Actor::CHolyWeave
//
//============================================================================

void Actor::CHolyWeave(void)
{
	float newX, newY;
	int weaveXY, weaveZ;
	float angle;

	weaveXY = Special2 >> 16;
	weaveZ = Special2 & 0xFFFF;
	angle = AngleMod360(Angles.yaw + 90.0);
	newX = Origin.x - cos(angle) * FloatBobOffsets[weaveXY] * 4.0;
	newY = Origin.y - sin(angle) * FloatBobOffsets[weaveXY] * 4.0;
	weaveXY = (weaveXY + (P_Random() % 5)) & 63;
	newX += cos(angle) * FloatBobOffsets[weaveXY] * 4.0;
	newY += sin(angle) * FloatBobOffsets[weaveXY] * 4.0;
	P_TryMove(this, newX, newY);
	Origin.z -= FloatBobOffsets[weaveZ] * 2.0;
	weaveZ = (weaveZ + (P_Random() % 5)) & 63;
	Origin.z += FloatBobOffsets[weaveZ] * 2.0;
	Special2 = weaveZ + (weaveXY << 16);
}

//============================================================================
//
//	Actor::A_CHolySeek
//
//============================================================================

void Actor::A_CHolySeek(void)
{
	Health--;
	if (Health <= 0)
	{
		Velocity.x /= 4.0;
		Velocity.y /= 4.0;
		Velocity.z = 0.0;
		SetState(DeathState);
		StateTime -= Random() * 0.1;
		return;
	}
	if (SpecialMobj1)
	{
		CHolySeekerMissile(itof(Args[0]), itof(Args[0]) * 2.0);
		if (!((level->tictime + 7) & 15))
		{
			Args[0] = 5 + (P_Random() / 20);
		}
	}
	CHolyWeave();
}

//============================================================================
//
//	Actor::A_CHolyCheckScream
//
//============================================================================

void Actor::A_CHolyCheckScream(void)
{
	A_CHolySeek();
	if (P_Random() < 20)
	{
		PlaySound('SpiritActive', CHAN_VOICE);
	}
	if (!SpecialMobj1)
	{
		CHolyFindTarget();
	}
}

//============================================================================
//
//	Actor::CHolyTailFollow
//
//============================================================================

void Actor::CHolyTailFollow(float dist)
{
	mobj_t *child;
	float an;
	float oldDistance, newDistance;

	child = SpecialMobj1;
	if (child)
	{
		an = atan2(child->Origin.y - Origin.y,
			Origin.x - child->Origin.x);
		oldDistance = MobjDist(*child, self);
		if (P_TryMove(child, Origin.x + dist * cos(an),
				Origin.y + dist * sin(an)))
		{
			newDistance = MobjDist2(*child, self) - 1.0;
			if (oldDistance < 1.0)
			{
				if (child->Origin.z < Origin.z)
				{
					child->Origin.z = Origin.z - dist;
				}
				else
				{
					child->Origin.z = Origin.z + dist;
				}
			}
			else
			{
				child->Origin.z = Origin.z + newDistance / oldDistance *
					(child->Origin.z - Origin.z);
			}
		}
		child->CHolyTailFollow(dist - 1.0);
	}
}

//============================================================================
//
//	Actor::CHolyTailRemove
//
//============================================================================

void Actor::CHolyTailRemove(void)
{
	mobj_t *child;

	child = SpecialMobj1;
	if (child)
	{
		child->CHolyTailRemove();
	}
	P_RemoveMobj(this);
}

//============================================================================
//
//	A_CHolyTail
//
//============================================================================

void Actor::A_CHolyTail(void)
{
	mobj_t *parent;

	parent = SpecialMobj2;

	if (parent)
	{
		if (parent->StateNum >= parent->DeathState)
		{
			// Ghost removed, so remove all tail parts
			CHolyTailRemove();
			return;
		}
		else if (P_TryMove(this,
				parent->Origin.x - 14.0 * cos(parent->Angles.yaw),
				parent->Origin.y - 14.0 * sin(parent->Angles.yaw)))
		{
			Origin.z = parent->Origin.z - 5.0;
		}
		CHolyTailFollow(10.0);
	}
}

//============================================================================
//
//	Actor::A_CHolySpawnPuff
//
//============================================================================

void Actor::A_CHolySpawnPuff(void)
{
	P_SpawnMobj(Origin.x, Origin.y, Origin.z, HolyMissilePuff);
}

//**************************************************************************
//
//  $Log$
//  Revision 1.19  2002/02/26 17:49:49  dj_jl
//  no message
//
//  Revision 1.18  2002/02/22 18:00:25  dj_jl
//  A lots of the renaming, beautification.
//
//  Revision 1.17  2002/02/14 19:20:59  dj_jl
//  Renamed Entity properties to new naming style
//
//  Revision 1.16  2002/02/06 17:28:53  dj_jl
//  Replaced Actor flags with boolean variables.
//
//  Revision 1.15  2002/02/02 19:17:26  dj_jl
//  Replacing pointers with references, beautification.
//
//  Revision 1.14  2002/01/28 18:50:19  dj_jl
//  Made some functions Actor members.
//  Replaced some pointers with references.
//
//  Revision 1.13  2002/01/25 18:04:35  dj_jl
//  Many global functions made Actor functions
//
//  Revision 1.12  2002/01/24 18:19:27  dj_jl
//  Split common class Entity, made SetMobjState an Entity function
//
//  Revision 1.11  2002/01/17 18:15:55  dj_jl
//  Renamed all map object classes
//
//  Revision 1.10  2002/01/15 18:27:00  dj_jl
//  Some property names with logical words starting with capital letter.
//  Moved everything from Entity class into Actor class.
//
//  Revision 1.9  2002/01/11 18:19:42  dj_jl
//  Started to use names in progs
//
//  Revision 1.8  2002/01/07 12:25:58  dj_jl
//  Changed copyright year
//
//  Revision 1.7  2001/12/12 18:57:42  dj_jl
//  State functions now are methods of Actor and Weapon classes
//
//  Revision 1.6  2001/10/22 17:23:13  dj_jl
//  Floatification of angles
//
//  Revision 1.5  2001/10/18 17:39:34  dj_jl
//  A lots of changes for Alpha 2
//
//  Revision 1.4  2001/09/24 17:24:10  dj_jl
//  Beautification
//
//  Revision 1.3  2001/08/30 17:33:18  dj_jl
//  Added sound channels
//  
//  Revision 1.2  2001/07/27 14:27:53  dj_jl
//  Update with Id-s and Log-s, some fixes
//
//**************************************************************************
