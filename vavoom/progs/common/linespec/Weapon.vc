//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Weapon : Inventory
	abstract;

const float LOWERSPEED		= 6.0;
const float RAISESPEED		= 6.0;

const float WEAPONBOTTOM	= 128.0;
const float WEAPONTOP		= 32.0;

enum
{
	FIRE_Primary,
	FIRE_Secondary,

	//	Special value for CheckAmmo.
	FIRE_Either
};

//	Type of ammo weapon needs to operate.
class<Ammo>		AmmoType1;
class<Ammo>		AmmoType2;
//	How much ammo to give when picked up.
int				AmmoGive1;
int				AmmoGive2;
//	How much ammo used in each shot.
int				AmmoUse1;
int				AmmoUse2;
//	Weapon given together with current one.
class<Weapon>	SisterWeaponType;

name			UpSound;
name			ReadySound;

//	Weapon slot this weapon is placed in.
//int				Slot;
//	Weapons with lower values have greater priority
int				SelectionOrder;
//	Kickback from this weapon
float			Kickback;

// Y-adjustment value for full screen
float			PSpriteSY;

//	For displaying weapon with player model.
int				PlayerModelVersion;

byte			FireMode;

float			BotCombatDist;

//	Switch away if got ammo for more powerfull weapon.
bool			bWimpyWeapon;
//	Don't auto fire
bool			bNoAutoFire;
//	This is a powered up version of another weapon
bool			bPoweredUp;
//	Does Heretic staff level 2 style kickback.
bool			bStaff2Kickback;
//	Always use extreme death
bool			bExtremeDeath;
//	Ammo is optional
bool			bAmmoOptional;
//	Ammo is optional for alternate fire
bool			bAltAmmoOptional;
//	Half a chance to play the reay sound.
bool			bReadySndHalf;
//	Don't bob weapon.
bool			bDontBob;
//	Doesn't allert monsters when fired.
bool			bNoAlert;
//	Not considered a weapon by gimme cheat
bool			bCheatNotWeapon;
//	Use axe's blood splatter
bool			bAxeBlood;
//	Never switch to this weapon when picked up
bool			bNoAutoSwitch;
//	Primary fire uses both ammo types
bool			bPrimaryUsesBoth;
//	Alternate fire uses both ammo types
bool			bAltUsesBoth;

//	It's a melee weapon
bool			bBotMelee;
//	It's a projectile weapon
bool			bBotProjectile;
//	It's a BFG
bool			bBotBfg;

//	Runtime in-inventory references.
Ammo			Ammo1;
Ammo			Ammo2;
Weapon			SisterWeapon;

replication
{
	reliable if (Role == ROLE_Authority && bNetOwner)
		Ammo1, Ammo2;
}

//==========================================================================
//
//	TryPickup
//
//==========================================================================

bool TryPickup(EntityEx Toucher)
{
	//	If sprites for ready state are missing, you can't pick up this
	// weapon.
	state ReadyState = FindState('Ready');
	if (ReadyState && AreStateSpritesPresent(ReadyState))
	{
		return ::TryPickup(Toucher);
	}
	return false;
}

//==========================================================================
//
//	HandlePickup
//
//==========================================================================

bool HandlePickup(Inventory Item)
{
	if (Item.Class == Class)
	{
		if (Weapon(Item).PickupForAmmo(self))
		{
			Item.bPickupGood = true;
		}
		return true;
	}

	if (Inventory)
	{
		return Inventory.HandlePickup(Item);
	}
	return false;
}

//==========================================================================
//
//	CreateCopy
//
//==========================================================================

Inventory CreateCopy(EntityEx Toucher)
{
	Weapon Copy = Weapon(::CreateCopy(Toucher));
	if (Copy)
	{
		Copy.AmmoGive1 = AmmoGive1;
		Copy.AmmoGive2 = AmmoGive2;
	}
	return Copy;
}

//==========================================================================
//
//	CreateTossable
//
//==========================================================================

Inventory CreateTossable()
{
	//	Drop the wapon that is meant to be placed in a level, i.e. that
	// normaly gives you ammo.
	if (SisterWeapon && !default.AmmoGive1 && !default.AmmoGive2 &&
		(SisterWeapon.default.AmmoGive1 > 0 ||
		SisterWeapon.default.AmmoGive2 > 0))
	{
		return SisterWeapon.CreateTossable();
	}

	Weapon Copy = Weapon(::CreateTossable());
	if (Copy)
	{
		//	If weapon has a sister weapon, remove it too.
		if (SisterWeapon)
		{
			SisterWeapon.Destroy();
		}
		//	Dropped weapons don't have ammo to prevent cheating.
		Copy.AmmoGive1 = 0;
		Copy.AmmoGive2 = 0;
	}
	return Copy;
}

//==========================================================================
//
//	AttachToOwner
//
//==========================================================================

void AttachToOwner(EntityEx NewOwner)
{
	::AttachToOwner(NewOwner);

	//	Set up references to ammo items since they are used a lots.
	Ammo1 = AddAmmo(NewOwner, AmmoType1, AmmoGive1);
	Ammo2 = AddAmmo(NewOwner, AmmoType2, AmmoGive2);
	SisterWeapon = AddWeapon(SisterWeaponType);

	if (Owner.Player && !bNoAutoSwitch)
	{
		//	Doom and Strife always switch to a new weapon, Heretic and Hexen
		// only switch to more powerful weapons, always check for ammo.
		int ReadySlot = -1;
		int ReadyIndex = -1;
		int NewSlot;
		int NewIndex;
		if (PlayerEx(Owner.Player).ReadyWeapon)
		{
			LineSpecialGameInfo(Level.Game).FindWeaponSlot(
				class<Weapon>(PlayerEx(Owner.Player).ReadyWeapon.Class),
				ReadySlot, ReadyIndex);
		}
		LineSpecialGameInfo(Level.Game).FindWeaponSlot(
			class<Weapon>(Class), NewSlot, NewIndex);
		if (CheckAmmo(FIRE_Either, false) &&
			(LineSpecialGameInfo(Level.Game).bAlwaysSwitchNewWeapon ||
			!PlayerEx(Owner.Player).ReadyWeapon || NewSlot > ReadySlot) ||
			(NewSlot == ReadySlot && NewIndex < ReadyIndex))
		{
			PlayerEx(Owner.Player).PendingWeapon = self;
		}
	}
}

//==========================================================================
//
//	AddAmmo
//
//	Returns false if the ammo can't be picked up at all
//
//==========================================================================

final Ammo AddAmmo(EntityEx Toucher, class<Ammo> AmmoType, int count)
{
	if (!AmmoType)
	{
		return none;
	}

	if (!bIgnoreSkill && (Level.World.GameSkill == sk_baby ||
		(Level.World.GameSkill == sk_nightmare &&
		LineSpecialGameInfo(Level.Game).bNightmareExtraAmmo)))
	{
		// extra ammo in baby mode and nightmare mode
		if (LineSpecialGameInfo(Level.Game).bBabyNightmareDoubleAmmo)
		{
			count <<= 1;
		}
		else
		{
			count += count >> 1;
		}
	}

	Ammo AmmoItem = Ammo(Toucher.FindInventory(AmmoType));
	if (!AmmoItem)
	{
		AmmoItem = Spawn(AmmoType,,,, false);
		AmmoItem.AttachToOwner(Toucher);
		AmmoItem.Amount = count;
	}
	else if (AmmoItem.Amount < AmmoItem.MaxAmount)
	{
		AmmoItem.Amount += count;
	}

	if (AmmoItem.Amount > AmmoItem.MaxAmount)
	{
		AmmoItem.Amount = AmmoItem.MaxAmount;
	}
	return AmmoItem;
}

//==========================================================================
//
//	AddExistingAmmo
//
//==========================================================================

final bool AddExistingAmmo(Ammo AmmoItem, int count)
{
	int oldammo;

	if (!AmmoItem || count <= 0)
	{
		return false;
	}
	if (AmmoItem.Amount == AmmoItem.MaxAmount)
	{
		return false;
	}

	if (!bIgnoreSkill && (Level.World.GameSkill == sk_baby ||
		(Level.World.GameSkill == sk_nightmare &&
		LineSpecialGameInfo(Level.Game).bNightmareExtraAmmo)))
	{
		// extra ammo in baby mode and nightmare mode
		if (LineSpecialGameInfo(Level.Game).bBabyNightmareDoubleAmmo)
		{
			count <<= 1;
		}
		else
		{
			count += count >> 1;
		}
	}

	oldammo = AmmoItem.Amount;
	AmmoItem.Amount += count;
	if (AmmoItem.Amount > AmmoItem.MaxAmount)
	{
		AmmoItem.Amount = AmmoItem.MaxAmount;
	}

	if (oldammo <= 0 && AmmoItem.Owner.Player)
	{
		PlayerEx(AmmoItem.Owner.Player).GotAmmo(AmmoItem);
	}
	return true;
}

//==========================================================================
//
//	AddWeapon
//
//==========================================================================

final Weapon AddWeapon(class<Weapon> WeaponType)
{
	if (!WeaponType)
	{
		return none;
	}
	Weapon Wpn = Weapon(EntityEx(Owner).FindInventory(WeaponType));
	if (!Wpn)
	{
		Wpn = Spawn(WeaponType,,,, false);
		Wpn.AttachToOwner(EntityEx(Owner));
	}
	return Wpn;
}

//==========================================================================
//
//	PickupForAmmo
//
//==========================================================================

bool PickupForAmmo(Weapon ExistingWeapon)
{
	// leave placed weapons forever on net games
	if (ShouldStay() && !bDropped)
	{
		return false;
	}
	bool gaveammo = false;
	if (AddExistingAmmo(ExistingWeapon.Ammo1, AmmoGive1))
	{
		gaveammo = true;
	}
	if (AddExistingAmmo(ExistingWeapon.Ammo2, AmmoGive2))
	{
		gaveammo = true;
	}
	return gaveammo;
}

//==========================================================================
//
//	ShouldStay
//
//==========================================================================

bool ShouldStay()
{
	return LineSpecialGameInfo(Level.Game).bWeaponsStay;
}

//==========================================================================
//
//	Use
//
//	Using a weapon makes it active.
//
//==========================================================================

bool Use(bool Pickup)
{
	//	Can't directly use powered up weapon.
	if (bPoweredUp)
	{
		return false;
	}

	//	See which weapon to use.
	Weapon UseWpn = self;
	if (SisterWeapon && SisterWeapon.bPoweredUp &&
		EntityEx(Owner).FindInventory(PowerWeaponLevel2))
	{
		UseWpn = SisterWeapon;
	}
	if (Owner.bIsPlayer && PlayerEx(Owner.Player).ReadyWeapon != UseWpn)
	{
		PlayerEx(Owner.Player).PendingWeapon = UseWpn;
	}
	return false;
}

//==========================================================================
//
//	GetUpState
//
//==========================================================================

state GetUpState()
{
	return FindState('Select');
}

//==========================================================================
//
//	GetDownState
//
//==========================================================================

state GetDownState()
{
	return FindState('Deselect');
}

//==========================================================================
//
//	GetReadyState
//
//==========================================================================

state GetReadyState()
{
	return FindState('Ready');
}

//==========================================================================
//
//	GetAttackState
//
//==========================================================================

state GetAttackState(bool Hold)
{
	state S = none;
	if (Hold)
	{
		S = FindState('Hold');
	}
	if (!S)
	{
		S = FindState('Fire');
	}
	return S;
}

//==========================================================================
//
//	GetAltAttackState
//
//==========================================================================

state GetAltAttackState(bool Hold)
{
	state S = none;
	if (Hold)
	{
		S = FindState('AltHold');
	}
	if (!S)
	{
		S = FindState('AltFire');
	}
	return S;
}

//==========================================================================
//
//  CheckAmmo
//
//  Returns true if there is enough mana to shoot.  If not, selects the
// next weapon to use.
//
//==========================================================================

bool CheckAmmo(byte Mode, bool AutoSwitch, optional bool RequireAmmo)
{
	if (Mode == FIRE_Either)
	{
		if (CheckAmmo(FIRE_Primary, false) || CheckAmmo(FIRE_Secondary, false))
		{
			return true;
		}
	}
	else
	{
		if (!RequireAmmo && bAmmoOptional)
		{
			return true;
		}

		int Amount1 = Ammo1 ? Ammo1.Amount : 0;
		int Amount2 = Ammo2 ? Ammo2.Amount : 0;

		bool Good = true;
		if ((Mode == FIRE_Primary || bAltUsesBoth) && Amount1 < AmmoUse1)
		{
			Good = false;
		}
		if ((Mode == FIRE_Secondary || bPrimaryUsesBoth) && Amount2 < AmmoUse2)
		{
			Good = false;
		}
		//	If weapon has no secondary fire, there's never enough ammo for it.
		if (Mode == FIRE_Secondary && !FindState('AltFire'))
		{
			Good = false;
		}
		if (Good)
		{
			return true;
		}
	}

	if (!AutoSwitch)
	{
		return false;
	}

	// out of ammo, pick a weapon to change to
	Weapon Best = PlayerEx(Owner.Player).BestWeapon();

	PlayerEx(Owner.Player).PendingWeapon = Best;
	Owner.Player.SetViewState(ps_weapon, GetDownState());
	return false;
}

//==========================================================================
//
//	DepleteAmmo
//
//==========================================================================

final bool DepleteAmmo(byte Mode, optional bool CheckEnough,
	optional int DehackedUse)
{
	//	Default parameter values
	if (!specified_CheckEnough)
	{
		CheckEnough = true;
	}
	if (!specified_DehackedUse)
	{
		DehackedUse = AmmoUse1;
	}

	if (CheckEnough && !CheckAmmo(Mode, false))
	{
		return false;
	}

	if ((Mode == FIRE_Primary || bAltUsesBoth) && Ammo1)
	{
		if (LineSpecialGameInfo(Level.Game).bDehacked)
		{
			Ammo1.Amount -= DehackedUse;
		}
		else
		{
			Ammo1.Amount -= AmmoUse1;
		}
	}
	if ((Mode == FIRE_Secondary || bPrimaryUsesBoth) && Ammo2)
	{
		Ammo2.Amount -= AmmoUse2;
	}

	//	Don't let ammo go below zero.
	if (Ammo1 && Ammo1.Amount < 0)
	{
		Ammo1.Amount = 0;
	}
	if (Ammo2 && Ammo2.Amount < 0)
	{
		Ammo2.Amount = 0;
	}
	return true;
}

//===========================================================================
//
//	EndPowerup
//
//===========================================================================

final void EndPowerup()
{
	if (SisterWeapon)
	{
		PlayerEx(Owner.Player).SetWeapon(SisterWeapon);
		if (SisterWeapon.GetReadyState() != GetReadyState())
		{
			PlayerEx(Owner.Player).SetViewState(ps_weapon,
				SisterWeapon.GetReadyState());
		}
	}
}

defaultproperties
{
	PickupSound = 'misc/w_pkup';
}
