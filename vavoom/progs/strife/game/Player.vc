//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Player : BasePlayer;

//
//  FIRST FIELDS ARE STATS THAT ARE SENT TO CLIENT FOR STATUS BAR
//

int Ammo[NUMAMMO];
int ReadyAmmo;

bool WeaponOwned[NUMWEAPONS];
int Sigil;

bool Backpack;
bool Communicator;

int Accuracy;
int Stamina;

int Keys;

int Inventory[NUMINVITEMS];
int InvFirst;
int InvCursor;

int Objectives;

//
//  ----------
//

int ArmorPoints;

int Quests;

int ReadyWeapon;

// For screen flashing (red or bright).
float DamageFlash;
float BonusFlash;

// Bit flags, for cheats and debug.
// See cheat_t, above.
int Cheats;

// Power ups. invinc and invis are timers.
float Powers[NUMPOWERS];

// Base height above floor for viewz.
float ViewHeight;
// Bob/squat speed.
float DeltaViewHeight;
// bounded/scaled total momentum.
float Bob;

// Refired shots are less accurate.
int Refire;

// Is wp_nochange if not changing.
int PendingWeapon;

// Who did damage (NULL for floors/ceilings).
Actor Attacker;

float JumpTime;
int LocalQuakeHappening;

// Armor type is 0-2.
int ArmorType;

TVec MoveDir;

//==========================================================================
//
//	PutClientIntoServer
//
//==========================================================================

void PutClientIntoServer()
{
	ViewEnts[ps_weapon] = SpawnObject(Weapon, none);
	ViewEnts[ps_weapon].Player = self;
	ViewEnts[ps_flash] = SpawnObject(Weapon, none);
	ViewEnts[ps_flash].Player = self;
}

//==========================================================================
//
//  SpawnClient
//
//==========================================================================

void SpawnClient()
{
	if (deathmatch)
	{
		DeathMatchSpawnPlayer();
	}
	else
	{
		SpawnPlayer(&level->playerstarts[RebornPosition * MAXPLAYERS +
			P_GetPlayerNum(self)]);
	}
}

//==========================================================================
//
//  NetGameReborn
//
//==========================================================================

void NetGameReborn()
{
	int i;

	// remove pitch and roll angles from corpse
	MO.Angles.pitch = 0.0;
	MO.Angles.roll = 0.0;

	// first dissasociate the corpse
	MO.Player = none;
	MO.bIsPlayer = false;

	// spawn at random spot if in death match
	if (deathmatch)
	{
		DeathMatchSpawnPlayer();
		return;
	}

	if (CheckSpot(&level->playerstarts[RebornPosition * MAXPLAYERS +
		P_GetPlayerNum(self)]))
	{
		SpawnPlayer(&level->playerstarts[RebornPosition * MAXPLAYERS +
			P_GetPlayerNum(self)]);
		return;
	}

	// try to spawn at one of the other players spots
	for (i = 0; i < MAXPLAYERS; i++)
	{
		if (CheckSpot(&level->playerstarts[RebornPosition * MAXPLAYERS + i]))
		{
			SpawnPlayer(&level->playerstarts[RebornPosition * MAXPLAYERS + i]);
			return;
		}
		// he's going to be inside something.  Too bad.
	}
	SpawnPlayer(&level->playerstarts[RebornPosition * MAXPLAYERS +
		P_GetPlayerNum(self)]);
}

//==========================================================================
//
//  DisconnectClient
//
//==========================================================================

void DisconnectClient()
{
	int i;

	if (MO)
	{
		MO.Player = none;
		MO.bIsPlayer = false;
		MO.Damage(none, none, 10000);
	}
	for (i = 0; i < NUMPSPRITES; i++)
	{
		if (ViewEnts[i])
		{
			ViewEnts[i].Destroy();
			ViewEnts[i] = none;
		}
	}
	bprint("%s left the game\n", ARR2STR(PlayerName));
	MO.PlayFullVolumeSound('Chat', CHAN_AUTO);
}

//==========================================================================
//
//  UserinfoChanged
//
//==========================================================================

void UserinfoChanged()
{
	if (!bSpawned)
	{
		return;
	}
	MO.Translation = Color;
}

//==========================================================================
//
//  DeathMatchSpawnPlayer
//
//  Spawns a player at one of the random death match spots called at level
// load and each death
//
//==========================================================================

void DeathMatchSpawnPlayer()
{
	int i;
	int j;

	for (j = 0; j < 20; j++)
	{
		i = P_Random() % level->numdeathmatchstarts;
		if (CheckSpot(&level->deathmatchstarts[i]))
		{
			SpawnPlayer(&level->deathmatchstarts[i]);
			return;
		}
	}

	// no good spot, so the player will probably get stuck 
	SpawnPlayer(&level->playerstarts[P_GetPlayerNum(self)]);
}

//==========================================================================
//
//  CheckSpot
//
//  Returns false if the player cannot be respawned at the given mthing_t
// spot because something is occupying it
//
//==========================================================================

bool CheckSpot(mthing_t * mthing)
{
	float x;
	float y;
	sector_t *sec;
	float an;
	Actor A;
	int mtnum;
	int sfxnum;
	int i;

	if (!MO)
	{
		// first spawn of level, before corpses
		for (i = 0; i < MAXPLAYERS; i++)
			if (svvars->Players[i] && svvars->Players[i].MO &&
				svvars->Players[i].MO.Origin.x == mthing->x &&
				svvars->Players[i].MO.Origin.y == mthing->y)
				return false;
		return true;
	}

	x = mthing->x;
	y = mthing->y;

	if (!MO.CheckPosition(vector(x, y, ONFLOORZ)))
	{
		return false;
	}

	// flush an old corpse if needed 
	if (bodyqueslot >= BODYQUESIZE)
		bodyque[bodyqueslot % BODYQUESIZE].Remove();
	bodyque[bodyqueslot % BODYQUESIZE] = MO;
	bodyqueslot++;

	// spawn a teleport fog 
	sec = GLevel.PointInSector(vector(x, y, 0.0));
	an = itof(45 * (mthing->angle / 45));

	A = Spawn(TeleportFog, vector(x + 20.0 * cos(an), y + 20.0 * sin(an),
		sec->floorheight));
	A.PlaySound('Teleport', CHAN_VOICE);	// don't start sound on first frame

	return true;
}

//==========================================================================
//
//  SpawnPlayer
//
//  Called when a player is spawned on the level. Most of the player
// structure stays unchanged between levels.
//
//==========================================================================

void SpawnPlayer(mthing_t * mthing)
{
	float x;
	float y;
	float z;

	Actor A;

	if (PlayerState == PST_REBORN)
		PlayerReborn();

	x = mthing->x;
	y = mthing->y;
	z = ONFLOORZ;
	A = Spawn(StrifePlayer, vector(x, y, z));
	A.Origin.z += mthing->height;
	A.LinkToWorld();

	// set color translations for player sprites
	A.Translation = Color;

	A.Angles.yaw = itof(45 * (mthing->angle / 45));
	A.Player = self;
	A.bIsPlayer = true;
	A.Health = Health;

	MO = A;
	PlayerState = PST_LIVE;
	Refire = 0;
	DamageFlash = 0.0;
	BonusFlash = 0.0;
	ExtraLight = 0;
	FixedColormap = 0;
	ViewHeight = VIEWHEIGHT;
	ViewAngles = A.Angles;
	bFixAngle = true;

	// setup gun psprite
	SetupPsprites();

	// give all cards in death match mode
	if (deathmatch)
	{
		Keys = 0x3f;
	}

	// wake up the status bar
	SB_Start();
}

//==========================================================================
//
//  PlayerReborn
//
//  Called after a player dies almost everything is cleared and initialized
//
//==========================================================================

void PlayerReborn()
{
	//  Clear player struct
	ClearPlayer(self);

	//  Set initial data
	bUseDown = true;	// don't do anything immediately
	bAttackDown = true;
	PlayerState = PST_LIVE;
	Health = INITIAL_HEALTH;
	WeaponOwned[wp_fist] = true;
	SetWeapon(wp_fist);
	LocalQuakeHappening = 0;
}

//==========================================================================
//
//  PlayerExitMap
//
//  Call when a player completes a level.
//
//==========================================================================

void PlayerExitMap(bool clusterChange)
{
	int i;

	for (i = 0; i < NUMPOWERS; i++)
	{
		Powers[i] = 0.0;
	}
	if (clusterChange)
	{
		Keys = 0;
	}

	Items = 0;
	MO.Angles.pitch = 0.0;
	MO.Translucency = 0;	// cancel invisibility
	ExtraLight = 0;			// cancel gun flashes
	FixedColormap = 0;		// cancel ir gogles
	DamageFlash = 0.0;		// no palette changes
	BonusFlash = 0.0;
}

//==========================================================================
//
//  GiveAmmo
//
//  Num is the number of clip loads, not the individual count (0= 1/2 clip).
// Returns false if the ammo can't be picked up at all
//
//==========================================================================

bool GiveAmmo(int ammo, int num)
{
	int oldammo;
	int max;

	if (ammo == am_noammo)
		return false;

	if (ammo < 0 || ammo > NUMAMMO)
//      I_Error ("P_GiveAmmo: bad type %i", ammo);
		Error("P_GiveAmmo: bad type");

	max = Backpack ? maxammo[ammo] * 2 : maxammo[ammo];
	if (Ammo[ammo] == max)
		return false;

	if (num)
		num *= clipammo[ammo];
	else
		num = clipammo[ammo] / 2;

	if (gameskill == sk_baby || gameskill == sk_nightmare)
	{
		// give double ammo in trainer mode,
		// you'll need in nightmare
		num <<= 1;
	}

	oldammo = Ammo[ammo];
	Ammo[ammo] += num;

	if (Ammo[ammo] > max)
		Ammo[ammo] = max;

	// If non zero ammo, 
	// don't change up weapons,
	// player was lower on purpose.
	if (oldammo)
		return true;

	// We were down to zero,
	// so select a new weapon.
	// Preferences are not user selectable.
	switch (ammo)
	{
	case am_clip:
		if (ReadyWeapon == wp_fist)
		{
			if (WeaponOwned[wp_riffle])
				PendingWeapon = wp_riffle;
		}
		break;

	case am_electric_arrow:
		if (ReadyWeapon == wp_fist)
		{
			if (WeaponOwned[wp_crossbow])
				PendingWeapon = wp_crossbow;
		}
		break;

	case am_cell:
		if (ReadyWeapon == wp_fist)
		{
			if (WeaponOwned[wp_flame])
				PendingWeapon = wp_flame;
		}
		break;

	case am_misl:
		if (ReadyWeapon == wp_fist)
		{
			if (WeaponOwned[wp_missile])
				PendingWeapon = wp_missile;
		}
	default:
		break;
	}

	return true;
}

//==========================================================================
//
//  GiveWeapon
//
//  The weapon name may have a MF_DROPPED flag ored in.
//
//==========================================================================

bool GiveWeapon(int weapon, int weapon2, bool dropped)
{
	bool gaveammo;
	bool gaveammo2;
	bool gaveweapon;

	if (netgame && (deathmatch < 2) && !dropped)
	{
		// leave placed weapons forever on net games
		if (WeaponOwned[weapon])
			return false;

		BonusFlash += BONUSADD;
		WeaponOwned[weapon] = true;

		if (deathmatch)
			GiveAmmo(weapon_ammo_type[weapon], 5);
		else
			GiveAmmo(weapon_ammo_type[weapon], 2);
		PendingWeapon = weapon;

		if (weapon2 != -1)
		{
			WeaponOwned[weapon2] = true;
			if (weapon_ammo_type[weapon] != weapon_ammo_type[weapon2])
			{
				if (deathmatch)
					GiveAmmo(weapon_ammo_type[weapon], 5);
				else
					GiveAmmo(weapon_ammo_type[weapon], 2);
			}
		}

		MO.PlaySound('PickupWeapon', CHAN_ITEM);
		return false;
	}

	if (weapon_ammo_type[weapon] != am_noammo)
	{
		// give one clip with a dropped weapon,
		// two clips with a found weapon
		if (dropped)
			gaveammo = GiveAmmo(weapon_ammo_type[weapon], 1);
		else
			gaveammo = GiveAmmo(weapon_ammo_type[weapon], 2);
	}
	else
		gaveammo = false;

	if (weapon2 != -1 && weapon_ammo_type[weapon2] != am_noammo &&
		weapon_ammo_type[weapon] != weapon_ammo_type[weapon2])
	{
		// give one clip with a dropped weapon,
		// two clips with a found weapon
		if (dropped)
			gaveammo2 = GiveAmmo(weapon_ammo_type[weapon2], 1);
		else
			gaveammo2 = GiveAmmo(weapon_ammo_type[weapon2], 2);
	}
	else
		gaveammo2 = false;

	if (WeaponOwned[weapon])
		gaveweapon = false;
	else
	{
		gaveweapon = true;
		WeaponOwned[weapon] = true;
		if (weapon2 != -1)
			WeaponOwned[weapon2] = true;
		PendingWeapon = weapon;
	}

	return (gaveweapon || gaveammo || gaveammo2);
}

//==========================================================================
//
//	GiveSigilPiece
//
//==========================================================================

void GiveSigilPiece()
{
	if (!WeaponOwned[wp_sigil])
		Sigil = 0;
	else if (Sigil == 4)
		Sigil = 0;
	else
		Sigil++;
	WeaponOwned[wp_sigil] = true;
	PendingWeapon = wp_sigil;
	MO.PlaySound('SigilUp', CHAN_WEAPON);
}

//==========================================================================
//
//	GiveBody
//
//	Returns false if the body isn't needed at all
//
//==========================================================================

bool GiveBody(int num)
{
	if (Health >= MAXHEALTH + Stamina)
		return false;

	Health += num;
	if (Health > MAXHEALTH + Stamina)
		Health = MAXHEALTH + Stamina;
	MO.Health = Health;

	return true;
}

//==========================================================================
//
//	GiveArmor
//
//	Returns false if the armor is worse than the current armor.
//
//==========================================================================

bool GiveArmor(int armortype)
{
	int hits;

	hits = armortype * 100;
	if (ArmorPoints >= hits)
		return false;	// don't pick up

	ArmorType = armortype;
	ArmorPoints = hits;

	return true;
}

//==========================================================================
//
//	GiveKey
//
//==========================================================================

bool GiveKey(int key)
{
	if (Keys & (1 << key))
		return false;

	BonusFlash = BONUSADD;
	Keys |= 1 << key;
	return true;
}

//==========================================================================
//
//  GiveItem
//
//==========================================================================

bool GiveItem(int item)
{
	if (Items & item)
	{
		return false;	// already got it
	}

	Items |= item;
	return true;
}

//==========================================================================
//
//	GiveInvItem
//
//==========================================================================

bool GiveInvItem(int item, int amount)
{
	Inventory[item] += amount;
	while (!Inventory[InvCursor] && InvCursor < NUMINVITEMS - 1)
	{
		InvCursor++;
	}
	while (!Inventory[InvCursor] && InvCursor > 0)
	{
		InvCursor--;
	}
	AdjustInvFirst();
	return true;
}

//==========================================================================
//
//	RemoveInvItem
//
//==========================================================================

bool RemoveInvItem(int item, int amount)
{
	if (Inventory[item] < amount)
	{
		return false;
	}
	Inventory[item] -= amount;
	while (!Inventory[InvCursor] && InvCursor < NUMINVITEMS - 1)
	{
		InvCursor++;
	}
	while (!Inventory[InvCursor] && InvCursor > 0)
	{
		InvCursor--;
	}
	AdjustInvFirst();
	return true;
}

//==========================================================================
//
//	InvRight
//
//==========================================================================

void InvRight()
{
	if (InvCursor < NUMINVITEMS - 1)
	{
		InvCursor++;
		while (!Inventory[InvCursor] && InvCursor < NUMINVITEMS - 1)
		{
			InvCursor++;
		}
		while (!Inventory[InvCursor] && InvCursor > 0)
		{
			InvCursor--;
		}
		AdjustInvFirst();
	}
}

//==========================================================================
//
//	InvLeft
//
//==========================================================================

void InvLeft()
{
	if (InvCursor > 0)
	{
		InvCursor--;
		while (!Inventory[InvCursor] && InvCursor > 0)
		{
			InvCursor--;
		}
		while (!Inventory[InvCursor] && InvCursor < NUMINVITEMS - 1)
		{
			InvCursor++;
		}
		AdjustInvFirst();
	}
}

//==========================================================================
//
//	InvUse
//
//==========================================================================

void InvUse()
{
	bool itemUsed;

	if (!Inventory[InvCursor])
	{
		return;
	}
	itemUsed = false;
	switch (InvCursor)
	{
	case ITEM_MedPatch:
		itemUsed = GiveBody(10);
		break;

	case ITEM_MedicalKit:
		itemUsed = GiveBody(25);
		break;

	case ITEM_SurgeryKit:
		itemUsed = GiveBody(100);
		break;

	case ITEM_MetalArmor:
		itemUsed = GiveArmor(2);
		break;

	case ITEM_LeatherArmor:
		itemUsed = GiveArmor(1);
		break;

	case ITEM_ShadowArmor:
		itemUsed = GivePower(pw_invisibility);
		break;

	case ITEM_EnvironmentalSuit:
		itemUsed = GivePower(pw_ironfeet);
		break;
/*	ITEM_Map,
	ITEM_Scanner,
	ITEM_TeleporterBeacon,
	ITEM_Targeter,*/
	}
	//	Remove item if it was used.
	if (itemUsed)
	{
		RemoveInvItem(InvCursor, 1);
	}
}

//==========================================================================
//
//	AdjustInvFirst
//
//==========================================================================

void AdjustInvFirst()
{
	int FirstIdx;
	int CursIdx;
	int Idx;
	int i;

	if (InvFirst > InvCursor)
	{
		InvFirst = InvCursor;
	}
	if (!Inventory[InvCursor])
	{
		return;
	}
	FirstIdx = 0;
	CursIdx = 0;
	Idx = 0;
	for (i = 0; i < NUMINVITEMS; i++)
	{
		if (i == InvFirst)
			FirstIdx = Idx;
		if (i == InvCursor)
			CursIdx = Idx;
		if (Inventory[i])
			Idx++;
	}
	while (FirstIdx - CursIdx > 5)
	{
		InvFirst--;
		FirstIdx--;
		while (!Inventory[InvFirst] && InvFirst > 0)
		{
			InvFirst--;
		}
	}
	while (FirstIdx - CursIdx < -5)
	{
		InvFirst++;
		FirstIdx++;
		while (!Inventory[InvFirst] && InvFirst < NUMINVITEMS - 1)
		{
			InvFirst++;
		}
	}
}

//==========================================================================
//
//	GivePower
//
//==========================================================================

bool GivePower(int power)
{
	if (power == pw_invulnerability)
	{
		Powers[power] = INVULNTIME;
		return true;
	}

	if (power == pw_invisibility)
	{
		Powers[power] = INVISTIME;
		MO.Translucency = 90;
		return true;
	}

	if (power == pw_infrared)
	{
		Powers[power] = INFRATIME;
		return true;
	}

	if (power == pw_ironfeet)
	{
		Powers[power] = IRONTIME;
		return true;
	}

	if (power == pw_strength)
	{
		GiveBody(100);
		Powers[power] = 1.0;
		return true;
	}

	if (Powers[power])
		return false;	// already got it

	Powers[power] = 1.0;
	return true;
}

//==========================================================================
//
//	GiveQuest
//
//==========================================================================

void GiveQuest(int QuestID)
{
	if (Quests & (1 << QuestID))
		cprint(self, "Already have quest %d", QuestID);
	Quests |= 1 << QuestID;
	cprint(self, "Got quest %d, quests %x", QuestID, Quests);
}

//==========================================================================
//
//  ThrustPlayer
//
//  Moves the given origin along a given angle.
//
//==========================================================================

void ThrustPlayer(float angle, float move, float deltaTime)
{
	MO.Velocity.x += move * cos(angle) * deltaTime;
	MO.Velocity.y += move * sin(angle) * deltaTime;
}

//==========================================================================
//
//  CalcHeight
//
//  Calculate the walking / running height adjustment
//
//==========================================================================

void CalcHeight(float deltaTime)
{
	float angle;
	float _Bob;

	// Regular movement bobbing
	// (needs to be calculated for gun swing
	// even if not on ground)
	// OPTIMIZE: tablify angle
	// Note: a LUT allows for effects
	//  like a ramp with low health.
	Bob = MO.Velocity.x * MO.Velocity.x + MO.Velocity.y * MO.Velocity.y;
	Bob /= 4.0 * 35.0 * 35.0;
	if (Bob > MAXBOB)
		Bob = MAXBOB;

	angle = 180.0 * 35.0 / 10.0 * level->time;
	_Bob = Bob / 2.0 * sin(angle);

	// move viewheight
	if (PlayerState == PST_LIVE)
	{
		ViewHeight += DeltaViewHeight * deltaTime;

		if (ViewHeight > VIEWHEIGHT)
		{
			ViewHeight = VIEWHEIGHT;
			DeltaViewHeight = 0.0;
		}

		if (ViewHeight < VIEWHEIGHT / 2.0)
		{
			ViewHeight = VIEWHEIGHT / 2.0;
			if (DeltaViewHeight <= 0.0)
				DeltaViewHeight = 0.00001;
		}

		if (DeltaViewHeight)
		{
			DeltaViewHeight += 256.0 * deltaTime;
			if (!DeltaViewHeight)
				DeltaViewHeight = 0.00001;
		}
	}
	ViewOrg.z = MO.Origin.z + ViewHeight + _Bob;

	if (ViewOrg.z > MO.CeilingZ - 4.0)
		ViewOrg.z = MO.CeilingZ - 4.0;
}

//==========================================================================
//
//  MovePlayer
//
//==========================================================================

void MovePlayer(float deltaTime)
{
	float forward;
	float side;

	// Do not let the player control movement
	//  if not onground.
	onground = MO.Origin.z <= MO.FloorZ || MO.bOnMobj;

	forward = ForwardMove * 5.0;
	side = SideMove * 5.0;

	if (!onground)
	{
		forward *= air_control;
		side *= air_control;
	}

	if (forward)
	{
		ThrustPlayer(MO.Angles.yaw, forward, deltaTime);
	}
	if (side)
	{
		ThrustPlayer(AngleMod360(MO.Angles.yaw - 90.0), side, deltaTime);
	}

	if (forward || side)
	{
		SetPlayerRunState();
	}

	if ((Buttons & BT_JUMP) && onground && !JumpTime)
	{
		//  Jump
		MO.Velocity.z = 9.0 * 35.0;
		JumpTime = 0.5;
	}
}

//==========================================================================
//
//  CheckWaterJump
//
//==========================================================================

void CheckWaterJump()
{
	TVec start;
	TVec end;
	TVec vforward;

	// check for a jump-out-of-water
	AngleVector(&MO.Angles, &vforward);
	start = MO.Origin;
	start.z += MO.Height * 0.5 + 8.0;
	vforward.z = 0.0;
	vforward = Normalize(vforward);
	end = start + vforward * 24.0;
	if (!TraceLine(start, end))
	{
		// solid at waist
		start.z = MO.Origin.z + MO.Height;
		end = start + vforward * 24.0;
		MoveDir = trace_plane_normal * -50.0;
		if (TraceLine(start, end))
		{
			// open at eye level
			MO.bWaterJump = true;
			MO.Velocity.z = 320.0;
			MO.ReactionTime = 2.0;	// safety net
		}
	}
}

//==========================================================================
//
//  WaterMove
//
//==========================================================================

void WaterMove(float deltaTime)
{
	float forward;
	float side;
	TVec vforward;
	TVec vright;
	TVec vup;
	TVec wishvel;

	// Do not let the player control movement
	//  if not onground.
	onground = MO.Origin.z <= MO.FloorZ || MO.bOnMobj;

	AngleVectors(&MO.Angles, &vforward, &vright, &vup);

	forward = ForwardMove;
	side = SideMove;

	wishvel = forward * vforward + side * vright;
	if (!forward && !side /* && !cmd.upmove */ )
		wishvel.z -= 60.0;	// drift towards bottom
//  else
//      wishvel.z += cmd.upmove;

	MO.Velocity += 3.5 * deltaTime * wishvel;

	if (forward || side)
	{
		SetPlayerRunState();
	}

	if (Buttons & BT_JUMP)
	{
		if (MO.WaterType == CONTENTS_WATER)
			MO.Velocity.z = 100.0;
		else if (MO.WaterType == CONTENTS_SLUDGE)
			MO.Velocity.z = 80.0;
		else
			MO.Velocity.z = 50.0;
	}
	CheckWaterJump();
}

//==========================================================================
//
//  WaterJump
//
//==========================================================================

void WaterJump()
{
	if (!MO.ReactionTime || !MO.WaterLevel)
	{
		MO.bWaterJump = false;
		MO.ReactionTime = 0.0;
	}
	MO.Velocity.x = MoveDir.x;
	MO.Velocity.y = MoveDir.y;
}

//==========================================================================
//
//  PlayerInSpecialSector
//
//  Called every tic frame that the player origin is in a special sector
//
//==========================================================================

void PlayerInSpecialSector()
{
	// Falling, not all the way down yet?
	if (MO.Origin.z != MO.Sector->floorheight)
		return;

	// Has hitten ground.
	switch (MO.Sector->special)
	{
	case 5:
		// HELLSLIME DAMAGE
		if (!Powers[pw_ironfeet])
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 10);
		break;

	case 7:
		// NUKAGE DAMAGE
		if (!Powers[pw_ironfeet])
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 5);
		break;

	case 16:
		// SUPER HELLSLIME DAMAGE
	case 4:
		// STROBE HURT
		if (!Powers[pw_ironfeet] || (P_Random() < 5))
		{
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 20);
		}
		break;

	case 9:
		// SECRET SECTOR
		SecretCount++;
		MO.Sector->special = 0;
		break;

	case 11:
		// EXIT SUPER DAMAGE! (for E1M8 finale)
		Cheats &= ~CF_GODMODE;

		if (!(level->tictime & 0x1f))
			MO.Damage(none, none, 20);

		if (Health <= 10)
			G_ExitLevel();
		break;

	default:
		break;
	};
}

//==========================================================================
//
//  PlayerInContents
//
//==========================================================================

void PlayerInContents()
{
	if (!MO.WaterLevel)
	{
		return;
	}
	switch (MO.WaterType)
	{
	case CONTENTS_SLUDGE:
		if (!Powers[pw_ironfeet])
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 10);
		break;

	case CONTENTS_LAVA:
		if (!Powers[pw_ironfeet] || (P_Random() < 5))
		{
			if (!(level->tictime & 0x1f))
				MO.Damage(none, none, 20);
		}
		break;
	}
}

//==========================================================================
//
//  DeathPlayerTick
//
//  Fall on your face when dying. Decrease POV height to floor height.
//
//==========================================================================

void DeathPlayerTick(float deltaTime)
{
	float angle;
	float delta;

	MovePsprites();

	onground = MO.Origin.z <= MO.FloorZ || MO.bOnMobj;

	// fall to the ground
	if (ViewHeight > 6.0)
		ViewHeight -= 1.0;

	if (ViewHeight < 6.0)
		ViewHeight = 6.0;

	DeltaViewHeight = 0.0;
	CalcHeight(deltaTime);

	if (Attacker && Attacker != MO)
	{
		angle = atan2(Attacker.Origin.y - MO.Origin.y,
			Attacker.Origin.x - MO.Origin.x);

		delta = AngleMod180(angle - MO.Angles.yaw);

		if (delta < 5.0 && delta > -5.0)
		{
			// Looking at killer,
			//  so fade damage flash down.
			MO.Angles.yaw = angle;

			if (DamageFlash)
			{
				DamageFlash -= deltaTime;
				if (DamageFlash <= 0.0)
					DamageFlash = 0.0;
			}
		}
		else if (delta > 0.0)
			MO.Angles.yaw += 5.0;
		else
			MO.Angles.yaw -= 5.0;
	}
	else if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (Buttons & BT_USE)
		PlayerState = PST_REBORN;
}

//==========================================================================
//
//  PlayerImpulse
//
//==========================================================================

void PlayerImpulse()
{
	int impulse;

	impulse = Impulse;
	Impulse = 0;

	if (impulse <= 8)
	{
		ChangeWeapon(impulse - 1);
	}
	else if (impulse == 13)
	{
		InvLeft();
	}
	else if (impulse == 14)
	{
		InvRight();
	}
	else if (impulse == 15)
	{
		InvUse();
	}
	else if (impulse == 16)
	{
		//  Suicide
		MO.Damage(none, none, 10000);
	}
	else if (impulse >= 200 && impulse <= 205)
	{
		ConChoiceImpulse(impulse - 200);
	}
}

//==========================================================================
//
//  PlayerTick
//
//==========================================================================

void PlayerTick(float deltaTime)
{
	// Copy view angles
	MO.Angles = ViewAngles;

	// fixme: do this in the cheat code
	if (Cheats & CF_NOCLIP)
	{
		MO.bColideWithThings = false;
		MO.bColideWithWorld = false;
	}
	else
	{
		MO.bColideWithThings = true;
		MO.bColideWithWorld = true;
	}

	if (MO.bJustAttacked)
	{
		ForwardMove = 100.0;
		SideMove = 0.0;
		MO.bJustAttacked = false;
	}

	if (PlayerState == PST_DEAD)
	{
		DeathPlayerTick(deltaTime);
		return;
	}

	if (JumpTime)
	{
		JumpTime -= deltaTime;
		if (JumpTime <= 0.0)
			JumpTime = 0.0;
	}

	// Move around.
	// Reactiontime is used to prevent movement
	//  for a bit after a teleport.
	if (MO.ReactionTime)
	{
		MO.ReactionTime -= deltaTime;
		if (MO.ReactionTime <= 0.0)
			MO.ReactionTime = 0.0;
		if (MO.bWaterJump)
			WaterJump();
	}
	else
	{
		if (MO.WaterLevel > 1)
			WaterMove(deltaTime);
		else
			MovePlayer(deltaTime);
	}

	CalcHeight(deltaTime);

	if (MO.Sector->special)
		PlayerInSpecialSector();
	PlayerInContents();

	// Check for weapon change.
	if (Impulse)
	{
		PlayerImpulse();
	}

	// check for use
	if (Buttons & BT_USE)
	{
		if (!bUseDown)
		{
			P_UseLines(self);
			bUseDown = true;
		}
	}
	else
	{
		bUseDown = false;
	}

	//  cycle psprites
	MovePsprites();

	//  Counters, time dependend power ups.

	//  Strength counts up to diminish fade.
	if (Powers[pw_strength])
		Powers[pw_strength] += deltaTime;

	if (Powers[pw_invulnerability])
	{
		Powers[pw_invulnerability] -= deltaTime;
		if (Powers[pw_invulnerability] <= 0.0)
			Powers[pw_invulnerability] = 0.0;
	}

	if (Powers[pw_invisibility])
	{
		Powers[pw_invisibility] -= deltaTime;
		if (Powers[pw_invisibility] <= 0.0)
		{
			Powers[pw_invisibility] = 0.0;
			MO.Translucency = 0;
		}
	}

	if (Powers[pw_infrared])
	{
		Powers[pw_infrared] -= deltaTime;
		if (Powers[pw_infrared] <= 0.0)
			Powers[pw_infrared] = 0.0;
	}

	if (Powers[pw_ironfeet])
	{
		Powers[pw_ironfeet] -= deltaTime;
		if (Powers[pw_ironfeet] <= 0.0)
			Powers[pw_ironfeet] = 0.0;
	}

	if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (BonusFlash)
	{
		BonusFlash -= deltaTime;
		if (BonusFlash <= 0.0)
			BonusFlash = 0.0;
	}

	// Handling colormaps.
	if (Powers[pw_invulnerability])
	{
		if (Powers[pw_invulnerability] > 4.0 ||
			(ftoi(4.0 * Powers[pw_invulnerability]) & 1))
			FixedColormap = INVERSECOLORMAP;
		else
			FixedColormap = 0;
	}
	else if (Powers[pw_infrared])
	{
		if (Powers[pw_infrared] > 4.0 ||
			(ftoi(4.0 * Powers[pw_infrared]) & 1))
		{
			// almost full bright
			FixedColormap = 1;
		}
		else
			FixedColormap = 0;
	}
	else
		FixedColormap = 0;
}

//==========================================================================
//
//  PaletteFlash
//
//  Do red-/gold-shifts from damage/items
//
//==========================================================================

void PaletteFlash()
{
	int cnt;
	int bzc;

	CShifts[CSHIFT_CONTENTS] = 0;
	if (MO.WaterLevel == 3)
	{
		switch (MO.WaterType)
		{
		case CONTENTS_WATER:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(130, 80, 50, 128);
			break;

		case CONTENTS_SLUDGE:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(128, 160, 128, 150);
			break;

		case CONTENTS_LAVA:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(255, 80, 0, 150);
			break;
		}
	}

	cnt = ftoi(32.0 * DamageFlash);

	if (Powers[pw_strength])
	{
		// slowly fade the berzerk out
		bzc = 12 - ftoi(Powers[pw_strength] / 2.0);

		if (bzc > cnt)
			cnt = bzc;
	}

	if (cnt)
	{
		Palette = (cnt + 7) >> 3;

		if (Palette >= NUMREDPALS)
			Palette = NUMREDPALS - 1;

		Palette += STARTREDPALS;
	}
	else if (BonusFlash)
	{
		Palette = (ftoi(32.0 * BonusFlash) + 7) >> 3;

		if (Palette >= NUMBONUSPALS)
			Palette = NUMBONUSPALS - 1;

		Palette += STARTBONUSPALS;
	}
	else if (Powers[pw_ironfeet] > 4.0 ||
		ftoi(4.0 * Powers[pw_ironfeet]) & 1)
		Palette = RADIATIONPAL;
	else
		Palette = 0;
}

//==========================================================================
//
//  SetViewPos
//
//==========================================================================

void SetViewPos()
{
	ViewOrg.x = MO.Origin.x;
	ViewOrg.y = MO.Origin.y;

	if (LocalQuakeHappening)
	{
		float intensity;

		intensity = itof(LocalQuakeHappening);
		ViewOrg.x += (Random() - 0.5) * (intensity * 4.0);
		ViewOrg.y += (Random() - 0.5) * (intensity * 4.0);
	}

	ViewAngles = MO.Angles;

	PaletteFlash();

	if (Weapon(ViewEnts[ps_weapon]).Ammo == am_noammo)
		ReadyAmmo = 1994;	// means "n/a"
	else
		ReadyAmmo = Ammo[Weapon(ViewEnts[ps_weapon]).Ammo];
}

//==========================================================================
//
//  SetPlayerRunState
//
//==========================================================================

void SetPlayerRunState()
{
	if (MO.StateNum == MO.IdleState)
	{
		MO.SetState(MO.SeeState);
	}
}

//==========================================================================
//
//  SpawnPlayerMissile
//
//  Tries to aim at a nearby monster
//
//==========================================================================

Actor SpawnPlayerMissile(classid type)
{
	Actor A;
	TVec dir;

	// see which target is to be aimed at
	dir = Aim(MO, 16.0 * 64.0);

	A = Spawn(type, MO.Origin + vector(0.0, 0.0, 32.0));

	if (A.SightSound)
		A.PlaySound(A.SightSound, CHAN_VOICE);

	A.Instigator = MO;
	A.Velocity = dir * A.Speed;
	VectorAngles(&dir, &A.Angles);

	A.CheckMissileSpawn();

	return A;
}

//==========================================================================
//
//  SpawnPlayerMissileAngle
//
//  Tries to aim at a nearby monster
//
//==========================================================================

Actor SpawnPlayerMissileAngle(classid type, float angle)
{
	Actor A;
	TVec dir;

	// see which target is to be aimed at
	dir = AimAngle(MO, 16.0 * 64.0, angle);

	A = Spawn(type, MO.Origin + vector(0.0, 0.0, 32.0));

	if (A.SightSound)
		A.PlaySound(A.SightSound, CHAN_VOICE);

	A.Instigator = MO;
	A.Velocity = dir * A.Speed;
	VectorAngles(&dir, &A.Angles);

	A.CheckMissileSpawn();

	return A;
}

//***************************************************************************
//
//  WEAPON UTILITES
//
//***************************************************************************

//==========================================================================
//
//	SetWeapon
//
//==========================================================================

void SetWeapon(int NewWeapon)
{
	ReadyWeapon = NewWeapon;
	PendingWeapon = wp_nochange;
	if (ViewEnts[ps_weapon])
	{
		ViewEnts[ps_weapon].Destroy();
	}
	ViewEnts[ps_weapon] = SpawnObject(WeaponClasses[NewWeapon], none);
	ViewEnts[ps_weapon].Player = self;
}

//===========================================================================
//
//  SetPsprite
//
//===========================================================================

void SetPsprite(int position, int stnum)
{
	ViewEnts[position].SetState(stnum);
}

//===========================================================================
//
//  BringUpWeapon
//
//  Starts bringing the pending weapon up from the bottom of the screen.
// Uses player
//
//===========================================================================

void BringUpWeapon()
{
	PendingWeapon = wp_nochange;
	ViewEnts[ps_weapon].SY = WEAPONBOTTOM;

	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).UpState);
}

//==========================================================================
//
//  ChangeWeapon
//
//  The actual changing of the weapon is done when the weapon psprite can
// do it (read: not in the middle of an attack).
//
//==========================================================================

void ChangeWeapon(int newweapon)
{
	if (newweapon == wp_crossbow && WeaponOwned[wp_crossbow2] &&
		ReadyWeapon == wp_crossbow)
	{
		newweapon = wp_crossbow2;
	}
	if (newweapon == wp_grenade && WeaponOwned[wp_grenade2] &&
		ReadyWeapon == wp_grenade)
	{
		newweapon = wp_grenade2;
	}
	if (newweapon == wp_blaster && WeaponOwned[wp_blaster2] &&
		ReadyWeapon == wp_blaster)
	{
		newweapon = wp_blaster2;
	}

	if (WeaponOwned[newweapon] && newweapon != ReadyWeapon)
	{
		// Do not go to ??? in shareware, even if cheated.
//FIXME     if ((newweapon != wp_plasma && newweapon != wp_bfg) || !shareware)
		{
			PendingWeapon = newweapon;
		}
	}
}

//===========================================================================
//
//  DropWeapon
//
//  Player died, so put the weapon away.
//
//===========================================================================

void DropWeapon()
{
	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).DownState);
}

//===========================================================================
//
//  SetupPsprites
//
//  Called at start of level for each player.
//
//===========================================================================

void SetupPsprites()
{
	int i;

	// remove all  ViewEnts
	for (i = 0; i < NUMPSPRITES; i++)
	{
		ViewEnts[i].StateNum = 0;
	}

	// spawn the gun
	BringUpWeapon();
}

//==========================================================================
//
//  MovePsprites
//
//  Called every tic by player thinking routine.
//
//==========================================================================

void MovePsprites()
{
	int i;
	ViewEntity e;

	for (i = 0; i < NUMPSPRITES; i++)
	{
		e = ViewEnts[i];
		// a null state means not active
		if (e.StateNum)
		{
			// drop tic count and possibly change state

			// a -1 tic count never changes
			if (e.StateTime != -1.0)
			{
				e.StateTime -= frametime;
				if (e.StateTime <= 0.0)
				{
					e.StateTime = 0.0;
					SetPsprite(i, e.NextState);
				}
			}
		}
	}

	ViewEnts[ps_flash].SX = ViewEnts[ps_weapon].SX;
	ViewEnts[ps_flash].SY = ViewEnts[ps_weapon].SY;
}

//==========================================================================
//
//  CheckAmmo
//
//  Returns true if there is enough ammo to shoot.
// If not, selects the next weapon to use.
//
//==========================================================================

int CheckAmmo()
{
	int ammo;
	int count;

	ammo = Weapon(ViewEnts[ps_weapon]).Ammo;

	// Minimal amount for one shot varies.
	if (ReadyWeapon == wp_blaster)
		count = 20;
	else if (ReadyWeapon == wp_blaster2)
		count = 30;
	else
		count = 1;	// Regular.

	// Some do not need ammunition anyway.
	// Return if current ammunition sufficient.
	if (ammo == am_noammo || Ammo[ammo] >= count)
		return true;

	// Out of ammo, pick a weapon to change to.
	// Preferences are set here.
	do
	{
		if (WeaponOwned[wp_flame] && Ammo[am_cell])
		{
			PendingWeapon = wp_flame;
		}
		else if (WeaponOwned[wp_riffle] && Ammo[am_clip])
		{
			PendingWeapon = wp_riffle;
		}
		else if (WeaponOwned[wp_crossbow]
			&& Ammo[am_electric_arrow])
		{
			PendingWeapon = wp_crossbow;
		}
		else if (WeaponOwned[wp_missile] && Ammo[am_misl])
		{
			PendingWeapon = wp_missile;
		}
		else
		{
			// If everything fails.
			PendingWeapon = wp_fist;
		}
	}
	while (PendingWeapon == wp_nochange);

	// Now set appropriate weapon overlay.
	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).DownState);

	return false;
}

//===========================================================================
//
//  FireWeapon
//
//===========================================================================

void FireWeapon()
{
	if (!CheckAmmo())
	{
		return;
	}

	MO.SetState(MO.MeleeState);
	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).AttackState);
	if (Weapon(ViewEnts[ps_weapon]).bRaiseNoiseAlert)
		P_NoiseAlert(MO, MO);
}

defaultproperties
{
}

//**************************************************************************
//
//  $Log$
//  Revision 1.9  2004/11/01 07:38:23  dj_jl
//  Almost all Hexen line specials are available in all games.
//
//  Revision 1.8  2004/10/18 06:35:21  dj_jl
//  Implemented many monsters, sigil and some more.
//
//  Revision 1.7  2004/10/14 06:21:35  dj_jl
//  Implemented Programmer.
//
//  Revision 1.6  2004/10/13 06:36:30  dj_jl
//  Implemented items also giving quests.
//
//  Revision 1.5  2004/10/12 06:39:07  dj_jl
//  Various improvements.
//
//  Revision 1.4  2004/08/28 12:35:24  dj_jl
//  Move general client functions to player class.
//
//  Revision 1.3  2004/08/23 19:24:01  dj_jl
//  Moved weapon utilites to player class.
//
//  Revision 1.2  2004/08/22 19:23:27  dj_jl
//  Moved many functions to player class.
//
//  Revision 1.1  2003/11/12 16:51:33  dj_jl
//  Changed player structure into a class
//
//**************************************************************************
