//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Weapon : ViewEntity;

const float
	MELEERANGE		= 64.0,
	PUNCHRANGE		= 80.0,
	MISSILERANGE	= (32.0 * 64.0),
	MAULERRANGE		= (33.0 * 64.0),
	ROBOTRANGE		= 264.0;

const float LOWERSPEED		= 6.0;
const float RAISESPEED		= 6.0;

const float WEAPONBOTTOM	= 128.0;
const float WEAPONTOP		= 32.0;

int Ammo;
int UpState;
int DownState;
int ReadyState;
int AttackState;
int HoldAttackState;
int FlashState;
int FireDeathWaveState;

bool bRaiseNoiseAlert;

//===========================================================================
//
//  A_WeaponReady
//
//  The player can fire the weapon or change to another weapon at this time.
// Follows after getting weapon up, or after previous attack/fire sequence.
//
//===========================================================================

void A_WeaponReady()
{
	float angle;

	// get out of attack state
	if (Player.MO.StateNum == Player.MO.MeleeState ||
		Player.MO.StateNum == Player.MO.MissileState)
	{
		Player.MO.SetState(Player.MO.IdleState);
	}

	if (Player.ReadyWeapon == StrifeDefs::wp_flame && StateNum == ReadyState)
	{
		Player.MO.PlaySound('weapons/flameidle', CHAN_WEAPON);
	}

	// check for change
	//  if player is dead, put the weapon away
	if (Player.PendingWeapon != StrifeDefs::wp_nochange || !Player.Health)
	{
		// change weapon
		//  (pending weapon should allready be validated)
		Player.SetPsprite(ps_weapon, DownState);
		return;
	}

	// check for fire
	//  the missile launcher and bfg do not auto fire
	if (Player.Buttons & BT_ATTACK)
	{
		if (!Player.bAttackDown || (Player.ReadyWeapon != StrifeDefs::wp_missile))
		{
			Player.bAttackDown = true;
			Player.FireWeapon();
			return;
		}
	}
	else
		Player.bAttackDown = false;

	// bob the weapon based on movement speed
	angle = AngleMod360(180.0 * Player.Level.Game.level->time);
	SX = 1.0 + Player.Bob * cos(angle);
	if (angle >= 180.0)
	{
		angle -= 180.0;
	}
	SY = WEAPONTOP + Player.Bob * sin(angle);
}

//===========================================================================
//
//  A_Lower
//
//  Lowers current weapon, and changes weapon at bottom.
//
//===========================================================================

void A_Lower()
{
	//	FIXME!!
	if (Player.Level.Game.frametime < 1.0 / 35.0)
		SY += LOWERSPEED;
	else
		SY += LOWERSPEED * 35.0 * Player.Level.Game.frametime;

	// Is already down.
	if (SY < WEAPONBOTTOM)
		return;

	// Player is dead.
	if (Player.PlayerState == PST_DEAD)
	{
		SY = WEAPONBOTTOM;

		// don't bring weapon back up
		return;
	}

	// The old weapon has been lowered off the screen,
	// so change the weapon and start raising it
	if (!Player.Health)
	{
		// Player is dead, so keep the weapon off screen.
		Player.SetPsprite(ps_weapon, S_NULL);
		return;
	}

	Player.SetWeapon(Player.PendingWeapon);

	Player.BringUpWeapon();
}

//===========================================================================
//
//  A_Raise
//
//===========================================================================

void A_Raise()
{
	//	FIXME!!
	if (Player.Level.Game.frametime < 1.0 / 35.0)
		SY -= RAISESPEED;
	else
		SY -= RAISESPEED * 35.0 * Player.Level.Game.frametime;

	if (SY > WEAPONTOP)
	{
		return;
	}

	SY = WEAPONTOP;

	// The weapon has been raised all the way,
	//  so change to the ready state.
	Player.SetPsprite(ps_weapon, ReadyState);
}

//===========================================================================
//
//  A_ReFire
//
//  The player can re-fire the weapon without lowering it entirely.
//
//===========================================================================

void A_ReFire()
{
	// check for fire
	//  (if a weaponchange is pending, let it go through instead)
	if ((Player.Buttons & BT_ATTACK)
		&& Player.PendingWeapon == StrifeDefs::wp_nochange && Player.Health)
	{
		Player.Refire++;
		Player.FireWeapon();
	}
	else
	{
		Player.Refire = 0;
		Player.CheckAmmo();
	}
}

//===========================================================================
//
//  A_CheckReload
//
//===========================================================================

void A_CheckReload()
{
	Player.CheckAmmo();
}

//===========================================================================
//
//  A_GunFlash
//
//===========================================================================

void A_GunFlash()
{
	Player.MO.SetState(Player.MO.MissileState);
	Player.SetPsprite(ps_flash, Weapon(Player.ViewEnts[ps_weapon]).FlashState);
}

//===========================================================================
//
//  A_Light0
//
//===========================================================================

void A_Light0()
{
	Player.ExtraLight = 0;
}

//===========================================================================
//
//  A_Light1
//
//===========================================================================

void A_Light1()
{
	Player.ExtraLight = 1;
}

//===========================================================================
//
//  A_Light2
//
//===========================================================================

void A_Light2()
{
	Player.ExtraLight = 2;
}

//==========================================================================
//
//	A_ClearFlash
//
//==========================================================================

void A_ClearFlash()
{
	Player.SetPsprite(ps_flash, S_NULL);
}

__states__
{
	S_LIGHTDONE('', 0, 0.0 / 35.0, S_NULL) { A_Light0(); }
	S_WAVE1('WAVE', 0 | FF_FULLBRIGHT, 3.0 / 35.0, S_WAVE2) { }
	S_WAVE2('WAVE', 1 | FF_FULLBRIGHT, 3.0 / 35.0, S_WAVE3) { }
	S_WAVE3('WAVE', 2 | FF_FULLBRIGHT, 3.0 / 35.0, S_WAVE4) { }
	S_WAVE4('WAVE', 3 | FF_FULLBRIGHT, 3.0 / 35.0, S_WAVE1) { }
	S_RBPY_1('RBPY', 0 | FF_FULLBRIGHT, 3.0 / 35.0, S_RBPY_2) { }
	S_RBPY_2('RBPY', 1 | FF_FULLBRIGHT, 3.0 / 35.0, S_RBPY_3) { }
	S_RBPY_3('RBPY', 2 | FF_FULLBRIGHT, 3.0 / 35.0, S_RBPY_4) { }
	S_RBPY_4('RBPY', 3 | FF_FULLBRIGHT, 3.0 / 35.0, S_RBPY_1) { }
	S_TARGETER1('TRGT', 0, -1.0, S_NULL) { }
	S_TARGETER2('TRGT', 1, -1.0, S_NULL) { }
	S_TARGETER3('TRGT', 2, -1.0, S_NULL) { }
}

defaultproperties
{
	bRaiseNoiseAlert = true;
	FireDeathWaveState = S_WAVE1;
}

//**************************************************************************
//
//  $Log$
//  Revision 1.11  2006/02/28 22:47:55  dj_jl
//  Replaced defines with constants.
//
//  Revision 1.10  2006/02/17 19:06:53  dj_jl
//  Moved common defs to classes.
//
//  Revision 1.9  2006/02/15 23:34:27  dj_jl
//  Moved all server progs global variables to classes.
//
//  Revision 1.8  2006/02/13 18:30:17  dj_jl
//  Moved all server progs global functions to classes.
//
//  Revision 1.7  2005/11/19 13:43:51  dj_jl
//  Renamed sounds.
//
//  Revision 1.6  2005/03/28 07:45:29  dj_jl
//  Some Boom line specials, fixed multiple bugs.
//
//  Revision 1.5  2004/08/23 19:24:01  dj_jl
//  Moved weapon utilites to player class.
//
//  Revision 1.4  2004/01/30 17:39:34  dj_jl
//  Implemented many action functions.
//
//  Revision 1.3  2003/11/12 16:51:33  dj_jl
//  Changed player structure into a class
//
//  Revision 1.2  2003/09/11 17:21:04  dj_jl
//  Silent weapons
//
//  Revision 1.1  2002/11/02 08:20:57  dj_jl
//  New style classes.
//
//**************************************************************************
