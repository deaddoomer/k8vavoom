//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class WeaponAssaultGun:Weapon;

//==========================================================================
//
//	A_FireRiffle
//
//==========================================================================

void A_FireRiffle(void)
{
	TVec dir;
	int damage;

	Player->MO.PlaySound('AssaultGunFire', CHAN_WEAPON);

	if (!Player->Ammo[Ammo])
		return;

	Player->MO.SetState(Player->MO.MissileState);
	Player->Ammo[Ammo]--;

	dir = Aim(Player->MO, 16.0 * 64.0);
	if (Player->Refire)
	{
		TAVec angles;

		VectorAngles(&dir, &angles);
		angles.yaw = AngleMod360(angles.yaw +
			(Random() - Random()) * 45.0 / 8.0);
		AngleVector(&angles, &dir);
	}
	damage = 5 * (P_Random() % 3 + 1);
	LineAttack(Player->MO, dir, MISSILERANGE, damage);
	//Player->MO.Effects |= EF_DL_MUZZLEFLASH;
}

__states__
{
	S_RIFFLE('RIFG', 0, 1.0 / 35.0, S_RIFFLE) { A_WeaponReady(); }
	S_RIFFLEDOWN('RIFG', 1, 1.0 / 35.0, S_RIFFLEDOWN) { A_Lower(); }
	S_RIFFLEUP('RIFG', 0, 1.0 / 35.0, S_RIFFLEUP) { A_Raise(); }
	S_RIFFLE1('RIFF', 0, 3.0 / 35.0, S_RIFFLE2) { A_FireRiffle(); }
	S_RIFFLE2('RIFF', 1, 3.0 / 35.0, S_RIFFLE3) { A_FireRiffle(); }
	S_RIFFLE3('RIFG', 3, 3.0 / 35.0, S_RIFFLE4) { A_FireRiffle(); }
	S_RIFFLE4('RIFG', 2, 0.0 / 35.0, S_RIFFLE5) { A_ReFire(); }
	S_RIFFLE5('RIFG', 1, 2.0 / 35.0, S_RIFFLE) { }
}

defaultproperties
{
	Ammo = am_clip;
	UpState = S_RIFFLEUP;
	DownState = S_RIFFLEDOWN;
	ReadyState = S_RIFFLE;
	AttackState = S_RIFFLE1;
	FlashState = S_LIGHTDONE;
}

//**************************************************************************
//
//  $Log$
//  Revision 1.1  2002/11/02 08:20:57  dj_jl
//  New style classes.
//
//**************************************************************************
