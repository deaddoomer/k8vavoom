//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

//**************************************************************************
//**************************************************************************
//
//	Imp
//
//**************************************************************************
//**************************************************************************

class Imp:Pawn
	__mobjinfo__(66)
{

int XCrashState;

//==========================================================================
//
//  A_ImpMeAttack
//
//==========================================================================

void A_ImpMeAttack(void)
{
	if (!Enemy)
	{
		return;
	}
	PlaySound(AttackSound, CHAN_WEAPON);
	if (CheckMeleeRange())
	{
		Enemy.Damage(self, self, 5 + (P_Random() & 7));
	}
}

//==========================================================================
//
//  A_ImpMsAttack
//
//==========================================================================

void A_ImpMsAttack(void)
{
	if (!Enemy || P_Random() > 64)
	{
		SetState(SeeState);
		return;
	}
	bSkullFly = true;
	PlaySound(AttackSound, CHAN_WEAPON);
	A_FaceTarget();
#if 0
	float an;
	float dist;

	an = Angles.yaw;
	Velocity.x = 12.0 * cos(an) * 35.0;
	Velocity.y = 12.0 * sin(an) * 35.0;
	dist = MobjDist2(Enemy, self);
	dist = dist / (12.0 * 35.0);
	if (dist < 1.0)
	{
		dist = 1.0;
	}
	Velocity.z = (Enemy.Origin.z + Enemy.Height / 2.0 - Origin.z) / dist;
#else
	Velocity = Normalize(MobjCenter(Enemy) - Origin) * 12.0 * 35.0;
#endif
}

//==========================================================================
//
//  A_ImpDeath
//
//==========================================================================

void A_ImpDeath(void)
{
	bSolid = false;
	bFloorClip = true;
	if (Origin.z <= FloorZ)
	{
		SetState(CrashState);
	}
}

//==========================================================================
//
//  A_ImpXDeath1
//
//==========================================================================

void A_ImpXDeath1(void)
{
	bSolid = false;
	bNoGravity = true;
	bFloorClip = true;
	Special1 = 666;	// Flag the crash routine
}

//==========================================================================
//
//  A_ImpXDeath2
//
//==========================================================================

void A_ImpXDeath2(void)
{
	bNoGravity = false;
	if (Origin.z <= FloorZ)
	{
		SetState(CrashState);
	}
}

//==========================================================================
//
//  A_ImpExplode
//
//==========================================================================

void A_ImpExplode(void)
{
	Actor mo;

	mo = Spawn(ImpChunk1, Origin);
	mo.Velocity.x = (Random() - Random()) * 4.0 * 35.0;
	mo.Velocity.y = (Random() - Random()) * 4.0 * 35.0;
	mo.Velocity.z = 9.0 * 35.0;
	mo = Spawn(ImpChunk2, Origin);
	mo.Velocity.x = (Random() - Random()) * 4.0 * 35.0;
	mo.Velocity.y = (Random() - Random()) * 4.0 * 35.0;
	mo.Velocity.z = 9.0 * 35.0;
	if (Special1 == 666)
	{
		// Extreme death crash
		SetState(XCrashState);
	}
}

__states__
{
	S_IMP_LOOK1('IMPX', 0, 'monsters/gargoyle.md2', 0, 10.0 / 35.0, S_IMP_LOOK2) { A_Look(); }
	S_IMP_LOOK2('IMPX', 1, 'monsters/gargoyle.md2', 1, 10.0 / 35.0, S_IMP_LOOK3) { A_Look(); }
	S_IMP_LOOK3('IMPX', 2, 'monsters/gargoyle.md2', 2, 10.0 / 35.0, S_IMP_LOOK4) { A_Look(); }
	S_IMP_LOOK4('IMPX', 1, 'monsters/gargoyle.md2', 3, 10.0 / 35.0, S_IMP_LOOK1) { A_Look(); }
	S_IMP_FLY1('IMPX', 0, 'monsters/gargoyle.md2', 4, 3.0 / 35.0, S_IMP_FLY2) { A_Chase(); }
	S_IMP_FLY2('IMPX', 0, 'monsters/gargoyle.md2', 5, 3.0 / 35.0, S_IMP_FLY3) { A_Chase(); }
	S_IMP_FLY3('IMPX', 1, 'monsters/gargoyle.md2', 6, 3.0 / 35.0, S_IMP_FLY4) { A_Chase(); }
	S_IMP_FLY4('IMPX', 1, 'monsters/gargoyle.md2', 7, 3.0 / 35.0, S_IMP_FLY5) { A_Chase(); }
	S_IMP_FLY5('IMPX', 2, 'monsters/gargoyle.md2', 8, 3.0 / 35.0, S_IMP_FLY6) { A_Chase(); }
	S_IMP_FLY6('IMPX', 2, 'monsters/gargoyle.md2', 9, 3.0 / 35.0, S_IMP_FLY7) { A_Chase(); }
	S_IMP_FLY7('IMPX', 1, 'monsters/gargoyle.md2', 10, 3.0 / 35.0, S_IMP_FLY8) { A_Chase(); }
	S_IMP_FLY8('IMPX', 1, 'monsters/gargoyle.md2', 11, 3.0 / 35.0, S_IMP_FLY1) { A_Chase(); }
	S_IMP_MEATK1('IMPX', 3, 'monsters/gargoyle.md2', 12, 6.0 / 35.0, S_IMP_MEATK2) { A_FaceTarget(); }
	S_IMP_MEATK2('IMPX', 4, 'monsters/gargoyle.md2', 13, 6.0 / 35.0, S_IMP_MEATK3) { A_FaceTarget(); }
	S_IMP_MEATK3('IMPX', 5, 'monsters/gargoyle.md2', 14, 6.0 / 35.0, S_IMP_FLY1) { A_ImpMeAttack(); }
	S_IMP_MSATK1_1('IMPX', 0, 'monsters/gargoyle.md2', 4, 10.0 / 35.0, S_IMP_MSATK1_2) { A_FaceTarget(); }
	S_IMP_MSATK1_2('IMPX', 1, 'monsters/gargoyle.md2', 6, 6.0 / 35.0, S_IMP_MSATK1_3) { A_ImpMsAttack(); }
	S_IMP_MSATK1_3('IMPX', 2, 'monsters/gargoyle.md2', 8, 6.0 / 35.0, S_IMP_MSATK1_4) { }
	S_IMP_MSATK1_4('IMPX', 1, 'monsters/gargoyle.md2', 10, 6.0 / 35.0, S_IMP_MSATK1_5) { }
	S_IMP_MSATK1_5('IMPX', 0, 'monsters/gargoyle.md2', 4, 6.0 / 35.0, S_IMP_MSATK1_6) { }
	S_IMP_MSATK1_6('IMPX', 1, 'monsters/gargoyle.md2', 6, 6.0 / 35.0, S_IMP_MSATK1_3) { }
	S_IMP_PAIN1('IMPX', 6, 'monsters/gargoyle.md2', 18, 3.0 / 35.0, S_IMP_PAIN2) { }
	S_IMP_PAIN2('IMPX', 6, 'monsters/gargoyle.md2', 19, 3.0 / 35.0, S_IMP_FLY1) { A_Pain(); }
	S_IMP_DIE1('IMPX', 6, 4.0 / 35.0, S_IMP_DIE2) { A_ImpDeath(); }
	S_IMP_DIE2('IMPX', 7, 5.0 / 35.0, S_IMP_DIE2) { }
	S_IMP_XDIE1('IMPX', 18, 5.0 / 35.0, S_IMP_XDIE2) { A_ImpXDeath1(); }
	S_IMP_XDIE2('IMPX', 19, 5.0 / 35.0, S_IMP_XDIE3) { }
	S_IMP_XDIE3('IMPX', 20, 5.0 / 35.0, S_IMP_XDIE4) { }
	S_IMP_XDIE4('IMPX', 21, 5.0 / 35.0, S_IMP_XDIE5) { A_ImpXDeath2(); }
	S_IMP_XDIE5('IMPX', 22, 5.0 / 35.0, S_IMP_XDIE5) { }
	S_IMP_CRASH1('IMPX', 8, 7.0 / 35.0, S_IMP_CRASH2) { A_ImpExplode(); }
	S_IMP_CRASH2('IMPX', 9, 7.0 / 35.0, S_IMP_CRASH3) { A_Scream(); }
	S_IMP_CRASH3('IMPX', 10, 7.0 / 35.0, S_IMP_CRASH4) { }
	S_IMP_CRASH4('IMPX', 11, -1.0, S_NULL) { }
	S_IMP_XCRASH1('IMPX', 23, 7.0 / 35.0, S_IMP_XCRASH2) { }
	S_IMP_XCRASH2('IMPX', 24, 7.0 / 35.0, S_IMP_XCRASH3) { }
	S_IMP_XCRASH3('IMPX', 25, -1.0, S_NULL) { }
}

defaultproperties
{
	Health = 40;
	GibsHealth = -20;
	Radius = 16.0;
	Height = 36.0;
	Mass = 50.0;
	StepSpeed = 10.0;
	PainChance = 200;
	bNoGravity = true;
	bFloat = true;
	bSpawnFloat = true;
	bFloorClip = false;
	SpawnState = S_IMP_LOOK1;
	SeeState = S_IMP_FLY1;
	MeleeState = S_IMP_MEATK1;
	MissileState = S_IMP_MSATK1_1;
	PainState = S_IMP_PAIN1;
	DeathState = S_IMP_DIE1;
	GibsDeathState = S_IMP_XDIE1;
	CrashState = S_IMP_CRASH1;
	XCrashState = S_IMP_XCRASH1;
	SightSound = 'ImpSight';
	ActiveSound = 'ImpActive';
	AttackSound = 'ImpAttack1';
	PainSound = 'ImpPain';
	DeathSound = 'ImpDeath';
}

}

//**************************************************************************
//**************************************************************************
//
//	ImpLeader
//
//**************************************************************************
//**************************************************************************

class ImpLeader:Imp
	__mobjinfo__(5)
{

//==========================================================================
//
//  A_ImpMsAttack2
//
//  Fireball attack of the imp leader.
//
//==========================================================================

void A_ImpMsAttack2(void)
{
	if (!Enemy)
	{
		return;
	}
	PlaySound(AttackSound, CHAN_WEAPON);
	if (CheckMeleeRange())
	{
		Enemy.Damage(self, self, 5 + (P_Random() & 7));
		return;
	}
	SpawnMissile(Enemy, ImpBall);
}

__states__
{
	S_IMP_MSATK2_1('IMPX', 3, 'monsters/gargoyle.md2', 15, 6.0 / 35.0, S_IMP_MSATK2_2) { A_FaceTarget(); }
	S_IMP_MSATK2_2('IMPX', 4, 'monsters/gargoyle.md2', 16, 6.0 / 35.0, S_IMP_MSATK2_3) { A_FaceTarget(); }
	S_IMP_MSATK2_3('IMPX', 5, 'monsters/gargoyle.md2', 17, 6.0 / 35.0, S_IMP_FLY1) { A_ImpMsAttack2(); }
}

defaultproperties
{
	Health = 80;
	GibsHealth = -40;
	MeleeState = S_NULL;
	MissileState = S_IMP_MSATK2_1;
	AttackSound = 'ImpAttack2';
}

}

//**************************************************************************
//
//  $Log$
//  Revision 1.23  2002/04/11 16:52:14  dj_jl
//  Made TryMove native.
//
//  Revision 1.22  2002/03/20 19:07:46  dj_jl
//  Got rid of a lots of legacy, beautification.
//
//  Revision 1.21  2002/03/16 17:45:13  dj_jl
//  Added Pawn and PlayerPawn classes, beautification.
//
//  Revision 1.20  2002/03/02 17:41:12  dj_jl
//  Renamed Enemy to Target. Beautification.
//
//  Revision 1.19  2002/02/26 17:49:48  dj_jl
//  no message
//
//  Revision 1.18  2002/02/22 18:00:16  dj_jl
//  A lots of the renaming, beautification.
//
//  Revision 1.17  2002/02/16 16:26:55  dj_jl
//  Removed precalculated sound IDs
//
//  Revision 1.16  2002/02/14 19:20:58  dj_jl
//  Renamed Entity properties to new naming style
//
//  Revision 1.15  2002/02/06 17:28:52  dj_jl
//  Replaced Actor flags with boolean variables.
//
//  Revision 1.14  2002/02/02 19:17:25  dj_jl
//  Replacing pointers with references, beautification.
//
//  Revision 1.13  2002/01/28 18:50:18  dj_jl
//  Made some functions Actor members.
//  Replaced some pointers with references.
//
//  Revision 1.12  2002/01/25 18:04:35  dj_jl
//  Many global functions made Actor functions
//
//  Revision 1.11  2002/01/24 18:19:26  dj_jl
//  Split common class Entity, made SetMobjState an Entity function
//
//  Revision 1.10  2002/01/17 18:15:53  dj_jl
//  Renamed all map object classes
//
//  Revision 1.9  2002/01/15 18:27:00  dj_jl
//  Some property names with logical words starting with capital letter.
//  Moved everything from Entity class into Actor class.
//
//  Revision 1.8  2002/01/07 12:23:17  dj_jl
//  Changed copyright year
//
//  Revision 1.7  2001/12/12 18:57:41  dj_jl
//  State functions now are methods of Actor and Weapon classes
//
//  Revision 1.6  2001/11/02 18:42:08  dj_jl
//  Damage method
//
//  Revision 1.5  2001/10/22 17:23:12  dj_jl
//  Floatification of angles
//
//  Revision 1.4  2001/09/24 17:24:10  dj_jl
//  Beautification
//
//  Revision 1.3  2001/08/29 17:42:13  dj_jl
//  Added sound channel
//  
//  Revision 1.2  2001/07/27 14:27:52  dj_jl
//  Update with Id-s and Log-s, some fixes
//
//**************************************************************************
