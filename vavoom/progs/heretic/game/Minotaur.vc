//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Minotaur : Pawn
	__mobjinfo__(9);

float ChargeSpeed;
bool bFloorFireRefired;

state ChargeAttackState;
state FloorFireAttackState;
state FloorFireAttackState2;

//==========================================================================
//
//	DropStuff
//
//==========================================================================

void DropStuff()
{
	DropItem(ArtifactSuperHeal, 0, 0.19921875);
	DropItem(ItemAmmoPhoenixWimpy, 10, 0.328125);
}

//==========================================================================
//
//  A_MinotaurAtk1
//
//  Melee attack.
//
//==========================================================================

void A_MinotaurAtk1()
{
	if (!Enemy)
	{
		return;
	}
	PlaySound('minotaur/melee', CHAN_WEAPON);
	if (CheckMeleeRange())
	{
		Enemy.Damage(self, self, HITDICE(4));
		if (Enemy.bIsPlayer)
		{
			// Squish the player
			Player(Enemy.Player).DeltaViewHeight = -512.0;
		}
	}
}

//==========================================================================
//
//  A_MinotaurDecide
//
//  Choose a missile attack.
//
//==========================================================================

void A_MinotaurDecide()
{
	float dist;

	if (!Enemy)
	{
		return;
	}
	PlaySound('minotaur/sight', CHAN_VOICE);
	dist = DistTo(Enemy);
	if (Enemy.Origin.z + Enemy.Height > Origin.z &&
		Enemy.Origin.z + Enemy.Height < Origin.z + Height &&
		dist < 8.0 * 64.0 && dist > 1.0 * 64.0 && P_Random() < 150)
	{
		// Charge attack
		// Don't call the state function right away
		SetState(ChargeAttackState);
		bSkullFly = true;
		A_FaceTarget();
		Velocity = Normalise(Enemy.Origin - Origin) * ChargeSpeed;
		Special1 = 35 / 2;	// Charge duration
	}
	else if (Enemy.Origin.z == Enemy.FloorZ &&
		dist < 9.0 * 64.0 && P_Random() < 220)
	{
		// Floor fire attack
		SetState(FloorFireAttackState);
		bFloorFireRefired = false;
	}
	else
	{
		// Swing attack
		A_FaceTarget();
		// Don't need to call SetState because the current state
		// falls through to the swing attack
	}
}

//==========================================================================
//
//  A_MinotaurAtk2
//
//  Swing attack.
//
//==========================================================================

void A_MinotaurAtk2()
{
	Actor A;
	float angle;
	TAVec ang;

	if (!Enemy)
	{
		return;
	}
	PlaySound('minotaur/attack2', CHAN_WEAPON);
	if (CheckMeleeRange())
	{
		Enemy.Damage(self, self, HITDICE(5));
		return;
	}
	A = SpawnMissile(Enemy, MinotaurMissile);
	if (A)
	{
		A.PlaySound('minotaur/attack2', CHAN_WEAPON);
		VectorAngles(&A.Velocity, &ang);
		angle = A.Angles.yaw;
		SpawnMissileAngles(MinotaurMissile,
			AngleMod360(angle - (45.0 / 8.0)), ang.pitch);
		SpawnMissileAngles(MinotaurMissile,
			AngleMod360(angle + (45.0 / 8.0)), ang.pitch);
		SpawnMissileAngles(MinotaurMissile,
			AngleMod360(angle - (45.0 / 16.0)), ang.pitch);
		SpawnMissileAngles(MinotaurMissile,
			AngleMod360(angle + (45.0 / 16.0)), ang.pitch);
	}
}

//==========================================================================
//
//  A_MinotaurAtk3
//
//  Floor fire attack.
//
//==========================================================================

void A_MinotaurAtk3()
{
	Actor A;

	if (!Enemy)
	{
		return;
	}
	PlaySound('minotaur/attack3', CHAN_WEAPON);
	if (CheckMeleeRange())
	{
		Enemy.Damage(self, self, HITDICE(5));
		if (Enemy.Player)
		{
			// Squish the player
			Player(Enemy.Player).DeltaViewHeight = -512.0;
		}
	}
	else
	{
		A = SpawnMissile(Enemy, MinotaurFloorFire);
		if (A != none)
		{
			A.PlaySound('minotaur/attack1', CHAN_VOICE);
		}
	}
	if (Random() < 0.75 && !bFloorFireRefired)
	{
		SetState(FloorFireAttackState2);
		bFloorFireRefired = true;
	}
}

//==========================================================================
//
//  A_MinotaurCharge
//
//==========================================================================

void A_MinotaurCharge()
{
	Actor puff;

	if (Special1)
	{
		puff = Spawn(PhoenixPuff, Origin);
		puff.Velocity.z = 2.0 * 35.0;
		Special1--;
	}
	else
	{
		bSkullFly = false;
		SetState(SeeState);
	}
}

//==========================================================================
//
//  A_BossDeath
//
//  Trigger special effects if all bosses are dead.
//
//==========================================================================

void A_BossDeath()
{
	if (!XLevel.bMinotaurSpecial)
	{
		//  Not a boss level
		return;
	}
	BossDeathSpecialAction();
}

__states__
{
	S_MNTR_LOOK1('MNTR', 0, 'monsters/minotaur/base.md2', 0, 2.5 / 35.0, S_MNTR_LOOK2) { A_Look(); }
	S_MNTR_LOOK2('MNTR', 0, 'monsters/minotaur/base.md2', 1, 2.5 / 35.0, S_MNTR_LOOK3) { }
	S_MNTR_LOOK3('MNTR', 0, 'monsters/minotaur/base.md2', 2, 2.5 / 35.0, S_MNTR_LOOK4) { }
	S_MNTR_LOOK4('MNTR', 0, 'monsters/minotaur/base.md2', 3, 2.5 / 35.0, S_MNTR_LOOK5) { }
	S_MNTR_LOOK5('MNTR', 1, 'monsters/minotaur/base.md2', 4, 2.5 / 35.0, S_MNTR_LOOK6) { A_Look(); }
	S_MNTR_LOOK6('MNTR', 1, 'monsters/minotaur/base.md2', 5, 2.5 / 35.0, S_MNTR_LOOK7) { }
	S_MNTR_LOOK7('MNTR', 1, 'monsters/minotaur/base.md2', 6, 2.5 / 35.0, S_MNTR_LOOK8) { }
	S_MNTR_LOOK8('MNTR', 1, 'monsters/minotaur/base.md2', 7, 2.5 / 35.0, S_MNTR_LOOK1) { }
	S_MNTR_WALK1('MNTR', 0, 'monsters/minotaur/base.md2', 8, 2.5 / 35.0, S_MNTR_WALK2) { A_Chase(); }
	S_MNTR_WALK2('MNTR', 0, 'monsters/minotaur/base.md2', 9, 2.5 / 35.0, S_MNTR_WALK3) { }
	S_MNTR_WALK3('MNTR', 1, 'monsters/minotaur/base.md2', 10, 2.5 / 35.0, S_MNTR_WALK4) { A_Chase(); }
	S_MNTR_WALK4('MNTR', 1, 'monsters/minotaur/base.md2', 11, 2.5 / 35.0, S_MNTR_WALK5) { }
	S_MNTR_WALK5('MNTR', 2, 'monsters/minotaur/base.md2', 12, 2.5 / 35.0, S_MNTR_WALK6) { A_Chase(); }
	S_MNTR_WALK6('MNTR', 2, 'monsters/minotaur/base.md2', 13, 2.5 / 35.0, S_MNTR_WALK7) { }
	S_MNTR_WALK7('MNTR', 3, 'monsters/minotaur/base.md2', 14, 2.5 / 35.0, S_MNTR_WALK8) { A_Chase(); }
	S_MNTR_WALK8('MNTR', 3, 'monsters/minotaur/base.md2', 15, 2.5 / 35.0, S_MNTR_WALK9) { }
	S_MNTR_WALK9('MNTR', 0, 'monsters/minotaur/base.md2', 16, 2.5 / 35.0, S_MNTR_WALK10) { A_Chase(); }
	S_MNTR_WALK10('MNTR', 0, 'monsters/minotaur/base.md2', 17, 2.5 / 35.0, S_MNTR_WALK11) { }
	S_MNTR_WALK11('MNTR', 1, 'monsters/minotaur/base.md2', 18, 2.5 / 35.0, S_MNTR_WALK12) { A_Chase(); }
	S_MNTR_WALK12('MNTR', 1, 'monsters/minotaur/base.md2', 19, 2.5 / 35.0, S_MNTR_WALK13) { }
	S_MNTR_WALK13('MNTR', 2, 'monsters/minotaur/base.md2', 20, 2.5 / 35.0, S_MNTR_WALK14) { A_Chase(); }
	S_MNTR_WALK14('MNTR', 2, 'monsters/minotaur/base.md2', 21, 2.5 / 35.0, S_MNTR_WALK15) { }
	S_MNTR_WALK15('MNTR', 3, 'monsters/minotaur/base.md2', 22, 2.5 / 35.0, S_MNTR_WALK16) { A_Chase(); }
	S_MNTR_WALK16('MNTR', 3, 'monsters/minotaur/base.md2', 23, 2.5 / 35.0, S_MNTR_WALK1) { }
	S_MNTR_ATK1_1('MNTR', 21, 'monsters/minotaur/base.md2', 24, 4.0 / 35.0, S_MNTR_ATK1_2) { A_FaceTarget(); }
	S_MNTR_ATK1_2('MNTR', 21, 'monsters/minotaur/base.md2', 25, 3.0 / 35.0, S_MNTR_ATK1_3) { }
	S_MNTR_ATK1_3('MNTR', 21, 'monsters/minotaur/base.md2', 26, 3.0 / 35.0, S_MNTR_ATK1_4) { }
	S_MNTR_ATK1_4('MNTR', 22, 'monsters/minotaur/base.md2', 27, 3.0 / 35.0, S_MNTR_ATK1_5) { A_FaceTarget(); }
	S_MNTR_ATK1_5('MNTR', 22, 'monsters/minotaur/base.md2', 28, 4.0 / 35.0, S_MNTR_ATK1_6) { }
	S_MNTR_ATK1_6('MNTR', 23, 'monsters/minotaur/base.md2', 29, 4.0 / 35.0, S_MNTR_ATK1_7) { A_MinotaurAtk1(); }
	S_MNTR_ATK1_7('MNTR', 23, 'monsters/minotaur/base.md2', 30, 4.0 / 35.0, S_MNTR_ATK1_8) { }
	S_MNTR_ATK1_8('MNTR', 23, 'monsters/minotaur/base.md2', 31, 4.0 / 35.0, S_MNTR_WALK1) { }
	S_MNTR_ATK2_1('MNTR', 21, 'monsters/minotaur/base.md2', 32, 4.0 / 35.0, S_MNTR_ATK2_2) { A_MinotaurDecide(); }
	S_MNTR_ATK2_2('MNTR', 21, 'monsters/minotaur/base.md2', 33, 3.0 / 35.0, S_MNTR_ATK2_3) { }
	S_MNTR_ATK2_3('MNTR', 21, 'monsters/minotaur/base.md2', 34, 3.0 / 35.0, S_MNTR_ATK2_4) { }
	S_MNTR_ATK2_4('MNTR', 24, 'monsters/minotaur/base.md2', 35, 2.0 / 35.0, S_MNTR_ATK2_5) { A_FaceTarget(); }
	S_MNTR_ATK2_5('MNTR', 24, 'monsters/minotaur/base.md2', 36, 2.0 / 35.0, S_MNTR_ATK2_6) { }
	S_MNTR_ATK2_6('MNTR', 25, 'monsters/minotaur/base.md2', 37, 3.0 / 35.0, S_MNTR_ATK2_7) { A_MinotaurAtk2(); }
	S_MNTR_ATK2_7('MNTR', 25, 'monsters/minotaur/base.md2', 38, 3.0 / 35.0, S_MNTR_ATK2_8) { }
	S_MNTR_ATK2_8('MNTR', 25, 'monsters/minotaur/base.md2', 39, 3.0 / 35.0, S_MNTR_WALK1) { }
	S_MNTR_ATK3_1('MNTR', 21, 'monsters/minotaur/base.md2', 24, 4.0 / 35.0, S_MNTR_ATK3_2) { A_FaceTarget(); }
	S_MNTR_ATK3_2('MNTR', 21, 'monsters/minotaur/base.md2', 25, 3.0 / 35.0, S_MNTR_ATK3_3) { }
	S_MNTR_ATK3_3('MNTR', 21, 'monsters/minotaur/base.md2', 26, 3.0 / 35.0, S_MNTR_ATK3_4) { }
	S_MNTR_ATK3_4('MNTR', 22, 'monsters/minotaur/base.md2', 27, 3.0 / 35.0, S_MNTR_ATK3_5) { A_FaceTarget(); }
	S_MNTR_ATK3_5('MNTR', 22, 'monsters/minotaur/base.md2', 28, 4.0 / 35.0, S_MNTR_ATK3_6) { }
	S_MNTR_ATK3_6('MNTR', 23, 'monsters/minotaur/base.md2', 29, 4.0 / 35.0, S_MNTR_ATK3_7) { A_MinotaurAtk3(); }
	S_MNTR_ATK3_7('MNTR', 23, 'monsters/minotaur/base.md2', 30, 4.0 / 35.0, S_MNTR_ATK3_8) { }
	S_MNTR_ATK3_8('MNTR', 23, 'monsters/minotaur/base.md2', 31, 4.0 / 35.0, S_MNTR_WALK1) { }
	S_MNTR_ATK3_9('MNTR', 23, 'monsters/minotaur/base.md2', 29, 4.0 / 35.0, S_MNTR_ATK3_10) { }
	S_MNTR_ATK3_10('MNTR', 23, 'monsters/minotaur/base.md2', 30, 4.0 / 35.0, S_MNTR_ATK3_11) { }
	S_MNTR_ATK3_11('MNTR', 23, 'monsters/minotaur/base.md2', 31, 4.0 / 35.0, S_MNTR_ATK3_1) { }
	S_MNTR_ATK4_1('MNTR', 20, 'monsters/minotaur/base.md2', 40, 2.0 / 35.0, S_MNTR_ATK4_2) { }
	S_MNTR_ATK4_2('MNTR', 20, 'monsters/minotaur/base.md2', 40, 2.0 / 35.0, S_MNTR_ATK4_2) { A_MinotaurCharge(); }
	S_MNTR_PAIN1('MNTR', 4, 'monsters/minotaur/base.md2', 41, 3.0 / 35.0, S_MNTR_PAIN2) { }
	S_MNTR_PAIN2('MNTR', 4, 'monsters/minotaur/base.md2', 41, 6.0 / 35.0, S_MNTR_WALK1) { A_Pain(); }
	S_MNTR_DIE1('MNTR', 5, 'monsters/minotaur/base.md2', 42, 6.0 / 35.0, S_MNTR_DIE2) { }
	S_MNTR_DIE2('MNTR', 6, 'monsters/minotaur/base.md2', 43, 5.0 / 35.0, S_MNTR_DIE3) { }
	S_MNTR_DIE3('MNTR', 7, 'monsters/minotaur/base.md2', 44, 6.0 / 35.0, S_MNTR_DIE4) { A_Scream(); }
	S_MNTR_DIE4('MNTR', 8, 'monsters/minotaur/base.md2', 45, 5.0 / 35.0, S_MNTR_DIE5) { }
	S_MNTR_DIE5('MNTR', 9, 'monsters/minotaur/base.md2', 46, 6.0 / 35.0, S_MNTR_DIE6) { }
	S_MNTR_DIE6('MNTR', 10, 'monsters/minotaur/base.md2', 47, 5.0 / 35.0, S_MNTR_DIE7) { }
	S_MNTR_DIE7('MNTR', 11, 'monsters/minotaur/base.md2', 48, 6.0 / 35.0, S_MNTR_DIE8) { }
	S_MNTR_DIE8('MNTR', 12, 'monsters/minotaur/base.md2', 49, 5.0 / 35.0, S_MNTR_DIE9) { A_NoBlocking(); }
	S_MNTR_DIE9('MNTR', 13, 'monsters/minotaur/base.md2', 50, 6.0 / 35.0, S_MNTR_DIE10) { }
	S_MNTR_DIE10('MNTR', 14, 'monsters/minotaur/base.md2', 51, 5.0 / 35.0, S_MNTR_DIE11) { }
	S_MNTR_DIE11('MNTR', 15, 'monsters/minotaur/base.md2', 52, 6.0 / 35.0, S_MNTR_DIE12) { }
	S_MNTR_DIE12('MNTR', 16, 'monsters/minotaur/base.md2', 53, 5.0 / 35.0, S_MNTR_DIE13) { }
	S_MNTR_DIE13('MNTR', 17, 'monsters/minotaur/base.md2', 54, 6.0 / 35.0, S_MNTR_DIE14) { }
	S_MNTR_DIE14('MNTR', 18, 'monsters/minotaur/base.md2', 55, 5.0 / 35.0, S_MNTR_DIE15) { }
	S_MNTR_DIE15('MNTR', 19, 'monsters/minotaur/base.md2', 56, 5.0 / 35.0, S_MNTR_DIE16) { }
	S_MNTR_DIE16('MNTR', 19, 'monsters/minotaur/base.md2', 57, -1.0, none) { A_BossDeath(); }
}

defaultproperties
{
	Health = 3000;
	Radius = 28.0;
	Height = 100.0;
	Mass = 800.0;
	StepSpeed = 16.0;
	PainChance = 0.09765625;
	MissileDamage = 7;
	ChargeSpeed	= 455.0;
	bDropOff = true;
	bBoss = true;
	bNoMorph = true;
	IdleState = S_MNTR_LOOK1;
	SeeState = S_MNTR_WALK1;
	MeleeState = S_MNTR_ATK1_1;
	MissileState = S_MNTR_ATK2_1;
	ChargeAttackState = S_MNTR_ATK4_1;
	FloorFireAttackState = S_MNTR_ATK3_1;
	FloorFireAttackState2 = S_MNTR_ATK3_9;
	PainState = S_MNTR_PAIN1;
	DeathState = S_MNTR_DIE1;
	SightSound = 'minotaur/sight';
	ActiveSound = 'minotaur/active';
	AttackSound = 'minotaur/attack1';
	PainSound = 'minotaur/pain';
	DeathSound = 'minotaur/death';
}
