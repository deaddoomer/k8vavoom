//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Player : BasePlayer;

const int MAXCHICKENHEALTH	= 30;
const float VIEWHEIGHT		= 41.0;

const float BLINKTHRESHOLD	= 4.0;

const float
	INVULNTIME		= 30.0,
	INVISTIME		= 60.0,
	INFRATIME		= 120.0,
	WPNLEV2TIME 	= 40.0,
	FLIGHTTIME 		= 60.0,
	CHICKENTIME 	= 40.0;

//
// Player internal flags, for cheats and debug.
//
const int
	CF_NOCLIP		= 1,	// No clipping, walk through barriers.
	CF_GODMODE		= 2;	// No damage, no health loss.

// 16 pixels of bob
const float MAXBOB	= 16.0;

const int
	INVERSECOLORMAP	= 32,

	STARTREDPALS	= 1,
	STARTBONUSPALS	= 9,
	NUMREDPALS		= 8,
	NUMBONUSPALS	= 4;

const int MAXHEALTH	= 100;

const float USERANGE	= 64.0;

struct inventory_t
{
	int type;
	int count;
};

//
//  FIRST FIELDS ARE STATS THAT ARE SENT TO CLIENT FOR STATUS BAR
//

int SBFlags;
int ReadyAmmo;

int ArtifactFlash;
int InvPtr;
int InvCurPos;

int ArmorPoints;

int ReadyArtifact;
inventory_t Inventory[HereticDefs::NUMARTIFACTS];
int InventorySlotNum;

int ReadyWeapon;

//
//  ----------
//

int Ammo[NUMAMMO];

float ChickenTime;	// player is a chicken if > 0

// Power ups. invinc and invis are timers.
float Powers[NUMPOWERS];

int Keys;

int Arti;	// artitype_t to use

// Base height above floor for viewz.
float ViewHeight;
// Bob/squat speed.
float DeltaViewHeight;
// bounded/scaled total momentum.
float Bob;

float FlyHeight;

float InventoryTime;
int ArtifactCount;

// Refired shots are less accurate.
int Refire;

bool Backpack;

// Bit flags, for cheats and debug.
// See cheat_t, above.
int Cheats;

// Is wp_nochange if not changing.
int PendingWeapon;

bool WeaponOwned[NUMWEAPONS];

// Who did damage (NULL for floors/ceilings).
Actor Attacker;

// For screen flashing (red or bright).
float DamageFlash;
float BonusFlash;

float JumpTime;
int LocalQuakeHappening;

// Armor type is 0-2.
int ArmorType;

int ChickenPeck;	// chicken peck countdown
Actor Rain1;	// active rain maker 1
Actor Rain2;	// active rain maker 2

int NewTorch;	// used in the torch flicker effect.
int NewTorchDelta;

TVec MoveDir;

float LastSectorDamageTime;

float HazardTime;
float LastHazardTime;

BotPlayer Bot;

bool onground;

//==========================================================================
//
//	PutClientIntoServer
//
//==========================================================================

void PutClientIntoServer()
{
	ViewEnts[ps_weapon] = Weapon(SpawnObject(Weapon));
	ViewEnts[ps_weapon].Player = self;
	ViewEnts[ps_flash] = Weapon(SpawnObject(Weapon));
	ViewEnts[ps_flash].Player = self;
	if (bIsBot)
	{
		Bot = BotPlayer(SpawnObject(BotPlayer));
		Bot.player = self;
		Bot.OnBeginPlay();
	}
}

//==========================================================================
//
//  SpawnClient
//
//==========================================================================

void SpawnClient()
{
	if (Level.Game.deathmatch)
	{
		DeathMatchSpawnPlayer();
	}
	else
	{
		SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
			GetPlayerNum()]);
	}
}

//==========================================================================
//
//  NetGameReborn
//
//==========================================================================

void NetGameReborn()
{
	int i;

	// remove pitch and roll angles from corpse
	MO.Angles.pitch = 0.0;
	MO.Angles.roll = 0.0;

	// first dissasociate the corpse
	MO.Player = none;
	MO.bIsPlayer = false;

	// spawn at random spot if in death match
	if (Level.Game.deathmatch)
	{
		DeathMatchSpawnPlayer();
		return;
	}

	if (CheckSpot(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
		GetPlayerNum()]))
	{
		SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
			GetPlayerNum()]);
		return;
	}

	// try to spawn at one of the other players spots
	for (i = 0; i < MAXPLAYERS; i++)
	{
		if (CheckSpot(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS + i]))
		{
			SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS + i]);
			return;
		}
	}

	// he's going to be inside something.  Too bad.
	SpawnPlayer(&Level.PlayerStarts[Level.Game.RebornPosition * MAXPLAYERS +
		GetPlayerNum()]);
}

//==========================================================================
//
//  DisconnectClient
//
//==========================================================================

void DisconnectClient()
{
	int i;

	if (Bot != none)
	{
		Bot.Destroy();
		Bot = none;
	}
	if (MO)
	{
		MO.Player = none;
		MO.bIsPlayer = false;
		Actor(MO).Damage(none, none, 10000);
	}
	for (i = 0; i < NUMPSPRITES; i++)
	{
		if (ViewEnts[i])
		{
			ViewEnts[i].Destroy();
			ViewEnts[i] = none;
		}
	}
	bprint("%s left the game\n", ARR2STR(PlayerName));
	MO.PlayFullVolumeSound('misc/chat', CHAN_AUTO);
}

//==========================================================================
//
//  UserinfoChanged
//
//==========================================================================

void UserinfoChanged()
{
	if (!bSpawned)
	{
		return;
	}
	MO.Translation = Color;
}

//==========================================================================
//
//  DeathMatchSpawnPlayer
//
//  Spawns a player at one of the random death match spots called at level
// load and each death
//
//==========================================================================

void DeathMatchSpawnPlayer()
{
	int i;
	int j;

	for (j = 0; j < 20; j++)
	{
		i = P_Random() % Level.NumDeathmatchStarts;
		if (CheckSpot(&Level.DeathmatchStarts[i]))
		{
			SpawnPlayer(&Level.DeathmatchStarts[i]);
			return;
		}
	}

	// no good spot, so the player will probably get stuck 
	SpawnPlayer(&Level.PlayerStarts[GetPlayerNum()]);
}

//==========================================================================
//
//  CheckSpot
//
//  Returns false if the player cannot be respawned at the given mthing_t
// spot because something is occupying it
//
//==========================================================================

bool CheckSpot(mthing_t * mthing)
{
	float x;
	float y;
	sector_t *sec;
	float an;
	Actor A;
	int i;

	if (!MO)
	{
		// first spawn of level, before corpses
		for (i = 0; i < MAXPLAYERS; i++)
			if (Level.Game.Players[i] && Level.Game.Players[i].MO &&
				Level.Game.Players[i].MO.Origin.x == mthing->x &&
				Level.Game.Players[i].MO.Origin.y == mthing->y)
				return false;
		return true;
	}

	x = mthing->x;
	y = mthing->y;

	MO.bNoPassMobj = true;
	if (!MO.CheckPosition(vector(x, y, Actor::ONFLOORZ)))
	{
		MO.bNoPassMobj = false;
		return false;
	}
	MO.bNoPassMobj = false;

	// spawn a teleport fog 
	sec = Level.XLevel.PointInSector(vector(x, y, 0.0));
	an = itof(45 * (mthing->angle / 45));

	A = HereticLevelInfo(Level).Spawn(TeleportFog, vector(x + 20.0 * cos(an), y + 20.0 * sin(an),
		sec->floorheight + Actor::TELEFOGHEIGHT));
	A.PlaySound('misc/teleport', CHAN_VOICE);

	return true;
}

//==========================================================================
//
//  SpawnPlayer
//
//  Called when a player is spawned on the level
//  Most of the player structure stays unchanged between levels
//
//==========================================================================

void SpawnPlayer(mthing_t * mthing)
{
	float x, y, z;
	Actor A;

	if (PlayerState == PST_REBORN)
	{
		PlayerReborn();
	}
	x = mthing->x;
	y = mthing->y;

	z = Actor::ONFLOORZ;
	A = HereticLevelInfo(Level).Spawn(HereticPlayer, vector(x, y, z));
	A.Origin.z += mthing->height;
	A.LinkToWorld();
	// set color translations for player sprites
	A.Translation = Color;

	A.Angles.yaw = itof(45 * (mthing->angle / 45));
	A.Player = self;
	A.bIsPlayer = true;
	A.Health = Health;
	MO = A;
	PlayerState = PST_LIVE;
	Refire = 0;
	DamageFlash = 0.0;
	BonusFlash = 0.0;
	ChickenTime = 0.0;
	Rain1 = none;
	Rain2 = none;
	ExtraLight = 0;
	FixedColormap = 0;
	LastSectorDamageTime = 0.0;
	LastHazardTime = 0.0;
	ViewHeight = VIEWHEIGHT;
	ViewAngles = A.Angles;
	bFixAngle = true;
	SetupPsprites();	// setup gun psprite
	if (Level.Game.deathmatch)
	{
		// Give all keys in death match mode
		Keys = 7;
	}
	if (bIsBot)
	{
		Bot.OnSpawn();
	}
}

//==========================================================================
//
//  PlayerReborn
//
//  Called after a player dies almost everything is cleared and initialized
//
//==========================================================================

void PlayerReborn()
{
	BotPlayer b;

	b = Bot;
	ClearPlayer();
	Bot = b;

	bUseDown = true;	// don't do anything immediately
	bAttackDown = true;
	PlayerState = PST_LIVE;
	Health = MAXHEALTH;
	WeaponOwned[wp_staff] = true;
	WeaponOwned[wp_goldwand] = true;
	Ammo[am_goldwand] = 50;
	SetWeapon(wp_goldwand);
	LocalQuakeHappening = 0;
}

//==========================================================================
//
//  PlayerExitMap
//
//  Call when a player completes a level.
//
//==========================================================================

void PlayerExitMap(bool clusterChange)
{
	int i;

	for (i = 0; i < InventorySlotNum; i++)
	{
		Inventory[i].count = 1;
	}
	ArtifactCount = InventorySlotNum;

	if (!Level.Game.deathmatch)
	{
		PlayerUseArtifact(HereticDefs::arti_fly);
	}

	for (i = 0; i < NUMPOWERS; i++)
	{
		Powers[i] = 0.0;
	}
	if (clusterChange)
	{
		Keys = 0;
	}
	if (ChickenTime)
	{
		SetWeapon(Actor(MO).Special1);	// Restore weapon
		ChickenTime = 0.0;
	}
	Items = 0;
	MO.Angles.pitch = 0.0;
	MO.Translucency = 0;	// cancel invisibility
	ExtraLight = 0;	// cancel gun flashes
	FixedColormap = 0;	// cancel ir gogles
	DamageFlash = 0.0;	// no palette changes
	BonusFlash = 0.0;
	Rain1 = none;
	Rain2 = none;
}

//==========================================================================
//
//  GiveAmmo
//
//  Returns true if the player accepted the ammo, false if it was
// refused (player has maxammo[ammo]).
//
//==========================================================================

bool GiveAmmo(int ammo, int count)
{
	int prevAmmo;
	int max;

	if (ammo == am_noammo)
	{
		return false;
	}
	if (ammo < 0 || ammo > NUMAMMO)
	{
		Error("GiveAmmo: bad type %i", ammo);
	}
	max = Backpack ? MainGameInfo(Level.Game).maxammo[ammo] * 2 : MainGameInfo(Level.Game).maxammo[ammo];
	if (Ammo[ammo] == max)
	{
		return false;
	}
	if (Level.Game.gameskill == sk_baby || Level.Game.gameskill == sk_nightmare)
	{
		// extra ammo in baby mode and nightmare mode
		count += count >> 1;
	}
	prevAmmo = Ammo[ammo];

	Ammo[ammo] += count;
	if (Ammo[ammo] > max)
	{
		Ammo[ammo] = max;
	}
	if (prevAmmo)
	{
		// Don't attempt to change weapons if the player already had
		// ammo of the type just given
		return true;
	}
	if (ReadyWeapon == wp_staff || ReadyWeapon == wp_gauntlets)
	{
		if (WeaponOwned[MainGameInfo(Level.Game).GetAmmoChange[ammo]])
		{
			PendingWeapon = MainGameInfo(Level.Game).GetAmmoChange[ammo];
		}
	}
	return true;
}

//==========================================================================
//
//  GiveWeapon
//
//  Returns true if the weapon or its ammo was accepted.
//
//==========================================================================

bool GiveWeapon(int weapon)
{
	bool gaveAmmo;
	bool gaveWeapon;

	if (Level.Game.netgame && !Level.Game.deathmatch)
	{
		// Cooperative net-game
		if (WeaponOwned[weapon])
		{
			return false;
		}
		BonusFlash += Actor::BONUSADD;
		WeaponOwned[weapon] = true;
		GiveAmmo(MainGameInfo(Level.Game).weapon_ammo_type[weapon], MainGameInfo(Level.Game).GetWeaponAmmo[weapon]);
		PendingWeapon = weapon;
		MO.PlaySound('misc/w_pkup', CHAN_ITEM);
		return false;
	}
	gaveAmmo = GiveAmmo(MainGameInfo(Level.Game).weapon_ammo_type[weapon], MainGameInfo(Level.Game).GetWeaponAmmo[weapon]);
	if (WeaponOwned[weapon])
	{
		gaveWeapon = false;
	}
	else
	{
		gaveWeapon = true;
		WeaponOwned[weapon] = true;
		if (MainGameInfo(Level.Game).WeaponValue[weapon] > MainGameInfo(Level.Game).WeaponValue[ReadyWeapon])
		{
			// Only switch to more powerful weapons
			PendingWeapon = weapon;
		}
	}
	return (gaveWeapon || gaveAmmo);
}

//==========================================================================
//
//  GiveBody
//
//  Returns false if the body isn't needed at all.
//
//==========================================================================

bool GiveBody(int num)
{
	int max;

	max = MAXHEALTH;
	if (ChickenTime)
	{
		max = MAXCHICKENHEALTH;
	}
	if (Health >= max)
	{
		return false;
	}
	Health += num;
	if (Health > max)
	{
		Health = max;
	}
	MO.Health = Health;
	return true;
}

//==========================================================================
//
//  GiveArmor
//
//  Returns false if the armor is worse than the current armor.
//
//==========================================================================

bool GiveArmor(int armortype)
{
	int hits;

	hits = armortype * 100;
	if (ArmorPoints >= hits)
	{
		return false;
	}
	ArmorType = armortype;
	ArmorPoints = hits;
	return true;
}

//==========================================================================
//
//  GiveKey
//
//==========================================================================

bool GiveKey(int key)
{
	if ((Keys & (1 << key)))
	{
		return false;
	}
	BonusFlash = Actor::BONUSADD;
	Keys |= 1 << key;
	return true;
}

//==========================================================================
//
//  GivePower
//
//  Returns true if power accepted.
//
//==========================================================================

bool GivePower(int power)
{
	if (power == pw_invulnerability)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = INVULNTIME;
		return true;
	}
	if (power == pw_weaponlevel2)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = WPNLEV2TIME;
		return true;
	}
	if (power == pw_invisibility)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = INVISTIME;
		MO.Translucency = 33;
		return true;
	}
	if (power == pw_flight)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = FLIGHTTIME;
		MO.bFly = true;
		MO.bNoGravity = true;
		if (MO.Origin.z <= MO.FloorZ)
		{
			FlyHeight = 10.0;	// thrust the player in the air a bit
		}
		return true;
	}
	if (power == pw_infrared)
	{
		if (Powers[power] > BLINKTHRESHOLD)
		{
			// Already have it
			return false;
		}
		Powers[power] = INFRATIME;
		return true;
	}
	if (Powers[power])
	{
		return false;	// already got it
	}
	Powers[power] = 1.0;
	return true;
}

//==========================================================================
//
//  GiveItem
//
//==========================================================================

bool GiveItem(int item)
{
	if (Items & item)
	{
		return false;	// already got it
	}
	Items |= item;
	return true;
}

//==========================================================================
//
//  GiveArtifact
//
//  Returns true if artifact accepted.
//
//==========================================================================

bool GiveArtifact(int arti)
{
	int i;

	i = 0;
	while (Inventory[i].type != arti && i < InventorySlotNum)
	{
		i++;
	}
	if (i == InventorySlotNum)
	{
		Inventory[i].count = 1;
		Inventory[i].type = arti;
		InventorySlotNum++;
	}
	else
	{
		if (Inventory[i].count >= 16)
		{
			// Player already has 16 of this item
			return false;
		}
		Inventory[i].count++;
	}
	if (ArtifactCount == 0)
	{
		ReadyArtifact = arti;
	}
	ArtifactCount++;
	return true;
}

//==========================================================================
//
//  InventoryLeft
//
//==========================================================================

void InventoryLeft()
{
	if (!InventoryTime)
	{
		InventoryTime = 5.0;
		return;
	}

	InventoryTime = 5.0;
	InvPtr--;
	if (InvPtr < 0)
	{
		InvPtr = 0;
	}
	else
	{
		InvCurPos--;
		if (InvCurPos < 0)
		{
			InvCurPos = 0;
		}
	}
}

//==========================================================================
//
//  InventoryRight
//
//==========================================================================

void InventoryRight()
{
	if (!InventoryTime)
	{
		InventoryTime = 5.0;
		return;
	}

	InventoryTime = 5.0;
	InvPtr++;
	if (InvPtr >= InventorySlotNum)
	{
		InvPtr--;
		if (InvPtr < 0)
			InvPtr = 0;
	}
	else
	{
		InvCurPos++;
		if (InvCurPos > 6)
		{
			InvCurPos = 6;
		}
	}
}

//==========================================================================
//
//  InventoryUse
//
//==========================================================================

void InventoryUse()
{
	ReadyArtifact = Inventory[InvPtr].type;
	// flag to denote that it's okay to use an artifact
	if (InventoryTime)
	{
		InventoryTime = 0.0;
	}
	else
	{
		Arti = ReadyArtifact;
	}
}

//==========================================================================
//
//  InventoryTick
//
//==========================================================================

void InventoryTick(float deltaTime)
{
	// turn inventory off after a certain amount of time
	if (InventoryTime)
	{
		InventoryTime -= deltaTime;
		if (InventoryTime <= 0.0)
		{
			InventoryTime = 0.0;
			ReadyArtifact = Inventory[InvPtr].type;
		}
	}
	if (ArtifactFlash)
	{
		ArtifactFlash--;
	}
}

//==========================================================================
//
//  ThrustPlayer
//
//  Moves the given origin along a given angle.
//
//==========================================================================

void ThrustPlayer(float angle, float move, float deltaTime)
{
	if (Powers[pw_flight] && !(MO.Origin.z <= MO.FloorZ))
	{
		MO.Velocity.x += move * cos(angle) * deltaTime;
		MO.Velocity.y += move * sin(angle) * deltaTime;
	}
	else if ((MO.Sector->special & SECSPEC_BASE_MASK) == SECSPEC_FrictionLow)
	{
		MO.Velocity.x += move / 4.0 * cos(angle) * deltaTime;
		MO.Velocity.y += move / 4.0 * sin(angle) * deltaTime;
	}
	else
	{
		MO.Velocity.x += move * cos(angle) * deltaTime;
		MO.Velocity.y += move * sin(angle) * deltaTime;
	}
}

//==========================================================================
//
//  CalcHeight
//
//  Calculate the walking / running height adjustment
//
//==========================================================================

void CalcHeight(float deltaTime)
{
	float angle;
	float bob;

	// Regular movement bobbing
	// (needs to be calculated for gun swing
	// even if not on ground)
	// OPTIMIZE: tablify angle
	// Note: a LUT allows for effects
	//  like a ramp with low health.
	Bob = MO.Velocity.x * MO.Velocity.x + MO.Velocity.y * MO.Velocity.y;
	Bob /= 4.0 * 35.0 * 35.0;
	if (Bob > MAXBOB)
		Bob = MAXBOB;

	if (MO.bFly && !onground)
	{
		Bob = 0.5;
	}

	angle = 180.0 * 35.0 / 10.0 * Level.Game.level->time;
	bob = Bob / 2.0 * sin(angle);

	// move viewheight
	if (PlayerState == PST_LIVE)
	{
		ViewHeight += DeltaViewHeight * deltaTime;

		if (ViewHeight > VIEWHEIGHT)
		{
			ViewHeight = VIEWHEIGHT;
			DeltaViewHeight = 0.0;
		}

		if (ViewHeight < VIEWHEIGHT / 2.0)
		{
			ViewHeight = VIEWHEIGHT / 2.0;
			if (DeltaViewHeight <= 0.0)
				DeltaViewHeight = 0.00001;
		}

		if (DeltaViewHeight)
		{
			DeltaViewHeight += 256.0 * deltaTime;
			if (!DeltaViewHeight)
				DeltaViewHeight = 0.00001;
		}
	}
	if (ChickenTime)
	{
		ViewOrg.z = MO.Origin.z + ViewHeight - 20.0;
	}
	else
	{
		ViewOrg.z = MO.Origin.z + ViewHeight + bob;
	}
	if (PlayerState != PST_DEAD && MO.Origin.z <= MO.FloorZ)
	{
		ViewOrg.z -= MO.FloorClip;
	}
	if (ViewOrg.z < MO.FloorZ + 4.0)
		ViewOrg.z = MO.FloorZ + 4.0;

	if (ViewOrg.z > MO.CeilingZ - 4.0)
		ViewOrg.z = MO.CeilingZ - 4.0;
}

//==========================================================================
//
//  MovePlayer
//
//==========================================================================

void MovePlayer(float deltaTime)
{
	float forward;
	float side;
	float fly;

	// Do not let the player control movement
	//  if not onground.
	onground = (MO.Origin.z <= MO.FloorZ) || Actor(MO).bOnMobj;

	forward = ForwardMove * 5.0;
	side = SideMove * 5.0;

	if (ChickenTime)
	{
		// Chicken speed
		forward *= 1.2;
		side *= 1.2;
	}
	if (!(onground || MO.bFly))
	{
		forward *= MainGameInfo(Level.Game).air_control;
		side *= MainGameInfo(Level.Game).air_control;
	}

	if (forward)
	{
		ThrustPlayer(MO.Angles.yaw, forward, deltaTime);
	}
	if (side)
	{
		ThrustPlayer(AngleMod360(MO.Angles.yaw - 90.0), side, deltaTime);
	}

	if (forward || side)
	{
		SetPlayerRunState();
	}

	fly = FlyMove / 16.0;
	if (fly && Powers[pw_flight])
	{
		if (FlyMove != TOCENTER)
		{
			FlyHeight = fly * 2.0;
			if (!MO.bFly)
			{
				MO.bFly = true;
				MO.bNoGravity = true;
				if (MO.Velocity.z <= -39.0 * 35.0)
				{
					// stop falling scream
					MO.StopSound(CHAN_VOICE);
				}
			}
		}
		else
		{
			MO.bFly = false;
			MO.bNoGravity = false;
		}
	}
	else if (fly > 0.0)
	{
		PlayerUseArtifact(HereticDefs::arti_fly);
	}
	if (MO.bFly)
	{
		MO.Velocity.z = FlyHeight * 35.0;
		if (FlyHeight)
		{
			FlyHeight /= 2.0;
		}
	}

	if ((Buttons & BT_JUMP) && onground && !JumpTime)
	{
		if (ChickenTime)
		{
			//  Smaller momentum because chicken haves smaller mass
			MO.Velocity.z = 6.0 * 35.0;
		}
		else
		{
			MO.Velocity.z = 9.0 * 35.0;
		}
		Actor(MO).bOnMobj = false;
		JumpTime = 0.5;
	}
}

//==========================================================================
//
//  CheckWaterJump
//
//==========================================================================

void CheckWaterJump()
{
	TVec start;
	TVec end;
	TVec vforward;

	// check for a jump-out-of-water
	AngleVector(&MO.Angles, &vforward);
	start = MO.Origin;
	start.z += MO.Height * 0.5 + 8.0;
	vforward.z = 0.0;
	vforward = Normalise(vforward);
	end = start + vforward * 24.0;
	if (!Level.TraceLine(start, end))
	{
		// solid at waist
		start.z = MO.Origin.z + MO.Height;
		end = start + vforward * 24.0;
		MoveDir = Level.trace_plane_normal * -50.0;
		if (Level.TraceLine(start, end))
		{
			// open at eye level
			Actor(MO).bWaterJump = true;
			MO.Velocity.z = 320.0;
			Actor(MO).ReactionTime = 2.0;	// safety net
		}
	}
}

//==========================================================================
//
//  WaterMove
//
//==========================================================================

void WaterMove(float deltaTime)
{
	float forward;
	float side;
	TVec vforward;
	TVec vright;
	TVec vup;
	TVec wishvel;

	// Do not let the player control movement
	//  if not onground.
	onground = (MO.Origin.z <= MO.FloorZ) || Actor(MO).bOnMobj;

	AngleVectors(&MO.Angles, &vforward, &vright, &vup);

	forward = ForwardMove;
	side = SideMove;
	if (ChickenTime)
	{
		// Chicken speed
		forward *= 1.2;
		side *= 1.2;
	}

	wishvel = vforward * forward + vright * side;
	if (!forward && !side /* && !cmd.upmove */ )
		wishvel.z -= 60.0;	// drift towards bottom
//  else
//      wishvel.z += cmd.upmove;

	MO.Velocity += 3.5 * deltaTime * wishvel;

	if (forward || side)
	{
		SetPlayerRunState();
	}

	if (Buttons & BT_JUMP)
	{
		if (MO.WaterType == CONTENTS_WATER)
			MO.Velocity.z = 100.0;
		else if (MO.WaterType == CONTENTS_SLUDGE)
			MO.Velocity.z = 80.0;
		else
			MO.Velocity.z = 50.0;
	}
	CheckWaterJump();
}

//==========================================================================
//
//  WaterJump
//
//==========================================================================

void WaterJump()
{
	if (!Actor(MO).ReactionTime || !MO.WaterLevel)
	{
		Actor(MO).bWaterJump = false;
		Actor(MO).ReactionTime = 0.0;
	}
	MO.Velocity.x = MoveDir.x;
	MO.Velocity.y = MoveDir.y;
}

//==========================================================================
//
//  PlayerInSpecialSector
//
//  Called every tic frame that the player origin is in a special sector.
//
//==========================================================================

void PlayerInSpecialSector(float deltaTime)
{
	float speed;
	float finean;

	if (MO.Origin.z != GetPlanePointZ(&MO.Sector->floor, MO.Origin))
	{
		// Player is not touching the floor
		return;
	}

	if (MO.Sector->special & SECSPEC_SECRET_MASK)
	{
		//	Secret area.
		SecretCount++;
		Level.Game.level->currentsecret++;
		MO.Sector->special &= ~SECSPEC_SECRET_MASK;
		centerprint("You found a secret area");
	}

	if (MO.Sector->special >= SECSPEC_LightFlicker &&
		MO.Sector->special <= 255)
	{
		switch (MO.Sector->special)
		{
		case SECSPEC_DamageHellslime:
			if (Level.Game.level->time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(none, none, 10);
			}
			break;
		case SECSPEC_DamageSludge:
			if (Level.Game.level->time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(none, none, 4);
			}
			break;
		case SECSPEC_DamageNukage:
			if (Level.Game.level->time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(none, none, 5);
			}
			break;
		case SECSPEC_LightStrobeFastDamage:
		case SECSPEC_DamageSuperHellslime:
			if (Level.Game.level->time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(none, none, 20);
			}
			break;
		case SECSPEC_DamageSuperHellslimeExit:
			// EXIT SUPER DAMAGE! (for E1M8 finale)
			Cheats &= ~CF_GODMODE;
	
			if (Level.Game.level->time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(none, none, 20);
			}
	
			if (Health <= 10)
			{
				G_ExitLevel(0);
			}
			break;
		case SECSPEC_DamageLavaWimpy:
			if (Level.Game.level->time - LastSectorDamageTime >= 16.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(HereticLevelInfo(Level).GLavaInflictor, none, 5);
				Actor(MO).HitFloorEffect();
			}
			break;
		case SECSPEC_DamageLavaHefty:
			if (Level.Game.level->time - LastSectorDamageTime >= 16.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(HereticLevelInfo(Level).GLavaInflictor, none, 8);
				Actor(MO).HitFloorEffect();
			}
			break;
		case SECSPEC_ScrollEastLavaDamage:
			ThrustPlayer(0.0, 1024.0, deltaTime);
			if (Level.Game.level->time - LastSectorDamageTime >= 16.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(HereticLevelInfo(Level).GLavaInflictor, none, 5);
				Actor(MO).HitFloorEffect();
			}
			break;
		case SECSPEC_DamageHazard:
			HazardTime += 2.0 * deltaTime;
			break;
		case SECSPEC_DamageInstantDeath:
			Actor(MO).Damage(none, none, 999);
			break;
		case SECSPEC_DamageSuperHazard:
			HazardTime += 4.0 * deltaTime;
			break;
		}
	}
	else
	{
		//	Extended sector damage type.
		switch (MO.Sector->special & SECSPEC_DAMAGE_MASK)
		{
		case 0x0100:
			if (Level.Game.level->time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(none, none, 5);
			}
			break;
		case 0x0200:
			if (Level.Game.level->time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(none, none, 10);
			}
			break;
		case 0x0300:
			if (Level.Game.level->time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(none, none, 20);
			}
			break;
		}
	}

	//	Apply any customizable damage
	if (MO.Sector->Damage)
	{
		if (MO.Sector->Damage < 20)
		{
			if (Level.Game.level->time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(none, none, MO.Sector->Damage);
			}
		}
		else if (MO.Sector->Damage < 50)
		{
			if (Level.Game.level->time - LastSectorDamageTime >= 32.0 / 35.0)
			{
				LastSectorDamageTime = Level.Game.level->time;
				Actor(MO).Damage(none, none, MO.Sector->Damage);
			}
		}
		else if (Level.Game.level->time - LastSectorDamageTime >= 1.0 / 35.0)
		{
			LastSectorDamageTime = Level.Game.level->time;
			Actor(MO).Damage(none, none, MO.Sector->Damage);
		}
	}

	switch (MO.Sector->special & SECSPEC_BASE_MASK)
	{
	case SECSPEC_ScrollCurrent:
		speed = itof((MO.Sector->tag - 100) % 10) / 16.0 * 35.0;
		finean = itof((MO.Sector->tag - 100) / 10) * 45.0;
		MO.Velocity.x += speed * cos(finean);
		MO.Velocity.y += speed * sin(finean);
		break;
	case SECSPEC_ScrollNorthSlow:
	case SECSPEC_ScrollNorthMedium:
	case SECSPEC_ScrollNorthFast:
		ThrustPlayer(90.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorthSlow], deltaTime);
		break;
	case SECSPEC_ScrollEastSlow:
	case SECSPEC_ScrollEastMedium:
	case SECSPEC_ScrollEastFast:
		ThrustPlayer(0.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollEastSlow], deltaTime);
		break;
	case SECSPEC_ScrollSouthSlow:
	case SECSPEC_ScrollSouthMedium:
	case SECSPEC_ScrollSouthFast:
		ThrustPlayer(270.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouthSlow], deltaTime);
		break;
	case SECSPEC_ScrollWestSlow:
	case SECSPEC_ScrollWestMedium:
	case SECSPEC_ScrollWestFast:
		ThrustPlayer(180.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollWestSlow], deltaTime);
		break;
	case SECSPEC_ScrollNorthWestSlow:
	case SECSPEC_ScrollNorthWestMedium:
	case SECSPEC_ScrollNorthWestFast:
		ThrustPlayer(135.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorthWestSlow], deltaTime);
		break;
	case SECSPEC_ScrollNorthEastSlow:
	case SECSPEC_ScrollNorthEastMedium:
	case SECSPEC_ScrollNorthEastFast:
		ThrustPlayer(45.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorthEastSlow], deltaTime);
		break;
	case SECSPEC_ScrollSouthEastSlow:
	case SECSPEC_ScrollSouthEastMedium:
	case SECSPEC_ScrollSouthEastFast:
		ThrustPlayer(315.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouthEastSlow], deltaTime);
		break;
	case SECSPEC_ScrollSouthWestSlow:
	case SECSPEC_ScrollSouthWestMedium:
	case SECSPEC_ScrollSouthWestFast:
		ThrustPlayer(225.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouthWestSlow], deltaTime);
		break;
	case SECSPEC_ScrollEast5:
	case SECSPEC_ScrollEast10:
	case SECSPEC_ScrollEast25:
	case SECSPEC_ScrollEast30:
	case SECSPEC_ScrollEast35:
		ThrustPlayer(0.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollEast5], deltaTime);
		break;
	case SECSPEC_ScrollNorth5:
	case SECSPEC_ScrollNorth10:
	case SECSPEC_ScrollNorth25:
	case SECSPEC_ScrollNorth30:
	case SECSPEC_ScrollNorth35:
		ThrustPlayer(90.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollNorth5], deltaTime);
		break;
	case SECSPEC_ScrollSouth5:
	case SECSPEC_ScrollSouth10:
	case SECSPEC_ScrollSouth25:
	case SECSPEC_ScrollSouth30:
	case SECSPEC_ScrollSouth35:
		ThrustPlayer(270.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollSouth5], deltaTime);
		break;
	case SECSPEC_ScrollWest5:
	case SECSPEC_ScrollWest10:
	case SECSPEC_ScrollWest25:
	case SECSPEC_ScrollWest30:
	case SECSPEC_ScrollWest35:
		ThrustPlayer(180.0, MainGameInfo(Level.Game).pushTab[(MO.Sector->special &
			SECSPEC_BASE_MASK) - SECSPEC_ScrollWest5], deltaTime);
		break;
	}
}

//==========================================================================
//
//  PlayerInContents
//
//==========================================================================

void PlayerInContents()
{
	if (!MO.WaterLevel)
	{
		return;
	}
	switch (MO.WaterType)
	{
	case CONTENTS_LAVA:
		if (!(Level.Game.level->tictime & 15))
		{
			Actor(MO).Damage(HereticLevelInfo(Level).GLavaInflictor, none, 8);
		}
		break;

	case CONTENTS_SLUDGE:
		if (!(Level.Game.level->tictime & 31))
		{
			Actor(MO).Damage(none, none, 4);
		}
		break;
	}
}

//==========================================================================
//
//  PrevWeapon
//
//==========================================================================

void PrevWeapon()
{
	int			i;
	int			CurIdx;
	int			NewWeap;

	CurIdx = 0;
	for (i = 0; i < NUMWEAPONS; i++)
	{
		if (ReadyWeapon == MainGameInfo(Level.Game).WeaponOrder[i])
		{
			CurIdx = i;
			break;
		}
	}
	for (i = NUMWEAPONS - 1; i > 0; i--)
	{
		NewWeap = MainGameInfo(Level.Game).WeaponOrder[(CurIdx + i) % NUMWEAPONS];
		if (WeaponOwned[NewWeap])
		{
			PendingWeapon = NewWeap;
			return;
		}
	}
}

//==========================================================================
//
//  NextWeapon
//
//==========================================================================

void NextWeapon()
{
	int			i;
	int			CurIdx;
	int			NewWeap;

	CurIdx = 0;
	for (i = 0; i < NUMWEAPONS; i++)
	{
		if (ReadyWeapon == MainGameInfo(Level.Game).WeaponOrder[i])
		{
			CurIdx = i;
			break;
		}
	}
	for (i = 1; i < NUMWEAPONS; i++)
	{
		NewWeap = MainGameInfo(Level.Game).WeaponOrder[(CurIdx + i) % NUMWEAPONS];
		if (WeaponOwned[NewWeap] && (MainGameInfo(Level.Game).WeaponInShareware[NewWeap] || !MainGameInfo(Level.Game).shareware))
		{
			PendingWeapon = NewWeap;
			return;
		}
	}
}

//==========================================================================
//
//  PlayerImpulse
//
//==========================================================================

void PlayerImpulse()
{
	int impulse;

	impulse = Impulse;
	Impulse = 0;
	Arti = 0;

	if (impulse <= NUMWEAPONS)
	{
		int newweapon;

		// The actual changing of the weapon is done when the weapon
		// psprite can do it (A_WeaponReady), so it doesn't happen in
		// the middle of an attack.
		newweapon = impulse - 1;
		if (newweapon == wp_staff && WeaponOwned[wp_gauntlets]
			&& !(ReadyWeapon == wp_gauntlets))
		{
			newweapon = wp_gauntlets;
		}
		if (WeaponOwned[newweapon] &&
			newweapon != ReadyWeapon)
		{
			if (MainGameInfo(Level.Game).WeaponInShareware[newweapon] || !MainGameInfo(Level.Game).shareware)
			{
				PendingWeapon = newweapon;
			}
		}
	}
	else if (impulse == 13)
	{
		InventoryLeft();
	}
	else if (impulse == 14)
	{
		InventoryRight();
	}
	else if (impulse == 15)
	{
		InventoryUse();
	}
	// Heretic artifact hot keys
	else if (impulse == 11)
	{
		if (!Powers[pw_weaponlevel2])
		{
			Arti = HereticDefs::arti_tomeofpower;
		}
	}
	else if (impulse == 16)
	{
		Actor(MO).Damage(none, none, 10000);
	}
	else if (impulse == 17)
	{
		PrevWeapon();
	}
	else if (impulse == 18)
	{
		NextWeapon();
	}

	if (Arti)
	{
		// Use an artifact
		PlayerUseArtifact(Arti);
	}
}

//==========================================================================
//
//  ChickenPlayerThink
//
//==========================================================================

void ChickenPlayerThink()
{
	if (Health > 0)
	{
		// Handle beak movement
		UpdateBeak(ViewEnts[ps_weapon]);
	}
	if (ftoi(32.0 * ChickenTime) & 15)
	{
		return;
	}
	if (!(MO.Velocity.x + MO.Velocity.y) && P_Random() < 160)
	{
		// Twitch view angle
		MO.Angles.yaw = AngleMod360(MO.Angles.yaw +
			(Random() - Random()) * 45.0 / 4.0);
		bFixAngle = true;
	}
	if ((MO.Origin.z <= MO.FloorZ) && (P_Random() < 32))
	{
		// Jump and noise
		MO.Velocity.z += 1.0 * 35.0;
		MO.SetState(Actor(MO).PainState);
		return;
	}
	if (P_Random() < 48)
	{
		// Just noise
		MO.PlaySound('chicken/attack', CHAN_VOICE);
	}
}

//==========================================================================
//
//  UndoPlayerChicken
//
//==========================================================================

bool UndoPlayerChicken()
{
	Actor fog;
	Actor A;

	MO.UnlinkFromWorld();
	A = HereticLevelInfo(Level).Spawn(HereticPlayer, MO.Origin);
	if (!A.TestLocation())
	{
		// Didn't fit
		A.Remove();
		MO.LinkToWorld();
		ChickenTime = 2.0;
		return false;
	}
	MO.LinkToWorld();
	// Set color translation
	A.Translation = Color;
	A.Angles.yaw = MO.Angles.yaw;
	A.Player = self;
	A.bIsPlayer = true;
	A.ReactionTime = 0.5;
	if (MO.bFly)
	{
		A.bFly = true;
		A.bNoGravity = true;
	}
	ChickenTime = 0.0;
	Powers[pw_weaponlevel2] = 0.0;
	A.Health = MAXHEALTH;
	Health = A.Health;
	PostChickenWeapon(Actor(MO).Special1);

	fog = HereticLevelInfo(Level).Spawn(TeleportFog, MO.Origin + vector(
		20.0 * cos(MO.Angles.yaw), 20.0 * sin(MO.Angles.yaw), Actor::TELEFOGHEIGHT));
	fog.PlaySound('misc/teleport', CHAN_VOICE);

	MO.SetState(Actor::S_FREETARGMOBJ);
	MO = A;

	return true;
}

//==========================================================================
//
//  DeathPlayerTick
//
//  Fall on your face when dying. Decrease POV height to floor height.
//
//==========================================================================

void DeathPlayerTick(float deltaTime)
{
	float angle;
	float delta;
	int lookDelta;

	MovePsprites();

	onground = (MO.Origin.z <= MO.FloorZ);

	if (MO.Class == BloodySkull)
	{
		// Flying bloody skull
		ViewHeight = 6.0;
		DeltaViewHeight = 0.0;
		//player->damagecount = 20;
		if (onground)
		{
#ifdef FIXME
			if (lookdir < 60)
			{
				lookDelta = (60 - lookdir) / 8;
				if (lookDelta < 1 && (level->tictime & 1))
				{
					lookDelta = 1;
				}
				else if (lookDelta > 6)
				{
					lookDelta = 6;
				}
				lookdir += lookDelta;
			}
#endif
		}
	}
	else
	{	// Fall to ground
		DeltaViewHeight = 0.0;
		if (ViewHeight > 6.0)
			ViewHeight -= 1.0;
		if (ViewHeight < 6.0)
			ViewHeight = 6.0;
#ifdef FIXME
		if (lookdir > 0)
		{
			lookdir -= 6;
		}
		else if (lookdir < 0)
		{
			lookdir += 6;
		}
		if (abs(lookdir) < 6)
		{
			lookdir = 0;
		}
#endif
	}
	CalcHeight(deltaTime);

	if (Attacker && Attacker != MO)
	{
		angle = atan2(Attacker.Origin.y - MO.Origin.y,
			Attacker.Origin.x - MO.Origin.x);
		delta = AngleMod180(angle - MO.Angles.yaw);
		if (delta < 5.0 && delta > -5.0)
		{
			// Looking at killer, so fade damage flash down
			MO.Angles.yaw = angle;
			if (DamageFlash)
			{
				DamageFlash -= deltaTime;
				if (DamageFlash <= 0.0)
					DamageFlash = 0.0;
			}
		}
		else if (delta > 0.0)
			MO.Angles.yaw += 5.0;
		else
			MO.Angles.yaw -= 5.0;
	}
	else if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (Buttons & BT_USE)
	{
		NewTorch = 0;
		NewTorchDelta = 0;
		Palette = 0;
		InvCurPos = 0;
		InvPtr = 0;
		PlayerState = PST_REBORN;
		// Let the mobj know the player has entered the reborn state.  Some
		// mobjs need to know when it's ok to remove themselves.
		PlayerPawn(MO).bSafeToRemove = true;
	}
}

//==========================================================================
//
//  SetPlayerRunState
//
//==========================================================================

void SetPlayerRunState()
{
	if (MO.State == Actor(MO).IdleState)
	{
		MO.SetState(Actor(MO).SeeState);
	}
}

//==========================================================================
//
//  PlayerTick
//
//==========================================================================

void PlayerTick(float deltaTime)
{
	int i;

	if (bIsBot)
	{
		Bot.BotTick(deltaTime);
	}

	// Copy view angles
	MO.Angles = ViewAngles;

	// fixme: do this in the cheat code
	if (Cheats & CF_NOCLIP)
	{
		MO.bColideWithThings = false;
		MO.bColideWithWorld = false;
	}
	else
	{
		MO.bColideWithThings = true;
		MO.bColideWithWorld = true;
	}

	if (Actor(MO).bJustAttacked)
	{
		ForwardMove = 100.0;
		SideMove = 0.0;
		Actor(MO).bJustAttacked = false;
	}

	InventoryTick(deltaTime);

	if (PlayerState == PST_DEAD)
	{
		DeathPlayerTick(deltaTime);
		return;
	}

	if (JumpTime)
	{
		JumpTime -= deltaTime;
		if (JumpTime <= 0.0)
			JumpTime = 0.0;
	}
	if (ChickenTime)
	{
		ChickenPlayerThink();
	}

	// Move around.
	// ReactionTime is used to prevent movement
	//  for a bit after a teleport.
	if (Actor(MO).ReactionTime)
	{
		Actor(MO).ReactionTime -= deltaTime;
		if (Actor(MO).ReactionTime <= 0.0)
			Actor(MO).ReactionTime = 0.0;
		if (Actor(MO).bWaterJump)
			WaterJump();
	}
	else
	{
		if (MO.WaterLevel > 1)
			WaterMove(deltaTime);
		else
			MovePlayer(deltaTime);
	}

	CalcHeight(deltaTime);

	if (MO.Sector->special)
		PlayerInSpecialSector(deltaTime);
	PlayerInContents();

	// Check for weapon change.
	if (Impulse)
	{
		PlayerImpulse();
	}

	// check for use
	if (Buttons & BT_USE)
	{
		if (!bUseDown)
		{
			P_UseLines();
			bUseDown = true;
		}
	}
	else
	{
		bUseDown = false;
	}

	// Chicken counter
	if (ChickenTime)
	{
		if (ChickenPeck)
		{
			// Chicken attack counter
			ChickenPeck -= 3;
		}
		ChickenTime -= deltaTime;
		if (ChickenTime <= 0.0)
		{
			// Attempt to undo the chicken
			ChickenTime = 0.0;
			UndoPlayerChicken();
		}
	}

	// cycle psprites
	MovePsprites();

	// Counters, time dependend power ups.

	if (Powers[pw_invulnerability])
	{
		Powers[pw_invulnerability] -= deltaTime;
		if (Powers[pw_invulnerability] <= 0.0)
			Powers[pw_invulnerability] = 0.0;
	}

	if (Powers[pw_invisibility])
	{
		Powers[pw_invisibility] -= deltaTime;
		if (Powers[pw_invisibility] <= 0.0)
		{
			Powers[pw_invisibility] = 0.0;
			MO.Translucency = 0;
		}
	}

	if (Powers[pw_infrared])
	{
		Powers[pw_infrared] -= deltaTime;
		if (Powers[pw_infrared] <= 0.0)
			Powers[pw_infrared] = 0.0;
	}

	if (Powers[pw_flight])
	{
		Powers[pw_flight] -= deltaTime;
		if (Powers[pw_flight] <= 0.0)
		{
			Powers[pw_flight] = 0.0;
			if (MO.Origin.z != MO.FloorZ)
			{
//FIXME             player->centering = true;
			}

			MO.bFly = false;
			MO.bNoGravity = false;
		}
	}

	if (Powers[pw_weaponlevel2])
	{
		Powers[pw_weaponlevel2] -= deltaTime;
		if (Powers[pw_weaponlevel2] <= 0.0)
		{
			Powers[pw_weaponlevel2] = 0.0;
			WeaponLevel2Fade();
		}
	}

	if (DamageFlash)
	{
		DamageFlash -= deltaTime;
		if (DamageFlash <= 0.0)
			DamageFlash = 0.0;
	}

	if (BonusFlash)
	{
		BonusFlash -= deltaTime;
		if (BonusFlash <= 0.0)
			BonusFlash = 0.0;
	}

	if (HazardTime)
	{
		HazardTime -= deltaTime;
		if (HazardTime <= 0.0)
			HazardTime = 0.0;
		if (Level.Game.level->time - LastHazardTime >= 32.0 / 35.0 &&
			HazardTime > 16.0)
		{
			LastHazardTime = Level.Game.level->time;
			Actor(MO).Damage(none, none, 5);
		}
	}

	// Handling colormaps.
	if (Powers[pw_invulnerability])
	{
		if (Powers[pw_invulnerability] > 4.0 ||
			(ftoi(4.0 * Powers[pw_invulnerability]) & 1))
			FixedColormap = INVERSECOLORMAP;
		else
			FixedColormap = 0;
	}
	else if (Powers[pw_infrared])
	{
		if (Powers[pw_infrared] <= BLINKTHRESHOLD)
		{
			if (ftoi(4.0 * Powers[pw_infrared]) & 1)
			{
				FixedColormap = 0;
			}
			else
			{
				FixedColormap = 1;
			}
		}
		else if (!(ftoi(2.0 * Level.Game.level->time) & 1))
		{
			if (NewTorch)
			{
				if (FixedColormap + NewTorchDelta > 7 ||
					FixedColormap + NewTorchDelta < 1 ||
					FixedColormap == NewTorch)
				{
					NewTorch = 0;
				}
				else
				{
					FixedColormap += NewTorchDelta;
				}
			}
			else
			{
				NewTorch = (P_Random() & 7) + 1;
				NewTorchDelta =
					(NewTorch == FixedColormap) ? 0 :
					((NewTorch > FixedColormap) ? 1 : -1);
			}
		}
	}
	else
		FixedColormap = 0;
}

//==========================================================================
//
//  PaletteFlash
//
//  sets the new palette based upon current values of player->DamageFlash
// and player->BonusFlash
//
//==========================================================================

void PaletteFlash()
{
	CShifts[CSHIFT_CONTENTS] = 0;
	if (MO.WaterLevel == 3)
	{
		switch (MO.WaterType)
		{
		case CONTENTS_WATER:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(130, 80, 50, 128);
			break;

		case CONTENTS_SLUDGE:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(128, 160, 128, 150);
			break;

		case CONTENTS_LAVA:
			CShifts[CSHIFT_CONTENTS] = CSHIFT(255, 80, 0, 150);
			break;
		}
	}

	if (DamageFlash)
	{
		Palette = (ftoi(32.0 * DamageFlash) + 7) >> 3;
		if (Palette >= NUMREDPALS)
		{
			Palette = NUMREDPALS - 1;
		}
		Palette += STARTREDPALS;
	}
	else if (BonusFlash)
	{
		Palette = (ftoi(32.0 * BonusFlash) + 7) >> 3;
		if (Palette >= NUMBONUSPALS)
		{
			Palette = NUMBONUSPALS - 1;
		}
		Palette += STARTBONUSPALS;
	}
	else
	{
		Palette = 0;
	}
}

//==========================================================================
//
//  SetViewPos
//
//==========================================================================

void SetViewPos()
{
	ViewOrg.x = MO.Origin.x;
	ViewOrg.y = MO.Origin.y;

	if (LocalQuakeHappening)
	{
		float intensity;

		intensity = itof(LocalQuakeHappening);
		ViewOrg.x += (Random() - 0.5) * (intensity * 4.0);
		ViewOrg.y += (Random() - 0.5) * (intensity * 4.0);
	}

	ViewAngles = MO.Angles;

	if (ChickenTime && ChickenPeck)
	{
		// Set chicken attack view position
		ViewOrg.x += itof(ChickenPeck) * cos(MO.Angles.yaw);
		ViewOrg.y += itof(ChickenPeck) * sin(MO.Angles.yaw);
	}

	PaletteFlash();

	SBFlags = Keys;
	if (Cheats & CF_GODMODE)
		SBFlags |= HereticDefs::SBF_GOD_MODE;
	if (Powers[pw_flight])
	{
		if (Powers[pw_flight] > BLINKTHRESHOLD ||
			!(ftoi(2.0 * Powers[pw_flight]) & 1))
		{
			SBFlags |= HereticDefs::SBF_POWER_FLIGHT;
		}
	}
	if (Powers[pw_weaponlevel2] && !ChickenTime)
	{
		if (Powers[pw_weaponlevel2] > BLINKTHRESHOLD ||
			!(ftoi(2.0 * Powers[pw_weaponlevel2]) & 1))
		{
			SBFlags |= HereticDefs::SBF_POWER_BOOK;
		}
	}
	if (MO.bFly)
	{
		SBFlags |= HereticDefs::SBF_IN_FLIGHT;
	}
	if (InventoryTime)
	{
		SBFlags |= HereticDefs::SBF_INVENTORY_OPEN;
	}

	if (ReadyWeapon > 0 && ReadyWeapon < 7)
		ReadyAmmo = Ammo[Weapon(ViewEnts[ps_weapon]).Ammo];
	else
		ReadyAmmo = 0;
}

//==========================================================================
//
//  PlayerUseArtifact
//
//==========================================================================

void PlayerUseArtifact(int arti)
{
	int i;

	for (i = 0; i < InventorySlotNum; i++)
	{
		if (Inventory[i].type == arti)
		{
			// Found match - try to use
			if (UseArtifact(arti))
			{
				// Artifact was used - remove it from inventory
				PlayerRemoveArtifact(i);
				MO.PlaySound('misc/invuse', CHAN_ITEM);
				ArtifactFlash = 4;
			}
			else
			{
				// Unable to use artifact, advance pointer
				PlayerNextArtifact();
			}
			break;
		}
	}
}

//==========================================================================
//
//  PlayerNextArtifact
//
//==========================================================================

void PlayerNextArtifact()
{
	InvPtr--;
	if (InvPtr < 6)
	{
		InvCurPos--;
		if (InvCurPos < 0)
		{
			InvCurPos = 0;
		}
	}
	if (InvPtr < 0)
	{
		InvPtr = InventorySlotNum - 1;
		if (InvPtr < 6)
		{
			InvCurPos = InvPtr;
		}
		else
		{
			InvCurPos = 6;
		}
	}
	ReadyArtifact = Inventory[InvPtr].type;
}

//==========================================================================
//
//  PlayerRemoveArtifact
//
//==========================================================================

void PlayerRemoveArtifact(int slot)
{
	int i;

	ArtifactCount--;
	if (!(--Inventory[slot].count))
	{
		// Used last of a type - compact the artifact list
		ReadyArtifact = HereticDefs::arti_none;
		Inventory[slot].type = HereticDefs::arti_none;
		for (i = slot + 1; i < InventorySlotNum; i++)
		{
			Inventory[i - 1].type = Inventory[i].type;
			Inventory[i - 1].count = Inventory[i].count;
		}
		InventorySlotNum--;
		// Set position markers and get next ReadyArtifact
		InvPtr--;
		if (InvPtr < 6)
		{
			InvCurPos--;
			if (InvCurPos < 0)
			{
				InvCurPos = 0;
			}
		}
		if (InvPtr >= InventorySlotNum)
		{
			InvPtr = InventorySlotNum - 1;
		}
		if (InvPtr < 0)
		{
			InvPtr = 0;
		}
		ReadyArtifact = Inventory[InvPtr].type;
	}
}

//==========================================================================
//
//  UseArtifact
//
//  Returns true if the artifact was used.
//
//==========================================================================

bool UseArtifact(int arti)
{
	switch (arti)
	{
	case HereticDefs::arti_health:
		if (!GiveBody(25))
		{
			return false;
		}
		break;
	case HereticDefs::arti_superhealth:
		if (!GiveBody(100))
		{
			return false;
		}
		break;
	case HereticDefs::arti_invulnerability:
		if (!GivePower(pw_invulnerability))
		{
			return false;
		}
		break;
	case HereticDefs::arti_torch:
		if (!GivePower(pw_infrared))
		{
			return false;
		}
		break;
	case HereticDefs::arti_fly:
		if (!GivePower(pw_flight))
		{
			return false;
		}
		if (MO.Velocity.z <= -35.0 * 35.0)
		{
			// stop falling scream
			MO.StopSound(CHAN_VOICE);
		}
		break;
	case HereticDefs::arti_teleport:
		UseArtiTele();
		break;
	case HereticDefs::arti_egg:
		UseArtiEgg();
		break;
	case HereticDefs::arti_invisibility:
		if (!GivePower(pw_invisibility))
		{
			return false;
		}
		break;
	case HereticDefs::arti_tomeofpower:
		if (!UseArtiTomeOfPower())
		{
			return false;
		}
		break;
	case HereticDefs::arti_firebomb:
		UseArtiFireBomb();
		break;
	default:
		return false;
	}
	return true;
}

//==========================================================================
//
//  UseArtiTele
//
//==========================================================================

void UseArtiTele()
{
	int i;
	int selections;
	float destX;
	float destY;
	float destAngle;

	if (Level.Game.deathmatch)
	{
		selections = Level.NumDeathmatchStarts;
		i = P_Random() % selections;
		destX = Level.DeathmatchStarts[i].x;
		destY = Level.DeathmatchStarts[i].y;
		destAngle = itof(45 * (Level.DeathmatchStarts[i].angle / 45));
	}
	else
	{
		destX = Level.PlayerStarts[0].x;
		destY = Level.PlayerStarts[0].y;
		destAngle = itof(45 * (Level.PlayerStarts[0].angle / 45));
	}
	Actor(MO).Teleport(vector(destX, destY, Actor::ONFLOORZ), destAngle, true, true,
		false);
	if (ChickenTime)
	{
		// Teleporting away will undo any morph effects
		UndoPlayerChicken();
	}
}

//==========================================================================
//
//  UseArtiEgg
//
//==========================================================================

void UseArtiEgg()
{
	PlayerPawn(MO).SpawnPlayerMissile(EggMissile);
	PlayerPawn(MO).SpawnPlayerMissileAngle(EggMissile,
		AngleMod360(MO.Angles.yaw - 7.5));
	PlayerPawn(MO).SpawnPlayerMissileAngle(EggMissile,
		AngleMod360(MO.Angles.yaw + 7.5));
	PlayerPawn(MO).SpawnPlayerMissileAngle(EggMissile,
		AngleMod360(MO.Angles.yaw - 15.0));
	PlayerPawn(MO).SpawnPlayerMissileAngle(EggMissile,
		AngleMod360(MO.Angles.yaw + 15.0));
}

//==========================================================================
//
//  UseArtiTomeOfPower
//
//==========================================================================

bool UseArtiTomeOfPower()
{
	if (ChickenTime)
	{
		// Attempt to undo chicken
		if (UndoPlayerChicken() == false)
		{
			// Failed
			Actor(MO).Damage(none, none, 10000);
		}
		else
		{
			// Succeeded
			ChickenTime = 0.0;
			MO.PlaySound('*evillaugh', CHAN_ITEM);
		}
	}
	else
	{
		if (!GivePower(pw_weaponlevel2))
		{
			return false;
		}
		Weapon(ViewEnts[ps_weapon]).WeaponLevel2Init();
	}
	return true;
}

//==========================================================================
//
//  UseArtiFireBomb
//
//==========================================================================

void UseArtiFireBomb()
{
	float angle = MO.Angles.yaw;
	Actor A = HereticLevelInfo(Level).Spawn(FireBomb, MO.Origin + vector(24.0 * cos(angle),
		24.0 * sin(angle), -MO.FloorClip));

	A.Instigator = Actor(MO);
}

//****************************************************************************
//
//  WEAPON UTILITES
//
//****************************************************************************

//==========================================================================
//
//	SetWeapon
//
//==========================================================================

void SetWeapon(int NewWeapon)
{
	ReadyWeapon = NewWeapon;
	PendingWeapon = wp_nochange;

	if (ViewEnts[ps_weapon])
	{
		ViewEnts[ps_weapon].Destroy();
	}
	ViewEnts[ps_weapon] = Weapon(SpawnObject(MainGameInfo(Level.Game).WeaponClasses[NewWeapon]));
	ViewEnts[ps_weapon].Player = self;
	PSpriteSY = Weapon(ViewEnts[ps_weapon]).PSpriteSY;
}

//===========================================================================
//
//  SetPsprite
//
//===========================================================================

void SetPsprite(int position, state stnum)
{
	ViewEnts[position].SetState(stnum);
}

//===========================================================================
//
//  BringUpWeapon
//
//  Starts bringing the pending weapon up from the bottom of the screen.
//
//===========================================================================

void BringUpWeapon()
{
	SetWeapon(ReadyWeapon);
	if (ReadyWeapon == wp_gauntlets)
	{
		MO.PlaySound('weapons/gauntletsactivate', CHAN_WEAPON);
	}
	ViewEnts[ps_weapon].SY = Weapon::WEAPONBOTTOM;
	if (Powers[pw_weaponlevel2])
	{
		SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).UpState2);
	}
	else
	{
		SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).UpState);
	}
}

//===========================================================================
//
//  DropWeapon
//
//  The player died, so put the weapon away.
//
//===========================================================================

void DropWeapon()
{
	if (Powers[pw_weaponlevel2])
	{
		SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).DownState2);
	}
	else
	{
		SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).DownState);
	}
}

//===========================================================================
//
//  SetupPsprites
//
//  Called at start of level for each player
//
//===========================================================================

void SetupPsprites()
{
	int i;

	// Remove all  ViewEnts
	for (i = 0; i < NUMPSPRITES; i++)
	{
		ViewEnts[i].State = none;
	}
	// Spawn the ready weapon
	BringUpWeapon();
}

//===========================================================================
//
//  MovePsprites
//
//  Called every tic by player thinking routine
//
//===========================================================================

void MovePsprites()
{
	int i;
	ViewEntity e;

	for (i = 0; i < NUMPSPRITES; i++)
	{
		e = ViewEnts[i];
		if (e.State)	// a null state means not active
		{
			// drop tic count and possibly change state
			if (e.StateTime != -1.0)	// a -1 tic count never changes
			{
				e.StateTime -= Level.Game.frametime;
				if (e.StateTime <= 0.0)
				{
					e.StateTime = 0.0;
					SetPsprite(i, e.NextState);
				}
			}
		}
	}
	ViewEnts[ps_flash].SX = ViewEnts[ps_weapon].SX;
	ViewEnts[ps_flash].SY = ViewEnts[ps_weapon].SY;
}

//===========================================================================
//
//  CheckAmmo
//
//  Returns true if there is enough ammo to shoot.  If not, selects the
// next weapon to use.
//
//===========================================================================

bool CheckAmmo()
{
	int ammo;
	int *ammoUse;
	int count;

	ammo = Weapon(ViewEnts[ps_weapon]).Ammo;
	if (Powers[pw_weaponlevel2] && !Level.Game.deathmatch)
	{
		ammoUse = &MainGameInfo(Level.Game).WeaponAmmoUsePL2[0];
	}
	else
	{
		ammoUse = &MainGameInfo(Level.Game).WeaponAmmoUsePL1[0];
	}
	count = ammoUse[ReadyWeapon];
	if (ammo == am_noammo || Ammo[ammo] >= count)
	{
		return true;
	}
	// out of ammo, pick a weapon to change to
	do
	{
		if (WeaponOwned[wp_skullrod]
			&& Ammo[am_skullrod] > ammoUse[wp_skullrod])
		{
			PendingWeapon = wp_skullrod;
		}
		else if (WeaponOwned[wp_blaster]
			&& Ammo[am_blaster] > ammoUse[wp_blaster])
		{
			PendingWeapon = wp_blaster;
		}
		else if (WeaponOwned[wp_crossbow]
			&& Ammo[am_crossbow] > ammoUse[wp_crossbow])
		{
			PendingWeapon = wp_crossbow;
		}
		else if (WeaponOwned[wp_mace]
			&& Ammo[am_mace] > ammoUse[wp_mace])
		{
			PendingWeapon = wp_mace;
		}
		else if (Ammo[am_goldwand] > ammoUse[wp_goldwand])
		{
			PendingWeapon = wp_goldwand;
		}
		else if (WeaponOwned[wp_gauntlets])
		{
			PendingWeapon = wp_gauntlets;
		}
		else if (WeaponOwned[wp_phoenixrod]
			&& Ammo[am_phoenixrod] > ammoUse[wp_phoenixrod])
		{
			PendingWeapon = wp_phoenixrod;
		}
		else
		{
			PendingWeapon = wp_staff;
		}
	}
	while (PendingWeapon == wp_nochange);
	if (Powers[pw_weaponlevel2])
	{
		SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).DownState2);
	}
	else
	{
		SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).DownState);
	}
	return false;
}

//===========================================================================
//
//  FireWeapon
//
//===========================================================================

void FireWeapon()
{
	state attackState;

	if (!CheckAmmo())
	{
		return;
	}
	MO.SetState(PlayerPawn(MO).Attack2State);
	if (Powers[pw_weaponlevel2])
	{
		attackState = Refire ?
			Weapon(ViewEnts[ps_weapon]).HoldAttackState2 :
			Weapon(ViewEnts[ps_weapon]).AttackState2;
	}
	else
	{
		attackState = Refire ?
			Weapon(ViewEnts[ps_weapon]).HoldAttackState :
			Weapon(ViewEnts[ps_weapon]).AttackState;
	}
	SetPsprite(ps_weapon, attackState);
	HereticLevelInfo(Level).NoiseAlert(Actor(MO), Actor(MO));
	if (ReadyWeapon == wp_gauntlets && !Refire)
	{
		// Play the sound for the initial gauntlet attack
		MO.PlaySound('weapons/gauntletsuse', CHAN_WEAPON);
	}
}

//===========================================================================
//
//  ActivateBeak
//
//===========================================================================

void ActivateBeak()
{
	SetWeapon(wp_beak);
	ViewEnts[ps_weapon].SY = Weapon::WEAPONTOP;
	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).ReadyState);
}

//===========================================================================
//
//  PostChickenWeapon
//
//===========================================================================

void PostChickenWeapon(int weapon)
{
	if (weapon == wp_beak)
	{
		// Should never happen
		weapon = wp_staff;
	}
	SetWeapon(weapon);
	ViewEnts[ps_weapon].SY = Weapon::WEAPONBOTTOM;
	SetPsprite(ps_weapon, Weapon(ViewEnts[ps_weapon]).UpState);
}

//===========================================================================
//
//  UpdateBeak
//
//===========================================================================

void UpdateBeak(ViewEntity e)
{
	e.SY = Weapon::WEAPONTOP + itof(ChickenPeck) / 2.0;
}

//===========================================================================
//
//  WeaponLevel2Fade
//
//===========================================================================

void WeaponLevel2Fade()
{
	Weapon(ViewEnts[ps_weapon]).WeaponLevel2Fade();
}

//==========================================================================
//
//  PTR_UseTraverse
//
//==========================================================================

bool PTR_UseTraverse(intercept_t * in)
{
	if (!in->line->special || (GET_SPAC(in->line->flags) != SPAC_USE &&
		GET_SPAC(in->line->flags) != SPAC_USETHROUGH))
	{
		TVec hit_point;
		opening_t *open;
		TVec useDir;

		AngleVector(&MO.Angles, &useDir);
		hit_point = MO.Origin + (USERANGE * in->frac) * useDir;
		open = LineOpenings(in->line, hit_point);
		open = FindOpening(open, hit_point.z, hit_point.z + MO.Height);
		if (!open)
		{
			return false;	// can't use through a wall
		}
		return true;	// not a special line, but keep checking
	}

	if (PointOnPlaneSide(MO.Origin, in->line) == 1)
	{
		//	Don't use back side unless it's a two-sided line.
		return in->line->backsector != NULL;
	}

	HereticLevelInfo(Level).ActivateLine(in->line, Actor(MO), 0, SPAC_USE);

	//WAS can't use more than one special line in a row
	//jff 3/21/98 NOW multiple use allowed with enabling line flag
	return GET_SPAC(in->line->flags) == SPAC_USETHROUGH;
}

//==========================================================================
//
//  P_UseLines
//
//  Looks for special lines in front of the player to activate.
//
//==========================================================================

void P_UseLines()
{
	float x1;
	float y1;
	float x2;
	float y2;
	TVec useDir;

	AngleVector(&MO.Angles, &useDir);
	x1 = MO.Origin.x;
	y1 = MO.Origin.y;
	x2 = x1 + USERANGE * useDir.x;
	y2 = y1 + USERANGE * useDir.y;
	P_PathTraverse(x1, y1, x2, y2, PT_ADDLINES, self, 'PTR_UseTraverse');
}

//==========================================================================
//
//  Cheat_God
//
//==========================================================================

void Cheat_God()
{
	Cheats ^= CF_GODMODE;
	if (Cheats & CF_GODMODE)
	{
		cprint("GOD MODE ON");
	}
	else
	{
		cprint("GOD MODE OFF");
	}
}

//==========================================================================
//
//  Cheat_NoClip
//
//==========================================================================

void Cheat_NoClip()
{
	Cheats ^= CF_NOCLIP;
	if (Cheats & CF_NOCLIP)
	{
		cprint("NO CLIPPING ON");
	}
	else
	{
		cprint("NO CLIPPING OFF");
	}
}

//==========================================================================
//
//  Cheat_Gimme
//
//==========================================================================

void Cheat_Gimme()
{
	if (Cmd_CheckParm("weapons"))
	{
		int i;

		ArmorPoints = 200;
		ArmorType = 2;
		Backpack = true;
		for (i = 0; i < NUMWEAPONS - 1; i++)
		{
			WeaponOwned[i] = true;
		}
		if (MainGameInfo(Level.Game).shareware)
		{
			WeaponOwned[wp_skullrod] = false;
			WeaponOwned[wp_phoenixrod] = false;
			WeaponOwned[wp_mace] = false;
		}
		for (i = 0; i < 6 /*NUMAMMO2 */ ; i++)
		{
			Ammo[i] = MainGameInfo(Level.Game).maxammo[i] * 2;
		}
		cprint("ALL WEAPONS");
	}
	if (Cmd_CheckParm("power"))
	{
		if (Powers[pw_weaponlevel2])
		{
			Powers[pw_weaponlevel2] = 0.0;
			cprint("POWER OFF");
		}
		else
		{
			UseArtifact(HereticDefs::arti_tomeofpower);
			cprint("POWER ON");
		}
	}
	if (Cmd_CheckParm("health"))
	{
		if (ChickenTime)
		{
			MO.Health = MAXCHICKENHEALTH;
			Health = MO.Health;
		}
		else
		{
			MO.Health = MAXHEALTH;
			Health = MO.Health;
		}
		cprint("FULL HEALTH");
	}
	if (Cmd_CheckParm("keys"))
	{
		Keys = 7;
		cprint("ALL KEYS");
	}
	if (Cmd_CheckParm("artifacts"))
	{
		int i;
		int j;

		// All artifacts
		for (i = HereticDefs::arti_none + 1; i < HereticDefs::NUMARTIFACTS; i++)
		{
			if (MainGameInfo(Level.Game).shareware && (i == HereticDefs::arti_superhealth || i == HereticDefs::arti_teleport))
			{
				continue;
			}
			for (j = 0; j < 16; j++)
			{
				GiveArtifact(i);
			}
		}
		cprint("YOU GOT IT");
	}
/*	int i;
	int j;
	int type;
	int count;

	type = cheat->args[0] - 'a' + 1;
	count = cheat->args[1] - '0';
	if (type == 26 && count == 0)
	{ // All artifacts
		for (i = arti_none+1; i < NUMARTIFACTS; i++)
		{
			if (shareware && (i == arti_superhealth || i == arti_teleport))
			{
				continue;
			}
			for (j = 0; j < 16; j++)
			{
				P_GiveArtifact(player, i, NULL);
			}
		}
		P_SetMessage(player, "YOU GOT IT", false);
	}
	else if (type > arti_none && type < NUMARTIFACTS
		&& count > 0 && count < 10)
	{
		if (shareware && (type == arti_superhealth || type == arti_teleport))
		{
			P_SetMessage(player, "BAD INPUT", false);
			return;
		}
		for (i = 0; i < count; i++)
		{
			P_GiveArtifact(player, type, NULL);
		}
		P_SetMessage(player, "YOU GOT IT", false);
	}
	else
	{ // Bad input
		P_SetMessage(player, "BAD INPUT", false);
	}*/
}

//==========================================================================
//
//  Cheat_KillAll
//
//==========================================================================

void Cheat_KillAll()
{
	HereticLevelInfo(Level).P_Massacre();
	cprint("MASSACRE");
}

//==========================================================================
//
//  Cheat_Morph
//
//==========================================================================

void Cheat_Morph()
{
	if (ChickenTime)
	{
		if (UndoPlayerChicken())
		{
			cprint("CHICKEN OFF");
		}
	}
	else if (Actor(MO).ChickenMorphPlayer())
	{
		cprint("CHICKEN ON");
	}
}

//==========================================================================
//
//  Cheat_NoWeapons
//
//==========================================================================

void Cheat_NoWeapons()
{
	int i;

	if (ChickenTime)
	{
		return;
	}
	for (i = 1; i < 8; i++)
	{
		WeaponOwned[i] = false;
	}
	PendingWeapon = wp_staff;
	cprint("CHEATER - YOU DON'T DESERVE WEAPONS");
}

defaultproperties
{
}
