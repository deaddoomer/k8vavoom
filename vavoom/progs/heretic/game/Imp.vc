//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2002 JÆnis Legzdi·ý
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

class Imp : Pawn
	__mobjinfo__(66)
	__scriptid__(5);

state XCrashState;

//==========================================================================
//
//  A_ImpMeAttack
//
//==========================================================================

void A_ImpMeAttack()
{
	if (!Enemy)
	{
		return;
	}
	PlaySound(AttackSound, CHAN_WEAPON);
	if (CheckMeleeRange())
	{
		Enemy.Damage(self, self, 5 + (P_Random() & 7));
	}
}

//==========================================================================
//
//  A_ImpMsAttack
//
//==========================================================================

void A_ImpMsAttack()
{
	if (!Enemy || Random() > 0.25)
	{
		SetState(SeeState);
		return;
	}
	bSkullFly = true;
	PlaySound(AttackSound, CHAN_WEAPON);
	A_FaceTarget();
	Velocity = Normalise(MobjCenter(Enemy) - Origin) * 12.0 * 35.0;
}

//==========================================================================
//
//  A_ImpDeath
//
//==========================================================================

void A_ImpDeath()
{
	bSolid = false;
	bFloorClip = true;
	if (Origin.z <= FloorZ)
	{
		SetState(CrashState);
	}
}

//==========================================================================
//
//  A_ImpXDeath1
//
//==========================================================================

void A_ImpXDeath1()
{
	bSolid = false;
	bNoGravity = true;
	bFloorClip = true;
	Special1 = 666;	// Flag the crash routine
}

//==========================================================================
//
//  A_ImpXDeath2
//
//==========================================================================

void A_ImpXDeath2()
{
	bNoGravity = false;
	if (Origin.z <= FloorZ)
	{
		SetState(CrashState);
	}
}

//==========================================================================
//
//  A_ImpExplode
//
//==========================================================================

void A_ImpExplode()
{
	Actor mo;

	mo = Spawn(ImpChunk1, Origin);
	mo.Velocity.x = (Random() - Random()) * 4.0 * 35.0;
	mo.Velocity.y = (Random() - Random()) * 4.0 * 35.0;
	mo.Velocity.z = 9.0 * 35.0;
	mo = Spawn(ImpChunk2, Origin);
	mo.Velocity.x = (Random() - Random()) * 4.0 * 35.0;
	mo.Velocity.y = (Random() - Random()) * 4.0 * 35.0;
	mo.Velocity.z = 9.0 * 35.0;
	if (Special1 == 666)
	{
		// Extreme death crash
		SetState(XCrashState);
	}
}

__states__
{
	//	Idle
	S_IMP_LOOK1('IMPX', 0, 'monsters/gargoyle/base.md2', 0, 0.1, S_IMP_LOOK2) { A_Look(); }
	S_IMP_LOOK2('IMPX', 0, 'monsters/gargoyle/base.md2', 1, 0.1, S_IMP_LOOK3) { }
	S_IMP_LOOK3('IMPX', 0, 'monsters/gargoyle/base.md2', 2, 0.1, S_IMP_LOOK4) { }
	S_IMP_LOOK4('IMPX', 1, 'monsters/gargoyle/base.md2', 3, 0.1, S_IMP_LOOK5) { A_Look(); }
	S_IMP_LOOK5('IMPX', 1, 'monsters/gargoyle/base.md2', 4, 0.1, S_IMP_LOOK6) { }
	S_IMP_LOOK6('IMPX', 1, 'monsters/gargoyle/base.md2', 5, 0.1, S_IMP_LOOK7) { }
	S_IMP_LOOK7('IMPX', 2, 'monsters/gargoyle/base.md2', 6, 0.1, S_IMP_LOOK8) { A_Look(); }
	S_IMP_LOOK8('IMPX', 2, 'monsters/gargoyle/base.md2', 7, 0.1, S_IMP_LOOK9) { }
	S_IMP_LOOK9('IMPX', 2, 'monsters/gargoyle/base.md2', 8, 0.1, S_IMP_LOOK10) { }
	S_IMP_LOOK10('IMPX', 1, 'monsters/gargoyle/base.md2', 9, 0.1, S_IMP_LOOK11) { A_Look(); }
	S_IMP_LOOK11('IMPX', 1, 'monsters/gargoyle/base.md2', 10, 0.1, S_IMP_LOOK12) { }
	S_IMP_LOOK12('IMPX', 1, 'monsters/gargoyle/base.md2', 11, 0.1, S_IMP_LOOK1) { }
	//	Flying
	S_IMP_FLY1('IMPX', 0, 'monsters/gargoyle/base.md2', 12, 0.1, S_IMP_FLY2) { A_Chase(); }
	S_IMP_FLY2('IMPX', 0, 'monsters/gargoyle/base.md2', 13, 0.1, S_IMP_FLY3) { A_Chase(); }
	S_IMP_FLY3('IMPX', 1, 'monsters/gargoyle/base.md2', 14, 0.1, S_IMP_FLY4) { A_Chase(); }
	S_IMP_FLY4('IMPX', 1, 'monsters/gargoyle/base.md2', 15, 0.1, S_IMP_FLY5) { A_Chase(); }
	S_IMP_FLY5('IMPX', 2, 'monsters/gargoyle/base.md2', 16, 0.1, S_IMP_FLY6) { A_Chase(); }
	S_IMP_FLY6('IMPX', 2, 'monsters/gargoyle/base.md2', 17, 0.1, S_IMP_FLY7) { A_Chase(); }
	S_IMP_FLY7('IMPX', 1, 'monsters/gargoyle/base.md2', 19, 0.1, S_IMP_FLY8) { A_Chase(); }
	S_IMP_FLY8('IMPX', 1, 'monsters/gargoyle/base.md2', 19, 0.1, S_IMP_FLY1) { A_Chase(); }
	//	Melee
	S_IMP_MEATK1('IMPX', 3, 'monsters/gargoyle/base.md2', 20, 0.1, S_IMP_MEATK2) { A_FaceTarget(); }
	S_IMP_MEATK2('IMPX', 3, 'monsters/gargoyle/base.md2', 21, 0.1, S_IMP_MEATK3) { }
	S_IMP_MEATK3('IMPX', 4, 'monsters/gargoyle/base.md2', 22, 0.1, S_IMP_MEATK4) { A_FaceTarget(); }
	S_IMP_MEATK4('IMPX', 4, 'monsters/gargoyle/base.md2', 23, 0.1, S_IMP_MEATK5) { }
	S_IMP_MEATK5('IMPX', 5, 'monsters/gargoyle/base.md2', 24, 0.1, S_IMP_MEATK6) { A_ImpMeAttack(); }
	S_IMP_MEATK6('IMPX', 5, 'monsters/gargoyle/base.md2', 25, 0.1, S_IMP_MEATK7) { }
	S_IMP_MEATK7('IMPX', 5, 'monsters/gargoyle/base.md2', 26, 0.1, S_IMP_FLY1) { }
	//	Skull-fly attack
	S_IMP_MSATK1_1('IMPX', 0, 'monsters/gargoyle/base.md2', 12, 0.1, S_IMP_MSATK1_2) { A_FaceTarget(); }
	S_IMP_MSATK1_2('IMPX', 0, 'monsters/gargoyle/base.md2', 13, 0.1, S_IMP_MSATK1_3) { }
	S_IMP_MSATK1_3('IMPX', 0, 'monsters/gargoyle/base.md2', 14, 0.1, S_IMP_MSATK1_4) { }
	S_IMP_MSATK1_4('IMPX', 1, 'monsters/gargoyle/base.md2', 15, 0.1, S_IMP_MSATK1_5) { A_ImpMsAttack(); }
	S_IMP_MSATK1_5('IMPX', 1, 'monsters/gargoyle/base.md2', 16, 0.1, S_IMP_MSATK1_6) { }
	S_IMP_MSATK1_6('IMPX', 2, 'monsters/gargoyle/base.md2', 17, 0.1, S_IMP_MSATK1_7) { }
	S_IMP_MSATK1_7('IMPX', 2, 'monsters/gargoyle/base.md2', 18, 0.1, S_IMP_MSATK1_8) { }
	S_IMP_MSATK1_8('IMPX', 1, 'monsters/gargoyle/base.md2', 19, 0.1, S_IMP_MSATK1_9) { }
	S_IMP_MSATK1_9('IMPX', 1, 'monsters/gargoyle/base.md2', 12, 0.1, S_IMP_MSATK1_10) { }
	S_IMP_MSATK1_10('IMPX', 0, 'monsters/gargoyle/base.md2', 13, 0.1, S_IMP_MSATK1_11) { }
	S_IMP_MSATK1_11('IMPX', 0, 'monsters/gargoyle/base.md2', 14, 0.1, S_IMP_MSATK1_12) { }
	S_IMP_MSATK1_12('IMPX', 1, 'monsters/gargoyle/base.md2', 15, 0.1, S_IMP_MSATK1_13) { }
	S_IMP_MSATK1_13('IMPX', 1, 'monsters/gargoyle/base.md2', 16, 0.1, S_IMP_MSATK1_6) { }
	//	Pain
	S_IMP_PAIN1('IMPX', 6, 'monsters/gargoyle/base.md2', 33, 0.1, S_IMP_PAIN2) { }
	S_IMP_PAIN2('IMPX', 6, 'monsters/gargoyle/base.md2', 34, 0.1, S_IMP_FLY1) { A_Pain(); }
	//	Deaths, no models because it doesn't fit right.
	S_IMP_DIE1('IMPX', 6, 4.0 / 35.0, S_IMP_DIE2) { A_ImpDeath(); }
	S_IMP_DIE2('IMPX', 7, 5.0 / 35.0, S_IMP_DIE2) { }
	S_IMP_XDIE1('IMPX', 18, 5.0 / 35.0, S_IMP_XDIE2) { A_ImpXDeath1(); }
	S_IMP_XDIE2('IMPX', 19, 5.0 / 35.0, S_IMP_XDIE3) { }
	S_IMP_XDIE3('IMPX', 20, 5.0 / 35.0, S_IMP_XDIE4) { }
	S_IMP_XDIE4('IMPX', 21, 5.0 / 35.0, S_IMP_XDIE5) { A_ImpXDeath2(); }
	S_IMP_XDIE5('IMPX', 22, 5.0 / 35.0, S_IMP_XDIE5) { }
	S_IMP_CRASH1('IMPX', 8, 7.0 / 35.0, S_IMP_CRASH2) { A_ImpExplode(); }
	S_IMP_CRASH2('IMPX', 9, 7.0 / 35.0, S_IMP_CRASH3) { A_Scream(); }
	S_IMP_CRASH3('IMPX', 10, 7.0 / 35.0, S_IMP_CRASH4) { }
	S_IMP_CRASH4('IMPX', 11, -1.0, none) { }
	S_IMP_XCRASH1('IMPX', 23, 7.0 / 35.0, S_IMP_XCRASH2) { }
	S_IMP_XCRASH2('IMPX', 24, 7.0 / 35.0, S_IMP_XCRASH3) { }
	S_IMP_XCRASH3('IMPX', 25, -1.0, none) { }
}

defaultproperties
{
	Health = 40;
	GibsHealth = -20;
	Radius = 16.0;
	Height = 36.0;
	Mass = 50.0;
	StepSpeed = 10.0;
	PainChance = 0.78125;
	bNoGravity = true;
	bFloat = true;
	bSpawnFloat = true;
	bFloorClip = false;
	IdleState = S_IMP_LOOK1;
	SeeState = S_IMP_FLY1;
	MeleeState = S_IMP_MEATK1;
	MissileState = S_IMP_MSATK1_1;
	PainState = S_IMP_PAIN1;
	DeathState = S_IMP_DIE1;
	GibsDeathState = S_IMP_XDIE1;
	CrashState = S_IMP_CRASH1;
	XCrashState = S_IMP_XCRASH1;
	SightSound = 'himp/sight';
	ActiveSound = 'himp/active';
	AttackSound = 'himp/attack';
	PainSound = 'himp/pain';
	DeathSound = 'himp/death';
}

//**************************************************************************
//
//  $Log$
//  Revision 1.5  2006/03/12 19:56:46  dj_jl
//  States as objects, added state variable type.
//
//  Revision 1.4  2006/02/15 23:33:11  dj_jl
//  Moved all server progs global variables to classes.
//
//  Revision 1.3  2005/11/18 23:36:50  dj_jl
//  Renamed sounds.
//
//  Revision 1.2  2004/12/27 12:23:12  dj_jl
//  Multiple small changes for version 1.16
//
//  Revision 1.1  2002/10/26 16:32:22  dj_jl
//  New style of defining classes.
//
//**************************************************************************
