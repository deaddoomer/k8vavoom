//------------------------------------------------------------------------------
actor DoomImp 3001
{
	game Doom
	SpawnID 5
	Health 60
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 200
	SeeSound "imp/sight"
	ActiveSound "imp/active"
	PainSound "imp/pain"
	DeathSound "imp/death"
	Obituary "$ob_imp"
	HitObituary "$ob_imphit"
	Monster
	+FloorClip
	states
	{
	Spawn:
		TROO AB 10 A_Look
		Loop
	See:
		TROO AABBCCDD 3 A_Chase
		Loop
	Melee:
	Missile:
		TROO E 8 A_FaceTarget
		TROO F 8 A_FaceTarget
		TROO G 6 A_TroopAttack
		Goto See
	Pain:
		TROO H 2
		TROO H 2 A_Pain
		Goto See
	Death:
		TROO I 8
		TROO J 8 A_Scream
		TROO K 6
		TROO L 6 A_NoBlocking
		TROO M -1
		Stop
	XDeath:
		TROO N 5
		TROO O 5 A_XScream
		TROO P 5
		TROO Q 5 A_NoBlocking
		TROO RST 5
		TROO U -1
		Stop
	Raise:
		TROO ML 8
		TROO KJI 6
		Goto See
	}
}

//------------------------------------------------------------------------------
actor DoomImpBall
{
	game Doom
	SpawnID 10
	Radius 6
	Height 8
	Speed 10
	FastSpeed 20
	Damage 3
	RenderStyle Add
	Alpha 1
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	Projectile
	+Randomize
	states
	{
	Spawn:
		BAL1 AB 4 Bright
		Loop
	Death:
		BAL1 CDE 6 Bright
		Stop
	}
}
