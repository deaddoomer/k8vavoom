//------------------------------------------------------------------------------
actor Loremaster 12
{
	Game Strife
	ConversationID 66, 63, 64
	Tag "PRIEST"
	Health 800
	Radius 15
	Height 56
	Speed 10
	FloatSpeed 5
	MinMissileChance 150
	Monster
	+FloorClip
	+NoGravity
	+LookAllAround
	+Float
	+InCombat
	+NoBlood
	+NotDmatch
	+NoBlockMonst
	+NoIceDeath
	DamageFactor "Fire", 0.5
	SeeSound "loremaster/sight"
	ActiveSound "loremaster/active"
	AttackSound "loremaster/attack"
	PainSound "loremaster/pain"
	DeathSound "loremaster/death"
	Obituary "$ob_loremaster"
	DropItem "Junk"
	states
	{
	Spawn:
		PRST A 10 A_Look
		PRST B 10 A_SentinelBob
		Loop
	See:
		PRST A 4 A_Chase
		PRST A 4 A_SentinelBob
		PRST B 4 A_Chase
		PRST B 4 A_SentinelBob
		PRST C 4 A_Chase
		PRST C 4 A_SentinelBob
		PRST D 4 A_Chase
		PRST D 4 A_SentinelBob
		Loop
	Melee:
		PRST E 4 A_FaceTarget
		PRST F 4 A_CustomMeleeAttack((Random(0, 255) & 9) * 5)
		PRST E 4 A_SentinelBob
		Goto See
	Missile:
		PRST E 4 A_FaceTarget
		PRST F 4 A_CustomMissile("LoreShot", 32, 0)
		PRST E 4 A_SentinelBob
		Goto See
	Death:
		PDED A 6
		PDED B 6 A_Scream
		PDED C 6
		PDED D 6 A_NoBlocking
		PDED E 6
		PDED FGHIJIJIJKL 5
		PDED MNOP 4
		PDED Q 4 A_SpawnItemEx("AlienSpectre5", 0, 0, 0, 0, 0, random(0, 255) * 0.0078125, 0, SXF_NOCHECKPOSITION)
		PDED RS 4
		PDED T -1
		Stop
	}
}

//------------------------------------------------------------------------------
actor LoreShot2
{
	ConversationID 98, -1, -1
	+NoBlockmap
	+NoGravity
	states
	{
	Spawn:
		TEND A 20
		Stop
	}
}
