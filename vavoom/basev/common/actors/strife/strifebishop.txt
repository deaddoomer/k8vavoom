//------------------------------------------------------------------------------
actor StrifeBishop 187
{
	game Strife
	ConversationID 64, -1, -1
	Health 500
	Radius 40
	Height 56
	Mass 500
	Speed 8
	PainChance 128
	MinMissileChance 150
	MaxDropoffHeight 32
	SeeSound "bishop/sight"
	ActiveSound "bishop/active"
	PainSound "bishop/pain"
	DeathSound "bishop/death"
	Obituary "$ob_stfbishop"
	DropItem "CrateOfMissiles", 256, 20
	Monster
	+FloorClip
	+InCombat
	+NoBlood
	+NotDMatch
	+FireResist
	+NoIceDeath
	states
	{
	Spawn:
		MLDR A 10 A_Look
		Loop
	See:
		MLDR AABBCCDD 3 A_Chase
		Loop
	Missile:
		MLDR E 3 A_FaceTarget
		MLDR F 2 Bright A_CustomMissile("BishopMissile", 64, 0, 0, CMF_AIMOFFSET)
		Goto See
	Pain:
		MLDR D 1 A_Pain
		Goto See
	Death:
		MLDR G 3 Bright
		MLDR H 5 Bright A_Scream
		MLDR I 4 Bright A_TossGib
		MLDR J 0 Bright A_AlertMonsters
		MLDR J 4 Bright A_Explode(64, 64)
		MLDR KL 4 Bright
		MLDR M 4 Bright A_NoBlocking
		MLDR N 4 Bright
		MLDR O 4 Bright A_TossGib
		MLDR P 4 Bright
		MLDR Q 4 Bright A_TossGib
		MLDR R 4 Bright
		MLDR S 4 Bright A_TossGib
		MLDR T 4 Bright
		MLDR U 4 Bright A_TossGib
		MLDR V 4 A_SpawnItemEx("AlienSpectre2", 0, 0, 0, 0, 0, random(0, 255) * 0.0078125, 0, SXF_NOCHECKPOSITION)
		Stop
	}
}

//------------------------------------------------------------------------------
actor BishopMissile
{
	Radius 10
	Height 14
	Speed 20
	MaxStepHeight 4
	Damage 10
	SeeSound "bishop/misl"
	DeathSound "bishop/mislx"
	Projectile
	+SeekerMissile
	+StrifeDamage
	states
	{
	Spawn:
		MISS A 4 Bright
		MISS B 3 Bright A_Tracer2
		MISS A 0 Bright A_PlaySoundEx("misc/missileinflight", "Voice")
		MISS A 0 Bright A_SpawnItemEx("MiniMissilePuff", 0, 0, random2() * 0.015625)
		MISS A 4 Bright A_SpawnItemEx("RocketTrail", -momx, -momy, 0, 0, 0, 1)
		Goto Spawn+1
	Death:
		SMIS A 0 Bright A_SetTranslucent(1,1)
		SMIS A 0 Bright A_StopSoundEx("Voice")
		SMIS A 0 Bright A_AlertMonsters
		SMIS A 5 Bright A_Explode(64, 64)
		SMIS B 5 Bright
		SMIS C 4 Bright
		SMIS DEFG 2 Bright
		Stop
	}
}
