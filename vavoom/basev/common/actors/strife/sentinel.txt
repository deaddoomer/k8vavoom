//------------------------------------------------------------------------------
actor Sentinel 3006
{
	Game Strife
	ConversationID 91, -1, -1
	Health 100
	Radius 23
	Height 53
	Mass 300
	Speed 7
	FloatSpeed 5
	PainChance 255
	MinMissileChance 150
	Monster
	+SpawnCeiling
	+NoGravity
	+DropOff
	+LookAllAround
	+Float
	+InCombat
	+NoBlood
	+NoBlockMonst
	+MissileMore
	SeeSound "sentinel/sight"
	ActiveSound "sentinel/active"
	DeathSound "sentinel/death"
	Obituary "$ob_sentinel"

	action native A_SentinelAttack();

	states
	{
	Spawn:
		SEWR A 10 A_Look
		Loop
	See:
		SEWR A 6 A_SentinelBob
		SEWR A 6 A_Chase
		Loop
	Missile:
		SEWR B 4 A_FaceTarget
		SEWR C 8 Bright A_SentinelAttack
		SEWR C 4 Bright A_SentinelRefire
		Goto Missile+1
	Pain:
		SEWR D 5 A_Pain
		Goto Missile+2
	Death:
		SEWR D 7 A_NoBlocking
		SEWR E 8 Bright A_TossGib
		SEWR F 5 Bright A_Scream
		SEWR GH 4 Bright A_TossGib
		SEWR I 4
		SEWR J 5
		Stop
	}
}

//------------------------------------------------------------------------------
actor SentinelFX1
{
	RenderStyle Add
	Radius 10
	Height 8
	Speed 40
	MaxStepHeight 4
	Projectile
	+StrifeDamage
	DamageType "Disintegrate"
	states
	{
	Spawn:
		SHT1 AB 4
		Loop
	Death:
		POW1 J 4
		Stop
	}
}

//------------------------------------------------------------------------------
actor SentinelFX2 : SentinelFX1
{
	Damage 1
	SeeSound "sentinel/plasma"
	states
	{
	Death:
		POW1 FGHI 4
		Goto Super::Death
	}
}
