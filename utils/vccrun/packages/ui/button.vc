// text button
class WidgetButton : Widget;


// ////////////////////////////////////////////////////////////////////////// //
protected string mText;
protected int mColor;
protected int mSelColor;
protected bool mWaitRelease;


void onCreated (string atext) {
  mText = atext;
  mBaseWidth = Video.textWidth(atext)+6;
  mBaseHeight = Video.textHeight(atext)+6;
}


// ////////////////////////////////////////////////////////////////////////// //
protected override void selfPaintAt (int x0, int y0) {
  Video.colorRGB = 0x40_40_40;
  Video.fillRect(x0, y0, mWidth, mHeight);
  if (mWaitRelease) {
    Video.drawBevel(x0, y0, mWidth, mHeight, 0x20_20_20, 0x60_60_60);
  } else {
    Video.drawBevel(x0, y0, mWidth, mHeight, 0x60_60_60, 0x20_20_20);
  }
  if (mText) {
    Video.colorRGB = (focused ? mSelColor : mColor);
    Video.setFont(fontName);
    Video.drawTextAt(x0+3+(mWaitRelease ? 1 : 0), y0+3+(mWaitRelease ? 1 : 0), mText);
  }
}


// ////////////////////////////////////////////////////////////////////////// //
override void onFocus () { if (mWaitRelease) setDirty(); mWaitRelease = false; }
override void onBlur () { if (mWaitRelease) setDirty(); mWaitRelease = false; }


// ////////////////////////////////////////////////////////////////////////// //
override bool onEvent (ref event_t evt) {
  if (evt.type == ev_mouse) return false;
  //writeln(GetClassName(Class), ":<", mId, ">: event: ", evt.type);
  if (evt.type == ev_keydown) {
    if (!focused) return false;
    switch (evt.data1) {
      case K_SPACE:
      case K_ENTER:
        if (!mWaitRelease) setDirty();
        mWaitRelease = true;
        return true;
      case K_MOUSE1:
        //writeln("mdown: (", evt.x, ",", evt.y, ")");
        if (evt.x >= 0 && evt.y >= 0 && evt.x < mWidth && evt.y < mHeight) {
          if (!mWaitRelease) setDirty();
          mWaitRelease = true;
          return true;
        }
        break;
    }
    return false;
  }

  if (evt.type == ev_keyup) {
    if (!focused) return false;
    switch (evt.data1) {
      case K_SPACE:
      case K_ENTER:
        if (mWaitRelease) { mWaitRelease = false; setDirty(); doOnAction(); }
        return true;
      case K_MOUSE1:
        //writeln("mup: (", evt.x, ",", evt.y, "); ", mWaitRelease);
        if (mWaitRelease) {
          mWaitRelease = false;
          setDirty();
          if (evt.x >= 0 && evt.y >= 0 && evt.x < mWidth && evt.y < mHeight) doOnAction();
          return true;
        }
        break;
    }
    return false;
  }

  return false;
}


// ////////////////////////////////////////////////////////////////////////// //
int color {
  get { return mColor; }
  set { if (mColor != value) { mColor = value; setDirty(); } }
}

int colorFocused {
  get { return mSelColor; }
  set { if (mSelColor != value) { mSelColor = value; setDirty(); } }
}


// ////////////////////////////////////////////////////////////////////////// //
defaultproperties {
  mFocusable = true;
  mWaitRelease = false;
  mColor = 0xff_7f_00;
  mSelColor = 0xff_ff_ff;
}
