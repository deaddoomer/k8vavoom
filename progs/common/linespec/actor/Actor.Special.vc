//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2019 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, either version 3 of the License, or
//**  (at your option) any later version.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
//  Special actions
//**************************************************************************

//==========================================================================
//
//  A_BossDeath
//
//  Possibly trigger special effects if on first boss level
//
//==========================================================================
final void A_BossDeath () {
  name myCName = GetClassName(GetClassReplacee(Class));

  if (!CheckBossDeath()) return;

  // check generic actions first
  foreach (auto i; 0..Level.SpecialActions.length) {
    if (Class == FindClassNoCase(Level.SpecialActions[i].TypeName)) {
      Level.ExecuteActionSpecial(Level.SpecialActions[i].Special,
                                 Level.SpecialActions[i].Args[0],
                                 Level.SpecialActions[i].Args[1],
                                 Level.SpecialActions[i].Args[2],
                                 Level.SpecialActions[i].Args[3],
                                 Level.SpecialActions[i].Args[4], nullptr, 0, self);
    }
  }

  if (!((myCName == 'BaronOfHell' && Level.bBaronSpecial) ||
        (myCName == 'Cyberdemon' && Level.bCyberDemonSpecial) ||
        (myCName == 'SpiderMastermind' && Level.bSpiderMastermindSpecial) ||
        (myCName == 'Arachnotron' && Level.bMap07Special) ||
        (myCName == 'Fatso' && Level.bMap07Special) ||
        (myCName == 'Ironlich' && Level.bIronLichSpecial) ||
        (myCName == 'Minotaur' && Level.bMinotaurSpecial) ||
        (myCName == 'Sorcerer2' && Level.bDSparilSpecial)))
  {
    return;
  }

  // victory!
  if (Level.bSpecialActionKillMonsters) {
    // kill any remaining monsters
    LineSpecialLevelInfo(Level).P_Massacre();
  }

  if (Level.bMap07Special) {
    if (myCName == 'Fatso') {
      Level.ExecuteActionSpecial(LNSPEC_FloorLowerToLowest, 666, 8, 0, 0, 0, nullptr, 0, none);
      return;
    }
    if (myCName == 'Arachnotron') {
      Level.ExecuteActionSpecial(LNSPEC_FloorRaiseByTexture, 667, 8, 0, 0, 0, nullptr, 0, none);
      return;
    }
  } else if (Level.bSpecialActionOpenDoor) {
    Level.ExecuteActionSpecial(LNSPEC_DoorOpen, 666, 64, 150, 0, 0, nullptr, 0, none);
    return;
  } else if (Level.bSpecialActionLowerFloor) {
    Level.ExecuteActionSpecial(LNSPEC_FloorLowerToLowest, 666, 8, 0, 0, 0, nullptr, 0, none);
    return;
  }
  Level.ExitLevel(0);
}


//==========================================================================
//
//  A_KeenDie
//
//  DOOM II special, map 32. Uses special tag 666.
//
//==========================================================================
final void A_KeenDie (optional int tag) {
  EntityEx Other;

  A_Fall();

  if (!tag) tag = 666;

  // scan the remaining thinkers to see if all Keens are dead
  foreach AllThinkers(class!Thinker(Class), Other) {
    if (Other != self && Other.Health > 0) {
      // other mobj of the same Class not dead
      return;
    }
  }
  Level.ExecuteActionSpecial(LNSPEC_DoorOpen, tag, 16, 150, 0, 0, nullptr, 0, none);
}


//==========================================================================
//
//  A_BrainDie
//
//  Killed brain, exit level.
//
//==========================================================================
final void A_BrainDie () {
  Level.ExitLevel(0);
}


//==========================================================================
//
//  A_GetHurt
//
//==========================================================================
final void A_GetHurt () {
  bInCombat = true;
  if ((P_Random()%5) == 0) {
    PlaySound(PainSound, CHAN_VOICE);
    --Health;
  }
  if (Health <= 0) Died(Target, none);
}


//==========================================================================
//
//  A_KlaxonBlare
//
//==========================================================================
final void A_KlaxonBlare () {
  --ReactionCount;
  if (ReactionCount <= 0) {
    Target = none;
    ReactionCount = 60;
    SetState(IdleState);
  }
  if (ReactionCount == 2) {
    // should make enemies go to Idle state again
    LineSpecialLevelInfo(Level).NoiseAlert(none, self);
  } else if (ReactionCount > 50) {
    PlaySound('misc/alarm', CHAN_VOICE);
  }
}


//==========================================================================
//
//  A_CheckTerrain
//
//==========================================================================
final void A_CheckTerrain () {
  if (Origin.z == EFloor.spGetPointZ(Origin)) {
    if ((Sector->special&SECSPEC_BASE_MASK) == SECSPEC_DamageInstantDeath) {
      Damage(none, none, 999, 'InstantDeath', spawnBlood:true);
    } else if ((Sector->special&SECSPEC_BASE_MASK) == SECSPEC_ScrollCurrent) {
      int anglespeed = Sector->sectorTag-100;
      float speed = float(anglespeed%10)/16.0*35.0;
      float finean = float(anglespeed/10)*45.0;
      Velocity.x += speed*cos(finean);
      Velocity.y += speed*sin(finean);
    }
  }
}


//============================================================================
//
//  A_Print
//
//============================================================================
final void A_Print (string Text, optional float Time, optional name FontName) {
  BasePlayer Plr;
       if (bIsPlayer) Plr = Player;
  else if (Target && Target.bIsPlayer) Plr = Target.Player;
  else return;
  if (!Time) Time = fclamp(GetCvarF('hud_notify_time'), 1.0, 7.0);
  Plr.ClientHudMessage(Text, FontName, 0, 123456, GetCvar('hud_font_color')/*CR_UNTRANSLATED*/, "", 0.5, 0.2, 0, 0, Time, 0.0, 0.0);
}


//============================================================================
//
//  A_PrintBold
//
//============================================================================
final void A_PrintBold (string Text, optional float Time, optional name FontName) {
  BasePlayer Plr;
       if (bIsPlayer) Plr = Player;
  else if (Target && Target.bIsPlayer) Plr = Target.Player;
  else return;
  if (!Time) Time = fclamp(GetCvarF('hud_notify_time'), 1.0, 7.0);
  Plr.ClientHudMessage(Text, FontName, 0, 123456, GetCvar('hud_font_color')/*CR_UNTRANSLATED*/, "", 0.5, 0.2, 0, 0, Time, 0.0, 0.0);
}


//============================================================================
//
//  A_SetBlend
//
//============================================================================
final void A_SetBlend (string Col1, float A, int Tics, optional string Col2) {
  int C1 = ParseColor(Col1);
  int C2 = ParseColor(Col2);
  A = fclamp(A, 0, 1);

  // huh?
  if (C1 == -1) C1 = 0;
  if (C2 == -1) C2 = 0;
  if (!specified_Col2) C2 = C1;

  FlashFader F = Spawn(FlashFader);
  F.Init(float((C1>>16)&0xff)/255.0, float((C1>>8)&0xff)/255.0, float(C1&0xff)/255.0,
         A,
         float((C2>>16)&0xff)/255.0, float((C2>>8)&0xff)/255.0, float(C2&0xff)/255.0,
         0.0, float(Tics)/35.0, self);
}


//============================================================================
//
//  A_CheckSight
//
//============================================================================
final void A_CheckSight (state Label) {
  // state jumps should not affect state chain results
  if (XLevel.StateCall) XLevel.StateCall->Result = false;

  foreach (auto i; 0..MAXPLAYERS) {
    if (!Level.Game.Players[i] || !Level.Game.Players[i].bSpawned) continue;
    if (!Level.Game.Players[i].Camera) {
      //k8: this can happens when player is switching cameras (why?)
      //    we can check it with brutalv20, with "+vscript_command give PowerStrength"
      dprint("%C: EMPTY CAMERA for player #%d!", self, i);
      continue;
    }
    if (Level.Game.Players[i].Camera.CanSee(self)) return;
  }

  DoJump(Label);
}


//==========================================================================
//
//  A_CheckFloor
//
//  Checks if an object hit the floor
//
//==========================================================================
final void A_CheckFloor (state Label) {
  // state jumps should not affect state chain results
  if (XLevel.StateCall) XLevel.StateCall->Result = false;
  if (Origin.z <= FloorZ) DoJump(Label);
}


//==========================================================================
//
//  A_CheckCeiling
//
//  Checks if an object hit the ceiling
//
//==========================================================================
final void A_CheckCeiling (state Label) {
  // state jumps should not affect state chain results
  if (XLevel.StateCall) XLevel.StateCall->Result = false;
  if (Origin.z >= CeilingZ) DoJump(Label);
}


//============================================================================
//
//  A_CheckPlayerDone
//
//============================================================================
final void A_CheckPlayerDone () {
  if (!bIsPlayer) Destroy();
}


//==========================================================================
//
//  A_SkullPop
//
//==========================================================================
final void A_SkullPop (optional class!Actor SpawnType) {
  if (!class!PlayerChunk(SpawnType)) SpawnType = BloodySkull;

  bSolid = false;

  EntityEx A = Spawn(SpawnType, Origin+vector(0.0, 0.0, 48.0), default, default, false);
  if (!A) Error("cannot spawn '%C' in A_SkullPop", SpawnType);

  A.Velocity.x = (Random()-Random())*2.0*35.0;
  A.Velocity.y = (Random()-Random())*2.0*35.0;
  A.Velocity.z = (2.0+Random()/4.0)*35.0;
  A.Health = Health;
  A.Angles.yaw = Angles.yaw;

  if (bIsPlayer) {
    // attach player mobj to bloody skull
    A.Player = Player;
    A.bIsPlayer = true;
    A.ObtainInventory(EntityEx(Player.MO));
    Player = none;
    bIsPlayer = false;
    A.Player.MO = A;
    if (A.Player.Camera == self) A.Player.Camera = A;
    PlayerEx(A.Player).DamageFlash = 1.0;
    LineSpecialLevelInfo(Level).AddPlayerCorpse(self);
  }
}
