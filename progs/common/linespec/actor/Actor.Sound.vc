//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2020 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
//  Sound functions
//**************************************************************************


//==========================================================================
//
//  A_PlaySound
//
//==========================================================================
[decorate] final void A_PlaySound (optional name Sound, optional [soundchannel] int ChannelNameOrNumber,
                        optional float volume, optional bool looping, optional float attenuation,
                        optional bool local)
{
  if (!specified_Sound) Sound = 'weapons/pistol';
  if (!Sound) return;
  //if (!specified_ChannelNameOrNumber) ChannelNameOrNumber = CHAN_BODY;
  if (!specified_ChannelNameOrNumber) ChannelNameOrNumber = CHAN_AUTO; //k8: use `auto` channel
  if (ChannelNameOrNumber < 0 || ChannelNameOrNumber > 7) ChannelNameOrNumber = CHAN_AUTO;
  PlaySound(Sound, ChannelNameOrNumber, volume!optional, attenuation!optional, looping!optional, local!optional);
}


//==========================================================================
//
//  A_PlaySound_NoNet
//
//==========================================================================
[decorate] final void A_PlaySound_NoNet (optional name Sound, optional [soundchannel] int ChannelNameOrNumber,
                                         optional float volume, optional bool looping, optional float attenuation,
                                         optional bool local)
{
  // if we're server, don't do anything here
  if (Level.Game.IsNetServer) {
    //printdebug("%C: ignored sound '%s'", self, Sound);
    return;
  }
  if (!specified_Sound) Sound = 'weapons/pistol';
  if (!Sound) return;
  //if (!specified_ChannelNameOrNumber) ChannelNameOrNumber = CHAN_BODY;
  if (!specified_ChannelNameOrNumber) ChannelNameOrNumber = CHAN_AUTO; //k8: use `auto` channel
  if (ChannelNameOrNumber < 0 || ChannelNameOrNumber > 7) ChannelNameOrNumber = CHAN_AUTO;
  PlaySound(Sound, ChannelNameOrNumber, volume!optional, attenuation!optional, looping!optional, local!optional);
}


//==========================================================================
//
//  A_PlaySoundEx
//
//==========================================================================
[decorate] final void A_PlaySoundEx (name Sound, [soundchannel] int ChannelNameOrNumber, optional bool Loop, optional int Attenuation) {
  // attenuation
  float Atten = ATTN_NORMAL;
  switch (Attenuation) {
    case -1: Atten = ATTN_STATIC; break;
    case 1: Atten = ATTN_NONE; break;
  }
  PlaySound(Sound, ChannelNameOrNumber, default, Atten, Loop);
}


//==========================================================================
//
//  A_PlayWeaponSound
//
//==========================================================================
[decorate] final void A_PlayWeaponSound (name Sound) {
  PlaySound(Sound, CHAN_WEAPON);
}


//==========================================================================
//
//  A_ActiveSound
//
//==========================================================================
[decorate] final void A_ActiveSound () {
  if (ActiveSound) PlaySound(ActiveSound, CHAN_VOICE);
}


//==========================================================================
//
//  A_LoopActiveSound
//
//==========================================================================
[decorate] final void A_LoopActiveSound () {
  if (ActiveSound) PlaySound(ActiveSound, CHAN_VOICE, default, default, true);
}


//==========================================================================
//
//  A_FLoopActiveSound
//
//==========================================================================
[decorate] final void A_FLoopActiveSound () {
  if (ActiveSound && !GetSoundPlayingInfo(self, GetSoundID(ActiveSound))) {
    PlaySound(ActiveSound, CHAN_VOICE);
  }
}


//==========================================================================
//
//  A_StopSound
//
//==========================================================================
[decorate] final void A_StopSound (optional [soundchannel] int ChannelNameOrNumber) {
  if (!specified_ChannelNameOrNumber) ChannelNameOrNumber = CHAN_VOICE;
  StopSound(ChannelNameOrNumber);
}


//==========================================================================
//
//  A_StopSoundEx
//
//==========================================================================
[decorate] final void A_StopSoundEx ([soundchannel] int ChannelNameOrNumber) {
  StopSound(ChannelNameOrNumber);
}


//==========================================================================
//
//  A_Pain
//
//==========================================================================
[decorate] final void A_Pain () {
  if (bIsPlayer && !PlayerEx(Player).MorphTime) {
         if (Health < 25) PlaySound('*pain25', CHAN_VOICE);
    else if (Health < 50) PlaySound('*pain50', CHAN_VOICE);
    else if (Health < 75) PlaySound('*pain75', CHAN_VOICE);
    else PlaySound('*pain100', CHAN_VOICE);
  } else {
    if (PainSound) PlaySound(PainSound, CHAN_VOICE);
  }
}


//==========================================================================
//
//  A_Scream
//
//  Death sound.
//
//==========================================================================
[decorate] final void A_Scream () {
  // remove muzzle flash & other light effects
  bDynamicLight = false;
  if (DeathSound) {
    // make boss death sounds full volume
    PlaySound(DeathSound, CHAN_VOICE, 1.0, (bBoss ? ATTN_NONE : ATTN_NORMAL));
  }
}


//==========================================================================
//
//  A_XScream
//
//  Explode death sound.
//
//==========================================================================
[decorate] final void A_XScream () {
  PlaySound((bIsPlayer ? '*gibbed' : 'misc/gibbed'), CHAN_BODY);
}


//==========================================================================
//
//  A_VileStart
//
//  Arch-vile attack start sound.
//
//==========================================================================
[decorate] final void A_VileStart () {
  PlaySound('vile/start', CHAN_WEAPON);
}


//==========================================================================
//
//  A_BrainPain
//
//  Brain pain sound.
//
//==========================================================================
[decorate] final void A_BrainPain () {
  PlaySound('brain/pain', CHAN_VOICE, 1.0, ATTN_NONE);
}


//==========================================================================
//
//  A_BrainAwake
//
//  Init boss spawner.
//
//==========================================================================
[decorate] final void A_BrainAwake () {
  // do just a sound, like in Boom
  PlaySound('brain/sight', CHAN_VOICE, 1.0, ATTN_NONE);
}


//==========================================================================
//
//  A_PlayerScream
//
//==========================================================================
[decorate] final void A_PlayerScream () {
  if (!bIsPlayer || DeathSound) {
    PlaySound(DeathSound, CHAN_VOICE);
    return;
  }

  name Sound;
  int Chan = CHAN_VOICE;

  // handle the different player death screams
  if ((Level.bFallingDamage || Level.bOldFallingDamage || Level.bStrifeFallingDamage) &&
      Velocity.z <= -39.0*35.0 && IsSoundPresent('*splat'))
  {
    // falling splat
    Sound = '*splat';
    Chan = CHAN_BODY;
  }
  if (!Sound && Special1 < 10 && IsSoundPresent('*wimpydeath')) {
    // wimpy death sound
    Sound = '*wimpydeath';
  }
  if (!Sound && Health <= -50) {
    if (Health > -100 && IsSoundPresent('*crazydeath')) {
      // crazy death sound
      Sound = '*crazydeath';
    }
    if (!Sound && IsSoundPresent('*xdeath')) {
      // extreme death sound
      Sound = '*xdeath';
    }
    if (!Sound && IsSoundPresent('*gibbed')) {
      // extreme death sound
      Sound = '*gibbed';
      Chan = CHAN_BODY;
    }
  }
  if (!Sound) {
    // normal death sound
    Sound = '*death';
  }

  PlaySound(Sound, Chan);
}
