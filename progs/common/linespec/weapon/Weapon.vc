//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2022 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class Weapon : Inventory abstract;

const float LOWERSPEED = 6.0;
const float RAISESPEED = 6.0;

const float WEAPONBOTTOM = 128.0;
const float WEAPONTOP    = 32.0;

enum {
  FIRE_Primary,
  FIRE_Secondary,
  // special value for CheckAmmo
  FIRE_Either,
}

// type of ammo weapon needs to operate
class!Ammo AmmoType1;
class!Ammo AmmoType2;
// how much ammo to give when picked up
int AmmoGive1;
int AmmoGive2;
// how much ammo used in each shot
int AmmoUse1;
int AmmoUse2;
// weapon given together with current one
class!Weapon SisterWeaponType;

name UpSound;
name ReadySound;


int SlotNumber = -1; // weapon slot this weapon is placed in
int SelectionOrder; // weapons with lower values have greater priority
/*int*/float SlotPosition = 0.5; // position this weapon has in each slot; default is middle
float Kickback; // kickback from this weapon

float PSpriteSY; // y-adjustment value for full screen

int PlayerModelVersion; // for displaying weapon with player model

ubyte FireMode;

float BotCombatDist;

int ReloadCounter;


bool bWimpyWeapon; // switch away if got ammo for more powerfull weapon
bool bNoAutoFire; // don't auto fire
bool bPoweredUp; // this is a powered up version of another weapon
bool bStaff2Kickback; // does Heretic staff level 2 style kickback
bool bAmmoOptional; // ammo is optional
bool bAltAmmoOptional; // ammo is optional for alternate fire
bool bCheckAnyAmmo; // can be selected if have any ammo (either normal, or alt)
bool bReadySndHalf; // half a chance to play the reay sound
bool bDontBob; // don't bob weapon
bool bNoAlert; // doesn't allert monsters when fired
bool bCheatNotWeapon; // not considered a weapon by gimme cheat
bool bAxeBlood; // use axe's blood splatter
bool bNoAutoSwitch; // never switch to this weapon when picked up
bool bPrimaryUsesBoth; // primary fire uses both ammo types
bool bAltUsesBoth; // alternate fire uses both ammo types
bool bGivenAsMorphWeapon; // this weapon was given when owner morphed into something

bool bBotMelee; // it's a melee weapon
bool bBotProjectile; // it's a projectile weapon
bool bBotBfg; // it's a BFG
bool bBotFastShoot; // this is something like Plasma Gun, do not release trigger

bool bBobDisabled; // set in `A_WeaponReady()`

bool bAllowTryPickupWeaponState;
bool bAllowPickupWeaponState;
bool bAllowTryPickupWeaponForAmmoState;
bool bAllowPickupWeaponForAmmoState;

// this is used to freeze bobbing when firing
// no need to save this
transient bool bBobFrozen;

// runtime in-inventory references
Ammo Ammo1;
Ammo Ammo2;
Weapon SisterWeapon;

float FOVScale;


replication {
  reliable if (Role == ROLE_Authority && bNetOwner)
    Ammo1, Ammo2;
}


//==========================================================================
//
//  IsExtraBDWWeapon
//
//==========================================================================
static final bool IsExtraBDWWeapon (class!Weapon wcls) {
  if (!wcls) return false;
  return (string(GetClassName(wcls)).strStartsWith("K8BDW_") /*|| string(GetClassName(wcls)).strStartsWith("BDW_")*/);
}


//==========================================================================
//
//  IsValidExtraBDWWeapon
//
//==========================================================================
private static final bool IsValidExtraBDWWeapon (class!Weapon wcls) {
  if (!wcls) return false;
  if (IsExtraBDWWeapon(wcls)) {
    // check if replacee can be spawned
    class!Weapon rpx = class!Weapon(GetClassReplacee(wcls));
    if (!rpx) return false;
    auto st = FindClassState(rpx, 'Spawn');
    if (!st) return false; // an abstract base class
    if (!AreStateSpritesPresent(st)) return false;
    // check if replacee can be "readied"
    st = FindClassState(rpx, 'Ready');
    if (!st) return false; // an abstract base class
    if (!AreStateSpritesPresent(st)) return false;
  }
  return true;
}


//==========================================================================
//
//  IsUsableWeaponClass
//
//==========================================================================
static final bool IsUsableWeaponClass (class!Weapon wcls) {
  if (!wcls) return false;
  auto st = FindClassState(wcls, 'Ready');
  if (!st) return false; // an abstract base class
  if (!AreStateSpritesPresent(st)) return false;
  return IsValidExtraBDWWeapon(wcls);
}


//==========================================================================
//
//  IsSpawnableWeaponClass
//
//==========================================================================
static final bool IsSpawnableWeaponClass (class!Weapon wcls) {
  if (!wcls) return false;
  auto st = FindClassState(wcls, 'Spawn');
  if (!st) return false; // an abstract base class
  if (!AreStateSpritesPresent(st)) return false;
  return IsValidExtraBDWWeapon(wcls);
}


//==========================================================================
//
//  IsCheckpointSaveAllowed
//
//==========================================================================
override bool IsCheckpointSaveAllowed () {
  if (SisterWeapon) return false;
  return true;
}


//==========================================================================
//
//  QS_Save
//
//==========================================================================
override void QS_Save () {
  ::QS_Save(); // inventory
  QS_PutInt("Ammo1.Amount", (Ammo1 ? Ammo1.Amount : 0));
  QS_PutInt("Ammo1.MaxAmount", (Ammo1 ? Ammo1.MaxAmount : 0));
  QS_PutInt("Ammo2.Amount", (Ammo2 ? Ammo2.Amount : 0));
  QS_PutInt("Ammo2.MaxAmount", (Ammo2 ? Ammo2.MaxAmount : 0));
}


//==========================================================================
//
//  QS_Load
//
//==========================================================================
override void QS_Load () {
  ::QS_Load(); // inventory
  if (Ammo1) {
    Ammo1.Amount = QS_GetInt("Ammo1.Amount");
    Ammo1.MaxAmount = QS_GetInt("Ammo1.MaxAmount");
  }
  if (Ammo2) {
    Ammo2.Amount = QS_GetInt("Ammo2.Amount");
    Ammo2.MaxAmount = QS_GetInt("Ammo2.MaxAmount");
  }
}


//==========================================================================
//
//  Destroyed
//
//==========================================================================
override void Destroyed () {
  if (SisterWeapon) {
    SisterWeapon.SisterWeapon = none;
    SisterWeapon.Destroy();
  }
  ::Destroyed();
}


//==========================================================================
//
//  TryPickup
//
//==========================================================================
override bool TryPickup (EntityEx Toucher) {
  if (!CheckInventoryRestrictions(Toucher)) return false;
  // if sprites for ready state are missing, you can't pick up this weapon
  state ReadyState = FindState('Ready');
  if (ReadyState && AreStateSpritesPresent(ReadyState)) {
    // ok to pickup a weapon
    // call `TryPickupWeapon` state, with player pawn as toucher
    // you can `Fail` it
    // works for Voodoo Dolls too
    if (bAllowTryPickupWeaponState && Toucher.Player && PlayerPawn(Toucher)) {
      state pst = FindState('TryPickupWeapon', Exact:true);
      if (pst) {
        if (!CallStateChain(Toucher, pst)) {
          //printdebug("%C: weapon rejected picking up for `%C`", self, Toucher);
          return false;
        }
      }
    }
    return ::TryPickup(Toucher);
  }
  return false;
}


//==========================================================================
//
//  HandlePickup
//
//==========================================================================
override bool HandlePickup (Inventory Item) {
  if (Item.Class == Class) {
    if (Weapon(Item).PickupForAmmo(self)) Item.bPickupGood = true;
    return true;
  }

  if (Inventory) return Inventory.HandlePickup(Item);
  return false;
}


//==========================================================================
//
//  CreateCopy
//
//==========================================================================
override Inventory CreateCopy (EntityEx Toucher) {
  Weapon Copy = Weapon(::CreateCopy(Toucher));
  if (Copy) {
    Copy.AmmoGive1 = AmmoGive1;
    Copy.AmmoGive2 = AmmoGive2;
  }
  return Copy;
}


//==========================================================================
//
//  CreateTossable
//
//==========================================================================
override Inventory CreateTossable (optional int DropAmount) {
  // drop the weapon that is meant to be placed in a level, i.e. that normally gives you ammo
  if (SisterWeapon && !default.AmmoGive1 && !default.AmmoGive2 &&
      (SisterWeapon.default.AmmoGive1 > 0 || SisterWeapon.default.AmmoGive2 > 0))
  {
    return SisterWeapon.CreateTossable(DropAmount!optional);
  }

  Weapon Copy = Weapon(::CreateTossable(DropAmount!optional));
  if (Copy) {
    // if weapon has a sister weapon, remove it too
    if (SisterWeapon) {
      SisterWeapon.SisterWeapon = none;
      SisterWeapon.Destroy();
    }
    // dropped weapons don't have ammo to prevent cheating
    Copy.AmmoGive1 = 0;
    Copy.AmmoGive2 = 0;
  }
  return Copy;
}


//==========================================================================
//
//  AttachToOwner
//
//==========================================================================
override void AttachToOwner (EntityEx NewOwner, optional Inventory oldInv) {
  ::AttachToOwner(NewOwner, oldInv!optional);

  // set up references to ammo items since they are used a lots
  Ammo1 = AddAmmo(NewOwner, AmmoType1, AmmoGive1);
  Ammo2 = AddAmmo(NewOwner, AmmoType2, AmmoGive2);
  SisterWeapon = AddWeapon(SisterWeaponType);

  // call `PickupWeapon` state, with player pawn as toucher
  // you cannot `Fail` it (it has no effect)
  // works for Voodoo Dolls too
  if (bAllowPickupWeaponState && Owner.Player && PlayerPawn(Owner)) {
    state pst = FindState('PickupWeapon', Exact:true);
    if (pst) CallStateChain(Owner, pst);
  }

  // don't autoswitch when Voodoo Doll picked up a weapon
  if (Owner.Player && PlayerPawn(Owner) && Owner.Player.MO == Owner && !bNoAutoSwitch && GetCvarB('wpn_autoswitch')) {
    // Doom and Strife always switch to a new weapon, Heretic and Hexen
    // only switch to more powerful weapons; always check for ammo
    auto pawn = PlayerPawn(Owner);
    if (!pawn) return;
    auto plr = PlayerEx(pawn.Player);
    if (!plr) return;

    bool doSwitch = LineSpecialGameInfo(Level.Game).bAlwaysSwitchNewWeapon;
    if (!doSwitch) {
      doSwitch = true;
      if (plr.ReadyWeapon) {
        int ReadySlot = -1, ReadyIndex = -1;
        if (pawn.FindWeaponSlot(class!Weapon(plr.ReadyWeapon.Class), out ReadySlot, out ReadyIndex)) {
          int NewSlot = -1, NewIndex = -1;
          if (pawn.FindWeaponSlot(class!Weapon(Class), out NewSlot, out NewIndex)) {
            doSwitch = (NewSlot > ReadySlot) || (NewSlot == ReadySlot && NewIndex > ReadyIndex);
          }
        }
      }
    }

    if (doSwitch && CheckAmmo(FIRE_Either, false)) {
      PlayerEx(Owner.Player).PendingWeapon = self;
    }
  }
}


//==========================================================================
//
//  AddAmmo
//
//  Returns false if the ammo can't be picked up at all
//
//  this is called only from `AttachToOwner()`
//
//==========================================================================
Ammo AddAmmo (EntityEx Toucher, class!Ammo AmmoType, int count) {
  if (!AmmoType) return none;

  if (!bIgnoreSkill) {
    // extra ammo in baby mode and nightmare mode
    count = int(float(count)*Level.World.GetAmmoFactor());
  }
  count = max(0, count); //k8: just in case

  Ammo AmmoItem = Ammo(Toucher.FindInventory(AmmoType));
  if (!AmmoItem) {
    AmmoItem = SpawnAmmoType(self, AmmoType);
    if (!AmmoItem) return none;
    Ammo a2 = Ammo(Toucher.FindInventory(class!Ammo(AmmoItem.Class)));
    if (a2) {
      delete AmmoItem;
      AmmoItem = a2;
    } else {
      AmmoItem.AttachToOwner(Toucher);
      AmmoItem.Amount = 0;
    }
  }

  int maxam = AmmoItem.k8GetAmmoKingMax();
  AmmoItem.Amount = clamp(AmmoItem.Amount+count, 0, maxam);

  return AmmoItem;
}


//==========================================================================
//
//  AddExistingAmmo
//
//==========================================================================
bool AddExistingAmmo (Ammo AmmoItem, int count, optional bool checkOnly) {
  int oldammo;

  if (!AmmoItem || count <= 0) return false;
  if (AmmoItem.Amount == AmmoItem./*MaxAmount*/k8GetAmmoKingMax()) return false;

  if (!bIgnoreSkill) {
    // extra ammo in baby mode and nightmare mode
    count = int(float(count)*Level.World.GetAmmoFactor());
  }

  oldammo = AmmoItem.Amount;
  AmmoItem.Amount += count;
  if (AmmoItem.Amount > AmmoItem./*MaxAmount*/k8GetAmmoKingMax()) {
    AmmoItem.Amount = AmmoItem./*MaxAmount*/k8GetAmmoKingMax();
  }

  if (checkOnly) {
    if (AmmoItem.Amount == oldammo) return false;
    AmmoItem.Amount = oldammo;
    return true;
  }

  if (oldammo <= 0 && AmmoItem.Owner.Player) {
    PlayerEx(AmmoItem.Owner.Player).GotAmmo(AmmoItem);
  }

  return true;
}


//==========================================================================
//
//  AddWeapon
//
//==========================================================================
Weapon AddWeapon (class!Weapon WeaponType) {
  if (!WeaponType) return none;
  Weapon Wpn = Weapon(EntityEx(Owner).FindInventory(WeaponType/*, disableReplacement:true*/));
  if (!Wpn) {
    Wpn = SpawnEntityChecked(class!Weapon, WeaponType, default, default, default/*, false*/);
    if (Wpn) Wpn.AttachToOwner(EntityEx(Owner));
  }
  return Wpn;
}


//==========================================================================
//
//  CanPickupForAmmo
//
//==========================================================================
bool CanPickupForAmmo (Weapon ExistingWeapon) {
  // leave placed weapons forever on net games
  if (!bDropped && ShouldStay()) return false;
  bool gaveammo = false;
  if (AddExistingAmmo(ExistingWeapon.Ammo1, AmmoGive1, checkOnly:true)) gaveammo = true;
  if (AddExistingAmmo(ExistingWeapon.Ammo2, AmmoGive2, checkOnly:true)) gaveammo = true;
  return gaveammo;
}


//==========================================================================
//
//  PickupForAmmo
//
//==========================================================================
bool PickupForAmmo (Weapon ExistingWeapon) {
  // leave placed weapons forever on net games
  if (!bDropped && ShouldStay()) return false;
  bool gaveammo = false;
  state pst;
  // call `PickupWeapon` state, with player pawn as toucher
  // you cannot `Fail` it (it has no effect)
  // works for Voodoo Dolls too
  if (bAllowTryPickupWeaponForAmmoState || bAllowPickupWeaponForAmmoState) {
    PlayerPawn pwn = PlayerPawn(ExistingWeapon.Owner);
    if (pwn && pwn.Player) {
      if (bAllowTryPickupWeaponForAmmoState) {
        pst = FindState('TryPickupWeaponForAmmo', Exact:true);
        if (pst) {
          if (!CallStateChain(pwn, pst)) return false;
        }
      }
      if (bAllowPickupWeaponForAmmoState) {
        pst = FindState('PickupWeaponForAmmo', Exact:true);
        if (pst) {
          if (!CallStateChain(pwn, pst)) return true;
        }
      }
    }
  }
  if (AddExistingAmmo(ExistingWeapon.Ammo1, AmmoGive1)) gaveammo = true;
  if (AddExistingAmmo(ExistingWeapon.Ammo2, AmmoGive2)) gaveammo = true;
  return gaveammo;
}


//==========================================================================
//
//  ShouldStay
//
//==========================================================================
override bool ShouldStay () {
  // if we are in coop, always let weapon stay, why not
  if (Level.Game.netgame && !Level.Game.deathmatch) return true;
  return LineSpecialGameInfo(Level.Game).bWeaponsStay;
}


//==========================================================================
//
//  ShouldRespawn
//
//==========================================================================
override bool ShouldRespawn () {
  // coop?
  if (Level.Game.netgame && !Level.Game.deathmatch) return true;
  return ::ShouldRespawn();
}


//==========================================================================
//
//  GetRespawnTime
//
//  returns item respawn time, in seconds
//  called from `Hide()`
//
//==========================================================================
override float GetRespawnTime (bool asRavenPickup) {
  // coop?
  if (Level.Game.netgame && !Level.Game.deathmatch) return 2*60; // two minutes
  return ::GetRespawnTime(asRavenPickup);
}


//==========================================================================
//
//  Use
//
//  Using a weapon makes it active.
//
//==========================================================================
override bool Use (bool Pickup) {
  // can't directly use powered up weapon
  if (bPoweredUp) return false;
  // see which weapon to use
  Weapon UseWpn = self;
  if (SisterWeapon && SisterWeapon.bPoweredUp &&
      EntityEx(Owner).FindInventory(PowerWeaponLevel2))
  {
    UseWpn = SisterWeapon;
  }
  if (Owner.bIsPlayer && PlayerEx(Owner.Player).ReadyWeapon != UseWpn) {
    PlayerEx(Owner.Player).PendingWeapon = UseWpn;
  }
  return false;
}


//==========================================================================
//
//  GetUpState
//
//==========================================================================
state GetUpState () {
  return FindState('Select');
}


//==========================================================================
//
//  GetDownState
//
//==========================================================================
state GetDownState () {
  return FindState('Deselect');
}


//==========================================================================
//
//  GetReadyState
//
//==========================================================================
state GetReadyState () {
  return FindState('Ready');
}


//==========================================================================
//
//  GetInstaReadyState
//
//==========================================================================
state GetInstaReadyState () {
  state res = FindState('ReadyForceInstant');
  return (res ? res : FindState('Ready'));
}


//==========================================================================
//
//  GetAttackState
//
//==========================================================================
state GetAttackState (bool Hold) {
  state S = none;
  if (Hold) S = FindState('Hold');
  if (!S) S = FindState('Fire');
  return S;
}


//==========================================================================
//
//  GetAltAttackState
//
//==========================================================================
state GetAltAttackState (bool Hold) {
  state S = none;
  if (Hold) S = FindState('AltHold');
  if (!S) S = FindState('AltFire');
  return S;
}


//==========================================================================
//
//  GetReloadState
//
//==========================================================================
state GetReloadState () {
  return FindState('Reload');
}


//==========================================================================
//
//  GetZoomState
//
//==========================================================================
state GetZoomState () {
  return FindState('Zoom');
}


//==========================================================================
//
//  CheckSpecialWeaponAmmo
//
//  Special hack for BDW Assault Rifle and BDW Shotgun
//
//==========================================================================
bool CheckSpecialWeaponAmmo (ubyte Mode) {
  name ammoName;
       if (GetClassName(Class) == 'BDW_Rifle') ammoName = 'BDW_RifleAmmo';
  else if (GetClassName(Class) == 'BDW_Shotgun') ammoName = 'BDW_ShotgunAmmo';
  else return true;

  for (Inventory a2 = EntityEx(Owner).Inventory; a2; a2 = a2.Inventory) {
    if (GetClassName(a2.Class) == ammoName) {
      //print("%C; AmountX=%s; AmmoUse1=%s", self, a2.Amount, AmmoUse1);
      return (a2.Amount > 0);
    }
  }

  return false;
}


//==========================================================================
//
//  CheckAmmoIfZero
//
//  Special hack for BDW Assault Rifle and BDW Shotgun
//
//==========================================================================
bool CheckAmmoAmount (ubyte Mode, optional bool RequireAmmo) {
  if (Mode == FIRE_Primary) {
    if (!RequireAmmo && bAmmoOptional) return true; //k8: is this right?
    //printdebug("%C: AmmoType1=%C; Ammo1=%C", self, AmmoType1, Ammo1);
    int Amount1 = (Ammo1 ? Ammo1.Amount : 0);
    //print("%C; Amount1=%s; AmmoUse1=%s", self, Amount1, AmmoUse1);
    if (Amount1 == 0 && AmmoUse1 == 0) return CheckSpecialWeaponAmmo(Mode);
    return (Amount1 >= AmmoUse1);
  } else {
    if (!RequireAmmo && bAltAmmoOptional) return true; //k8: is this right?
    int Amount2 = (Ammo2 ? Ammo2.Amount : 0);
    if (Amount2 == 0 && AmmoUse2 == 0) return CheckSpecialWeaponAmmo(Mode);
    return (Amount2 >= AmmoUse2);
  }
}


//==========================================================================
//
//  CheckAmmo
//
//  Returns true if there is enough mana to shoot.  If not, selects the
//  next weapon to use.
//
//==========================================================================
bool CheckAmmo (ubyte Mode, bool AutoSwitch, optional bool RequireAmmo) {
  //if (!Owner) printerror("%C: this weapon has no owner! (next inv is %C)", self, Inventory);
  // search for infinite ammo power; any subclass will do
  if (EntityEx(Owner).FindInfiniteAmmoPowerup()) {
    // don't waste ammo if we have an infinite ammo powerup
    return true;
  }

  if (Mode == FIRE_Either) {
    if (CheckAmmo(FIRE_Primary, false, RequireAmmo!optional) ||
        CheckAmmo(FIRE_Secondary, false, RequireAmmo!optional))
    {
      return true;
    }
  } else {
    if (!RequireAmmo && bAmmoOptional) return true;

    bool Good = true;
    if (bCheckAnyAmmo) {
      // for any fire mode, any ammo will do
      if (!CheckAmmoAmount(FIRE_Primary, RequireAmmo!optional) && !CheckAmmoAmount(FIRE_Secondary, RequireAmmo!optional)) Good = false;
    } else {
      if ((Mode == FIRE_Primary || bAltUsesBoth) && !CheckAmmoAmount(FIRE_Primary, RequireAmmo!optional)) Good = false;
      if (Good && (Mode == FIRE_Secondary || bPrimaryUsesBoth) && !CheckAmmoAmount(FIRE_Secondary, RequireAmmo!optional)) Good = false;
      if (Good && Mode == FIRE_Secondary && !FindState('AltFire')) Good = false;
    }

    /*
    int Amount1 = (Ammo1 ? Ammo1.Amount : 0);
    int Amount2 = (Ammo2 ? Ammo2.Amount : 0);

    if (bCheckAnyAmmo) {
      // for any fire mode, any ammo will do
      if ((Mode == FIRE_Primary || Mode == FIRE_Secondary) &&
          Amount1 < AmmoUse1 && Amount2 < AmmoUse2)
      {
        //HACK
        if ((Amount1 == 0 && AmmoUse1 == 0) && (Amount2 == 0 && AmmoUse2 == 0)) {
          // more checks!
          Good = false;
        } else {
          Good = false;
        }
      }
    } else {
      //HACK!
      if ((Mode == FIRE_Primary || bAltUsesBoth) && Amount1 < AmmoUse1) {
        Good = false;
      } else {
        //HACK
        if (Amount1 == 0 && AmmoUse1 == 0) {
          // more checks!
          Good = false;
        }
      }
      print("%C is %B (both=%B; Amount1=%s; AmmoUse1=%s)", self, Good, bAltUsesBoth, Amount1, AmmoUse1);
      //HACK!
      if (Good && (Mode == FIRE_Secondary || bPrimaryUsesBoth) && Amount2 < AmmoUse2) {
        //HACK
        if (Amount2 == 0 && AmmoUse2 == 0) {
          // more checks!
          Good = false;
        } else {
          Good = false;
        }
      }
      // if weapon has no secondary fire, there's never enough ammo for it
      if (Good && Mode == FIRE_Secondary && !FindState('AltFire')) Good = false;
    }
    */

    if (Good) return true;
  }

  if (!AutoSwitch) return false;

  // out of ammo, pick a weapon to change to
  Weapon Best = PlayerEx(Owner.Player).BestWeapon();

  PlayerEx(Owner.Player).PendingWeapon = Best;
  Owner.Player.SetViewObject(self);
  Owner.Player.SetViewState(PS_WEAPON, GetDownState());
  return false;
}


//==========================================================================
//
//  DepleteAmmo
//
//==========================================================================
bool DepleteAmmo (ubyte Mode, optional bool CheckEnough, optional int DehackedUse) {
  // search for infinite ammo power; any subclass will do
  if (EntityEx(Owner).FindInfiniteAmmoPowerup()) {
    // don't waste ammo if we have an infinite ammo powerup
    return true;
  }

  // default parameter values
  if (!specified_CheckEnough) CheckEnough = true;
  if (!specified_DehackedUse) DehackedUse = AmmoUse1;

  if (CheckEnough && !CheckAmmo(Mode, false)) return false;

  /*
  print("DepleteAmmo0(%C): primary=%s, secondary=%s, Ammo1=%C(%d), Ammo2=%C(%d)", self,
    (Mode == FIRE_Primary ? "tan" : "ona"), (Mode == FIRE_Secondary ? "tan" : "ona"),
    Ammo1, (Ammo1 ? Ammo1.Amount : -666),
    Ammo2, (Ammo2 ? Ammo2.Amount : -666));
  */

  if ((Mode == FIRE_Primary || bAltUsesBoth) && Ammo1) {
    if (LineSpecialGameInfo(Level.Game).bDehacked) {
      Ammo1.Amount -= DehackedUse;
    } else {
      Ammo1.Amount -= AmmoUse1;
    }
  }
  if ((Mode == FIRE_Secondary || bPrimaryUsesBoth) && Ammo2) Ammo2.Amount -= AmmoUse2;

  // don't let ammo go below zero
  if (Ammo1 && Ammo1.Amount < 0) Ammo1.Amount = 0;
  if (Ammo2 && Ammo2.Amount < 0) Ammo2.Amount = 0;

  /*
  print("DepleteAmmo1(%C): primary=%s, secondary=%s, Ammo1=%C(%d), Ammo2=%C(%d)", self,
    (Mode == FIRE_Primary ? "tan" : "ona"), (Mode == FIRE_Secondary ? "tan" : "ona"),
    Ammo1, (Ammo1 ? Ammo1.Amount : -666),
    Ammo2, (Ammo2 ? Ammo2.Amount : -666));
  */

  return true;
}


//===========================================================================
//
//  EndPowerup
//
//===========================================================================
void EndPowerup () {
  if (SisterWeapon) {
    PlayerEx(Owner.Player).SetWeapon(SisterWeapon);
    if (SisterWeapon.GetReadyState() != GetReadyState()) {
      SisterWeapon.bBobDisabled = true;
      PlayerEx(Owner.Player).SetViewObject(SisterWeapon);
      PlayerEx(Owner.Player).SetViewState(PS_WEAPON, SisterWeapon.GetReadyState());
    }
  }
}


states {
LightDone:
  SHTG E 0 A_Light0
  Stop
}


defaultproperties {
  PickupSound = 'misc/w_pkup';
}
