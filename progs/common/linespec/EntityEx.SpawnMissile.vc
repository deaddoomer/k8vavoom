//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**    $Id$
//**
//**    Copyright (C) 1999-2006 Jānis Legzdiņš
//**
//**    This program is free software; you can redistribute it and/or
//**  modify it under the terms of the GNU General Public License
//**  as published by the Free Software Foundation; either version 2
//**  of the License, or (at your option) any later version.
//**
//**    This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**************************************************************************

//==========================================================================
//
//	PlayMissileSpawnSound
//
//==========================================================================

final void PlayMissileSpawnSound(EntityEx Source)
{
	if (SightSound)
	{
		if (!bSpawnSoundSource)
		{
			PlaySound(SightSound, CHAN_VOICE);
		}
		else if (Source)
		{
			Source.PlaySound(SightSound, CHAN_WEAPON);
		}
		else
		{
			PlaySound(SightSound, CHAN_WEAPON);
		}
	}
}

//==========================================================================
//
//  CheckMissileSpawn
//
//  Moves the missile forward a bit and possibly explodes it right there.
//
//==========================================================================

final bool CheckMissileSpawn()
{
	if (bRandomise && StateTime > 0.0)
	{
		StateTime -= Random() * 0.1;
		if (StateTime < 1.0 / 35.0)
			StateTime = 1.0 / 35.0;
	}

	// move a little forward so an angle can be computed if it
	// immediately explodes
	UnlinkFromWorld();
	if (Speed > 100.0 * 35.0)
	{
		//	Ultra-fast missile
		Origin += Velocity * 0.0142857143 / 8.0;
	}
	else
	{
		//	Normal missile
		Origin += Velocity * 0.0142857143;
	}
	LinkToWorld();
	tmtrace_t tmtrace;
	if (!TryMoveEx(&tmtrace, Origin, false))
	{
		//	Don't explode ripping missiles that spawn inside another thing.
		if (!tmtrace.BlockingMobj || !bRip ||
			EntityEx(tmtrace.BlockingMobj).bDontRip)
		{
			if (CountsAsKill())
			{
				bCountKill = false;
				Level.TotalKills--;
			}
			//	Don't explode missiles on horizon lines.
			if (tmtrace.BlockingLine && tmtrace.BlockingLine->special == LNSPEC_LineHorizon)
			{
				Destroy();
			}
			else
			{
				ExplodeMissile(EntityEx(tmtrace.BlockingMobj));
			}
			return false;
		}
	}
	return true;
}

//==========================================================================
//
//  RayLineIntersection
//
//==========================================================================

final TVec RayLineIntersection (TVec rayO, TVec rayE, TVec vv1, TVec vv2) {
  TVec rayDir = rayE-rayO;
  rayDir.z = 0; // not interested
  //rayDir = Normalise(rayDir);
  float den = (vv2.y-vv1.y)*rayDir.x-(vv2.x-vv1.x)*rayDir.y;
  if (fabs(den) < 0.01) return rayO;
  float e = rayO.y-vv1.y;
  float f = rayO.x-vv1.x;
  float invden = 1.0/den;
  float ua = ((vv2.x-vv1.x)*e-(vv2.y-vv1.y)*f)*invden;
  if (ua >= 0 && ua <= 1) {
    float ub = (rayDir.x*e-rayDir.y*f)*invden;
    if (ua != 0 || ub != 0) {
      TVec res;
      res.x = rayO.x+ua*rayDir.x;
      res.y = rayO.y+ua*rayDir.y;
      res.z = rayO.z;
      return res;
    }
  }
  return rayO;
}

//==========================================================================
//
//  ExplodeMissile
//
//==========================================================================

final bool ExplodeMissile(EntityEx Victim, optional line_t *line)
{
	if (bExploCount)
	{
		if (++Special2 < Special1)
		{
			return false;
		}
	}

	//k8:TODO: this should be strictly client-side
	if (line && DecalName) {
		if (GetCvarB('k8ExplodeMissleTest')) print("MISSILE DECAL: <%s> (side:%d:%d) vel:(%f,%f,%f)", NameToStr(DecalName), PointOnPlaneSide(Origin, line), PointOnPlaneSide(Origin-Velocity, line), Velocity.x, Velocity.y, Velocity.z);
		//TVec v1, v2;
		TVec dir = Normalise(Velocity);
		trsplat_t tr;
		if (TraceSplat(Origin-dir*2, dir, 64.0, &tr)) {
			if (GetCvarB('k8ExplodeMissleTest')) {
				auto side = PointOnPlaneSide(Origin-Velocity, line); // move out a little, just in case
				sector_t *sec = (side ? line->backsector : line->frontsector);
				print("  MISSILE SPLAT: side=%d; org=(%f,%f,%f); hit=(%f,%f,%f); sameline:%s; samesec:%s", tr.side, tr.org.x, tr.org.y, tr.org.z, tr.hit.x, tr.hit.y, tr.hit.z, (tr.line == line ? "tan" : "ona"), (tr.sec == sec ? "tan" : "ona"));
			}
			//if (GetCvarB('k8ExplodeMissleTest')) print("    itt=(%f,%f,%f)", itt.x, itt.y, itt.z);
			SpawnDecal(tr.hit, DecalName, tr.sec, tr.line);
		} else {
			if (GetCvarB('k8ExplodeMissleTest')) print("  MISSILE SPLAT TRACER FAILED!");
		}
		/*
		if (line->frontsector && line->backsector) {
			//!auto side = PointOnPlaneSide(Origin-Velocity, line); // move out a little, just in case
			//!if (side) { v1 = *line->v1; v2 = *line->v2; } else { v1 = *line->v2; v2 = *line->v1; }
			//!sector_t *sec = (side ? line->backsector : line->frontsector);
			//!TVec itt = RayLineIntersection(Origin-Velocity*2, Origin+Velocity*4, v1, v2);
			//!itt.z = Origin.z;
			if (TraceSplat(Origin-dir*2, dir, 64.0, &tr)) {
				if (GetCvarB('k8ExplodeMissleTest')) print("  MISSILE SPLAT: side=%d; org=(%f,%f,%f); hit=(%f,%f,%f); sameline:%s; samesec:%s", tr.side, tr.org.x, tr.org.y, tr.org.z, tr.hit.x, tr.hit.y, tr.hit.z, (tr.line == line ? "tan" : "ona"), (tr.sec == sec ? "tan" : "ona"));
				if (GetCvarB('k8ExplodeMissleTest')) print("    itt=(%f,%f,%f)", itt.x, itt.y, itt.z);
				SpawnDecal(tr.hit, DecalName, tr.sec, tr.line);
			} else {
				if (GetCvarB('k8ExplodeMissleTest')) print("  MISSILE SPLAT TRACER FAILED!");
			}
			//!if (GetCvarB('k8ExplodeMissleTest')) print("  ISECT(%d)=(%f,%f,%f)  orig=(%f,%f,%f)", side, itt.x, itt.y, itt.z, Origin.x, Origin.y, Origin.z);
			//!SpawnDecal(itt, DecalName, (side ? line->backsector : line->frontsector), line);
		} else {
			if (line->frontsector) {
				//!v1 = *line->v1; v2 = *line->v2;
				//!TVec itt = RayLineIntersection(Origin-Velocity*2, Origin+Velocity*4, v1, v2);
				//!itt.z = Origin.z;
				//!if (GetCvarB('k8ExplodeMissleTest')) print("  XSECT(0)=(%f,%f,%f)  orig=(%f,%f,%f)", itt.x, itt.y, itt.z, Origin.x, Origin.y, Origin.z);
				//!SpawnDecal(itt, DecalName, line->frontsector, line);
			}
			if (line->backsector) {
				//!v1 = *line->v2; v2 = *line->v1;
				//!TVec itt = RayLineIntersection(Origin-Velocity*2, Origin+Velocity*4, v1, v2);
				//!itt.z = Origin.z;
				//!if (GetCvarB('k8ExplodeMissleTest')) print("  XSECT(1)=(%f,%f,%f)  orig=(%f,%f,%f)", itt.x, itt.y, itt.z, Origin.x, Origin.y, Origin.z);
				//!SpawnDecal(itt, DecalName, line->backsector, line);
			}
		}
		*/
	} else {
		if (GetCvarB('k8ExplodeMissleTest')) {
			if (Victim) print("MISSILE VICTIM: <%s>", NameToStr(GetClassName(Victim.Class)));
			else if (DecalName) {
				print("MISSILE DECAL, BUT NO LINE: <%s>", NameToStr(DecalName));
				print("%p", line->frontsector);
			}
		}
	}

	Velocity = vector(0.0, 0.0, 0.0);

	state ExplState = none;
	if (Victim && (Victim.bShootable || Victim.bCorpse))
	{
		if (Victim.bNoBlood)
		{
			ExplState = FindState('Crash');
		}
		if (!ExplState)
		{
			ExplState = FindState('Death', 'Extreme');
		}
	}
	if (!ExplState)
	{
		ExplState = FindState('Death');
	}

	if (!SetState(ExplState))
	{
		return false;
	}

	if (bDehExplosion)
	{
		if (LineSpecialGameInfo(Level.Game).DehExplosionStyle == 255)
		{
			//	Explosion style is controlled by a cvar.
			if (GetCvar('addrocketexplosion'))
			{
				RenderStyle = STYLE_Add;
				Alpha = 1.0;
			}
			else
			{
				RenderStyle = STYLE_Translucent;
				Alpha = 0.666;
			}
		}
		else
		{
			RenderStyle = LineSpecialGameInfo(Level.Game).DehExplosionStyle;
			Alpha = LineSpecialGameInfo(Level.Game).DehExplosionAlpha;
		}
	}

	if (bRandomise)
	{
		StateTime -= Random() * 0.1;
		if (StateTime < 1.0 / 35.0)
			StateTime = 1.0 / 35.0;
	}

	bMissile = false;

	if (DeathSound)
	{
		PlaySound(DeathSound, CHAN_VOICE, 1.0, bFullVolDeath ? ATTN_NONE :
			ATTN_NORMAL);
	}

	if (!bLeaveTrail && bDynamicLight)
	{
		SendExplosion(DLightColour, DLightRadius, Origin);
		bDynamicLight = false;
	}
	bLeaveTrail = false;
	return true;
}

//==========================================================================
//
//  SpawnMissile
//
//  Returns NULL if the missile exploded immediately, otherwise returns
// a Actor reference to the missile.
//
//==========================================================================

final EntityEx SpawnMissile(EntityEx dest, class<EntityEx> type,
	optional float SpawnHeight)
{
	EntityEx A;
	TVec org;
	TVec dir;

	org = Origin;
	if (specified_SpawnHeight)
	{
		org.z += SpawnHeight;
	}
	else if (type.default.bIgnoreFloorStep)
	{
		org.z = ONFLOORZ + FloorClip;
	}
	else if (type.default.bIgnoreCeilingStep)
	{
		org.z = ONCEILINGZ;
	}
	else
	{
		org.z += 32.0;
	}
	if (org.z != ONFLOORZ)
	{
		org.z -= FloorClip;
	}
	A = Spawn(type, org);

	A.PlayMissileSpawnSound(self);

	A.Target = self;	// where it came from

	if (dest)
	{
		dir = dest.Origin - Origin;
		if (dest.bShadow)
		{
			// Invisible target
			VectorRotateAroundZ(&dir, (Random() - Random()) * 45.0 / 2.0);
		}
	}
	else
	{
		dir = Velocity;
	}
	dir = Normalise(dir);
	A.Velocity = dir * A.Speed;
	// Floor and ceiling huggers should never have a vertical component to their velocity
	if (A.bIgnoreCeilingStep || A.bIgnoreFloorStep)
	{
		A.Velocity.z = 0.0;
	}
	// Adjust the trajectory if the missile will go over the target's head.
	else if (dest && Origin.z >= dest.Origin.z && Origin.z <= dest.Origin.z + dest.Height && A.Origin.z >= dest.Origin.z + dest.Height)
	{
		float dist = A.DistTo(dest);

		dir.z += (dest.Origin.z + dest.Height - A.Origin.z) / dist;
		dir = Normalise(dir);

		A.Velocity.z = dir.z * A.Speed;
	}
	VectorAngles(&dir, &A.Angles);

	return A.CheckMissileSpawn() ? A : none;
}

//==========================================================================
//
//  SpawnMissileXYZ
//
//  Returns NULL if the missile exploded immediately, otherwise returns
// a Actor reference to the missile.
//
//==========================================================================

final EntityEx SpawnMissileXYZ(TVec org, EntityEx dest, class<EntityEx> type)
{
	TVec		dir;
	EntityEx	A;

	if (org.z != ONFLOORZ)
	{
		org.z -= FloorClip;
	}
	A = Spawn(type, org);
	A.PlayMissileSpawnSound(self);
	A.Target = self;	// Originator
	dir = dest.Origin - Origin;
	if (dest.bShadow)
	{
		// Invisible target
		VectorRotateAroundZ(&dir, (Random() - Random()) * 45.0);
	}
	dir = Normalise(dir);
	A.Velocity = dir * A.Speed;
	// Floor and ceiling huggers should never have a vertical component to their velocity
	if (A.bIgnoreCeilingStep || A.bIgnoreFloorStep)
	{
		A.Velocity.z = 0.0;
	}
	// Adjust the trajectory if the missile will go over the target's head.
	else if (dest && Origin.z >= dest.Origin.z && Origin.z <= dest.Origin.z + dest.Height && A.Origin.z >= dest.Origin.z + dest.Height)
	{
		float dist = A.DistTo(dest);
		dir.z += (dest.Origin.z + dest.Height - A.Origin.z) / dist;
		dir = Normalise(dir);

		A.Velocity.z = dir.z * A.Speed;
	}
	VectorAngles(&dir, &A.Angles);
	return A.CheckMissileSpawn() ? A : none;
}

//==========================================================================
//
//  SpawnMissileZAimed
//
//==========================================================================

final EntityEx SpawnMissileZAimed(float z, EntityEx dest, class<EntityEx> type)
{
	EntityEx	A;
	TVec		dir;
	TVec        org;
	float		dist;

	org = Origin + vector(0.0, 0.0, z != ONFLOORZ ? z - FloorClip : z);

	A = Spawn(type, org);

	A.PlayMissileSpawnSound(self);

	A.Target = self;	// where it came from

	dist = DistTo2(dest);
	dir.x = cos(Angles.yaw);
	dir.y = sin(Angles.yaw);
	dir.z = dist != 0.0 ? (dest.Origin.z - Origin.z) / dist : A.Speed;
	// fuzzy player
	if (dest.bShadow)
	{
		VectorRotateAroundZ(&dir, (Random() - Random()) * 45.0 / 2.0);
	}
	dir = Normalise(dir);
	A.Velocity = dir * A.Speed;
	// Floor and ceiling huggers should never have a vertical component to their velocity
	if (A.bIgnoreCeilingStep || A.bIgnoreFloorStep)
	{
		A.Velocity.z = 0.0;
	}
	// Adjust the trajectory if the missile will go over the target's head.
	else if (dest && Origin.z >= dest.Origin.z && Origin.z <= dest.Origin.z + dest.Height && A.Origin.z >= dest.Origin.z + dest.Height)
	{
		float dist2 = A.DistTo(dest);
		dir.z += (dest.Origin.z + dest.Height - A.Origin.z) / dist2;
		dir = Normalise(dir);

		A.Velocity.z = dir.z * A.Speed;
	}
	VectorAngles(&dir, &A.Angles);

	return A.CheckMissileSpawn() ? A : none;
}

//==========================================================================
//
//  SpawnMissileAngles
//
//  Returns NULL if the missile exploded immediately, otherwise returns
// a Actor reference to the missile.
//
//==========================================================================

final EntityEx SpawnMissileAngles(class<EntityEx> type, float yaw, float pitch,
	optional float SpawnHeight)
{
	TVec		org;
	EntityEx	A;
	TVec		dir;

	org = Origin;
	if (specified_SpawnHeight)
	{
		org.z += SpawnHeight;
	}
	else if (type.default.bIgnoreFloorStep)
	{
		org.z = ONFLOORZ + FloorClip;
	}
	else if (type.default.bIgnoreCeilingStep)
	{
		org.z = ONCEILINGZ;
	}
	else
	{
		org.z += 32.0;
	}
	if (org.z != ONFLOORZ)
	{
		org.z -= FloorClip;
	}
	A = Spawn(type, org);
	A.PlayMissileSpawnSound(self);
	A.Target = self;	// Originator
	A.Angles.yaw = yaw;
	A.Angles.pitch = pitch;
	AngleVector(&A.Angles, &dir);
	A.Velocity = A.Speed * dir;
	// Floor and ceiling huggers should never have a vertical component to their velocity
	if (A.bIgnoreCeilingStep || A.bIgnoreFloorStep)
	{
		A.Velocity.z = 0.0;
	}
	return A.CheckMissileSpawn() ? A : none;
}

//==========================================================================
//
//  SpawnMissileAngle
//
//  Returns NULL if the missile exploded immediately, otherwise returns
// a Actor reference to the missile.
//
//==========================================================================

final EntityEx SpawnMissileAngle(class<EntityEx> type, float angle,
	float momz, optional float SpawnHeight)
{
	TVec org;
	EntityEx mo;

	org = Origin;
	if (specified_SpawnHeight)
	{
		org.z += SpawnHeight;
	}
	else if (type.default.bIgnoreFloorStep)
	{
		// Minotaur floor fire missile
		org.z = ONFLOORZ + FloorClip;
	}
	else if (type.default.bIgnoreCeilingStep)
	{
		org.z = ONCEILINGZ;
	}
	else
	{
		org.z += 32.0;
	}
	if (org.z != ONFLOORZ)
	{
		org.z -= FloorClip;
	}
	mo = Spawn(type, org);
	mo.PlayMissileSpawnSound(self);
	mo.Target = self;	// Originator
	mo.Angles.yaw = angle;
	mo.Velocity.x = mo.Speed * cos(angle);
	mo.Velocity.y = mo.Speed * sin(angle);
	mo.Velocity.z = momz;
	return mo.CheckMissileSpawn() ? mo : none;
}

//==========================================================================
//
//  SpawnMissileAngleSpeed
//
//  Returns NULL if the missile exploded immediately, otherwise returns
// a Actor reference to the missile.
//
//==========================================================================

final EntityEx SpawnMissileAngleSpeed(class<EntityEx> type, float angle,
	float momz, float speed)
{
	TVec org;
	EntityEx mo;

	org = Origin;
	if (org.z != ONFLOORZ)
	{
		org.z -= FloorClip;
	}
	mo = Spawn(type, org);
	mo.Tracer = self;	// Originator
	mo.Angles.yaw = angle;
	mo.Velocity.x = speed * cos(angle);
	mo.Velocity.y = speed * sin(angle);
	mo.Velocity.z = momz;
	mo.PlayMissileSpawnSound(self);
	return mo.CheckMissileSpawn() ? mo : none;
}

//==========================================================================
//
//	SpawnSubMissile
//
//==========================================================================

final EntityEx SpawnSubMissile(class<EntityEx> type, EntityEx AInstigator)
{
	EntityEx	other;
	TVec		dir;

	other = Spawn(type, Origin);
	if (!other)
	{
		return none;
	}

	other.Target = AInstigator;
	other.Angles = Angles;

	other.Velocity.x = other.Speed * cos(Angles.yaw);
	other.Velocity.y = other.Speed * sin(Angles.yaw);
	AimLineAttack(dir, Angles, 1024.0);
	other.Velocity.z = other.Speed * dir.z;
	other.CheckMissileSpawn();
	return other;
}

//==========================================================================
//
//  SpawnPlayerMissile
//
//==========================================================================

final EntityEx SpawnPlayerMissile(class<EntityEx> type, optional name decalType)
{
	EntityEx	AimTarget;

	return SpawnPlayerMissileEx(type, false, AimTarget,,, decalType);
}

//==========================================================================
//
//  SpawnPlayerMissileAngle
//
//==========================================================================

final EntityEx SpawnPlayerMissileAngle(class<EntityEx> type, float angle)
{
	EntityEx	AimTarget;

	return SpawnPlayerMissileEx(type, false, AimTarget, angle);
}

//===========================================================================
//
//  SpawnPlayerMissileAngleXYZ
//
//===========================================================================

final EntityEx SpawnPlayerMissileAngleXYZ(TVec org, class<EntityEx> type,
	float angle)
{
	EntityEx	AimTarget;

	return SpawnPlayerMissileEx(type, false, AimTarget, angle, org);
}

//==========================================================================
//
//  SpawnPlayerMissileEx
//
//  Tries to aim at a nearby monster
//
//==========================================================================

final EntityEx SpawnPlayerMissileEx(class<EntityEx> type, bool RetExploded,
	out EntityEx AimTarget, optional float angle, optional TVec SpawnOrg, optional name decalType)
{
	EntityEx	A;
	TVec		dir;
	TVec		org;

	// Try to find a target
	if (specified_angle)
	{
		AimTarget = Aim(dir, 16.0 * 64.0, angle);
	}
	else
	{
		AimTarget = Aim(dir, 16.0 * 64.0);
	}

	org = specified_SpawnOrg ? SpawnOrg : Origin;
	if (type.default.bIgnoreFloorStep)
	{
		org.z = ONFLOORZ;
		dir.z = 0.0;
		dir = Normalise(dir);
	}
	else if (type.default.bIgnoreCeilingStep)
	{
		org.z = ONCEILINGZ;
		dir.z = 0.0;
		dir = Normalise(dir);
	}
	else
	{
		org.z += Height * 0.5 - FloorClip;
		//	Missiles are spawned 4 units lower than hitscan attacks.
		//  Add an extra unit to simulate Doom's original behaviour.
		if (bIsPlayer)
		{
			org.z += PlayerPawn(Player.MO).AttackZOffset - 3.0;
		}
		else
		{
			org.z += 5.0;
		}
	}
	A = Spawn(type, org);
	if (specified_decalType && decalType) {
		print("missile decal set to <%s>", NameToStr(decalType));
		A.DecalName = decalType;
	}

	A.PlayMissileSpawnSound(self);

	A.Target = self;
	A.Velocity = dir * A.Speed;
	// Adjust the trajectory if the missile will go over the target's head.
	if (AimTarget && Origin.z >= AimTarget.Origin.z && Origin.z <= AimTarget.Origin.z + AimTarget.Height && A.Origin.z >= AimTarget.Origin.z + AimTarget.Height)
	{
		float dist = A.DistTo(AimTarget);
		dir.z += (AimTarget.Origin.z + AimTarget.Height - A.Origin.z) / dist;
		dir = Normalise(dir);

		A.Velocity.z = dir.z * A.Speed;
	}
	VectorAngles(&dir, &A.Angles);

	return A.CheckMissileSpawn() || RetExploded ? A : none;
}
