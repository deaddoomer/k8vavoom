//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2022 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class FastProjectile : Actor;

//#define USE_OLD_FASTPROJECTILE_CODE


//==========================================================================
//
//  Tick
//
//  Thinker for the ultra-fast blaster PL2 ripper-spawning missile.
//
//==========================================================================
override void Tick (float DeltaTime) {
  if (Level.bFrozen && ((!bIsPlayer && !bNoTimeFreeze) ||
      (bIsPlayer && !(PlayerEx(Player).Cheats&PlayerEx::CF_TIMEFREEZE))))
  {
    return;
  }

  #ifdef USE_OLD_FASTPROJECTILE_CODE
  int Count = 8;

  //print("%C: velocity=%s", self, Velocity);
  // clamp some values (done by C++ code)
  /*
  Velocity = vclamp(Velocity, -8191*2, 8191*2);
  Origin = vclamp(Origin, -100000, 100000);
  */
  //Speed = fclamp(Speed, -4095, 4095);

  float velxDT = Velocity.x*DeltaTime;
  float velyDT = Velocity.y*DeltaTime;

  if (Radius > 0.0) {
    //print("  0count=%s", Count);
    bool xbad = (velxDT/float(Count) > Radius);
    bool ybad = (velyDT/float(Count) > Radius);
    if (xbad || ybad) {
      // must take smaller steps
      for (;;) {
        // multiply
        int newCount = Count*2;
        bool newxbad = (xbad ? (velxDT/float(newCount) > Radius) : false);
        bool newybad = (ybad ? (velyDT/float(newCount) > Radius) : false);
        if (newxbad || newybad) {
          xbad = newxbad;
          ybad = newybad;
          Count = newCount;
        } else {
          break;
        }
      }
      // several step sizes
      int currStep = Count;
      while (currStep > 1) {
        for (;;) {
          int newCount = Count+currStep;
          bool newxbad = (xbad ? (velxDT/float(newCount) > Radius) : false);
          bool newybad = (ybad ? (velyDT/float(newCount) > Radius) : false);
          if (newxbad || newybad) {
            xbad = newxbad;
            ybad = newybad;
            Count = newCount;
          } else {
            break;
          }
        }
        if (currStep > 3) {
          //print(" NCS: %s (%s); count=%s", currStep, currStep-currStep/3, Count);
          currStep -= currStep/3;
        } else {
          //print(" NCS: %s (%s); count=%s", currStep, currStep-1, Count);
          --currStep;
        }
      }
      // and final one
      while (xbad || ybad) {
        ++Count;
        if (xbad) xbad = (velxDT/float(Count) > Radius);
        if (ybad) ybad = (velyDT/float(Count) > Radius);
      }
      // just a check
      if (velxDT/float(Count) > Radius || velyDT/float(Count) > Radius) FatalError("FastProjectile::Tick step calculation failed");
    }
    //print("  1count=%s", Count);
  }

  // handle movement
  if (Velocity || Origin.z != FloorZ) {
    tmtrace_t tmtrace;
    float xfrac = velxDT/float(Count);
    float yfrac = velyDT/float(Count);
    float zfrac = Velocity.z*DeltaTime/float(Count);
    //printdebug("FAST(%C): xfrac=%s; yfrac=%s; zfrac=%s; org=%s; radius=%s; height=%s", self, xfrac, yfrac, zfrac, Origin, Radius, Height);
    bool changexy = xfrac || yfrac;
    foreach (auto i; 0..Count) {
      if (changexy) {
        //printdebug("FAST(%C): i=%s/%s; xfrac=%s; yfrac=%s; org=%s", self, i, Count-1, xfrac, yfrac, Origin);
        if (!TryMoveEx(out tmtrace, vector(Origin.x+xfrac, Origin.y+yfrac, Origin.z), AllowDropOff:true)) {
          //printdebug("FAST(%C):   failed! mobj=%s; line=%s; Box=%s/%s; fc=%s/%s", self, tmtrace.BlockingMobj, (tmtrace.BlockingLine ? tmtrace.BlockingLine-&XLevel.Lines[0] : -1), Origin.z, Origin.z+Height, FloorZ, CeilingZ);
          // blocked move
          if (tmtrace.BlockingMobj) {
            BlasterHitMobj(tmtrace.BlockingMobj);
          } else {
            // hit the wall
            ExplodeMissile(Victim:none, line:tmtrace.BlockingLine);
          }
          return;
        }
      }
      Origin.z += zfrac;
      if (Origin.z <= FloorZ) {
        // hit the floor
        Origin.z = FloorZ;
        HitFloorType();
        //printdebug("FAST(%C): FLOOR HIT!", self);
        ExplodeMissile(Victim:none);
        return;
      }
      if (Origin.z+Height > CeilingZ) {
        // hit the ceiling
        Origin.z = CeilingZ-Height;
        //printdebug("FAST(%C): CEILING HIT!", self);
        ExplodeMissile(Victim:none);
        return;
      }
      if (changexy) SpawnBlasterEffects();
    }
  }

  #else
  // split move in multiple steps if moving too fast
  // this is slower than the old code, but much more precise
  float xmove = Velocity.x*DeltaTime;
  float ymove = Velocity.y*DeltaTime;

  int Steps = 1;
  float MyMaxStep = fmax(fabs(xmove), fabs(ymove));
  float MaxStep = Radius*0.9;
  if (MaxStep <= 0.0) MaxStep = MAXMOVESTEP;

  if (MyMaxStep > MaxStep) Steps = int(MyMaxStep/MaxStep)+1;

  float xfrac = xmove/float(Steps);
  float yfrac = ymove/float(Steps);
  float zfrac = Velocity.z*DeltaTime/float(Steps);

  //printdebug("%C: vel=%s; xfrac=%s; yfrac=%s; zfrac=%s", self, Velocity, xfrac, yfrac, zfrac);
  if (Velocity || Origin.z != FloorZ) {
    tmtrace_t tmtrace;
    //printdebug("FAST(%C): xfrac=%s; yfrac=%s; zfrac=%s; org=%s; radius=%s; height=%s; xmove=%s; ymove=%s; steps=%s; dest=%s", self, xfrac, yfrac, zfrac, Origin, Radius, Height, xmove, ymove, Steps, Origin+vector(xmove, ymove, Velocity.z*DeltaTime));
    //printdebug("FAST(%C): END:: xfrac=%s; yfrac=%s; zfrac=%s; org=%s", self, xfrac, yfrac, zfrac, Origin+vector(xmove, ymove, Velocity.z*DeltaTime));
    bool changexy = (xfrac || yfrac);
    foreach (auto i; 0..Steps) {
      if (changexy) {
        //printdebug("FAST(%C): i=%s/%s; xfrac=%s; yfrac=%s; org=%s", self, i, Steps-1, xfrac, yfrac, Origin);
        if (!TryMoveEx(out tmtrace, vector(Origin.x+xfrac, Origin.y+yfrac, Origin.z), AllowDropOff:true)) {
          //printdebug("FAST(%C):   failed! mobj=%s; line=%s; Box=%s/%s; fc=%s/%s", self, tmtrace.BlockingMobj, (tmtrace.BlockingLine ? tmtrace.BlockingLine-&XLevel.Lines[0] : -1), Origin.z, Origin.z+Height, FloorZ, CeilingZ);
          // blocked move
          if (tmtrace.BlockingMobj) {
            BlasterHitMobj(tmtrace.BlockingMobj);
          } else {
            // hit the wall
            ExplodeMissile(Victim:none, line:tmtrace.BlockingLine);
          }
          return;
        }
      }
      Origin.z += zfrac;
      if (Origin.z <= FloorZ) {
        // hit the floor
        Origin.z = FloorZ;
        HitFloorType();
        //printdebug("FAST(%C): FLOOR HIT!", self);
        ExplodeMissile(Victim:none);
        return;
      }
      if (Origin.z+Height > CeilingZ) {
        // hit the ceiling
        Origin.z = CeilingZ-Height;
        //printdebug("FAST(%C): CEILING HIT!", self);
        ExplodeMissile(Victim:none);
        return;
      }
      if (changexy) SpawnBlasterEffects();
    }
    //printdebug("FAST(%C): DONE! xfrac=%s; yfrac=%s; zfrac=%s; org=%s", self, xfrac, yfrac, zfrac, Origin);
  }
  #endif

  // cycle through states, calling action functions at transitions
  AdvanceState(DeltaTime);
}


//==========================================================================
//
//  BlasterHitMobj
//
//==========================================================================
bool BlasterHitMobj (Entity Other) {
  return ExplodeMissile(EntityEx(Other));
}


//==========================================================================
//
//  SpawnBlasterEffects
//
//==========================================================================
void SpawnBlasterEffects () {
}


defaultproperties {
  bMissile = true;
  bNoBlockmap = true;
  bDropOff = true;
  bNoGravity = true;
  bNoTeleport = true;
  bActivateImpact = true;
  bActivatePCross = true;
  bBloodSplatter = true;
  bAlwaysTick = true;
}
