//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2023 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class MenuChoiceBase : Widget;

int CursorXOffs;
int CursorXOffsHR; // for right-aligned text
name cvarSession; // if not empty, value will be saved in it, and read back on creation
string customHelpText;

int TextColor = CR_RED;
int TextColorFocus = CR_YELLOW;
int ValueColor = CR_TAN;
int ValueColorFocus = CR_YELLOW;
int TextHeightAdd;

bool HighlightBackground;

name FontName = 'smallfont';


//==========================================================================
//
//  UpdateFontName
//
//==========================================================================
void UpdateFontName () {
  string fname = GetCvarS('ui_font_menuitems');
  if (fname && !FontName.strEquCI(fname)) FontName = name(fname);
}


//==========================================================================
//
//  FixMinimumHeight
//
//==========================================================================
void FixMinimumHeight () {
  UpdateFontName();
  auto oldFont = GetFont();
  scope(exit) SetFont(oldFont);
  SetFont(FontName);
  int height = FontHeight()+TextHeightAdd;
  // hack for default doom font
  if (height == 10 && TextHeightAdd == 2) height = 11;
  if (Height < height) Height = height;
}


//==========================================================================
//
//  FixFractionalValue
//
//  used in sliders to fix fractional values
//
//==========================================================================
static final float FixFractionalValue (float value, float delta) {
  float mult = 1;
  delta = fabs(delta);
  if (!delta) delta = 1;
  while (delta < 1) { mult *= 10; delta *= 10; }
  if (fabs(value*mult-ftrunc(value*mult)) < 0.0001) {
    //print("nvfixT: %s -> %s", value*mult, ftrunc(value*mult));
    value = ftrunc(value*mult)/mult;
  } else if (fabs(value*mult-fround(value*mult)) < 0.0001) {
    //print("nvfixR: %s -> %s", value*mult, fround(value*mult));
    value = fround(value*mult)/mult;
  }
  return value;
}


//==========================================================================
//
//  getHelp
//
//==========================================================================
string getHelp () {
  string res = "";
       if (customHelpText) res = customHelpText;
  else if (cvarSession) res = GetCvarHelp(cvarSession);
  else return "";
  return TranslateString(res);
}


//==========================================================================
//
//  isReadOnly
//
//==========================================================================
final bool isReadOnly () {
  if (!cvarSession) return false;
  return CvarIsReadOnly(cvarSession);
}


//==========================================================================
//
//  FixLatchedColor
//
//==========================================================================
void FixLatchedColor () {
  if (!cvarSession) return;
  if (isReadOnly) {
    TextColor = CR_BLACK;
    TextColorFocus = CR_BLACK;
    Enabled = false;
    Focusable = false;
    return;
  }
  int cf = CvarGetFlags(cvarSession);
  if (cf == -1 || !(cf&CVAR_LATCH)) return;
  if (TextColor != default.TextColor) return;
  if (TextColorFocus != default.TextColorFocus) return;
  TextColor = CR_SAPPHIRE;
  TextColorFocus = CR_ICE;
}


//==========================================================================
//
//  setSavedInt
//
//==========================================================================
final static void setSavedInt (name Name, int val) {
  if (!CvarExists(Name)) {
    CreateCvar(Name, va("%d", val), "___saved_menu__value___", CVAR_HIDDEN);
  } else {
    if (CvarIsReadOnly(Name)) return;
    SetCvar(Name, val);
    int cf = CvarGetFlags(Name);
    if (cf > 0 && (cf&CVAR_LATCH)) CvarUnlatchAll();
  }
}


//==========================================================================
//
//  getSavedInt
//
//==========================================================================
final static int getSavedInt (name Name, int defval) {
  if (!CvarExists(Name)) return defval;
  return GetCvar(Name);
}


//==========================================================================
//
//  setSavedFloat
//
//==========================================================================
final static void setSavedFloat (name Name, float val) {
  if (!CvarExists(Name)) {
    CreateCvarFloat(Name, val, "___saved_menu__value___", CVAR_HIDDEN);
  } else {
    if (CvarIsReadOnly(Name)) return;
    SetCvarF(Name, val);
    int cf = CvarGetFlags(Name);
    if (cf > 0 && (cf&CVAR_LATCH)) CvarUnlatchAll();
  }
}


//==========================================================================
//
//  getSavedFloat
//
//==========================================================================
final static float getSavedFloat (name Name, float defval) {
  if (!CvarExists(Name)) return defval;
  return GetCvarF(Name);
}


//==========================================================================
//
//  SetNewValue
//
//  return `false` if value is unnaceptable
//  return `true` (and possibly clamp value) if it is acceptable
//  acceptable value will be saved in session cvar (if it is set)
//
//==========================================================================
bool SetNewValue (ref float newValue) {
  if (cvarSession) setSavedFloat(cvarSession, newValue);
  return true;
}


//==========================================================================
//
//  SetSessionCvar
//
//==========================================================================
void SetSessionCvar (name Name, optional float defval) {
  cvarSession = Name;
  if (Name) defval = getSavedFloat(Name, defval);
  SetNewValue(ref defval);
  FixLatchedColor();
}


//==========================================================================
//
//  GetClGame
//
//==========================================================================
ClientGameShared GetClGame () {
  auto par = FindParentWithClass(MenuScreen);
  return (par ? par.ClGame : none);
}


//==========================================================================
//
//  DrawBackground
//
//==========================================================================
void DrawBackground () {
  /*
  if (IsFocused) {
    //ShadeRect(0, 0, Width, Height-2, 0.6);
    int hgt = Height;
    if (hgt == 10) hgt -= 2; else hgt -= 1;
    FillRect(0, 0, Width, hgt, RGB(152, 205, 50), 0.6);
  }
  */
}


//==========================================================================
//
//  OnFocusReceived
//
//==========================================================================
override void OnFocusReceived () {
  MenuScreen pp = FindParentWithClass(MenuScreen);
  if (pp) pp.MakeItemSelected(self);
  ::OnFocusReceived();
}


//==========================================================================
//
//  OnEvent
//
//==========================================================================
override bool OnEvent (ref event_t evt) {
  /*
  if (evt.dest == self) {
    printdebug("%C: evt=%s; Parent.Width=%s; Width=%s; Height=%s", self, evt.type, Parent.Width, Width, Height);
  }
  */

  if (evt.type == ev_keydown) {
    /*
    if (evt.keycode == K_MOUSE1 && evt.dest == self) {
      printdebug("%s: mdown!", self);
      SetFocus();
      //evt.eaten = true;
      //return true;
    }
    */
    //printdebug("key=%s (shift=%B)", evt.keycode, evt.bShift);
    if (cvarSession && CvarExists(cvarSession)) {
      if (evt.bShift && evt.keycode == "/") {
        print("option cvar: %s", cvarSession);
        return true;
      }
      // restore default value
      if (evt.keycode == K_BACKSPACE) { // [R]estore
        string s = GetCvarDefault(cvarSession);
        if (s) {
          if (isReadOnly) return true;
          bool err;
          float newValue = atof(s, out err);
          if (!err) {
            //print("KD! %s [%s]", cvarSession, newValue);
            if (SetNewValue(ref newValue)) LocalSound('menu/change');
            return true;
          }
        }
      }
    }
  }

  // "on mouse down"
  if (evt.dest == self && evt.type == ev_keydown && evt.keycode == K_MOUSE1) {
    // focus if we're clicked
    if (evt.x >= 0 && evt.y >= 0 && evt.x < Width && evt.y < Height) {
      if (!IsFocused) SetFocus();
      return true;
    }
  }

  // "on click"
  if (evt.type == ev_click && evt.dest == self) {
    if (evt.keycode == K_MOUSE1) {
      SetFocus();
      return true;
    }
  }

  return ::OnEvent(evt);
}


defaultproperties {
  Focusable = true;
  bUseScissor = true;
  Width = 320;
  //Height = 200;
  //Height = 24;
  Height = 1;
  cvarSession = '';
  CursorXOffsHR = 200;
  HighlightBackground = true;
  TextHeightAdd = 2;
}
