//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2020 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class MenuChoiceEnum : MenuChoice;

struct EnumValue {
  string caption;
  float value;
}

string Text;
bool bLeftAlign;

float Value;
array!EnumValue vallist;

delegate bool ValueChanging (Object Sender, float NewValue);


//==========================================================================
//
//  SetText
//
//==========================================================================
void SetText (string AText) {
  Text = TranslateString(AText);
  FixMinimumHeight();
}


//==========================================================================
//
//  findValue
//
//==========================================================================
int findValue (float val) {
  foreach (auto idx, ref EnumValue v; vallist) {
    if (v.value == val) return idx;
  }
  return -1;
}


//==========================================================================
//
//  SetOrigin
//
//==========================================================================
override void SetOrigin (int x, int y) {
  if (bLeftAlign) {
    SetPos(x, y);
  } else {
    CursorXOffs = /*200*/CursorXOffsHR;
    SetPos(x-CursorXOffs, y);
  }
}


//==========================================================================
//
//  SetNewValue
//
//==========================================================================
override bool SetNewValue (ref float newValue) {
  if (ValueChanging) {
    if (!ValueChanging(self, newValue)) return false;
  }
  if (!::SetNewValue(ref newValue)) return false;
  Value = newValue;
  return true;
}


//==========================================================================
//
//  ClearAndReset
//
//==========================================================================
void ClearAndReset () {
  if (vallist.length) {
    vallist.reset();
    Value = 0;
    if (cvarSession) setSavedFloat(cvarSession, 0);
  }
}


//==========================================================================
//
//  AddItem
//
//==========================================================================
void AddItem (string AValue, optional float val) {
  if (!specified_val && vallist.length) val = int(vallist[$-1].value)+1;
  vallist.length += 1;
  vallist[$-1].caption = TranslateString(AValue);
  vallist[$-1].value = val;
}


//==========================================================================
//
//  SetInitialValue
//
//==========================================================================
void SetInitialValue (float AValue) {
  Value = AValue;
  //int idx = findValue(AValue);
  if (cvarSession) setSavedFloat(cvarSession, Value);
}


//==========================================================================
//
//  OnDraw
//
//==========================================================================
override void OnDraw () {
  DrawBackground();

  SetFont('smallfont');

  string caption;
  int idx = findValue(Value);
  if (idx < 0) {
    caption = (float(int(Value)) == Value ? va("i:%d", int(Value)) : va("f:%f", Value));
  } else {
    caption = vallist[idx].caption;
  }
  int Col = (IsFocused ? TextColorFocus : TextColor);
  if (bLeftAlign) {
    SetTextAlign(hleft, vtop);
    DrawText(0, 0, Text, Col);
  } else {
    SetTextAlign(hright, vtop);
    DrawText(CursorXOffs, 0, Text, Col);
  }
  int xx = (bLeftAlign ? TextWidth(Text)+8 : CursorXOffs+10);
  SetTextAlign(hleft, vtop);
  DrawText(xx, 0, caption, (IsFocused ? ValueColorFocus : ValueColor));
}


//==========================================================================
//
//  OnEvent
//
//==========================================================================
override bool OnEvent (ref event_t evt) {
  if (evt.bubbling && evt.type == ev_keydown && vallist.length != 0) {
    float newValue;
    int idx;
    if (evt.keycode >= K_MOUSE1 && evt.keycode <= K_MOUSE9) return ::OnEvent(evt);
    switch (ConvertBoundKey(evt.keycode)) {
      case K_LEFTARROW:
      //case K_MOUSE2:
      case K_MWHEELUP:
        if (evt.keycode != K_MWHEELUP || (IsFocused() && evt.y >= 0 && evt.y < Height)) {
          idx = findValue(Value);
          if (idx < 0) idx = 0; else idx = (idx+vallist.length-1)%vallist.length;
          newValue = vallist[idx].value;
          if (SetNewValue(ref newValue)) LocalSound('menu/change');
          return true;
        }
        break;
      case K_RIGHTARROW:
      case K_ENTER:
      case K_PADENTER:
      //case K_MOUSE1:
      case K_MWHEELDOWN:
        if (evt.keycode != K_MWHEELDOWN || (IsFocused() && evt.y >= 0 && evt.y < Height)) {
          idx = findValue(Value);
          if (idx < 0) idx = 0; else idx = (idx+1)%vallist.length;
          newValue = vallist[idx].value;
          if (SetNewValue(ref newValue)) LocalSound('menu/change');
          return true;
        }
        break;
    }
  }
  return ::OnEvent(evt);
}


override void OnMouseClick (int X, int Y, int Button, Widget Dest) {
  if (Dest != self) return;
  if (IsFocused) {
    /*
    if (vallist.length == 0) return;
    int idx = findValue(Value);
    if (Button == K_MOUSE1) {
      if (idx < 0) idx = 0; else idx = (idx+1)%vallist.length;
    } else if (Button == K_MOUSE2) {
      if (idx < 0) idx = 0; else idx = (idx+vallist.length-1)%vallist.length;
    } else {
      return;
    }
    float newValue = vallist[idx].value;
    if (SetNewValue(ref newValue)) LocalSound('menu/change');
    */
    MenuComboBoxPopup pp = MenuComboBoxPopup(GetRootWidget.NewChild(MenuComboBoxPopup));
    pp.text = "test!";
    printdebug("%s: created", pp.UniqueId);
    LocalSound('menu/change');
  } else {
    SetFocus();
  }
}


defaultproperties {
  Width = 360;
  Height = 10;
  cvarSession = '';
}


// ////////////////////////////////////////////////////////////////////////// //
class MenuComboBoxPopup : Widget;

private string mText;


final string text {
  get { return mText; }
  set { setText(value); }
}


//==========================================================================
//
//  setText
//
//==========================================================================
void setText (string text) {
  text = text.xstrip;
  mText = SplitTextWithNewlines(text, GetVirtualWidth()-16);

  SetFont('smallfont');
  SetTextAlign(hleft, vtop);
  SetSize(TextWidth(mText)+8, TextHeight(mText)+8);
}


//==========================================================================
//
//  OnDraw
//
//==========================================================================
override void OnDraw () {
  if (!mText || Width < 1 || Height < 1) return;
  //printdebug("%s: cob=%B; focused=%B", UniqueId, CloseOnBlur, IsFocused());
  SetOrigin((GetVirtualWidth()-Width)/2, (GetVirtualHeight()-Height)/2);
  SetFont('smallfont');
  SetTextAlign(hleft, vtop);
  ShadeRect(0, 0, Width, Height, /*GetCvarF('menu_darkening')*/0.8);
  DrawRect(0, 0, Width, Height, RGB(255, 127, 0));
  DrawText(4, 4, mText, CR_ORANGE);
}


override bool OnEvent (ref event_t evt) {
  //printdebug("%s: event:%s", UniqueId, evt.type);
  if (evt.bubbling) {
    if (evt.type == ev_keydown) {
      if (evt.keycode >= K_MOUSE1 && evt.keycode <= K_MOUSE9) {
        if (evt.x < 0 || evt.y < 0 || evt.x >= Width || evt.y >= Height) {
          printdebug("xclick!");
          Destroy();
          return true;
        }
      }
    }
    return true;
  }
  return ::OnEvent(evt);
}


//==========================================================================
//
//  OnDraw
//
//==========================================================================
override void OnClose () {
  printdebug("%s: closing", UniqueId);
}


defaultproperties {
  X = 0;
  Y = 0;
  Width = 0;
  Height = 0;
  Focusable = true;
  CloseOnBlur = true;
}
