//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2020 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class MenuChoiceEnum : MenuChoice;

struct EnumValue {
  string caption;
  float value;
}

string Text;
bool bLeftAlign;

float Value;
array!EnumValue vallist;

MenuComboBoxPopup cbox;

delegate bool ValueChanging (Object Sender, float NewValue);


//==========================================================================
//
//  SetText
//
//==========================================================================
void SetText (string AText) {
  Text = TranslateString(AText);
  FixMinimumHeight();
}


//==========================================================================
//
//  findValue
//
//==========================================================================
int findValue (float val) {
  foreach (auto idx, ref EnumValue v; vallist) {
    if (v.value == val) return idx;
  }
  return -1;
}


//==========================================================================
//
//  SetOrigin
//
//==========================================================================
override void SetOrigin (int x, int y) {
  if (bLeftAlign) {
    SetPos(x, y);
  } else {
    CursorXOffs = /*200*/CursorXOffsHR;
    SetPos(x-CursorXOffs, y);
  }
}


//==========================================================================
//
//  SetNewValue
//
//==========================================================================
override bool SetNewValue (ref float newValue) {
  if (ValueChanging) {
    if (!ValueChanging(self, newValue)) return false;
  }
  if (!::SetNewValue(ref newValue)) return false;
  Value = newValue;
  return true;
}


//==========================================================================
//
//  ClearAndReset
//
//==========================================================================
void ClearAndReset () {
  if (vallist.length) {
    vallist.reset();
    Value = 0;
    if (cvarSession) setSavedFloat(cvarSession, 0);
  }
}


//==========================================================================
//
//  AddItem
//
//==========================================================================
void AddItem (string AValue, optional float val) {
  if (!specified_val && vallist.length) val = int(vallist[$-1].value)+1;
  vallist.length += 1;
  vallist[$-1].caption = TranslateString(AValue);
  vallist[$-1].value = val;
}


//==========================================================================
//
//  SetInitialValue
//
//==========================================================================
void SetInitialValue (float AValue) {
  Value = AValue;
  //int idx = findValue(AValue);
  if (cvarSession) setSavedFloat(cvarSession, Value);
}


//==========================================================================
//
//  OnDraw
//
//==========================================================================
override void OnDraw () {
  DrawBackground();

  SetFont('smallfont');

  string caption;
  int idx = findValue(Value);
  if (idx < 0) {
    caption = (float(int(Value)) == Value ? va("i:%d", int(Value)) : va("f:%f", Value));
  } else {
    caption = vallist[idx].caption;
  }
  int Col = (IsFocused ? TextColorFocus : TextColor);
  if (bLeftAlign) {
    SetTextAlign(hleft, vtop);
    DrawText(0, 0, Text, Col);
  } else {
    SetTextAlign(hright, vtop);
    DrawText(CursorXOffs, 0, Text, Col);
  }
  int xx = (bLeftAlign ? TextWidth(Text)+8 : CursorXOffs+10);
  SetTextAlign(hleft, vtop);
  DrawText(xx, 0, caption, (IsFocused ? ValueColorFocus : ValueColor));
}


//==========================================================================
//
//  OnEvent
//
//==========================================================================
override bool OnEvent (ref event_t evt) {
  if (evt.bubbling && evt.type == ev_keydown && vallist.length != 0) {
    float newValue;
    int idx;
    if (evt.keycode >= K_MOUSE1 && evt.keycode <= K_MOUSE9) return ::OnEvent(evt);
    switch (ConvertBoundKey(evt.keycode)) {
      case K_LEFTARROW:
      //case K_MOUSE2:
      case K_MWHEELUP:
        if (evt.keycode != K_MWHEELUP || (IsFocused() && evt.y >= 0 && evt.y < Height)) {
          idx = findValue(Value);
          if (idx < 0) idx = 0; else idx = (idx+vallist.length-1)%vallist.length;
          newValue = vallist[idx].value;
          if (SetNewValue(ref newValue)) LocalSound('menu/change');
          return true;
        }
        break;
      case K_ENTER:
      case K_PADENTER:
      case K_SPACE:
        OpenComboPopup();
        return true;
      case K_RIGHTARROW:
      //case K_MOUSE1:
      case K_MWHEELDOWN:
        if (evt.keycode != K_MWHEELDOWN || (IsFocused() && evt.y >= 0 && evt.y < Height)) {
          idx = findValue(Value);
          if (idx < 0) idx = 0; else idx = (idx+1)%vallist.length;
          newValue = vallist[idx].value;
          if (SetNewValue(ref newValue)) LocalSound('menu/change');
          return true;
        }
        break;
    }
  }
  return ::OnEvent(evt);
}


void OpenComboPopup (optional int X, optional int Y) {
  if (!vallist) return;
  int idx = findValue(Value);
  /*MenuComboBoxPopup*/ cbox = MenuComboBoxPopup(GetRootWidget.NewChild(MenuComboBoxPopup));
  if (!cbox) return;
  cbox.choice = self;
  if (!specified_X) {
    auto oldFont = GetFont();
    scope(exit) SetFont(oldFont);
    SetFont('smallfont');
    int xx = (bLeftAlign ? TextWidth(Text)+8 : CursorXOffs+10);
    X = xx;
  }
  if (!specified_Y) Y = -2;
  LocalXYToRoot(out cbox.xbase, out cbox.ybase, X, Y);
  foreach (int c, const ref auto vv; vallist) cbox.addItem(vv.caption, (c == idx));
  LocalSound('menu/change');
}


void SetComboChoice (int idx) {
  if (idx < 0 || idx >= vallist.length) return;
  float newValue = vallist[idx].value;
  if (SetNewValue(ref newValue)) LocalSound('menu/change');
}


override void OnMouseClick (int X, int Y, int Button, Widget Dest) {
  if (Dest != self) return;
  if (IsFocused) {
    /*
    if (vallist.length == 0) return;
    int idx = findValue(Value);
    if (Button == K_MOUSE1) {
      if (idx < 0) idx = 0; else idx = (idx+1)%vallist.length;
    } else if (Button == K_MOUSE2) {
      if (idx < 0) idx = 0; else idx = (idx+vallist.length-1)%vallist.length;
    } else {
      return;
    }
    float newValue = vallist[idx].value;
    if (SetNewValue(ref newValue)) LocalSound('menu/change');
    */
    if (Button == K_MOUSE1 && vallist) {
      OpenComboPopup(/*X*/default, /*Y*/default);
    }
  } else {
    SetFocus();
  }
}


override void OnClose () {
  if (cbox) delete cbox;
}


override void OnVisibilityChanged (bool bNewVisibility) {
  if (!bNewVisibility) {
    if (cbox) delete cbox;
  }
  ::OnVisibilityChanged(bNewVisibility);
}


defaultproperties {
  Width = 360;
  Height = 10;
  cvarSession = '';
}


// ////////////////////////////////////////////////////////////////////////// //
class MenuComboBoxPopup : Widget;

array!string items;
int currItem;
int defItem;
int xbase, ybase;
MenuChoiceEnum choice;


//==========================================================================
//
//  addItem
//
//==========================================================================
void addItem (string text, bool current) {
  text = text.xstrip;
  if (!text) text = "<undefined>";
  if (current) {
    currItem = items.length;
    defItem = currItem;
  }
  items[$] = text;

  auto oldFont = GetFont();
  scope(exit) SetFont(oldFont);

  SetFont('smallfont');
  //SetTextAlign(hleft, vtop);
  int hgt = 0, wdt = 0;
  foreach (auto str; items) {
    hgt += TextHeight(str);
    wdt = max(wdt, TextWidth(str));
  }

  SetSize(wdt+8, hgt+8);

  int x0 = xbase-1;
  if (x0+wdt+10 > Parent.Width) x0 = Parent.Width-wdt-10;
  if (x0 < 0) x0 = 0;

  int y0 = ybase-1;
  if (y0+hgt+10 > Parent.Height) y0 = Parent.Height-hgt-10;
  if (y0 < 0) y0 = 0;

  SetOrigin(x0, y0);
}


//==========================================================================
//
//  OnDraw
//
//==========================================================================
override void OnDraw () {
  if (!items || Width < 1 || Height < 1) return;
  //printdebug("%s: cob=%B; focused=%B", UniqueId, CloseOnBlur, IsFocused());
  SetFont('smallfont');
  SetTextAlign(hleft, vtop);
  ShadeRect(0, 0, Width, Height, /*GetCvarF('menu_darkening')*/0.8);
  DrawRect(0, 0, Width, Height, RGB(255, 255, 255));
  //DrawText(4, 4, mText, CR_ORANGE);

  int y = 4;
  foreach (int idx, auto str; items) {
    int hgt = TextHeight(str);
    //if (idx == currItem) FillRect(1, y, Width-2, hgt, RGB(152, 205, 50), 0.6);
    if (idx == currItem) FillRect(1, y, Width-2, hgt, RGB(152*2, 205, 50), 0.8);
    DrawText(4, y, str, (idx == currItem ? CR_WHITE : idx == defItem ? CR_YELLOW : /*CR_OLIVE*/CR_TEAL));
    y += hgt;
  }
}


override bool OnEvent (ref event_t evt) {
  //printdebug("%s: event:%s", UniqueId, evt.type);
  if (evt.bubbling) {
    if (evt.type == ev_keydown) {
      if (evt.keycode >= K_MOUSE1 && evt.keycode <= K_MOUSE9) {
        if (evt.keycode != K_MOUSE1 || evt.x < 0 || evt.y < 0 || evt.x >= Width || evt.y >= Height) {
          //printdebug("xclick!");
          Destroy();
          return true;
        }
      }
      switch (ConvertBoundKey(evt.keycode)) {
        case K_ESCAPE:
          Destroy();
          return true;
        case K_ENTER: case K_PADENTER:
          if (choice) choice.SetComboChoice(currItem);
          Destroy();
          return true;
        case K_UPARROW: case K_MWHEELUP:
          if (currItem > 0) --currItem;
          return true;
        case K_DOWNARROW: case K_MWHEELDOWN:
          if (currItem < items.length-1) ++currItem;
          return true;
        case K_HOME:
          currItem = 0;
          return true;
        case K_END:
          currItem = items.length-1;
          return true;
      }
    }
    //return true;
  }
  return ::OnEvent(evt);
}


int FindItemAt (int x, int y) {
  if (x < 1 || y < 4 || x >= Width-1 || y >= Height-4) return -1;
  auto oldFont = GetFont();
  scope(exit) SetFont(oldFont);
  SetFont('smallfont');
  int yy = 4;
  foreach (int idx, auto str; items) {
    int hgt = TextHeight(str);
    if (y >= yy && y < yy+hgt) return idx;
    yy += hgt;
  }
  return -1;
}


override bool OnMouseMove (int OldX, int OldY, int NewX, int NewY, Widget Dest) {
  if (Dest != self) return false;
  if (NewX >= 1 && NewY >= 4 && NewX < Width-1 && NewY < Height-4) {
    int idx = FindItemAt(NewX, NewY);
    if (idx >= 0) currItem = idx;
    return true;
  }
  return false;
}


override void OnMouseClick (int X, int Y, int Button, Widget Dest) {
  if (Dest != self) return;
  int idx = FindItemAt(X, Y);
  if (idx >= 0) {
    currItem = idx;
    if (choice) choice.SetComboChoice(currItem);
    Destroy();
  }
}


//==========================================================================
//
//  OnDraw
//
//==========================================================================
override void OnClose () {
  //printdebug("%s: closing", UniqueId);
  if (choice) choice.cbox = none;
}


defaultproperties {
  X = 0;
  Y = 0;
  Width = 0;
  Height = 0;
  Focusable = true;
  CloseOnBlur = true;
}
