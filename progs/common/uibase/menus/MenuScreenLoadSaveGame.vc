//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2020 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class MenuScreenLoadSaveGame : MenuScreen;

enum QUICKSAVE_SLOT = -666; // from the C++ source code

bool autosaves;
int lastSelectedIndex = -669; // to update date string
string datestr = "";
string qsdatestr = "";


//==========================================================================
//
//  SignalSlotDeletion
//
//==========================================================================
void SignalSlotDeletion () {
  lastSelectedIndex = default.lastSelectedIndex;
  datestr = "";
  qsdatestr = "";
}


//==========================================================================
//
//  CreateLoadSaveChoices
//
//==========================================================================
void CreateLoadSaveChoices (class!MenuSaveSlot destClass) {
  foreach (auto i; 0..10) {
    MenuSaveSlot newChoice = destClass(NewChild(destClass));
    newChoice.SetOrigin(ChoicesStartX, ChoicesStartY+i*18);
    newChoice.SlotNum = (autosaves ? -1-i : i);
    newChoice.Status = SV_GetSaveString(newChoice.SlotNum, &newChoice.Text);
    newChoice.customHelpText = "Press \"TAB\" to switch between normal and autosaves.";
  }
}


//==========================================================================
//
//  OnDraw
//
//==========================================================================
override void OnDraw () {
  ::OnDraw();
  if (Items.length == 0) return;
  // refresh `datestr`
  if (lastSelectedIndex != CursorPos) {
    lastSelectedIndex = CursorPos;
    if (CursorPos >= 0 && CursorPos < Items.length) {
      MenuSaveSlot ss = MenuSaveSlot(Items[CursorPos]);
      if (ss) {
        SV_GetSaveDateString(ss.SlotNum, &datestr);
        if (datestr && datestr != "UNKNOWN") datestr = va("DATE: %s", datestr); else datestr = "";
      } else {
        datestr = "";
      }
    } else {
      datestr = "";
    }
  }
  // refresh `qsdatestr`
  if (!qsdatestr) {
    SV_GetSaveDateString(QUICKSAVE_SLOT, &qsdatestr);
    if (!qsdatestr || qsdatestr == "UNKNOWN") qsdatestr = " "; else qsdatestr = "QUICKSAVE: "~qsdatestr;
  }
  // draw it all
  SetFont('smallfont');
  //SetTextAlign(hleft, vtop);
  SetTextAlign(hright, vtop);
  DrawText(Width-16, Items[$-1].Y+21, datestr);
  if (qsdatestr) {
    DrawText(Width-16, Items[0].Y-10, qsdatestr);
  }
}


//==========================================================================
//
//  RefreshLoadSaveChoices
//
//==========================================================================
void RefreshLoadSaveChoices () {
  datestr = "";
  qsdatestr = "";
  lastSelectedIndex = default.lastSelectedIndex;
  foreach (auto i; 0..10) {
    if (i >= Items.length) break;
    MenuSaveSlot ss = MenuSaveSlot(Items[i]);
    if (!ss) continue;
    ss.SlotNum = (autosaves ? -1-i : i);
    ss.Status = SV_GetSaveString(ss.SlotNum, &ss.Text);
  }
  if (autosaves && Items.length > 9) {
    MenuSaveSlot ss = MenuSaveSlot(Items[$-1]);
    assert(ss);
    ss.SlotNum = QUICKSAVE_SLOT;
    ss.Status = SV_GetSaveString(ss.SlotNum, &ss.Text);
  }
}


//==========================================================================
//
//  SwitchAutoNormal
//
//  virtual, so children can fix the title accordingly
//
//==========================================================================
void SwitchAutoNormal () {
  autosaves = !autosaves;
  RefreshLoadSaveChoices();
}


//==========================================================================
//
//  OnKeyDown
//
//==========================================================================
override bool OnKeyDown (int key) {
  int nkey = ConvertBoundKey(key);
  switch (nkey) {
    case K_TAB: SwitchAutoNormal(); return true;
  }
  return ::OnKeyDown(key);
}


defaultproperties {
  ChoicesStartX = 80;
  ChoicesStartY = 40;
  SelectorType = MenuSelector_Big;
  Title = "LOAD OR SAVE GAME";
  Height = 240;
}
