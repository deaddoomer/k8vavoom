//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2020 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class MenuScreenSkill : MenuScreen;


//==========================================================================
//
//  CreateChoices
//
//==========================================================================
override void CreateChoices () {
  int maxwdt = -1;
  foreach (auto i; 0..P_GetNumSkills()) {
    MenuChoiceSkill Btn = MenuChoiceSkill(NewChild(MenuChoiceSkill));
    SkillDef *SDef = P_GetSkillDef(i);
    Btn.SetPos(ChoicesStartX, currentY);
    int wdt = -1;
    if (SDef->bMenuNameIsPic) {
      Btn.PicHandle = R_RegisterPic(name(SDef->MenuName));
      picinfo_t pinf;
      R_GetPicInfo(Btn.PicHandle, &pinf);
      wdt = pinf.width;
      if (Btn.Height < pinf.height) Btn.Height = pinf.height+8;
    } else {
      Btn.SetText(SDef->MenuName);
    }
    foreach (auto j; 0..SDef->PlayerClassNames.length) {
      if (!stricmp(SDef->PlayerClassNames[j].ClassName, ClGame.SkillClassName)) {
        Btn.SetText(SDef->PlayerClassNames[j].MenuName);
        Btn.PicHandle = 0;
      }
    }
    if (SDef->TextColor) Btn.Text = "\c["~SDef->TextColor~"]"~Btn.Text;
    if (wdt < 0) wdt = Btn.CalcTextWidth();
    maxwdt = max(maxwdt, wdt);
    Btn.SkillIndex = i;
    Btn.bMustConfirm = SDef->bMustConfirm;
    Btn.ConfirmationText = SDef->ConfirmationText;
    Btn.Activated = &ActivateSkill;
    currentY += Btn.Height;
  }
  maxwdt += 60;
  if (Width < maxwdt) Width = maxwdt;
}


//==========================================================================
//
//  SetDefaultChoice
//
//==========================================================================
override void SetDefaultChoice () {
  ::SetDefaultChoice();

  int goodSkill = max(0, P_GetNumSkills()-2);
  foreach (auto i; 0..P_GetNumSkills()) {
    auto sdef = P_GetSkillDef(i);
    if (sdef->bMustConfirm) {
      goodSkill = i-1;
      break;
    }
  }

  foreach (auto i; 0..goodSkill) CycleNextChoice();
}


//==========================================================================
//
//  ActivateSkill
//
//==========================================================================
void ActivateSkill (Object Sender) {
  MenuChoiceSkill Btn = MenuChoiceSkill(Sender);
  if (Btn.bMustConfirm) {
    ClGame.SkillToConfirm = Btn.SkillIndex;
    ClGame.MessageBoxStartMessage(
      Btn.ConfirmationText ? Btn.ConfirmationText :
      "Are you sure? This skill level\nisn't even remotely fair.",
      ClientGameShared::MessageBoxType.Nightmare, true);
  } else {
    ClGame.NewGame(Btn.SkillIndex);
  }
}


defaultproperties {
  ChoicesStartX = 38;
  ChoicesStartY = 40;
  SelectorType = MenuSelector_Big;
  Title = "CHOOSE SKILL LEVEL:";
  MenuScrollable = true;
  AllowVerticalResize = true;
}
