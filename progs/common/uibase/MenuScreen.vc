//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2020 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class MenuScreen : Widget;

ClientGameShared ClGame;

MenuTitleText titleItem;
Widget ItemContainer;
array!MenuChoice Items;
MenuChoice_BackButton BackButton;

bool MenuScrollable;
bool AllowVerticalResize;
int OfsYPrev;
int CursorPos;
int CursorPrev;

int ChoicesStartX;
int ChoicesStartY;

class!MenuSelectorBase SelectorType;
MenuSelectorBase Selector;

string Title;
int TitleX;
int TitleY;

name ActivateSound;
name DeactivateSound;
name ClearMenusSound;

int currentY;

HelpText helpText;

int WidthThreshold = 380;

#ifdef K8_DEVELOPER
enum k8Developer = true;
#else
enum k8Developer = false;
#endif


//==========================================================================
//
//  IsMultiplayerEnabled
//
//==========================================================================
final static bool IsMultiplayerEnabled () {
  return !GetCvarB('dbg_disable_multiplayer');
}


//==========================================================================
//
//  OnDestroy
//
//==========================================================================
override void OnDestroy () {
  //printdebug("%C: OnDestroy", self);
  //if (helpText) printdebug("%C: destroying helpText(%s)", self, helpText.UniqueId);
  delete helpText;
  ::OnDestroy();
}


//==========================================================================
//
//  OnCreate
//
//==========================================================================
override void OnCreate () {
  ::OnCreate();
  ItemContainer = CreateItemContainer();
}


//==========================================================================
//
//  OnVisibilityChanged
//
//  Called when this widget is shown or hidden.
//
//==========================================================================
override void OnVisibilityChanged (bool bNewVisibility) {
  if (helpText) {
    if (bNewVisibility) {
      //printdebug("raising help text (%s)", helpText.UniqueId);
      helpText.Show();
      //helpText.Raise();
    } else {
      helpText.Hide();
    }
  }
  ::OnVisibilityChanged(bNewVisibility);
}


//==========================================================================
//
//  OnBeforePopMenu
//
//  this is called before `ClGame.PopMenu()`
//  return `false` to prevent closing (but still eat the event)
//  `userAbort` is true if closed via `esc`
//
//==========================================================================
bool OnBeforePopMenu (bool userAbort) {
  return true;
}


//==========================================================================
//
//  DoPopMenu
//
//  call this instead of `ClGame.PopMenu()`
//
//==========================================================================
void DoPopMenu (bool userAbort) {
  if (OnBeforePopMenu(userAbort)) {
    ClGame.PopMenu();
  }
}


//==========================================================================
//
//  ShowAndActivate
//
//==========================================================================
void ShowAndActivate () {
  Show();
  Raise();
  SetFocus();
  if (helpText) {
    //printdebug("showing help text (%s)", helpText.UniqueId);
    helpText.Show();
    //helpText.Raise();
  }
}


//==========================================================================
//
//  FinishCreatingMenu
//
//==========================================================================
void FinishCreatingMenu () {
  //if (AllowVerticalResize) Height = max(Height, Items[$-1].Y+Items[$-1].Height+4);
  CreateSelector();
  SetDefaultChoice();
  if (AllowVerticalResize && Items.length > 0) {
    int newHeight = max(Height, Items[$-1].Y+Items[$-1].Height+2);
    if (newHeight > GetVirtualHeight()-28) {
      newHeight = GetVirtualHeight()-28;
      MenuScrollable = true;
    }
    Height = newHeight;
  }

  FixTitleWidthClosePos();

  // center menu
  SetPos((GetVirtualWidth()-Width)/2, (GetVirtualHeight()-Height)/2);
}


//==========================================================================
//
//  CreateMenu
//
//==========================================================================
void CreateMenu () {
  if (!helpText) helpText = GetRootWidget().NewChild(HelpText);
  helpText.OnTop = true;
  helpText.text = "";
  CreateTitle();
  CreateBackButton();
  currentY = ChoicesStartY;
  CreateChoices();
  FinishCreatingMenu();
  /* this is in `FinishCreatingMenu()` now
  CreateSelector();
  SetDefaultChoice();
  if (AllowVerticalResize && Items.length > 0) {
    int newHeight = max(Height, Items[$-1].Y+Items[$-1].Height+2);
    if (newHeight > GetVirtualHeight()-8) {
      newHeight = GetVirtualHeight()-8;
      MenuScrollable = true;
    }
    Height = newHeight;
  }
  FixTitleWidthClosePos();
  // center menu
  SetPos((GetVirtualWidth()-Width)/2, (GetVirtualHeight()-Height)/2);
  */
}


//==========================================================================
//
//  OnChildAdded
//
//==========================================================================
override void OnChildAdded (Widget Child) {
  // do not add "close" button to item list
  if (MenuChoice(Child) && !MenuChoice_BackButton(Child)) {
    if (Items.length > 1024) FatalError("Too many menu items");
    Items[Items.Length] = MenuChoice(Child);
  }
  ::OnChildAdded(Child);
}


//==========================================================================
//
//  CreateTitle
//
//==========================================================================
void CreateTitle () {
  if (Title) {
    if (!titleItem) titleItem = NewChild(MenuTitleText);
    titleItem.Text = Title;
    titleItem.SetOrigin(TitleX, TitleY);
    titleItem.FixSize();
  } else {
    delete titleItem;
  }
}


//==========================================================================
//
//  ChangeTitle
//
//==========================================================================
void ChangeTitle (string newtitle) {
  //if (!titleItem) return; // no title, cannot insert it now
  //if (!newtitle) newtitle = " "; //FIXME
  if (Title == newtitle) return;
  Title = newtitle;
  CreateTitle();
}


//==========================================================================
//
//  CreateBackButton
//
//  create the back button on the top left corner of the menu screen
//
//==========================================================================
void CreateBackButton () {
  if (!BackButton) {
    BackButton = NewChild(MenuChoice_BackButton);
    BackButton.Width = BackButton.CalcTextWidth()+8;
    BackButton.SetOrigin(Width-20, 1);
  }
}


//==========================================================================
//
//  CreateItemContainer
//
//==========================================================================
Widget CreateItemContainer () {
  return none;
}


//==========================================================================
//
//  GetItemContainer
//
//==========================================================================
Widget GetItemContainer () {
  return (ItemContainer ? ItemContainer : Widget(self));
}


//==========================================================================
//
//  CreateChoices
//
//==========================================================================
void CreateChoices () {
}


//==========================================================================
//
//  FixTitleWidthClosePos
//
//==========================================================================
void FixTitleWidthClosePos () {
  //print("FixTitleWidthClosePos: %C", Class);
  if (titleItem) {
    //titleItem.centerText = false;
    titleItem.FixSize();
    titleItem.SetPos((Width-titleItem.Width)/2, titleItem.Y);
  }
  if (BackButton) BackButton.SetOrigin(Width-20, 1);
}


//==========================================================================
//
//  SetLastHelp
//
//==========================================================================
void SetLastHelp (string help) {
  if (Items.length) Items[$-1].customHelpText = help;
}


//==========================================================================
//
//  SetChoiceInitialSizeSimple
//
//==========================================================================
void SetChoiceInitialSizeSimple (MenuChoice cc, optional bool doCurOffs/*=true*/) {
  if (!cc) return;
  if (!specified_doCurOffs) doCurOffs = true;
  cc.Width = Width;
  if (Width > WidthThreshold) {
    //print("[%s] wdt=%d", text, Width);
    //!cc.Width = Width;
    if (doCurOffs) {
      cc.CursorXOffs = Width/2;
      cc.CursorXOffsHR = Width/2;
    }
  }
}


//==========================================================================
//
//  SetChoiceInitialSizePos
//
//==========================================================================
void SetChoiceInitialSizePos (MenuChoice cc) {
  if (!cc) return;
  cc.Width = Width;
  if (Width > WidthThreshold) {
    //print("[%s] wdt=%d", text, Width);
    //!cc.Width = Width;
    cc.CursorXOffs = Width/2;
    cc.CursorXOffsHR = Width/2;
    cc.SetOrigin(Width/2, currentY);
  } else {
    cc.SetOrigin(ChoicesStartX, currentY);
  }
}


//==========================================================================
//
//  SetTextButtonInitialSizePos
//
//==========================================================================
void SetTextButtonInitialSizePos (MenuTextButton cc) {
  if (!cc) return;
  cc.SetOrigin(ChoicesStartX, currentY);
  if (Width > WidthThreshold) cc.Width = Width;
}


//==========================================================================
//
//  CreateOnOffCvar
//
//  should be called from `CreateChoices()` (and `currentY` must be set)
//
//==========================================================================
MenuChoice_OnOff CreateOnOffCvar (string text, name cvname) {
  auto onoff = GetItemContainer().NewChild(MenuChoice_OnOff);
  SetChoiceInitialSizePos(onoff);
  onoff.Text = text;
  onoff.SetSessionCvar(cvname);
  currentY += onoff.Height;
  return onoff;
}


//==========================================================================
//
//  CreateEnumCvar
//
//  should be called from `CreateChoices()` (and `currentY` must be set)
//
//==========================================================================
MenuChoiceEnum CreateEnumCvar (string text, name cvname,
  optional string item0, optional string item1, optional string item2,
  optional string item3, optional string item4, optional string item5,
  optional string item6, optional string item7, optional string item8,
  optional string item9, optional string item10, optional string item11,
  optional string item12, optional string item13, optional string item14,
  optional string item15, optional string item16, optional string item17,
  optional string item18, optional string item19, optional string item20,
  optional string item21, optional string item22, optional string item23,
  optional string item24, optional string item25, optional string item26)
{
  auto en = GetItemContainer().NewChild(MenuChoiceEnum);
  SetChoiceInitialSizePos(en);
  en.Text = text;
  if (specified_item0) en.AddItem(item0);
  if (specified_item1) en.AddItem(item1);
  if (specified_item2) en.AddItem(item2);
  if (specified_item3) en.AddItem(item3);
  if (specified_item4) en.AddItem(item4);
  if (specified_item5) en.AddItem(item5);
  if (specified_item6) en.AddItem(item6);
  if (specified_item7) en.AddItem(item7);
  if (specified_item8) en.AddItem(item8);
  if (specified_item9) en.AddItem(item9);
  if (specified_item10) en.AddItem(item10);
  if (specified_item11) en.AddItem(item11);
  if (specified_item12) en.AddItem(item12);
  if (specified_item13) en.AddItem(item13);
  if (specified_item14) en.AddItem(item14);
  if (specified_item15) en.AddItem(item15);
  if (specified_item16) en.AddItem(item16);
  if (specified_item17) en.AddItem(item17);
  if (specified_item18) en.AddItem(item18);
  if (specified_item19) en.AddItem(item19);
  if (specified_item20) en.AddItem(item20);
  if (specified_item21) en.AddItem(item21);
  if (specified_item22) en.AddItem(item22);
  if (specified_item23) en.AddItem(item23);
  if (specified_item24) en.AddItem(item24);
  if (specified_item25) en.AddItem(item25);
  if (specified_item26) en.AddItem(item26);
  en.SetSessionCvar(cvname);
  currentY += en.Height;
  return en;
}


//==========================================================================
//
//  CreateFloatSliderCvar
//
//  should be called from `CreateChoices()` (and `currentY` must be set)
//
//==========================================================================
MenuChoiceSlider CreateFloatSliderCvar (string text, name cvname, float delta, optional float min, optional float max) {
  if (Items.length > 0 && Items[$-1] !isa MenuChoiceSlider && Items[$-1].Height == 10) {
    Items[$-1].Height = 8;
    currentY -= 2;
  }
  auto slider = GetItemContainer().NewChild(MenuChoiceSlider);
  SetChoiceInitialSizePos(slider);
  slider.Text = text;
  slider.cvarSession = cvname;
  slider.ValueDelta = delta;
  if (specified_min) slider.MinValue = min;
  if (specified_max) slider.MaxValue = max;
  slider.SetSessionCvar(cvname);
  currentY += slider.Height;
  return slider;
}


//==========================================================================
//
//  CreateFloatNumericCvar
//
//  should be called from `CreateChoices()` (and `currentY` must be set)
//
//==========================================================================
MenuChoiceNumeric CreateFloatNumericCvar (string text, name cvname, float delta, optional float min, optional float max) {
  auto slider = GetItemContainer().NewChild(MenuChoiceNumeric);
  SetChoiceInitialSizePos(slider);
  slider.Text = text;
  slider.cvarSession = cvname;
  slider.ValueDelta = delta;
  if (specified_min) slider.MinValue = min;
  if (specified_max) slider.MaxValue = max;
  slider.SetSessionCvar(cvname);
  currentY += slider.Height;
  return slider;
}


//==========================================================================
//
//  CreateChoiceSubHeader
//
//  should be called from `CreateChoices()` (and `currentY` must be set)
//  default color: CR_ORANGE
//
//==========================================================================
MenuTextButton CreateChoiceSubHeader (string text, optional int color) {
  auto btn = GetItemContainer().NewChild(MenuSmallTextButton);
  btn.TextHAlign = hcenter;
  btn.TextVAlign = vbottom;
  if (Width > WidthThreshold) {
    btn.Width = Width;
    btn.CursorXOffs = Width/2;
    btn.SetOrigin(0, currentY);
  } else {
    btn.SetOrigin(ChoicesStartX, currentY);
  }
  btn.Text = text;
  btn.Focusable = false;
  if (specified_color) {
    btn.TextColor = color;
    btn.TextColorFocus = color;
  } else {
    btn.TextColor = CR_ORANGE;
    btn.TextColorFocus = CR_ORANGE;
  }
  btn.Height = 12;
  currentY += btn.Height;
  return btn;
}


//==========================================================================
//
//  CreateSubMenuText
//
//==========================================================================
MenuTextButton CreateSubMenuText (string text, class!MenuScreen asubmenuClass) {
  auto btn = GetItemContainer().NewChild(MenuSmallTextButton);
  btn.SetOrigin(ChoicesStartX, currentY);
  if (Width > WidthThreshold) btn.Width = Width;
  btn.Text = text;
  btn.SetSubMenuClass(asubmenuClass);
  currentY += btn.Height;
  return btn;
}


//==========================================================================
//
//  CreateSubMenuBigText
//
//==========================================================================
MenuTextButton CreateSubMenuBigText (string text, class!MenuScreen asubmenuClass) {
  auto btn = GetItemContainer().NewChild(MenuBigTextButton);
  btn.SetOrigin(ChoicesStartX, currentY);
  if (Width > WidthThreshold) btn.Width = Width;
  btn.Text = text;
  btn.SetSubMenuClass(asubmenuClass);
  currentY += btn.Height;
  return btn;
}


//==========================================================================
//
//  CreateSubMenuTextNamed
//
//==========================================================================
MenuTextButton CreateSubMenuTextNamed (string text, name menuName) {
  auto btn = GetItemContainer().NewChild(MenuSmallTextButton);
  btn.SetOrigin(ChoicesStartX, currentY);
  if (Width > WidthThreshold) btn.Width = Width;
  btn.Text = text;
  btn.SetSubMenuName(string(menuName));
  currentY += btn.Height;
  return btn;
}


//==========================================================================
//
//  CreateSubMenuBigTextNamed
//
//==========================================================================
MenuTextButton CreateSubMenuBigTextNamed (string text, name menuName) {
  auto btn = GetItemContainer().NewChild(MenuBigTextButton);
  btn.SetOrigin(ChoicesStartX, currentY);
  if (Width > WidthThreshold) btn.Width = Width;
  btn.Text = text;
  btn.SetSubMenuName(string(menuName));
  currentY += btn.Height;
  return btn;
}


//==========================================================================
//
//  CreateSelector
//
//==========================================================================
void CreateSelector () {
  if (SelectorType) Selector = GetItemContainer().NewChild(SelectorType);
}


//==========================================================================
//
//  CalcTotalHeight
//
//==========================================================================
final int CalcTotalHeight () {
  foreach (auto it; Items; reverse) {
    if (it.IsFocusable && it.IsEnabled) {
      return it.Y+it.Height;
    }
  }
  return 0;
}


//==========================================================================
//
//  GetItemYS
//
//==========================================================================
final int GetItemYS (int idx) {
  if (idx < 0) return 0;
  if (idx >= Items.Length) return CalcTotalHeight();
  return Items[idx].Y;
}


//==========================================================================
//
//  GetItemYE
//
//==========================================================================
final int GetItemYE (int idx) {
  if (idx < 0) return 0;
  if (idx >= Items.Length) return CalcTotalHeight();
  return Items[idx].Y+Items[idx].Height;
}


//==========================================================================
//
//  GetItemYH
//
//==========================================================================
final int GetItemYH (int idx) {
  if (idx < 0) return 0;
  if (idx >= Items.Length) return 0;
  return Items[idx].Height;
}


//==========================================================================
//
//  FixTopIndex
//
//==========================================================================
final void FixTopIndex () {
  if (Items.length == 0) return;
  if (CursorPos < 0) CursorPos = Items.length-1; else if (CursorPos >= Items.length) CursorPos = 0;
  if (helpText) {
    helpText.text = Items[CursorPos].getHelp();
    helpText.Raise();
  }
  if (Items.length == 1) { if (helpText) helpText.atTop = false; CursorPos = 0; ChildrenOfsY = 0; return; }
  if (!MenuScrollable) { if (helpText) helpText.atTop = false; ChildrenOfsY = 0; return; }
  // check if this is first selectable item
  bool isFirstItem = true;
  foreach (auto index, auto item; Items) {
    if (!item.Focusable || !item.Enabled) continue;
    if (MenuChoice_BackButton(item)) continue;
    isFirstItem = index == CursorPos;
    break;
  }
  if (isFirstItem) { if (helpText) helpText.atTop = false; ChildrenOfsY = 0; return; }
  // make sure that cursor is visible
  int th = CalcTotalHeight();
  int ys = GetItemYS(CursorPos);
  int ye = GetItemYE(CursorPos);
  //print("Menu <%C>; ofs=%d; ys=%d; ye=%d; th=%d; height=%d; hh=%d; bs=%d; OriginY=%s", Class, ChildrenOfsY, ys, ye, th, Height, helpText.Height, GetVirtualHeight()-helpText.Height-4, Y2Virtual(ye));
       if (ys+ChildrenOfsY < ChoicesStartY) ChildrenOfsY = -ys+ChoicesStartY;
  else if (ye+ChildrenOfsY > Height) ChildrenOfsY = -(ye-Height);
  // make sure that help is visible
  if (helpText && helpText.Height > 0) {
    helpText.atTop = (Y2Virtual(ye)+ChildrenOfsY >= GetVirtualHeight()-helpText.Height-4);
  }
}


//==========================================================================
//
//  CursorMoved
//
//==========================================================================
void CursorMoved () {
  if (Items.length == 0) return;
  FixTopIndex();
  if ((CursorPrev == CursorPos && ChildrenOfsY == OfsYPrev) || MenuChoice_BackButton(Items[CursorPos])) return;
  if (CursorPos >= 0 && CursorPos < Items.length) SetCurrentFocusChild(Items[CursorPos]);
  if (Selector) {
    Selector.SetOriginCentered(Items[CursorPos].X+Items[CursorPos].CursorXOffs, Items[CursorPos].Y+Items[CursorPos].CursorYOffs, Items[CursorPos].Height);
    //print("000: cp=%s; %s %s %s w=%s", CursorPos, Items[CursorPos].X, Items[CursorPos].CursorXOffs, Items[CursorPos].X+Items[CursorPos].CursorXOffs, Width);
  }
  CursorPrev = CursorPos;
  OfsYPrev = ChildrenOfsY;
  //printdebug("CURR: %C:%s: height=%s", Items[CursorPos], Items[CursorPos].UniqueId, Items[CursorPos].Height);
}


//==========================================================================
//
//  SetDefaultChoice
//
//==========================================================================
void SetDefaultChoice () {
  // default cursor position
  ChildrenOfsY = 0;
  OfsYPrev = 0;
  CursorPos = 0;
  CursorPrev = 0;

  // force first available item to have focus
  foreach (int idx, MenuChoice item; Items) {
    if (item.Focusable && item.Enabled && !MenuChoice_BackButton(item)) {
      CursorPrev = -1;
      CursorPos = idx;
      CursorMoved();
      break;
    }
  }
}


//==========================================================================
//
//  MakeItemSelected
//
//==========================================================================
bool MakeItemSelected (MenuChoice it) {
  if (!it || MenuChoice_BackButton(it)) return false;
  foreach (int idx, MenuChoice item; Items) {
    if (item == it && item.Focusable && item.Enabled && !MenuChoice_BackButton(item)) {
      if (CursorPos == idx) return true; // nothing to do
      CursorPrev = -1;
      CursorPos = idx;
      CursorMoved();
      return true;
    }
  }
  return false;
}


//==========================================================================
//
//  GetChoiceForSaving
//
//==========================================================================
int GetChoiceForSaving () {
  return (CursorPos+(-ChildrenOfsY)*10000);
}


//==========================================================================
//
//  SetSavedChoice
//
//  should be called after menu is fully created, and default choice set
//
//==========================================================================
void SetSavedChoice (int saval) {
  int idx = saval%10000;
  int top = saval/10000;
  if (idx < 0 || idx >= Items.length || CursorPos == idx) return;
  if (Items[idx].Focusable && Items[idx].Enabled) {
    CursorPrev = -1;
    CursorPos = idx;
    ChildrenOfsY = -top;
    CursorMoved();
  }
}


//==========================================================================
//
//  CyclePrevChoice
//
//==========================================================================
void CyclePrevChoice () {
  if (Items.length < 2) return;
  CursorPrev = CursorPos;
  do {
    if (!CursorPos) CursorPos = Items.length-1; else --CursorPos;
  } while ((!Items[CursorPos].Focusable || !Items[CursorPos].Enabled) && !MenuChoice_BackButton(Items[CursorPos]) && CursorPrev != CursorPos);
  CursorMoved();
}


//==========================================================================
//
//  CycleNextChoice
//
//==========================================================================
void CycleNextChoice () {
  if (Items.length < 2) return;
  CursorPrev = CursorPos;
  do {
    if (CursorPos == Items.length-1) CursorPos = 0; else ++CursorPos;
  } while ((!Items[CursorPos].Focusable || !Items[CursorPos].Enabled) && !MenuChoice_BackButton(Items[CursorPos]) && CursorPrev != CursorPos);
  CursorMoved();
}


//==========================================================================
//
//  DoHome
//
//==========================================================================
void DoHome () {
  if (Items.length < 2) return;
  CursorPrev = CursorPos;
  for (CursorPos = 0; CursorPos < Items.Length; ++CursorPos) {
    if (Items[CursorPos].Focusable && Items[CursorPos].Enabled && !MenuChoice_BackButton(Items[CursorPos])) break;
  }
  CursorMoved();
}


//==========================================================================
//
//  DoEnd
//
//==========================================================================
void DoEnd () {
  if (Items.length < 2) return;
  CursorPrev = CursorPos;
  for (CursorPos = Items.Length-1; CursorPos >= 0; --CursorPos) {
    if (Items[CursorPos].Focusable && Items[CursorPos].Enabled && !MenuChoice_BackButton(Items[CursorPos])) break;
  }
  CursorMoved();
}


//==========================================================================
//
//  OnDraw
//
//  fade all the screen buffer, so that the menu is more readable,
//  especially now that we use the small hudfont in the menus
//
//==========================================================================
override void OnDraw () {
  ShadeRect(0, 0, Width, Height, GetCvarF('menu_darkening'));
}


//==========================================================================
//
//  OnPostDraw
//
//==========================================================================
override void OnPostDraw () {
  int y0 = ChildrenOfsY; // negative means "scrolled down"
  int y1 = CalcTotalHeight()+y0;
  if (y0 < 0 || y1 > Height) {
    SetFont('consolefont');
    if (y0 < 0) DrawText(4, 4, "\x1a", CR_GOLD); // up arrow
    if (y1 > Height) DrawText(4, 4+FontHeight(), "\x1b", CR_GOLD); // down arrow
    SetFont('smallfont');
  }
}


//==========================================================================
//
//  OnEvent
//
//==========================================================================
override bool OnEvent (ref event_t evt) {
  if (evt.type == ev_broadcast) {
    if (evt.data1 == ev_resolution) {
      //printdebug("%C: *** RESOLUTION ***", self);
      // center menu
      SetPos((GetVirtualWidth()-Width)/2, (GetVirtualHeight()-Height)/2);
    }
    return ::OnEvent(evt);
  }

  if (evt.bubbling && evt.type == ev_keydown && evt.keycode == K_MOUSE2) {
    DoPopMenu(userAbort:true);
    return true;
  }

  if (evt.bubbling && evt.type == ev_keydown) {
    int key = ConvertBoundKey(evt.keycode);
    switch (key) {
      case K_UPARROW:
      case K_PAD8:
      case K_MWHEELUP:
        if (Items.length > 1) {
          CyclePrevChoice();
          LocalSound('menu/cursor');
        }
        return true;
      case K_DOWNARROW:
      case K_PAD2:
      case K_MWHEELDOWN:
        if (Items.length > 1) {
          CycleNextChoice();
          LocalSound('menu/cursor');
        }
        return true;
      case K_HOME:
      case K_PAD7:
        if (Items.length > 1) {
          DoHome();
          LocalSound('menu/cursor');
        }
        return true;
      case K_END:
      case K_PAD1:
        if (Items.length > 1) {
          DoEnd();
          LocalSound('menu/cursor');
        }
        return true;
      case K_ESCAPE:
        DoPopMenu(userAbort:true);
        return true;
    }
  }

  return ::OnEvent(evt);
}


defaultproperties {
  X = 160;
  Y = 140;
  Width = 320;
  Height = 200;

  TitleX = 160;
  TitleY = 24;
  Focusable = true;
  UseScissor = true;
  MenuScrollable = false;
  AllowVerticalResize = false;

  ActivateSound = 'menu/activate';
  DeactivateSound = 'menu/backup';
  ClearMenusSound = 'menu/clear';

  //WantMouseInput = true;
}
