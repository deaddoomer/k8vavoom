//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2022 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
// base Status Bar class (including fullscreen)
// it also manages various in-game messages (like "you picked up XXX")
class StatusBarShared : Widget;

protected ClientGameBase ClGame;

enum NUM_NOTIFY_LINES = 8;
enum NUM_CHAT_LINES = 16;

const float CenterMessageFadeoutTime = 0.3;

struct NotifyMessage {
  string text;
  string nick; // for chat
  int dupcount;
  float time;
}

// sorted by time
array!NotifyMessage notifyMessages; // NUM_NOTIFY_LINES
int notifyMsgCount;

// sorted by time
array!NotifyMessage chatMessages; // NUM_CHAT_LINES
int chatMsgCount;

array!SplitLine CenterMessageLines;
int CenterMessageHeight;
float CenterTime;

// main widget for HUD messages (like the ones from ACS)
HUDMessageContainer HudMsgWin;

bool lastWasFullscreen;

HUDMinimap minimap;


enum {
  SB_DMG_TOP,
  SB_DMG_BOTTOM,
  SB_DMG_LEFT,
  SB_DMG_RIGHT,
  SB_DMG_LEFT_TOP,
  SB_DMG_LEFT_BOTTOM,
  SB_DMG_RIGHT_TOP,
  SB_DMG_RIGHT_BOTTOM,

  SB_DMG_MAXDIR,
};

const int SB_DMG_UP_FLAG = 1;
const int SB_DMG_DOWN_FLAG = 2;

const float SB_DMG_TIMEOUT = 0.8;
const float SB_DMG_ALPHA = 0.8;

// the size of the widget is 640x512
// 640x512
// ofs is from the center
const float SB_DMG_XOFS = 0.05;
const float SB_DMG_YOFS = 0.05;
// height/width fraction
const float SB_DMG_SIZE = 0.1;
const float SB_DMG_SMALL_SIZE_B = 0.7;
const float SB_DMG_SMALL_SIZE_S = 0.3;
// in pixels
const int SB_DMG_THICKNESS = 8;


#include "StatusBarShared.notifymsg.vc"
#include "StatusBarShared.centermsg.vc"
#include "StatusBarShared.advdamage.vc"


//==========================================================================
//
//  OnCreate
//
//==========================================================================
override void OnCreate () {
  ::OnCreate();
  minimap = NewChild(HUDMinimap);
  HudMsgWin = NewChild(HUDMessageContainer);
  HudMsgWin.ClGame = ClGame;
  Width = GetVirtualWidth();
  Height = GetVirtualHeight();
  //HudMsgWin.Width = Width;
  //HudMsgWin.Height = Height;
  lastWasFullscreen = false;
  // load healthbar graphics
  R_RegisterPicPath('graphics/healthbar/healthbar_bottom.png');
  R_RegisterPicPath('graphics/healthbar/healthbar_top.png');
  R_RegisterPicPath('graphics/healthbar/healthbar_border.png');
}


//==========================================================================
//
//  SetClGame
//
//  called from `ClientGame.RootWindowCreated()`
//
//==========================================================================
void SetClGame (ClientGameBase aclgame) {
  ClGame = aclgame;
  HudMsgWin.ClGame = ClGame;
}


//==========================================================================
//
//  SetClGame
//
//==========================================================================
ClientGameBase GetClGame () {
  return ClGame;
}


//==========================================================================
//
//  StartMap
//
//  WARNING! this can be called several times for one map! (k8:FIXME!)
//  called from `ClientGame.StatusBarStartMap()`
//
//==========================================================================
void StartMap () {
  ClearNotify();
  ClearCenterMessage();
  ClearHudMessages();
}


//==========================================================================
//
//  OnDraw
//
//==========================================================================
override void OnDraw () {
  DrawNotify();
  DrawChat();
  DrawCenterMessage();
}


//==========================================================================
//
//  Tick
//
//==========================================================================
override void Tick (float DeltaTime) {
  auto root = GetRootWidget();
  //print("SBTICK: root=%C; sz=(%d,%d)", root, root.Width, root.Height);
  if (root && (Width != root.Width || Height != root.Height)) {
    SetSize(root.Width, root.Height);
  }
  UpdateNotify(DeltaTime);
  UpdateChat(DeltaTime);
  UpdateCenterMessage(DeltaTime);
  UpdateMinimap(DeltaTime);
}


//==========================================================================
//
//  UpdateMinimap
//
//==========================================================================
void UpdateMinimap (float DeltaTime) {
  if (!minimap) return; // just in case
  if (GetCvarB('am_active') || !GetCvarB('minimap_active')) {
    if (minimap.Visible) minimap.Hide();
    return;
  }

  float xsize = GetCvarF('minimap_size_x');
  float ysize = GetCvarF('minimap_size_y');
  int newSizeX = clamp(cast(int)fround(Width*xsize), 8, Width);
  int newSizeY = clamp(cast(int)fround(Height*ysize), 8, Height);

  float xpos = GetCvarF('minimap_position_x');
  float ypos = GetCvarF('minimap_position_y');
  int newPosX, newPosY;

  if (xpos < 0.0f) {
    // from the right edge
    newPosX = cast(int)fround(Width*(-xpos))-newSizeX;
  } else {
    // from the left edge
    newPosX = cast(int)fround(Width*xpos);
  }
  newPosX = clamp(newPosX, 0, Width-newSizeX);

  if (ypos < 0.0f) {
    // from the right edge
    newPosY = cast(int)fround(Height*(-ypos))-newSizeY;
  } else {
    // from the left edge
    newPosY = cast(int)fround(Height*ypos);
  }
  newPosY = clamp(newPosY, 0, Height-newSizeY);

  if (!minimap.Visible) minimap.Show();

  if (newPosX != minimap.PosX || newPosY != minimap.PosY ||
      newSizeX != minimap.Width || newSizeY != minimap.Height)
  {
    minimap.SetConfiguration(newPosX, newPosY, newSizeX, newSizeY);
  }
}


//**************************************************************************
//  HUD messages
//**************************************************************************

//==========================================================================
//
//  ClearHudMessages
//
//==========================================================================
void ClearHudMessages () {
  if (HudMsgWin) HudMsgWin.ClearHudMessages();
}


//==========================================================================
//
//  AddHudMessage
//
//==========================================================================
void AddHudMessage (string Message, name aFont, int Type, int Id, int Color,
  string ColorName, float x, float y, int HudWidth, int HudHeight,
  float HoldTime, float Time1, float Time2, float Alpha)
{
  if (!HudMsgWin) return;
  HudMsgWin.AddHudMessage(Message, aFont, Type, Id, Color, ColorName, x, y, HudWidth, HudHeight, HoldTime, Time1, Time2, Alpha);
}



//**************************************************************************
//  StatusBar
//**************************************************************************

// fullscreen statusbar
// the screen is not 640x480
void SB_DrawFullscreenStats () {}

// normal statusbar, called in order
void SB_DrawMainBarPre (bool inAutomap) {} // the screen is not 640x480
void SB_FillMainBar (bool inAutomap) {} // this fills the sides with the rock texture; the screen is 640x480
void SB_DrawMainBar (bool inAutomap) {} // the screen is 640x480
void SB_DrawMainBarPost (bool inAutomap) {} // the screen is not 640x480


struct SB_SavedVScreenState {
  int width, height;
  float scaleY;
}


//==========================================================================
//
//  SB_SaveVScreen
//
//==========================================================================
final void SB_SaveVScreen (out SB_SavedVScreenState sss) {
  sss.width = GetVirtualWidth();
  sss.height = GetVirtualHeight();
  sss.scaleY = ScaleY;
}


//==========================================================================
//
//  SB_SetCorrected640x480
//
//==========================================================================
final void SB_SetCorrected640x480 (out SB_SavedVScreenState sss) {
  SB_SaveVScreen(out sss);
  SetVirtualScreen(640, 480);
  if (Width != 640 || Height != 480) SetSize(640, 480);
  ScaleY = ScaleY*R_GetAspectRatio();
  Y = -int(480.0*ScaleY-480);
}


//==========================================================================
//
//  SB_RestoreVScreen
//
//==========================================================================
final void SB_RestoreVScreen (const ref SB_SavedVScreenState sss) {
  SetVirtualScreen(sss.width, sss.height);
  if (Width != sss.width || Height != sss.height) SetSize(sss.width, sss.height);
  ScaleY = sss.scaleY;
  if (X || Y) SetOrigin(0, 0);
}


//==========================================================================
//
//  SB_DrawMainBarCorrected
//
//==========================================================================
void SB_DrawMainBarCorrected (bool inAutomap, bool doFill) {
  SB_SavedVScreenState sss;
  SB_SetCorrected640x480(out sss);
  scope(exit) SB_RestoreVScreen(ref sss);
  if (doFill) SB_FillMainBar(inAutomap);
  SB_DrawMainBar(inAutomap);
}


//==========================================================================
//
//  SB_DrawMainPart
//
//  this is common code for all games
//
//==========================================================================
void SB_DrawMainPart (int sb_view) {
  int sblocks = R_GetScreenBlocks();
  if (sblocks >= R_SCREEN_BLOCKS_NO_STATUSBAR) return;
  auto ovw = GetVirtualWidth();
  auto ovh = GetVirtualHeight();
  if (Width != ovw || Height != ovh) SetSize(ovw, ovh);
  if (sb_view == SB_VIEW_FULLSCREEN || sblocks >= R_SCREEN_BLOCKS_FS_STATUSBAR) {
    SB_DrawFullscreenStats();
  } else {
    bool inAutomap = (sb_view == SB_VIEW_AUTOMAP);
    SB_DrawMainBarPre(inAutomap);
    SB_DrawMainBarCorrected(inAutomap, doFill:true);
    SB_DrawMainBarPost(inAutomap);
  }
}


// ////////////////////////////////////////////////////////////////////////// //
// global locations
const int WI_SPACINGY = 33;

// deathmatch stuff
const int DM_MATRIXX = 202;
const int DM_MATRIXY = 208;

const int DM_TOTALSX = 329;
const int DM_DEATHSX = 429;

const int DM_SPACINGX = 40;


//==========================================================================
//
//  SB_DrawDeathmatchStats
//
//==========================================================================
void SB_DrawDeathmatchStats () {
  SetFont('bigfont');

  PlayerReplicationInfo RepInfo;

  SetTextAlign(hcenter, vtop);
  // draw stat titles (top line)
  DrawText(DM_TOTALSX, DM_MATRIXY-WI_SPACINGY, "FRAGS");
  DrawText(DM_DEATHSX, DM_MATRIXY-WI_SPACINGY, "DEATHS");

  // draw stats
  int y = DM_MATRIXY;

  foreach ClGame.GLevel.LevelInfo.AllThinkers(PlayerReplicationInfo, RepInfo) {
    if (RepInfo.PlayerNum == ClGame.cl.ClientNum) {
      // your face
      int star = R_RegisterPic('stfst01');
      picinfo_t info;
      R_GetPicInfo(star, &info);
      DrawPic(DM_MATRIXX-info.width-5, y-8, star);
    }
    SetTextAlign(hleft, vtop);
    SetFont('smallfont');
    DrawText(DM_MATRIXX, y+5, RepInfo.PlayerName);
    SetTextAlign(hright, vtop);
    SetFont('bigfont');
    DrawText(DM_TOTALSX, y, va("%s", RepInfo.Frags));
    DrawText(DM_DEATHSX, y, va("%s", RepInfo.Deaths));
    //print("  f:%s d:%s -- %s", RepInfo.Frags, RepInfo.Deaths, RepInfo.PlayerName);
    y += 24;
  }
}


// ////////////////////////////////////////////////////////////////////////// //
// net game stuff
const int NG_STATSY = 210;
const int NG_STATSX = (172+32/2);

const int NG_SPACINGX = 64;


//==========================================================================
//
//  SB_DrawCoopStats
//
//==========================================================================
void SB_DrawCoopStats () {
  picinfo_t info;
  PlayerReplicationInfo RepInfo;

  int pwidth = TextWidth("%");

  SetFont('bigfont');
  SetTextAlign(hright, vtop);
  // draw stat titles (top line)
  DrawText(NG_STATSX+NG_SPACINGX, NG_STATSY, "kills");
  DrawText(NG_STATSX+2*NG_SPACINGX, NG_STATSY, "items");
  DrawText(NG_STATSX+3*NG_SPACINGX, NG_STATSY, "scrt");
  DrawText(NG_STATSX+4*NG_SPACINGX, NG_STATSY, "frgs");

  // draw stats
  int y = NG_STATSY+TextHeight("kills");

  foreach ClGame.GLevel.LevelInfo.AllThinkers(PlayerReplicationInfo, RepInfo) {
    int i = RepInfo.PlayerNum;
    int x = NG_STATSX;

    if (i == ClGame.cl.ClientNum) {
      // your face
      int star = R_RegisterPic('stfst01');
      DrawPic(x-info.width, y, star);
    }

    SetTextAlign(hright, vtop);
    x += NG_SPACINGX;
    DrawText(x-pwidth, y+10, va("%d", RepInfo.KillCount));
    x += NG_SPACINGX;
    DrawText(x-pwidth, y+10, va("%d", RepInfo.ItemCount));
    x += NG_SPACINGX;
    DrawText(x-pwidth, y+10, va("%d", RepInfo.SecretCount));
    x += NG_SPACINGX;

    SetTextAlign(hright, vtop);
    DrawText(x, y+10, va("%d", RepInfo.Frags));

    y += WI_SPACINGY;
  }
}


//==========================================================================
//
//  SB_DrawNetworkStats
//
//  called from `ClientGame.StatusBarDrawer()`
//
//==========================================================================
void SB_DrawNetworkStats () {
  if (!ClGame) return;
  if (!ClGame.GLevel) return;
  if (ClGame.Game.NetMode <= GameInfo::NM_Standalone) return;
  if (!ClGame.cl || !ClGame.cl.MO || ClGame.cl.MO.Health > 0) return;
  if (ClGame.deathmatch) {
    SB_DrawDeathmatchStats();
  } else {
    SB_DrawCoopStats();
  }
}


//==========================================================================
//
//  SB_Drawer
//
//  called from `ClientGame.StatusBarDrawer()`
//
//==========================================================================
void SB_Drawer (int sb_view) {
  SB_Housekeeping(sb_view);
}



//==========================================================================
//
//  SB_UpdateWidgets
//
//  called from `ClientGame.StatusBarUpdateWidgets()`
//
//==========================================================================
void SB_UpdateWidgets () {
}


//==========================================================================
//
//  SB_Housekeeping
//
//  called from `SB_Drawer()`
//
//==========================================================================
void SB_Housekeeping (int sb_view) {
  bool fs = (sb_view == SB_VIEW_FULLSCREEN);
  lastWasFullscreen = fs;
}


defaultproperties {
}
